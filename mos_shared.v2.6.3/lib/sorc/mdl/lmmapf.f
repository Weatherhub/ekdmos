      SUBROUTINE LMMAPF(KFILDO,XMESHL,ORIENT,XLAT,XLATLL,XLONLL,
     1                  FDSINS,FDMS,NX,NY,IER)
C
C        FEBRUARY 1995   GLAHN   TDL   MOS-2000 
C
C        PURPOSE 
C            TO RETURN THE SIN OF THE LATITUDE AND THE MAP FACTOR FOR
C            EACH GRIDPOINT FOR A LAMBERT MAP.  MAP FACTOR EQUATION
C            GIVEN IN GLAHN, MONTHLY WEATHER REVIEW, VOL 118, NO. 12,
C            PP. 2781-2783.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS CORRECT.
C                       (INPUT)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES WEST.
C                       (INPUT)
C              XLATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES NORTH.  (INPUT)
C              XLONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES WEST.  (INPUT)
C       FDSINS(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).  (OUTPUT)
C         FDMS(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (OUTPUT)
C                       TO SAVE THE ARRAY.  SEE FDSINS( , ) 
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C                 IER = STATUS RETURN.
C                       0 = GOOD RETURN.
C                       SEE LMIJLL FOR OTHER POSSIBILITIES.
C              COXLAT = COLATITUDE OF XLAT IN RADIANS.  (INTERNAL)
C              COALAT = COLATITUDE OF ALAT IN RADIANS.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            LMIJLL
C
      PARAMETER (PI=3.14159,
     1           RADPDG=PI/180.)
c
      DIMENSION FDSINS(NX,NY),FDMS(NX,NY)
C
      IER=0
C
      COXLAT=RADPDG*(90.-XLAT)
      F=SIN(COXLAT)/(TAN(COXLAT/2.)**COS(COXLAT))
C
C        CALCULATE THE LATITUDE AND LONGITUDE OF EACH GRIDPOINT, THEN
C        THE SINE OF THE LATITUDE AND THE MAP FACTOR.
C
      DO 130 JY=1,NY
      DO 120 IX=1,NX
      XI=IX
      YJ=JY
      CALL LMIJLL(KFILDO,XI,YJ,XMESHL,ORIENT,XLAT,
     1            XLATLL,XLONLL,ALAT,ALON,IER)
      IF(IER.NE.0)GO TO 140
C        IF A PROBLEM OCCURS IN LMIJLL, A DIAGNOSTIC WILL HAVE
C        BEEN PRINTED THERE.
      COALAT=RADPDG*(90.-ALAT)
      FDSINS(IX,JY)=SIN(RADPDG*ALAT)
      FDMS(IX,JY)=F*((TAN(COALAT/2.)**COS(COXLAT))/SIN(COALAT))
 120  CONTINUE
 130  CONTINUE
C
 140  RETURN
      END      
      
      
