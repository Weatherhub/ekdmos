      SUBROUTINE PSMAPF(KFILDO,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1                  FDSINS,FDMS,NX,NY,IER)
C
C        FEBRUARY 1995   GLAHN   TDL   MOS-2000 
C        JUNE     1997   GLAHN   D COMPILE OPTION COMMENTED OUT
C
C        PURPOSE 
C            TO RETURN THE SIN OF THE LATITUDE AND THE MAP FACTOR FOR
C            EACH GRIDPOINT FOR A POLAR STEREOGRAPHIC MAP.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS CORRECT.
C                       (INPUT)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES WEST.
C                       (INPUT)
C              ALATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES NORTH.  (INPUT)
C              ALONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES WEST.  (INPUT)
C       FDSINS(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).  (OUTPUT)
C         FDMS(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (OUTPUT)
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                 XNP = THE IX POSITION OF THE NORTH POLE RELATIVE TO THE
C                       LOWER LEFT CORNER OF THE GRID.  (INTERNAL)
C                 YNP = THE JY POSITION OF THE NORTH POLE RELATIVE TO THE
C                       LOWER LEFT CORNER OF THE GRID.  (INTERNAL)
C              SINRXL = SINE OF THE LATITUDE AT WHICH XMESHL IS CORRECT.
C                       (INTERNAL)
C              POL2EQ = DISTANCE FROM THE NORTH POLE TO THE EQUATOR IN 
C                       GRID UNITS.  NOTE THAT THIS IS THE DISTANCE
C                       ON THE IMAGE PLANE, NOT ON THE EARTH.  (INTERNAL)
C              P2EQSQ = DISTANCE FROM THE NORTH POLE TO THE EQUATOR IN
C                       GRID UNITS SQUARED.  NOTE THAT THIS IS THE DISTANCE
C                       ON THE IMAGE PLANE, NOT ON THE EARTH.  (INTERNAL)
C                  PI = PI.  SET BY PARAMETER.  (INTERNAL)
C               RERTH = RADIUS OF EARTH IN M.  SET BY PARAMETER.  (INTERNAL)
C              RADPDG = RADIANS PER DEGREE.  SET BY PARAMETER.  (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      PARAMETER (PI=3.14159)
      PARAMETER (RERTH=6371200.)
      PARAMETER (RADPDG=PI/180.)
C
      DIMENSION FDSINS(NX,NY),FDMS(NX,NY)
C
      IER=0
C
      CALL PSLLIJ(KFILDO,90.,0.,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1               XNP,YNP)
C        XNP AND YNP ARE THE NORTH POLE GRIDPOINT POSITIONS
C        CORRESPONDING TO THE GRID BEING USED.  (BECAUSE OF SIMPLICITY
C        OF CALCULATIONS FOR POLAR STEREOGRAPHIC MAP, ONCE THE POLE
C        POSTION IS KNOWN, THE SINE OF THE LATITUDE AND MAP
C        FACTOR ARE EASILY CALCULATED.  TIME HAS NOT BEEN DEVOTED 
C        TO TRYING TO MAKE THIS SIMPLIFICATION FOR THE LAMBERT.)
C
      POL2EQ=RERTH*(1.+SIN(XLAT*RADPDG))/XMESHL
C        POL2EQ = THE DISTANCE FROM THE POLE TO THE EQUATOR IN 
C        GRID UNITS ON THE IMAGE PLANE FOR A POLAR STEREOGRAPHIC MAP,
C        ON WHICH THE XMESHL APPLIES AT XLAT.
C***D     WRITE(KFILDO,215)XMESHL,XLAT,ORIENT,ALATLL,ALONLL,POL2EQ,
C***D    1                 RADPDG,XNP,YNP
C***D215  FORMAT(/' IN PSMAPF'
C***D    1       /'     XMESHL      XLAT    ORIENT    ALATLL    ALONLL',
C***D    2         '    POL2EQ    RADPDG       XNP       YNP'/' '9F10.3)
C
      SINRXL=SIN(XLAT*RADPDG)
      P2EQSQ=POL2EQ**2
C
      DO 250 JY=1,NY
      DYSQ=(YNP-JY)**2
C
      DO 249 IX=1,NX
      DXSQ=(XNP-IX)**2
      FDSINS(IX,JY)=(P2EQSQ-(DXSQ+DYSQ))/(P2EQSQ+(DXSQ+DYSQ))
      FDMS(IX,JY)=(1.+SINRXL)/(1.+FDSINS(IX,JY))
C***D     WRITE(KFILDO,248)IX,JY,XNP,YNP,DXSQ,DYSQ,
C***D    1                 FDSINS(IX,JY),FDMS(IX,JY)
C***D248  FORMAT(' IX,JY,XNP,YNP,DXSQ,DYSQ,FDSINS(IX,JY),FDMS(IX,JY)',
C***D    1       ' '2I4,6F10.3)
 249  CONTINUE
 250  CONTINUE
      RETURN
      END
