      SUBROUTINE DATPRO(KFILDO,IDATE,NWORK,ND8,INCCYL,NDATES,IP2,IP3,
     1                  IER)
C
C        DECEMBER  1994   GLAHN   TDL   MOS-2000
C        SEPTEMBER 1998   GLAHN   ADDED STOP ON INCCYL = 0
C        MARCH     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS
C                                 TO CONFORM TO FORTRAN 90
C                                 STANDARDS ON IBM SP
C        MAY       2000   GLAHN   CHANGED I4 TO I5 FOR NUMBER OF DATES;
C                                 PERIOD IN FORMATS 103, 138, 160
C        SEPTEMBER 2003   GLAHN   ADDED CHECK ON HOUR TO BE 00 TO 23
C        MARCH     2004   GLAHN   ADDED FORMAT 155 TO ALWAYS PRINT
C                                 NUMBER OF DATES EVEN WHEN IP3 = 0
C        MARCH     2005   GLAHN   ADDED POSSIBLE WRITE TO IP2 AT 114
C
C        PURPOSE
C            ACCEPTS A DATE LIST IN IDATE( ), WHICH MAY CONTAIN NEGATIVE
C            VALUES INDICATING A DATE SPAN FROM THAT DATE BACKWARD THRU
C            THE PREVIOUS DATE, AND (1) MODIFIES DATES FROM YYMMDDHH TO
C            YYYYMMDDHH, (2) MAKES SURE THE DATES ARE IN ASCENDING ORDER,
C            AND (3) EXPANDS THE LIST IN IDATE( ) TO INCLUDE ALL DATES
C            IN THE SPANS IF THERE ARE ANY.  AN ERROR IN THE DATE LIST 
C            WILL CAUSE THE LIST TO BE PRINTED.  ERRORS WILL ALWAYS BE
C            WRITTEN TO UNIT NUMBER KFILDO.  DATES BETWEEN JAN. 1 1960
C            AND DEC. 31, 2059 ARE ACCOMMODATED.
C
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            IDATE(J) = INITIAL DATE LIST (J=1,NDATES) WHICH MAY CONTAIN
C                       NEGATIVE VALUES INDICATING A DATE SPAN.
C                       ON OUTPUT, IDATE(J) WILL CONTAIN THE COMPLETE
C                       DATE LIST WITH THE DATES IN THE SPANS FILLED IN
C                       (J=1,NDATES), WHERE NDATES HAS BEEN INCREASED
C                       IF NECESSARY.  DATES ARE INPUT AS YYMMDDHH AND
C                       OUTPUT AS YYYYMMDDHH.  ZEROS IN THE LIST ARE ERRORS.
C                       (INPUT-OUTPUT)
C            NWORK(J) = A WORK ARRAY (J=1,ND8).  (INTERNAL)
C                 ND8 = DIMENSION OF IDATE( ) AND NWORK( ).  (INPUT)
C              INCCYL = INCREMENT IN HOURS BETWEEN DATE/TIMES THAT
C                       ARE PUT INTO IDATE( ).  IT MUST BE GT 0.  (INPUT)
C              NDATES = ON INPUT, NUMBER OF VALUES INPUT IN IDATE( ).
C                       ON OUTPUT, NUMBER OF VALUES OUTPUT IN IDATE( ).
C                       (INPUT-OUTPUT)
C                 IP2 = INDICATES WHETHER (>1) OR NOT (=0) THE INPUT DATES
C                       IN IDATE( ) WILL BE PRINTED.  WHEN THERE ARE NO
C                       ERRORS, THE PRINT WILL BE TO UNIT IP2; WHEN THERE
C                       ARE ERRORS, PRINT WILL BE TO UNIT KFILDO AS WELL AS
C                       TO UNIT IP2.  (INPUT)
C                 IP3 = INDICATES WHETHER (>1) OR NOT (=0) THE OUTPUT DATES
C                       IN IDATE( ) WILL BE PRINTED.  WHEN THERE ARE NO
C                       ERRORS, THE PRINT WILL BE TO UNIT IP3; WHEN THERE
C                       ARE ERRORS, PRINT WILL BE TO UNIT KFILDO AS WELL AS
C                       TO UNIT IP3.  THE NUMBER OF DATES WILL ALWAYS
C                       BE WRITTEN EVEN IF IP3 = 0.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       25 = ERROR IN LIST.
C                       OTHER ERRORS MAY BE RETURNED FROM DATGEN.
C                NERR = COUNTS ERRORS IN DATES.  WHEN ONE OR MORE ERRORS OCCUR,
C                       THE ROUTINE PRINTS THE DATES AND ALL THE ERRORS
C                       TO UNIT KFILDO.  (INTERNAL)
C                ITAG = INDICATES WHETHER (=1) OR NOT (=0) THE DATE LIST
C                       INCLUDES A DATE SPAN.  (INTERNAL)
C               MTEST = 12-WORD ARRAY CONTAINING THE NUMBER OF
C                       DAYS FOR EACH MONTH OF A NON LEAP YEAR.  (INTERNAL)
C
C        NONSYSTEM SUBROUTINES USED
C            DATGEN
C
      DIMENSION IDATE(ND8),NWORK(ND8)
      DIMENSION MTEST(12)
C
      DATA MTEST/31,28,31,30,31,30,31,31,30,31,30,31/
C
      IER=0
      NERR=0
      ITAG=0 
C
C        THE DATES ARE INPUT AS YYMMDDHH.  MAKE THEM  YYYYMMDDHH.      
C 
      IF(INCCYL.LE.0)THEN
         WRITE(KFILDO,101)INCCYL
 101     FORMAT(/,' ****INCCYL =',I6,' LE 0.  STOP AT 101 IN DATPRO.')
         STOP 101
      ENDIF
C
      DO 102 J=1,NDATES
      ISIGN=1
      IF(IDATE(J).LT.0)ISIGN=-1
      IF(IABS(IDATE(J))/1000000.GT.60)THEN
         IDATE(J)=IDATE(J)+1900000000*ISIGN
      ELSE
         IDATE(J)=IDATE(J)+2000000000*ISIGN
      END IF
 102  CONTINUE
C
C        PRINT INPUT LIST IF DESIRED.
C
      IF(IP2.NE.0)WRITE(IP2,103)NDATES,(IDATE(J),J=1,NDATES) 
 103  FORMAT(/' ',I5,' INPUT DATES AS READ.'/(1X,10I12)) 
C
C        CHECK WHETHER 1ST DATE IS POSITIVE.
C
      IF(IDATE(1).GT.0)GO TO 105
C        THE FIRST VALUE IS IN ERROR.  IT SHOULD NOT BE NEGATIVE TO INDICATE
C        A DATE SPAN, BECAUSE A POSITIVE NUMBER MUST PRECEED A NEGATIVE
C        VALUE.
      WRITE(KFILDO,104)
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,104)
 104  FORMAT(/' ****THE FIRST DATE IS IN ERROR.  IT MUST BE POSITIVE.')
      NERR=NERR+1
 105  IF(NDATES.EQ.1)GO TO 136
C
C        MAKE SURE ALL DATES ARE OF CORRECT FORMAT.
C
      DO 115 N=1,NDATES
      JY=IABS(IDATE(N))/1000000 
      JM=IABS(IDATE(N))/10000-JY*100
      JD=IABS(IDATE(N))/100-JY*10000-JM*100
      JH=MOD(IABS(IDATE(N)),100)
      MTEST(2)=28 
C        THE YEAR 2000 IS A LEAP YEAR.  NO TEST IS MADE FOR CENTURY
C        NON LEAP YEARS.
      IF(MOD(JY,4).EQ.0)MTEST(2)=29
C
      IF(JM.GE.1.AND.JM.LE.12)GO TO 111
      WRITE(KFILDO,110)N,IDATE(N)
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,110)N,IDATE(N)
 110  FORMAT(/,' ****INCORRECT MONTH IN IDATE(',I5,') =',I12)
      NERR=NERR+1
      GO TO 115
C
 111  IF(JD.GE.1.AND.JD.LE.MTEST(JM))GO TO 113
      WRITE(KFILDO,112)N,IDATE(N)
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,112)N,IDATE(N)
 112  FORMAT(/,' ****INCORRECT DAY IN IDATE(',I5,') =',I12)
      IER=25
      NERR=NERR+1
      GO TO 115
C
 113  IF(JH.GE.0.AND.JH.LE.23)GO TO 115
      WRITE(KFILDO,114)N,IDATE(N)
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,114)N,IDATE(N)
 114  FORMAT(/,' ****INCORRECT HOUR IN IDATE(',I5,') =',I12)
      IER=25
      NERR=NERR+1

 115  CONTINUE
C
C        INSURE DATES ARE IN ASCENDING ORDER.  SET ITAG = 1 TO INDICATE
C        ONE OR MORE DATE SPANS. 
C
      DO 134 N=2,NDATES  
      IF(IDATE(N).GE.0)GO TO 128  
      ITAG=1  
 128  IF(IABS(IDATE(N-1)).LT.IABS(IDATE(N)))GO TO 134 
      K=N-1  
      WRITE(KFILDO,132)K,IDATE(K),N,IDATE(N)
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,132)K,IDATE(K),N,IDATE(N)
 132  FORMAT(/,' ****INPUT DATES OUT OF ORDER OR NOT DEFINED CORRECTLY', 
     1        '   NDATE(',I5,') =',I12,/,57X,'NDATE(',I5,') =',I12)
      NERR=NERR+1
 134  CONTINUE  
C
C        PRINT DATES WHEN IP2 NE 0 OR WHEN THERE HAS BEEN AN ERROR.
C
 136  IF(NERR.NE.0)WRITE(KFILDO,138)NDATES,(IDATE(K),K=1,NDATES)
C        WHEN AN ERROR OCCURRS, THE DATES ARE WRITTEN TO KFILDO. 
      IF(NERR.NE.0.AND.IP2.NE.0.AND.IP2.NE.KFILDO)
     1             WRITE(IP2,138)NDATES,(IDATE(K),K=1,NDATES)
C        THE DATES MAY BE WRITTEN TWICE, TO KFILDO AND TO IP2
C        WHEN THERE ARE ERRORS DETECTED. 
 138  FORMAT(/,' ',I5,' INPUT DATES.',/,(1X,6I12))
      IF(NERR.EQ.0)GO TO 139
C
C        THERE HAS BEEN ONE OR MORE ERRORS.  PRINT AND RETURN.
C
      WRITE(KFILDO,1380)NERR
      IF(IP2.NE.0.AND.IP2.NE.KFILDO)WRITE(IP2,1380)NERR
 1380 FORMAT(/,' ****THERE HAVE BEEN',I4,' ERRORS IN THE DATE LIST.',
     1         '  RETURN FROM DATPRO AT 1380.')
      IER=25
      GO TO 200
C       
C        INSERT DATES WHEN THERE IS DATE SPANNING.  
C
 139  IF(ITAG.EQ.0)GO TO 150  
      ND=0
C        ND COUNTS THE ITEMS USED IN IDATE( ).  
      LD=0
C        LD COUNTS THE ITEMS PUT INTO NWORK( ).  
 140  ND=ND+1 
      IF(ND.GT.NDATES)GO TO 146  
      ND=ND+1 
      IF(ND.GT.NDATES)GO TO 142  
      IF(IDATE(ND).LE.0)GO TO 144 
 142  ND=ND-1  
      LD=LD+1 
      NWORK(LD)=IDATE(ND)  
      GO TO 140 
C
 144  LD=LD+1 
      CALL DATGEN(KFILDO,IP2,IDATE(ND-1),-IDATE(ND),INCCYL,
     1            NWORK,LD,ND8,IER)
      IF(IER.EQ.0)GO TO 140
C
 146  NDATES=LD  
C
      DO 148 N=1,NDATES 
      IDATE(N)=NWORK(N) 
 148  CONTINUE  
C
 150  IF(IER.NE.0)WRITE(KFILDO,160)NDATES,(IDATE(J),J=1,NDATES)
C        WHEN AN ERROR OCCURS, THE DATES ARE WRITTEN TO KFILDO. 
      IF(IP3.EQ.KFILDO.AND.IER.NE.0)GO TO 200
C
      IF(IP3.EQ.0)THEN
C           THE NUMBER OF DATES IS ALWAYS WRITTEN, EITHER
C           TO KFILDO OR IP3.
         WRITE(KFILDO,155)NDATES
 155     FORMAT(/' ',I5,' GENERATED OUTPUT DATES.') 
      ELSE
         WRITE(IP3,160)NDATES,(IDATE(K),K=1,NDATES) 
C           THE DATES MAY BE WRITTEN TWICE, TO KFILDO AND TO IP3
C           WHEN THERE ARE ERRORS DETECTED. 
 160     FORMAT(/' ',I5,' GENERATED OUTPUT DATES.'/(1X,10I12)) 
C
      ENDIF
C
 200  RETURN
      END
