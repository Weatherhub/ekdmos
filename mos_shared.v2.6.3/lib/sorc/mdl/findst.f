      SUBROUTINE FINDST(KFILDO,IPX,NAME,CCALL,ND1,NSTA,
     1           CCALLD,MSTA,INDEXC,IER)
C
C        JUNE      1995   GLAHN   TDL   MOS-2000
C        AUGUST    1996   GLAHN   VARIABLE IPRINT ADDED
C        FEBRUARY  1997   GLAHN   ADDED TO PRINT AT STATEMENT 110
C        JULY      1997   GLAHN   ADDED SUBSTITUTE STATIONS IN
C                                 CCALL( , ); ADDED ND1 TO ARGUMENT LIST
C        AUGUST    1997   GLAHN   ADDED JPRINT AND JPRMAX
C        OCTOBER   1998   GLAHN   ADDED MSTA TO PRINT AT 110
C        OCTOBER   1998   GLAHN   INDEXC( ) SET = 99999999 FOR MISSING
C        OCTOBER   1998   GLAHN   SPEEDED UP SEARCH; CHANGED PRINT OF
C                                 STATIONS NOT ON INPUT
C        NOVEMBER  1998   GLAHN   ADDED /D PRINT FOR DIRECTORY
C        APRIL     1999   GLAHN   /D FOR NOT PRINTING MISSING STATIONS
C        MAY       1999   DALLAVALLE   SET CODE TO NOT PRINT THE MISSING
C                                 STATIONS; /D CONSTRUCT WAS 
C                                 RETAINED IN A COMMENT BLOCK
C                                 IN CASE A DEVELOPER NEEDS TO SEE
C                                 THE MISSING STATIONS IN THE DIRECTORY.
C                                 IN THAT CASE, THE DEVELOPER WILL NEED
C                                 TO MOVE THE SOURCE CODE INTO A LOCAL
C                                 DIRECTORY AND MODIFY THE CODE.
C                                 NOTE THAT THE /D CONSTRUCT DOES NOT
C                                 WORK ON EITHER THE CRAY OR THE IBM
C                                 AND SO HAS TO BE REMOVED THERE.
C        MAY       2000   GLAHN   ADDED COMMAS TO FORMAT STATEMENTS
C        AUGUST    2000   GLAHN   ADDED NOTE UNDER PURPOSE
C        OCTOBER   2000   GLAHN   MODIFIED SEARCH TO ALWAYS USE FIRST
C                                 COLUMN OF DIRECTORY BEFORE LINKS
C                                 AND LINKS IN ORDER L TO R; ADDED 
C                                 CCALLM( ) TO NOT OBLITERATE CCALLD( )
C        MARCH     2001   GLAHN   ADDED MTOT=MTOT+1 BELOW 129
C        APRIL     2001   DALLAVALLE REMOVED MTOT=MTOT+1 - CODE DID NOT
C                                 WORK CORRECTLY
C        OCTOBER   2003   GLAHN   CHANGED FORMATS 110 AND 142 TO
C                                 ACCOMMODATE LARGE NUMBER OF STATIONS;
C                                 FEW COMMENTS
C
C        PURPOSE
C           TO INITIALIZE THE ARRAY INDEXC( ) WITH THE LOCATIONS OF
C           THE STATIONS IN CCALL( , ) IN THE CALL LETTER RECORD 
C           IN CCALLD( ).  CALLED FROM RDDIR.  NOT USED IN READING
C           THE INITIAL STATION LIST.
C
C           NOTE:  THE LIST OF STATIONS NOT FOUND ON THE FILE IS PRINTED
C           TO KFILDO UNLESS COMPILED WITH /D.  THIS IS VOLUMINOUS
C           OUTPUT FOR READING HOURLY DATA.  THIS NOTE IS NOT APPLICABLE
C           WITH THE CHANGES IMPLEMENTED IN MAY 1999.
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C                 IPX = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                       THE FILE WHOSE UNIT NUMBER IS IPX.  IPX MAY
C                       BE IP12 IN CALL FROM RDDIR, AND SOMETHING
C                       ELSE IF CALLED FROM OTHER ROUTINES.  (INPUT)
C                NAME = THE NAME OF THE FILE WHOSE CALL LETTERS ARE IN
C                       CCALLD( ).  USED FOR PRINTOUT ONLY.
C                       (CHARACTER*60)  (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (K=1,NSTA),
C                       PRIMARY STATION (J=1) AND 5 SUBSTITUTE STATIONS
C                       (J=2,6).  THE LOCATION OF EACH STATION IN THE
C                       CCALLD( ) RECORD IS PLACED INTO INDEXC( ). 
C                       (CHARACTER*8)  (INPUT)
C                 ND1 = FIRST DIMENSION OF CCALL( , ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS IN CCALL( , ).  (INPUT)
C           CCALLD(K) = 8 STATION CALL LETTERS READ FROM AN INPUT FILE
C                       (K=1,MSTA).  (INPUT)
C                MSTA = NUMBER OF WORDS IN CCALLD( ).  (INTPUT)
C           INDEXC(K) = LOCATIONS OF THE STATIONS IN CCALLD( )
C                       CORRESPONDING TO CCALL(K) (K=1,NSTA).  IF THE 
C                       STATION CAN'T BE FOUND IN THE DIRECTORY,
C                       INDEXC( ) IS SET TO 99999999.  NOTE THAT THIS
C                       MUST BE LARGER THAN THE MAXIMUM NUMBER OF
C                       STATIONS BEING DEALT WITH (GRIDPOINTS IN THE
C                       CASE OF GRIDPOINT DEVELOPMENT).  (OUTPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                       120 = CAN'T FIND ONE OR MORE STATIONS IN 
C                             CCALLD( ).  THIS IS SET ONLY WHEN
C                             THE MISSING STATIONS ARE PRINTED.
C               KOUNT = USED TO COUNT THE NUMBER OF STATIONS NOT
C                       FOUND ON THE INPUT, FOR PRINTOUT.  (INTERNAL)
C               IPPRT = NORMALLY 0, BUT WHEN COMPILED WITH /D,
C                       INCREMENTS BY 1 WHEN CALLED. 
C                       THIS CONTROLS PRINTING OF STATIONS
C                       MISSING FROM THE DIRECTORY.  WHEN COMPILED
C                       NORMALLY, THE MISSING STATIONS ARE PRINTED
C                       TO UNIT NO. KFILDO; WHEN COMPILED WITH /D,
C                       THE MISSING STATIONS ARE NOT PRINTED.  THE
C                       LATTER WOULD PROBABLY BE DONE ONLY WHEN
C                       READING HOURLY DATA.  (INTERNAL)
C           IFOUND(K) = 0 UNTIL STATION HAS BEEN FOUND, THEN SET TO L,
C                       THE OUTSIDE LOOP INDEX.  (AUTOMATIC)  (INTERNAL)
C                MTOT = THE TOTAL NUMBER OF STATIONS FOUND.  WHEN 
C                       MTOT = NSTA, THE SEARCH CAN STOP.  (INTERNAL)
C           CCALLM(K) = ARRAY TO HOLD MISSING STATIONS (K=1,KOUNT).
C                       (AUTOMATIC)  (INTERNAL)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      CHARACTER*8 CCALL(ND1,6),
     1            CCALLD(MSTA),
     2            BLANK
      CHARACTER*8 CCALLM(NSTA)
C        CCALLM( ) IS AN AUTOMATIC ARRAY.
      CHARACTER*60 NAME
C
      DIMENSION INDEXC(NSTA)
      DIMENSION IFOUND(NSTA)
C        IFOUND( ) IS AN AUTOMATIC ARRAY.
C
      DATA BLANK/'        '/
      DATA IPPRT/0/
C
      SAVE IPPRT
C
C     CALL TIMPR(KFILDO,KFILDO,'START FINDST        ')
C
      IER=0
      MTOT=0
C 
C        INITIALIZE IFOUND( ).
C
      DO 100 K=1,NSTA
      IFOUND(K)=0
 100  CONTINUE      
C
C********THE FOLLOWING BLOCK HAD THE /D CONSTRUCT.  THIS WAS DISABLED
C********SO AS TO RUN ON THE CRAY AND IBM.   JPD (5-14-99)
C****D     IPPRT=IPPRT+1
C****C        IPPRT = 0 EXCEPT WHEN COMPILED WITH /D.
C****C
C****D     IF(IPPRT.EQ.1)THEN
C****D        WRITE(KFILDO,105)
C****D105     FORMAT(/' STATIONS MISSING IN THE DIRECTORIES WILL',
C****D    1           ' NOT BE PRINTED OR AN ERROR INDICATED.')
C****C           THE ABOVE DIAGNOSTIC WILL OCCUR ONLY ONCE PER RUN.
C****D     ENDIF
C********END OF THE /D BLOCK
C
      IPPRT=IPPRT+1
C        IF IPPRT IS SET TO ZERO, THEN THE PRINT WILL BE TURNED ON.
      IF(IPPRT.EQ.1)THEN
         WRITE(KFILDO,105)
 105     FORMAT(/' STATIONS MISSING IN THE DIRECTORIES WILL',
     1           ' NOT BE PRINTED OR AN ERROR INDICATED.')
C           THE ABOVE DIAGNOSTIC WILL OCCUR ONLY ONCE PER RUN.
      ENDIF
C
C        PRINT LIST OF STATIONS ON INPUT FILE WHEN DESIRED.
C
      IF(IPX.EQ.0)GO TO 120
      WRITE(IPX,110)MSTA,NAME,(K,CCALLD(K),K=1,MSTA)
 110  FORMAT(/,' ',I7,' STATIONS ON INPUT FILE ',A60,/,
     1       ('  ',7(I8,1X,A8)))
C        THE PRINT ABOVE FILLS A LINE AT 10 ITEMS PER LINE.
C        FOR STATIONS OR GRIDPOINTS IN EXCESS OF 9999, THE I4
C        WON'T ACCOMMODATE THE PRINT OF K.  THIS SHOULD BE A
C        RARE PROBLEM, AND WON'T CAUSE AN ERROR.
C
C        FIND THE STATIONS IN CCALL( , ) IN THE CCALLD( ) LIST.
C        NOTE THAT AS SOON AS THE INDEX( ) FOR THE STATION
C        CCALL( , ) IS FILLED, THE INNER LOOPS ARE EXITED.  UP
C        TO 5 SUBSTITUTE STATIONS ARE ALLOWED.
C
 120  NSTART=1
      NEND=MSTA
C
      DO 135 L=1,6
C
      IF(MTOT.EQ.NSTA)GO TO 136
C
      DO 130 K=1,NSTA
      IF(IFOUND(K).NE.0)GO TO 130
C        IFOUND(K) NE 0 MEANS DON'T SEARCH FOR IT; IT HAS BEEN
C        FOUND.
C
 125  DO 127 N=NSTART,NEND
      GO TO(1260,1260,126,126,126,126),L
C        THE FIRST COLUMN IS ALMOST NEVER BLANK,
C        (IT COULD BE IF OLD CALL LETTERS WERE BEING
C        USED, BUT THIS IS RARE AND LIKELY AN ERROR)
C        AND COLUMN 2 IS USUALLY FILLED ALSO.  A GO TO HERE
C        SHOULD BE FASTER THAN A CHARACTER COMPARE.
C
 126  IF(CCALL(K,L).EQ.BLANK)GO TO 129
C        ONCE A BLANK IS ENCOUNTERED IN THE SUBSTITUTE
C        STATION LIST, THE SEARCH IS DISCONTINUED.  THE
C        "CALL LETTERS" COLUMN IS ALWAYS CHECKED.  THIS
C        ALLOWS FOR THE FACT THERE MAY BE A STATION IN
C        COLUMN 3 BUT NOT 2 (AS IN THE CURRENT DIRECTORY
C        AS OF OCTOBER 2000).  ALSO THERE ARE LIKELY LESS
C        TOTAL CHECKS, BECAUSE COLUMN 2 IS USUALLY FILLED.
C
 1260 IF(CCALL(K,L).NE.CCALLD(N))GO TO 127
C        THERE IS A MATCH.
      INDEXC(K)=N
      IFOUND(K)=L
      MTOT=MTOT+1
      NSTART=N+1
C
      IF(NSTART.GT.MSTA)THEN
         NSTART=1
      ENDIF
C
      NEND=MSTA
      GO TO 130
C
 127  CONTINUE
C
C        DROP THROUGH HERE MEANS STATION HAS NOT BEEN FOUND.
C
      IF(NSTART.NE.1)THEN
         NEND=NSTART-1
         NSTART=1
         GO TO 125
C
      ENDIF     
C
      IF(IPPRT.EQ.0)THEN
         IER=120
C           A MISSING STATION IS COUNTED AS AN ERROR
C           ONLY WHEN A **** PRINT IS MADE AT FORMAT
C           142 BELOW.  THAT IS, WHEN THE MISSING
C           STATIONS ARE NOT PRINTED, WHEN COMPILED
C           WITH A /D, AN ERROR IS NOT INDICATED.
      ENDIF
C
 129  INDEXC(K)=99999999
      NEND=MSTA
C        ANY TIME CONTROL COMES HERE, NSTART = 1.  SINCE 
C        STATIONS WILL USUALLY BE FOUND, THIS IS UNUSUAL.
C        NEXT SEARCH WILL START AT THE TOP OF THE LIST.
 130  CONTINUE 
C
 135  CONTINUE
C
C        PRINT WHEN STATIONS NOT FOUND.
C
 136  CONTINUE
C
C***      WRITE(KFILDO,141)((CCALL(K,L),L=1,6),INDEXC(K),IFOUND(K),K=1,NSTA)
C*** 141  FORMAT(/' 141 IN FINDST--CCALL( , ), INDEX( ), IFOUND( )',/,
C***     1      ('   ',6A8,I6,I3))
C
      IF(IPPRT.EQ.0)THEN
         KOUNT=0
C
         DO 140 K=1,NSTA
         IF(INDEXC(K).NE.99999999)GO TO 140
         KOUNT=KOUNT+1
         CCALLM(KOUNT)=CCALL(K,1)
 140     CONTINUE
C
         IF(KOUNT.NE.0)THEN
            WRITE(KFILDO,142)KOUNT,NAME,(CCALLM(K),K=1,KOUNT)
 142        FORMAT(/' ****',I7,' STATIONS NOT FOUND ON FILE ',A60,/,
     1            ('     ',10(A8,1X)))
         ENDIF
C
      ENDIF
C
C     CALL TIMPR(KFILDO,KFILDO,'END FINDST          ')
C
      RETURN
      END    
