      SUBROUTINE L1D1(KFILDO,KFIL10,NFIRST,
     1                ID,IDPARS,THRESH,JD,NDATE,
     2                KFILRA,RACESS,NUMRA,
     3                ICALL,CCALL,ICALLD,CCALLD,NAME,
     4                NELEV,STALAT,STALON,
     5                ITIMEZ,ISDATA,SDATA,SDATA1,L1DATA,DIR,ND1,NSTA,
     6                NGRIDC,NGRID,ND11,NSLAB,IPACK,IWORK,DATA,ND5,
     7                LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8                NBLOCK,LASTD,NSTORE,NFETCH,
     9                IS0,IS1,IS2,IS4,ND7,
     A                FD1,FD2,FD3,FD4,FD5,FD6,FD7,
     B                FDVERT,FDTIME,FDSINS,FDMS,ND2X3,IP12,IP16,
     C                ICCFFF,IXX,IYY,ICC,
     D                MD,MDPARS,LD,
     E                THRES,PROB,IDEF,NDEF,NTHRES,NREG,L1DCMB,
     F                ISTAV,L3264B,L3264W,MISTOT,IER)
C
C        JUNE      1995   GLAHN   TDL   MOS-2000
C        AUGUST    1996   GLAHN   ADDED MISSS AND MISTOT
C        SEPTEMBER 1996   GLAHN   ADDED 2-DIGIT CYCLE TIME, NDEF IN CALL
C        OCTOBER   1996   GLAHN   MADE MORE GENERIC, ALONG WITH L1D50CFFF 
C        NOVEMBER  1996   GLAHN   REARRANGED TO ALLOW MORE USE OF L1DATA 
C        NOVEMBER  1996   GLAHN   ADDED KFILRA, RACESS, NUMRA, ICALLD
C        JUNE      1997   GLAHN   SLIGHT CHANGE TO COMMENTS, NOT CODE
C                                 D COMPILE OPTION COMMENTED OUT
C        JANUARY   1998   GLAHN   ADDED SUBSTITUTE STATIONS IN CCALL( , );
C                                 MODIFIED DIAGNOSTICS
C        MAY       1998   GLAHN   ADDED ITIMEZ( ) 
C        NOVEMBER  1998   GLAHN   CHANGED DIMENSIONS OF CCALLD( )
C                                 AND ICALLD( , ) FROM ND1 TO ND5
C        AUGUST    1999   GLAHN   ADDED IP12 TO CALL, CALL TO L12D2
C        APRIL     2003   GLAHN   COMMAS IN FORMATS; SPELL CHECK
C        MAY       2003   GLAHN   REARRANGED DIMENSION STATEMENTS
C        JUNE      2003   GLAHN   ADDED NAME AND NGRID TO CALL AND TO
C                                 CALLS TO L12D2
C 
C        PURPOSE 
C            TO SUPPORT L1D BY READING TRANSFORMATION DATA, COMPUTING,
C            WHEN IT DOESN'T ALREADY EXIST, THE INDEX RECORD AND STORING
C            IT IN THE MOS-2000 STORAGE SYSTEM; CALLING L12D2 TO GET THE
C            VARIABLE TO LINEARIZE; AND CALLING L1D3 TO DO THE 
C            LINEARIZATION.  THESE 3 ROUTINES, L1D1, L12D2, AND L1D3,
C            SHOULD NOT HAVE TO BE CHANGED WHEN ADDING A NEW VARIABLE
C            TO LINEARIZE, UNLESS THE LINEARIZATION ALGORITHM (NOT JUST
C            THE ACTUAL THRESHOLD AND PROBABILITY VALUES) IS CHANGED.
C            BY DEFINING THE VARIABLE TO LINEARIZE IN THE CALLING ROUTINE
C            ALLOWS L1D1 TO BE GENERIC TO ALL 1-D LINEARIZATIONS.
C
C        DATA SET USE 
C            KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C            KFILT  - UNIT NUMBER FOR READING THRESHOLDS, ETC.
C                     THIS IS SET TO 97.  FILE SHOULD BE CLOSED ON EXIT.
C                     (INPUT)  
C
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C              NFIRST = 1 FOR THE 1ST DATE.
C               ID(J) = THE ORIGINAL PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE ORIGINAL
C                       PREDICTOR ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C              THRESH = THE BINARY THRESHOLD ASSOCIATED WITH IDPARS( ).
C                       (INPUT)
C               JD(J) = THE BASIC INTEGER ORIGINAL PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C           KFILRA(J) = HOLDS THE UNIT NUMBERS FOR ACCESSING THE MOS-2000
C                       EXTERNAL RANDOM ACCESS FILES (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES CORRESPONDING TO KFILRA(J) (J=1,NUMRA).
C                       (CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF UNIT NUMBERS AND NAMES IN KFILRA( )
C                       AND RACESS( ).  (INPUT)
C        ICALL(L,K,J) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,NSTA) (J=1,6).
C                       EQUIVALENCED TO CCALL( , )  (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ).  EQUIVALENCED TO ICALL( , , ). 
C                       (CHARACTER*8)  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).
C                       EQUIVALENCED TO CCALLD( ).  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS LIST IS USED 
C                       IN L1D1 TO READ THE REGION LISTS.  (INTERNAL)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C            NELEV(K) = ELEVATION OF STATIONS (K=1,NSTA).  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           ITIMEZ(K) = TIME ZONE INDICATOR.  THE NUMBER OF HOURS
C                       THE STATION IS DIFFERENT FROM UTC (K=1,NSTA).
C                       (INPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C            SDATA(K) = LINEARIZED VARIABLE TO RETURN (K=1,NSTA).  (OUTPUT)
C           SDATA1(K) = WORK ARRAY RESERVED FOR USE IN L2D2 (K=1,NSTA).
C                       (INTERNAL)
C           L1DATA(K) = THE ARRAY RESERVED FOR USE BY LINEARIZATION AND
C                       CONSTANT ROUTINES (K=1,NSTA).  THE VALUES ARE THE
C                       REGION NUMBERS L THAT CORRESPOND TO THRES( ,L) AND
C                       PROB( ,L) (I.E., A REGION DIRECTORY).  (INTERNAL)  
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN NGRIDC( ,M).
C                       (INPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                       (INPUT)
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN DIR( , , ),
C                       MAXIMUM OF ND11.  (INPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ) AND DIR( , , ).  (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , , ) AND
C                       IN NGRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID.  SEE LSTORE(10, ).  FOR THE
C                       COMPUTATION ROUTINES RETURNING A GRID, THIS
C                       VALUE MUST BE OUTPUT BY GFETCH.  (INTERNAL) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ).
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C               LASTL = THE LAST LOCATION IN CORE( ) USED FOR MOS-2000 INTERNAL
C                       STORAGE.  INITIALIZED TO 0 ON FIRST ENTRY TO GSTORE.
C                       ALSO INITIALIZED IN U201 IN CASE GSTORE IS NOT ENTERED.
C                       (INPUT-OUTPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR MOS-2000
C                       INTERNAL STORAGE.  (INPUT)
C              NSTORE = THE NUMBER OF TIMES GSTORE HAS BEEN ENTERED.  GSTORE
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.  GFETCH
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C  FD1(J),FD2(J), ETC = WORK ARRAYS (J=1,ND2X3).  THESE MAY BE USED IN
C                       ROUTINES AS 2-DIMENSIONAL ARRAYS, WHERE THE
C                       TOTAL ARRAY SIZE IS ND2*ND3=ND2X3 AS DECLARED IN
C                       THE CALLING PROGRAM.  (INTERNAL)
C           FDVERT(J) = TEMPORARY STORAGE RESERVED FOR SUBROUTINE VERTP
C                       (J=1,ND2X3).
C           FDTIME(J) = TEMPORARY STORAGE RESERVED FOR SUBROUTINE TEMEP
C                       (J=1,ND2X3).
C       FDSINS(IX,JY) = USED TO SAVE THE SIN OF THE LATITUDE IN SUBROUTINE
C                       PSMAPF (IX=1,NX) (JY=1,NY).  THE USER MUST NOT
C                       USE THIS ARRAY EXCEPT IN CALLING PSMAPF.
C                       (INPUT/OUTPUT)
C         FDMS(IX,JY) = USED TO SAVE THE MAP FACTOR IN SUBROUTINE
C                       PSMAPF (IX=1,NX) (JY=1,NY).  THE USER MUST NOT
C                       USE THIS ARRAY EXCEPT IN CALLING PSMAPF.
C                       (INPUT/OUTPUT)
C               ND2X3 = DIMENSION OF FD1( ), FD2( ), ETC.  (INPUT)
C                IP12 = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE INPUT VECTOR FILES WILL BE WRITTEN 
C                       TO UNIT IP12.  (INPUT)
C                IP16 = INDICATES WHETHER (>0) OR NOT (=0) CERTAIN DIAGNOSTICS
C                       WILL BE WRITTEN TO UNIT IP16 FOR LINEARIZATION
C                       ROUTINES (E.G., STATIONS IN THRESHOLD LISTS THAT
C                       ARE NOT BEING DEALT WITH IN THIS RUN).
C              ICCFFF = CCCFFF OF THE VARIABLE TO LINEARIZE.  (INPUT)
C                 IXX = THE SEASON NUMBER.  (INPUT)
C                 IYY = THE PROJECTION NUMBER.  (INPUT)
C                 ICC = THE CYCLE TIME.  (INPUT)
C               MD(J) = THE ID OF THE PREDICTOR TO LINEARIZE (J=1,4).  (INPUT)
C           MDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO MD( ) (J=1,15).
C                       THIS IS THE VARIABLE TO LINEARIZE, THE 
C                       DEFINITION OF THE ELEMENTS BEING THE SAME AS
C                       FOR IDPARS( ).  (INPUT)
C               LD(J) = THE BASIC ID (J=1,4) OF THE PREDICTOR TO LINEARIZE.
C                       THIS IS THE SAME AS MD(J), EXCEPT THAT THE PORTIONS 
C                       PERTAINING TO PROCESSING ARE OMITTED (REFER TO
C                       JD( ) ABOVE.  (INPUT)
C          THRES(J,L) = THRESHOLDS (J=1,NOCAT) (L=1,NREG).  (INTERNAL)
C           PROB(J,L) = PROBABILITIES (RELATIVE FREQUENCIES) ASSOCIATED WITH
C                       THE CATEGORY DEFINED BY GE THRES(J-1) AND LT THRES(J),
C                       WHERE THRES(0) IS ASSUMED TO BE LESS THAN ANY
C                       POSSIBLE VALUE (J=1,NOCAT) (L=1,NREG).  (INTERNAL)
C           IDEF(J,L) = THE DEFINITION OF THE THRESHOLD/PROBABILITY
C                       COMBINATIONS FOR THIS CCCFFF (J=1,4) (L=1,L1DCMB).
C                       THE DEFINITION OF THE 4 VALUES OF J:
C                       J = 1--CCC,
C                       J = 2--FFF,
C                       J = 3--DATA SOURCE DD,
C                       J = 4--SEASON NUMBER IXX,
C                       J = 5--PROJECTION NUMBER IYY,
C                       J = 6--CYCLE TIME ICC,
C                       J = 7--LOWER LEVEL IN IDPARS(6), AND
C                       J = 8--UPPER LEVEL IN IDPARS(7).
C                       THESE 8 VALUES, ARE IN THE THRESHOLD FILE NAME
C                       AND ALSO FOR SAFETY WITHIN THE FIRST RECORD OF
C                       THE FILE.  NOTE THAT THIS ARRAY IS SPECIFIC
C                       TO THIS ROUTINE, BUT THE ARRAY IS FILLED BY L1D1.
C                       (INPUT/OUTPUT)
C                NDEF = THE NUMBER OF THE THRESHOLD/PROBABILITY COMBINATION
C                       TO BE USED NEXT.  WHEN THIS NUMBER OF COMBINATIONS
C                       IS ABOUT TO BE STORED, IT IS CHECKED FOR ITS MAX
C                       VALUE OF L1DCMB.  (INPUT/OUTPUT)
C              NTHRES = NUMBER OF THRESHOLDS THAT CAN BE DEALT WITH IN THIS
C                       ROUTINE.  (INPUT)
C                NREG = NUMBER OF REGIONS THAT CAN BE DEALT WITH IN THIS
C                       ROUTINE.  (INPUT)
C              L1DCMB = THE NUMBER OF LINEARIZATION THRESHOLDS AND
C                       PROBABILITY COMBINATIONS THAT CAN BE ACCOMMODATED
C                       FOR THIS PARTICULAR CCCFFF.  THE COMBINATIONS
C                       CAN VARY WITH SEASON NUMBER (IXX), PROJECTION 
C                       NUMBER (IYY), CYCLE TIME (ICC), DATA SOURCE (DD)
C                       AND UPPER AND LOWER LEVELS.  (INPUT)
C               ISTAV = 1 WHEN THE DATA RETURNED ARE STATION DATA.  
C                       0 WHEN THE DATA ARE NOT AVAILABLE FOR RETURN.
C                         (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING USED
C                       (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.  (INPUT)
C              MISTOT = TOTAL NUMBER OF TIMES A MISSING INDICATOR
C                       HAS BEEN ENCOUNTERED IN UNPACKING GRIDS.
C                       (INPUT-OUTPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                        47 = DATA CANNOT BE FOUND.
C                        48 = DIMENSION ND5 IS NOT LARGE ENOUGH FOR SDATA( )
C                             TO HOLD THE DATA.
C                        49 = NUMBER OF WORDS RETURNED FROM UNPACK NOT WHAT'S
C                             EXPECTED.
C                        99 = COULD NOT FIND PREDICTOR DEFINED IN L1D.
C                       111 = ID IN THE FILE DOES NOT MATCH CCCFFF.
C                       112 = TOO MANY THRESHOLDS FOR PARAMETER NTHRES.
C                       113 = TOO MANY REGIONS IN THE FILE FOR THE VALUE
C                             SET FOR NREG.
C                       114 = TOO MANY THRESHOLD COMBINATIONS; NO MORE ROOM 
C                             IN IDEF( , ).
C                       115 = UNEXPECTED ERROR.
C                       118 = THE NUMBER OF STATIONS AVAILABLE ON THE 
C                             THRESHOLD FILE IS LESS THAN NSTA.
C                       900- = IOSTAT RETURNS FROM SYSTEM ON READING DISK.
C               KFILT = UNIT NUMBER FOR READING THRESHOLDS, ETC.  (INTERNAL)
C               NPACK = 1 FOR UNPACKED DATA.  THIS IS STORED IN LSTORE(7, ).
C                       (INTERNAL)
C               KD(J) = THE 4-WORD ID OF THE INDEX VARIABLE TO STORE
C                       (J=1,4).  IT IS COMPOSED OF:
C                         KD(1) = CCCFFF
C                         KD(2) = DDXXYYCC
C                         KD(3) = IDPARS(6)
C                         KD(4) = IDPARS(7)
C                       (INTERNAL)
C              LTFILE = FILE NAME FOR THRESHOLDS AND ASSOCIATED PROBABILITIES.
C                       FORMAT IS 'L1DQQQQQQDDXXYYCCBBBBTTTT.CN' WHERE
C                         QQQQQQ = COMPOSED OF THE CCCFFF OF THE SPECIFIC PREDICTOR,
C                             DD = THE DATA SOURCE, 
C                             XX = THE SEASON NUMBER,
C                             YY = THE PROJECTION NUMBER,
C                             CC = THE CYCLE NUMBER,
C                           BBBB = BOTTOM LEVEL, AND
C                           TTTT = TOP LEVEL.
C                       (CHARACTER*28)  (INTERNAL)
C              NWORDS = THE NUMBER OF WORDS IN THE DATA ARRAY RETURNED FROM 
C                       GFETCH.  NOT USED.  (INTERNAL)
C              NTIMES = THE NUMBER OF TIMES GFETCH HAS BEEN ACCESSED.
C                       NOT USED.  (INTERNAL)
C               NOCAT = THE NUMBER OF CATEGORIES ACTUALLY BEING DEALT WITH
C                       IN THIS ROUTINE.  MUST BE LE NTHRES.  MUST BE SAVED
C                       FOR REUSE UPON REENTRY.  (INTERNAL)
C              NRRDAT = A DATE FAR INTO THE FUTURE SO THAT THE INDEX RECORD
C                       STORED WILL NOT BE DISCARDED.  (INTERNAL)
C               NTRDC = THE NUMBER OF VALUES TO READ PER RECORD WITH
C                       SUBROUTINE RDC.  IT MUST MATCH THE FORMAT IN THE CALL.
C                       (INTERNAL)
C              IFOUND = SET TO 0 UNTIL A STATION READ IS FOUND IN THE STATION
C                       LIST, THEN IT IS SET TO 1 TO ASSIST WITH DIAGNOSTICS.
C                       CAREFUL--UNDER /D CONTROL.  (INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (INTERNAL)
C                         
C        NON SYSTEM SUBROUTINES CALLED
C            GFETCH, GSTORE, RDC, L12D2, L1D3 
C
      PARAMETER (NTRDC=7)
      PARAMETER (NTTEMP=50)
C
      CHARACTER*8 CCALL(ND1,6),
     1            CCALLD(ND5),
     2            CTEMP(NTRDC)
      CHARACTER*20 NAME(ND1)
      CHARACTER*28 LTFILE
      CHARACTER*60 RACESS(NUMRA)
C
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION ICALL(L3264W,ND1,6),
     1          NELEV(ND1),STALAT(ND1),STALON(ND1),ITIMEZ(ND1),
     2          ISDATA(ND1),SDATA(ND1),SDATA1(ND1),L1DATA(ND1)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5),ICALLD(L3264W,ND5)
      DIMENSION FD1(ND2X3),FD2(ND2X3),FD3(ND2X3),FD4(ND2X3),
     1          FD5(ND2X3),FD6(ND2X3),FD7(ND2X3),
     2          FDVERT(ND2X3),FDTIME(ND2X3),FDSINS(ND2X3),FDMS(ND2X3)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION DIR(ND1,2,ND11),NGRIDC(6,ND11)
      DIMENSION KFILRA(NUMRA)
      DIMENSION THRES(NTHRES,NREG,L1DCMB),PROB(NTHRES,NREG,L1DCMB)
      DIMENSION IDEF(8,L1DCMB)
      DIMENSION TEMPTH(NTTEMP),TEMPPR(NTTEMP)
      DIMENSION MD(4),MDPARS(15),LD(4),KD(4)
C
      DATA NCOMBO/9999/
      DATA NRRDAT/2100010100/
      DATA NOCAT/1/
C        NOCAT IS PUT INTO DATA STATEMENT SO THAT IT WILL BE SAVED.
C
      IER=0
C
C        GET THE VARIABLE TO LINEARIZE.  MD( ) AND LD( )
C        HOLD THE RELEVANT INFO AND INCORPORATE ICCFFF.
C        BY OBTAINING THE VARIABLE HERE, L1DATA( ) USED
C        IN L1D1 FOR HOLDING THE REGION DIRECTORY CAN 
C        BE USED IF NECESSARY BY ROUTINES CALLED BY L12D2.
C
      CALL L12D2(KFILDO,KFIL10,NFIRST,
     1           MD,MDPARS,THRESH,LD,NDATE,
     2           KFILRA,RACESS,NUMRA,
     3           ICALL,CCALL,ICALLD,CCALLD,NAME,
     4           NELEV,STALAT,STALON,
     5           ITIMEZ,ISDATA,SDATA,SDATA1,L1DATA,DIR,ND1,NSTA,
     6           NGRIDC,NGRID,ND11,NSLAB,IPACK,IWORK,DATA,ND5,
     7           LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8           NBLOCK,LASTD,NSTORE,NFETCH,
     9           IS0,IS1,IS2,IS4,ND7,
     A           FD1,FD2,FD3,FD4,FD5,FD6,FD7,
     B           FDVERT,FDTIME,FDSINS,FDMS,ND2X3,IP12,IP16,
     C           ISTAV,L3264B,L3264W,MISTOT,IER)
      IF(IER.NE.0)GO TO 300
C        FOR IER NE 0, THE DATA ARRAY SDATA( ) WILL BE SET TO
C        MISSING AND ISTOP INCREMENTED IN PRED1 OR PRED2.
C
C        RETRIEVE THE THRESHOLD RECORD IF IT IS AVAILABLE.
C
      KD(1)=IDPARS(1)*1000+IDPARS(2)
      KD(2)=IDPARS(4)*1000000+IXX*10000+IYY*100+ICC
      KD(3)=IDPARS(6)
      KD(4)=IDPARS(7)
      CALL GFETCH(KFILDO,KFIL10,KD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,ISDATA,L1DATA,ND1,
     2            NWORDS,NPACK,0,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
C        FOR UNPACKED DATA, WORK ARRAYS ARE NOT USED.
C        THE INDEX VARIABLE IS TO BE IN L1DATA( ).
C        THE RETURNED VALUES NTIMES, MISSS, AND NWORDS ARE NOT
C        NEEDED OR USED.  IT IS ASSUMED NWORDS = NSTA, SINCE THIS
C        ROUTINE STORED THE DATA.  THE DATE OF SUCH RECORDS IS 0.
C        NOTE THAT ND1 IS DIMENSION OF IPACK( ), ETC., NOT ND5,
C        BECAUSE DIMENSION OF L1DATA( ) IS ND1.  THIS SHOULD BE
C        SUFFICIENT, AND ND5 MUST BE GE ND1.
      IF(MISSP.NE.0)MISTOT=MISTOT+1
      IF(IER.EQ.0)GO TO 260
      IF(IER.NE.47)GO TO 300
C        IF IER NE 0 NOR 47, A FATAL ERROR HAS OCCURRED FOR THIS
C        VARIABLE.  THE DATA ARRAY SDATA( ) WILL BE SET TO MISSING
C        AND ISTOP INCREMENTED IN PRED1 OR PRED2.

C        INDEX RECORD NOT AVAILABLE, BECAUSE IER EQ 47.
C        COMPUTE FILE NAME FROM WHICH TO READ THRESHOLDS, ETC.
C
      KFILT=97
C        THE UNIT 97 IS USED FOR ALL ROUTINES OF THIS SORT.  THE
C        FILE IS CLOSED ON EXIT.
      WRITE(LTFILE,129,IOSTAT=IOS,ERR=131)
     1      IDPARS(1),IDPARS(2),IDPARS(4),IXX,IYY,ICC,IDPARS(6),
     2      IDPARS(7)
 129  FORMAT('L1D',2I3.3,3I2.2,I2.2,2I4.4,'.CN')    
      GO TO 135
C
 131  WRITE(KFILDO,132)IDPARS(1),IDPARS(2)
 132  FORMAT(/' ****TROUBLE COMPOSING FILE NAME FOR VARIABLE ',2I3.3,
     1        ' IN L1D1 AT 130.')
      IER=IOS
      GO TO 300
C
 135  OPEN(UNIT=KFILT,FILE=LTFILE,FORM='FORMATTED',STATUS='OLD',
     1     IOSTAT=IOS,ERR=136)
      IF(IOS.EQ.0)GO TO 140
C
 136  WRITE(KFILDO,137)LTFILE,IOS
 137  FORMAT(/' ****TROUBLE OPENING FILE ',A28,' IN L1D1 AT 135,',
     1        ' IOS =',I5)
      CLOSE(UNIT=KFILT)
      IER=IOS
      GO TO 300
C
C        VERIFY THAT THIS IS THE CORRECT FILE.
C
 140  READ(KFILT,141,IOSTAT=IOS,ERR=142,END=142)IDCCC,IDFFF,
     1     IDD,IDX,IDY,IDC,IDB,IDT
 141  FORMAT(2I3,6I5)
      GO TO 144
C
 142  WRITE(KFILDO,143)LTFILE,IOS
 143  FORMAT(/' ****TROUBLE READING FILE ',A28,' IN L1D1 AT 142,',
     1        ' IOS =',I5)
      CLOSE(UNIT=KFILT)
      IER=IOS
      GO TO 300
C
 144  IF(IDCCC.EQ.IDPARS(1).AND.
     1   IDFFF.EQ.IDPARS(2).AND.
     2   IDD  .EQ.IDPARS(4).AND.
     3   IDX  .EQ.IXX.AND.
     4   IDY  .EQ.IYY.AND.
     5   IDC  .EQ.ICC.AND.
     6   IDB  .EQ.IDPARS(6).AND.
     7   IDT  .EQ.IDPARS(7))GO TO 150
      WRITE(KFILDO,145)LTFILE,IDCCC,IDFFF,IDD,IDX,IDY,IDC,IDB,IDT,
     1                 IDPARS(1),IDPARS(2),IDPARS(4),IXX,IYY,ICC,
     2                 IDPARS(6),IDPARS(7)
 145  FORMAT(/' ****ID IN THE FILE ',A28,' = ',2I3.3,6I5/
     1        '     DOES NOT MATCH FILE NAME INFO =       ',
     2              2I3.3,6I5,' IN L1D1 AT 145.')
      CLOSE(UNIT=KFILT)
      IER=111
      GO TO 300
C
C        READ THE NUMBER OF THRESHOLDS.  THIS MUST BE CONSTANT
C        FOR ALL REGIONS.
C
 150  READ(KFILT,151,IOSTAT=IOS,ERR=152,END=152)NOCAT
 151  FORMAT(I4)
      GO TO 160
C
 152  WRITE(KFILDO,153)LTFILE,IOS
 153  FORMAT(/' ****ERROR OR END OF FILE READING FILE ',A28,
     1        ' IN L1D1 AT 150, IOS =',I5)
      IER=IOS
      GO TO 300
C
C        MAKE SURE NTHRES IS LARGE ENOUGH.
C
 160  IF(NTHRES.GE.NOCAT)GO TO 165
      WRITE(KFILDO,161)NOCAT,LTFILE
 161  FORMAT(/' ****TOO MANY THRESHOLDS =',I4,' INDICATED IN FILE ',A28,
     1        ' IN L1D1 AT 160.')
      CLOSE(UNIT=KFILT)
      IER=112
      GO TO 300
C
C        PUT LARGE VALUE INTO TEMPTH(NOCAT).  THIS VALUE IS NOT
C        ACTUALLY USED IN THE CURRENT ALGORITHM.
C
 165  TEMPTH(NOCAT)=99999999.
C        THIS VALUE PRINTS AS 100000000 ON 32-BIT HP MACHINE.
C
C        MAKE SURE THERE IS ROOM FOR ANOTHER SET OF THRESHOLDS.
C
      IF(NDEF.LE.L1DCMB)GO TO 168
C        THE DIMENSION IS ABOUT TO BE EXCEEDED FOR IDEF( , ).
      WRITE(KFILDO,166)IDPARS(1),IDPARS(2),
     1                 ((IDEF(J,L),J=1,8),L=1,L1DCMB)
 166  FORMAT(/' ****NO MORE ROOM IN IDEF( ,NDEF)',
     1        ' IN L1D1 AT 166 FOR VARIABLE ICCFFF = ',2I3.3,
     2        ' IDEF ='/('    ',8I5))
      IER=114
      GO TO 300
C
C        READ THRESHOLDS AND ASSOCIATED PROBABILITIES.
C
 168  KOUNTS=0
C        KOUNTS COUNTS THE STATIONS FOR WHICH REGIONAL INDICES HAVE BEEN
C        PUT INTO L1DATA( ).
      KOUNTR=1
C        KOUNTR COUNTS THE REGIONS FOR WHICH THRESHOLDS HAVE BEEN READ. 
C        THE ARRAY L1DATA( ) MUST BE ZEROED.
C
      DO 169 J=1,NSTA
      L1DATA(J)=0
 169  CONTINUE     
C      
 170  READ(KFILT,171,IOSTAT=IOS,ERR=172,END=174)
     1    (TEMPTH(J),J=1,NOCAT-1)
      READ(KFILT,171,IOSTAT=IOS,ERR=172,END=174)
     1    (TEMPPR(J),J=1,NOCAT)
 171  FORMAT(28F8.0)
C        READING INTO TEMPORARY ARRAY SO THAT THE NREG WON'T HAVE TO
C        BE GREATER THAN THE NUMBER OF REGIONS.  WHILE ONLY NOCAT-1
C        THRESHOLDS ARE NEEDED, NOCAT PROBABILITIES ARE NEEDED. 
C***D     WRITE(KFILDO,1710)(TEMPTH(J),TEMPPR(J),J=1,NOCAT)
C***D1710 FORMAT(/' THRESHOLDS READ'/(' ',F14.2,F10.2))
      GO TO 190
C
 172  WRITE(KFILDO,173)LTFILE,IOS
 173  FORMAT(/' ****ERROR READING FILE ',A28,' IN L1D1 AT 170,',
     1        ' IOS =',I5)
      CLOSE(UNIT=KFILT)
      IER=IOS
      GO TO 300
C
 174  WRITE(KFILDO,175)LTFILE,IOS
 175  FORMAT(/' ****END OF FILE WITHOUT TERMINATOR ON FILE ',A28,
     1        ' IN L1D1 AT 170, IOS =',I5,'.  NOT NECESSARILY ',
     2        'AN ERROR.')
C        THIS CONDITION DOES NOT NECESSARILY CAUSE AN ERROR AND
C        IS NOT DIAGNOSED AS SUCH.
      CLOSE(UNIT=KFILT)
      GO TO 250
C
 190  IF(TEMPTH(1).EQ.9999.)GO TO 250
C
C        ANOTHER SET OF THRESHOLDS AND PROBABILITIES HAS
C        BEEN READ.  IS THERE ROOM IN PERMANENT STORAGE?
C
      IF(KOUNTR.LE.NREG)GO TO 194
      WRITE(KFILDO,192)LTFILE,NREG
 192  FORMAT(/' ****TOO MANY REGIONS IN FILE ',A28,
     1        ' ONLY',I3,' ALLOWED.  IN L1D1 AT 192.')
      IER=113
      GO TO 300
C
 194  DO 195 J=1,NOCAT
      THRES(J,KOUNTR,NDEF)=TEMPTH(J)
      PROB(J,KOUNTR,NDEF)=TEMPPR(J)
 195  CONTINUE
C
C        NOW READ THE ASSOCIATED STATION LIST.
C 
      CALL RDC(KFILDO,IP16,KFILT,CCALLD,ND5,CTEMP,NTRDC,'(7(A8,1X))',
     1         JSTA,'99999999',IER)
C        NTRDC = 7 WITH FMT = '(7(A8,1X))' MEANS THAT UP TO 7 VALUES WILL
C        BE READ PER RECORD.  FEWER CAN BE PRESENT.  IF THE LAST
C        VALUE IS NOT COMPLETE, IT WILL BE BLANK FILLED ON THE RIGHT.
C        THAT IS, 'OKC     ' COULD BE 'OKC' OR 'OKC '.
C
C***D     IF(JSTA.NE.0)THEN
C***D        IF(IP16.NE.0)WRITE(IP16,199)KOUNTR,(CCALLD(J),J=1,JSTA)
C***D199     FORMAT(/' STATIONS READ FOR REGION NO.',I4/(' ',10A8,1X))
C***D     ENDIF
C
      IF(IER.EQ.0)GO TO 200
C
      CLOSE(UNIT=KFILT)
C        IF IER NE 0, A DIAGNOSTIC WILL HAVE BEEN PRINTED BY RDC.
      GO TO 300
C
 200  IF(JSTA.NE.0)GO TO 202
      WRITE(KFILDO,201)KOUNTR,LTFILE
 201  FORMAT(/' ****NO STATIONS IN LIST FOR REGION ',I3,' ON FILE ',
     1          A28,'.  IN L1D1 AT 201.')
C        NO NEED TO UPDATE KOUNTR.
      GO TO 170
C
C        PUT THE STATIONS IN THE LIST INTO A KEY RECORD ACCORDING
C        TO THE ORDER OF STATIONS IN CCALL( , ).
C
 202  DO 210 J=1,JSTA
C
C***D      IFOUND=0
C
      DO 208 N=1,NSTA
C****D     WRITE(KFILDO,2020)J,CCALLD(J),N,CCALL(N,1),L1DATA(N)
C****D2020 FORMAT(/' J, CCALLD(J), N, CCALL(N,1), L1DATA(N)',
C****D    1        I4,1X,A8,I4,1X,A8,I6)
      IF(CCALLD(J).NE.CCALL(N,1).AND.
     1   CCALLD(J).NE.CCALL(N,2).AND.
     2   CCALLD(J).NE.CCALL(N,3).AND.
     3   CCALLD(J).NE.CCALL(N,4).AND.
     4   CCALLD(J).NE.CCALL(N,5).AND.
     5   CCALLD(J).NE.CCALL(N,6))GO TO 208
C        MATCH HAS BEEN FOUND.
      IF(L1DATA(N).EQ.0)GO TO 204
      WRITE(KFILDO,203)CCALLD(J),LTFILE
 203  FORMAT(/' ****DUPLICATE STATION ',A8,' FOUND IN FILE ',A28,
     1        ' IN L1D1 AT 202.  LOCATION IN PREVIOUS REGION USED.')
      GO TO 210
C
 204  L1DATA(N)=KOUNTR 
      KOUNTS=KOUNTS+1
C***D      IFOUND=1
C        DUPLICATE STATIONS ARE ALLOWED IN THE CCALL( , ) LIST.
C
 208  CONTINUE
C
C***D      IF(IFOUND.EQ.1)GO TO 210
C***D      IF(IP16.NE.0)WRITE(IP16,209)CCALLD(J),LTFILE
C***D 209  FORMAT(/' ****STATION',1X,A8,' IN FILE ',A28,' NOT FOUND',
C***D     1        ' IN STATION LIST IN L1D1 AT 209.',
C***D     2        '  THIS MAY NOT BE AN ERROR.')
 210  CONTINUE
C
      KOUNTR=KOUNTR+1
      GO TO 170
C
C        ALL THRESHOLDS, ETC. HAVE BEEN READ AND PROCESSED.
C 
 250  CLOSE(UNIT=KFILT)
      WRITE(KFILDO,251)LTFILE,KOUNTS,NSTA,(LD(J),J=1,4)
 251  FORMAT(/' THRESHOLDS HAVE BEEN READ ON FILE ',A28,
     1        ' FOR',I4,' OF THE',I4,' STATIONS',
     2        ' FOR VARIABLE '/
     3        '     ',I9.9,1X,I9.9,1X,I9.9,1X,I3.3,'.')
      IF(KOUNTS.EQ.NSTA)GO TO 255
C        UNLESS ALL NSTA STATIONS HAVE BEEN FOUND, A DIAGNOSTIC
C        IS WRITTEN.  HOWEVER, IER NOT SET NE 0 ONLY IF NO 
C        STATION IS FOUND.
      NOUNT=1
C
      DO 252 N=1,NSTA
      IF(L1DATA(N).NE.0)GO TO 252
      CCALLD(NOUNT)=CCALL(N,1)
      NOUNT=NOUNT+1
 252  CONTINUE
C
      WRITE(KFILDO,2525)(CCALLD(N),N=1,NSTA-KOUNTS)
 2525 FORMAT(' THRESHOLDS NOT FOUND FOR THE FOLLOWING STATIONS:'/
     1      ('    ',10(1X,A8)))
C
      IF(KOUNTS.EQ.0)THEN
         IER=118 
      ENDIF
C
C        STORE INTO INTERNAL STORAGE SYSTEM.  NCOMBO IS NOT
C        MEANINGFUL, BUT IS SET = 1.
C
 255  NCOMBO=1
      CALL GSTORE(KFILDO,KFIL10,KD,NCOMBO,LSTORE,ND9,LITEMS,
     1            L1DATA,NSTA,1,NRRDAT,0,
     2            CORE,ND10,LASTL,NBLOCK,LASTD,NSTORE,L3264B,IER)
C        NOTE THAT L1DATA( ) IS INTEGER.  ALTHOUGH THE CORRESPONDING
C        VARIABLE IN GSTORE IS REAL, THIS IS OK.
C
C        STORE THE DEFINITION OF THE THRESHOLDS AND PROBABILITIES.
C
      IDEF(1,NDEF)=ICCFFF/1000
      IDEF(2,NDEF)=MOD(ICCFFF,1000)
      IDEF(3,NDEF)=IDD
      IDEF(4,NDEF)=IXX
      IDEF(5,NDEF)=IYY
      IDEF(6,NDEF)=ICC
      IDEF(7,NDEF)=IDPARS(6)
      IDEF(8,NDEF)=IDPARS(7)
      JCOM=NDEF
      NDEF=NDEF+1
      GO TO 280
C
C        FIND THE COMBINATION OF THRESHOLDS NEEDED.
C
 260  DO 270 JCOM=1,L1DCMB
      IF(IDEF(1,JCOM).EQ.ICCFFF/1000.AND.
     1   IDEF(2,JCOM).EQ.MOD(ICCFFF,1000).AND.
     2   IDEF(3,JCOM).EQ.IDPARS(4).AND.
     3   IDEF(4,JCOM).EQ.IXX.AND.
     4   IDEF(5,JCOM).EQ.IYY.AND.
     5   IDEF(6,JCOM).EQ.ICC.AND.
     6   IDEF(7,JCOM).EQ.IDPARS(6).AND.
     7   IDEF(8,JCOM).EQ.IDPARS(7))GO TO 280
 270  CONTINUE
C
C        DROP THROUGH HERE MEANS THE COMBINATION HAS NOT BEEN
C        FOUND.  THIS IS AN ERROR THAT SHOULD NOT HAPPEN.
      WRITE(KFILDO,272)IDPARS(1),IDPARS(2)
 272  FORMAT(/' ****UNEXPECTED ERROR IN L1D1 FOR CCCFFF = ',2I3.3,
     1        ' AT 272.')
      IER=115
      GO TO 300
C
C        COMPUTE THE LINEARIZED PREDICTOR.
C
 280  CALL L1D3(KFILDO,L1DATA,SDATA,ND1,NSTA,
     1          THRES(1,1,JCOM),PROB(1,1,JCOM),NTHRES,NREG,NOCAT,IER)
C        FOR IER NE 0, THE DATA ARRAY SDATA( ) WILL BE SET TO
C        MISSING AND ISTOP INCREMENTED IN PRED1 OR PRED2.
 300  ISTAV=1
C        ISTAV = 1 INDICATES DATA ARE AT STATIONS.  WHEN
C        IER NE 0, IT WOULDN'T REALLY MATTER. 
      RETURN
      END
