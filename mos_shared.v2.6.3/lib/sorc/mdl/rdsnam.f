      SUBROUTINE RDSNAM(KFILDI,KFILDO,INUNIT,NAM,MODNUM,JFOPEN,ND6,
     1                  NUMIN,STATUX,FORMX,IP,IER)
C 
C        FEBRUARY  1995   GLAHN   TDL   MOS-2000 
C        OCTOBER   1996   GLAHN   ADDED CHECK FOR UNIT NOS. 10 AND 97
C        NOVEMBER  1996   GLAHN   ADDED "NOT" TO STATUX TO NOT OPEN FILE
C        FEBRUARY  1997   GLAHN   ADDED JFOPEN TO CALL
C        MARCH     1997   GLAHN   ADDED IP(23) PRINT CAPABILITY
C        FEBRUARY  1998   GLAHN   REMOVED DELETION OF AN EXISTING FILE
C        MAY       1998   GLAHN   CHANGED TEST ON STATUX(1:3) TO (1:7)
C                                 FOR 'SCRATCH' AFTER STATEMENT 124
C        SEPTEMBER 1998   GLAHN   CHECK FOR ND6 GE 1; NAM(1) = ' '
C        JANUARY   1999   GLAHN   IER CHANGED FROM 29 TO 31 TO AGREE WITH
C                                 COMMENTS AND WRITEUP
C        JULY      1999   GLAHN   INSERTED INQUIRE FOR OPERATIONS;
C                                 REMOVED TEST FOR UNITS 10 AND 97
C        JANUARY   2000   GLAHN   CHANGED COMMENT FOR IER
C        FEBRUARY  2000   GLAHN   CHANGED END FROM 112 TO 140 AT 110;
C                                 DOESN'T ACCEPT UNIT NUMBER = 0
C        APRIL     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO CONFORM
C                                      TO FORTRAN 90 STANDARDS ON THE IBM SP
C        APRIL 19  2000   MCE     MODIFIED FOR IBM OPERATIONS; REMOVED 
C                                 INQUIRE STATEMENT - DOES NOT WORK; ALSO
C                                 REMOVED FILENAME FROM OPEN STATEMENTS
C        JUNE      2005   RLC     WE LOOKED INTO UPDATING THIS TO THE 
C                                 DEVELOPMENT VERSION, BUT DECIDED THAT 
C                                 THIS ORIGINAL OPERATIONAL VERSION STILL
C                                 WORKS AND IS MORE STRAIGHT-FORWARD FOR
C                                 OUR OPERATIONAL NEEDS.
C        JULY      2012   ENGLE   MODIFIED FOR TRANSITION TO INTEL/LINUX;
C                                 ADDED CONVERTX; ADDED CHECK IF FILE
C                                 IS TO BE OPENED WITH 'UNFORMATTED'
C                                 AND 'OLD', THEN FIRST CALL CKFILEND
C                                 TO DETERMINE ENDIAN; MODIFIED OPEN
C                                 STATEMENT TO INCLUDE CONVERT=
C                                 SPECIFIER.
C        NOVEMBER  2012   ENGLE   FURTHER MODIFIED LOGIC REGARDING THE
C                                 OPENING OF FILES THAT ARE 'FORAMATTED'
C                                 VS. 'UNFORMATTED' AND HANDLING
C                                 ENDIANNESS.
C        DECEMBER  2012   ENGLE   ADDED CHECKS FOR IOSTAT VALUES 10 AND
C                                 29 WHEN OPENING FILES. RDSNAM WILL NOW
C                                 STOP AT 127 WHEN IOSTAT=10.
C
C        PURPOSE  
C            TO READ UNIT NUMBERS AND DATA SET NAMES FOR ONE OR 
C            MORE DATA SETS.  SEQUENTIAL UNIT NUMBERS
C            CAN BE THE SAME WHEN THE FILES ARE TO BE OPENED AND
C            USED SEQUENTIALLY.  IN THE CASE OF DUPLICATE UNIT
C            NUMBERS, AND THEY MUST BE SEQUENTIAL, ONLY THE FIRST
C            FILE IN THE SEIES IS OPENED, AND THEN ONLY AS 'NEW',
C            'OLD', OR 'SCRATCH' AS INDICATED BY STATUX AND AS 
C            'FORMATTED' OR 'UNFORMATTED' AS INDICATED BY FORMX.
C            WHEN A FILE IS OPENED AS 'OLD' AND AN ERROR OCCURS
C            INDICATING THE FILE IS NOT PRESENT, A TRY WILL BE MADE
C            TO OPEN IT AS A NEW FILE.  A FILE IS NOT OPENED WHEN
C            IT IS THE SAME AS THE DEFAULT INPUT, KFILDI.  IT APPEARS
C            THAT OPENING A FILE AS OLD WITH THE SAME UNIT NUMBER
C            AS ONE ALREADY OPEN DOES NOT CREATE AN ERROR.  IF THE
C            UNIT NUMBER READ EQUALS A VALUE IN IP( ), THE
C            OFFENDING IP( ) VALUE WILL BE SET TO KFILDO, AND 
C            PROCESSING CONTINUES WITH A DIAGNOSTIC AND AND 
C            ERROR STATUS OF 31.  AN INQUIRE STATEMENT IS USED
C            TO DETERMINE WHETHER OR NOT A UNIT NUMBER IS OPEN.
C            THIS IS FOR OPERATIONS ON THE IBM, WHEN THE FILES
C            ARE OPENED EXTERNAL TO THE FORTRAN PROGRAM, AND THE
C            FILE NAME MAY NOT MATCH THE ONE READ FROM THE .CN 
C            CONTROL FILE.
C
C        DATA SET USE 
C           KFILDI    - UNIT NUMBER FOR INPUT FILE.  (INPUT)
C           KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C           INUNIT( ) - UNIT NUMBERS FOR INPUT FILES, WHEN NON ZERO.
C                       (OPENED FOR INPUT) 
C
C        VARIABLES 
C              KFILDI = UNIT NUMBER FOR INPUT FILE.  (INPUT)
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C           INUNIT(J) = UNIT NUMBERS FOR DATA SETS (J=1,NUMIN).  (OUTPUT)
C              NAM(J) = DATA SET NAMES FOR THE UNIT NUMBERS IN INUNIT(J)
C                       (J=1,NUMIN).  (CHARACTER*(*))  (OUTPUT)
C           MODNUM(J) = "MODEL" NUMBER CORRESPONDING TO INUNIT(J), AND
C                       NAM(J) (J=1,NUMIN).  THIS MAY NOT BE MEANINGFUL
C                       FOR SOME USES.  (OUTPUT)
C           JFOPEN(J) = FOR EACH FILE CORRESPONDING TO INUNIT(J), 
C                       JFOPEN(J) IS SET TO 1 IF THE FILE IS OPEN OR
C                       AN OPEN HAS BEEN ATTEMPTED, AND SET TO 2 
C                       OTHERWISE (J=1,NUMIN).  (OUTPUT)
C                 ND6 = DIMENSION OF INUNIT( ), NAM( ), JFOPEN( ),
C                       AND MODNUM( ).  (INPUT)
C               NUMIN = THE NUMBER OF VALUES IN INUNIT( ), NAMES IN
C                       NAM( ), AND MODEL NUMBERS IN MODNUM( ),
C                       MAXIMUM OF ND6.  (OUTPUT)
C              STATUX = HOLDS THE CHARACTER STRING 'NEW', 'OLD', OR
C                       'SCRATCH' AND IS USED IN THE OPEN STATEMENT AS
C                       STATUS=STATUX.  CALLING PROGRAM CAN USE 'NOT'
C                       AS THIS VALUE WHEN THE FILE IS NOT TO BE 
C                       OPENED.  (CHARACTER*(*))  (INPUT)
C               FORMX = HOLDS THE CHARACTER STRING 'UNFORMATTED' OR
C                       'FORMATTED' AND IS USED IN THE OPEN STATEMENT AS
C                       FORM=FORMX.  (CHARACTER*(*))  (INPUT)
C               IP(J) = UNIT NUMBERS USED FOR OUTPUT (J=1,25).  ANY UNIT
C                       NUMBER READ IN RDSNAM CANNOT EQUAL IP( ) OR INPUT
C                       DATA MAY BE OVERWRITTEN.  THIS IS A PANIC FATAL
C                       ERROR.
C                       IP(23) INDICATES WHETHER (>0) OR NOT (=0) 
C                       A STATEMENT THAT THE FILE IS OPENED IS WRITTEN
C                       TO UNIT IP(23).  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       29 = TOO MANY INPUT DATA SETS INDICATED.
C                       30 = CANNOT FIND TERMINATOR.
C                       31 = TROUBLE OPENING FILE OR THE UNIT NUMBER READ
C                            IS THE SAME AS AN IP( ) NUMBER.
C              IBLANK = DESIGNATES WHETHER (=1) OR NOT (=0) A BLANK LINE
C                       IS TO BE PRINTED BEFORE "OPENING FILE..." STATEMENT.
C                       (INTERNAL)
C                 INX = SCRATCH VARIABLE TO READ UNIT NUMBER INTO.  BY
C                       NOT READING DIRECTLY INTO INUNIT( ), ND6 NEED
C                       NOT BE LARGE ENOUGH TO ACCOMMODATE THE TERMINATOR.
C                       (INTERNAL)
C                MODX = SCRATCH VARIABLE TO READ MODEL NUMBER INTO.
C                       (INTERNAL)
C                NAMX = SCRATCH VARIABLE TO READ DATA SET NAME INTO.  TO
C                       AVOID HAVING TO PASS THIS VARIABLE THROUGH THE
C                       ARGUMENT, ITS SIZE IS HARDWIRED TO 60 CHARACTERS.
C                       IF THE SIZE OF NAM( ) AND SIMILAR VARIABLES IS
C                       INCREASED IN THE MAIN PROGRAM, THEN THE SIZE OF NAMX
C                       WILL HAVE TO BE INCREASED IF THE DATA SET NAME IS
C                       ACTUALLY LARGER THAN 60 CHARACTERS.  (CHARACTER*60)
C                       (INTERNAL)
C              WHTSIT = THE NAME OF AN OPEN FILE FURNISHED BY INQUIRE.
C                       (CHARACTER*60)  (INTERNAL)
C                  OP = FURNISHED BY INQUIRE TO TELL WHETHER OR NOT THE
C                       FILE IS OPEN.  (LOGICAL)  (INTERNAL)
C               BLANK = BLANKS.  (CHARACTER*60)  (INTERNAL)
C            CONVERTX = CHARACTER HOLDING THE KEYWORD USED TO OPEN
C                       RANDOM ACCESS FILE WITH THE CORRECT ENDIAN.
C                       (CHARACTER*20) (INTERNAL).
C 
C        NONSYSTEM SUBROUTINES USED 
C            IERX 
C
      CHARACTER*(*) NAM(ND6),STATUX,FORMX
      CHARACTER*4 STATE
      CHARACTER*60 NAMX,WHTSIT,BLANK
C
      LOGICAL OP
C
      DIMENSION INUNIT(ND6),MODNUM(ND6),JFOPEN(ND6)
      DIMENSION IP(25)
C
      DATA BLANK/' '/
CINTEL
      CHARACTER(LEN=20) :: CONVERTX
      CONVERTX='BIG_ENDIAN'
CINTEL
C
      IER=0
      IBLANK=1
C        IBLANK DESIGNATES WHETHER (=1) OR NOT (=0) A BLANK LINE IS TO
C        BE PRINTED BEFORE "OPENING FILE..." STATEMENT.
C
      IF(ND6.GE.1)THEN
         INUNIT(1)=0
         NAM(1)=' '
C           INUNIT =(1) INITIALIZED TO ZERO AND NAM(1) TO BLANK 
C           IN CASE NO FILE NUMBERS OR NAMES ARE READ IN.
      ELSE
         WRITE(KFILDO,105)
 105     FORMAT(/' ****ND6 LE 0.  STOP IN RDSNAM AT 105')
         STOP 105
      ENDIF
C
C        READ INPUT DATA SET NAMES AND UNIT NUMBERS AND OPEN THE FILES.
C        TERMINATOR IS A UNIT NUMBER = 99.
C
      J=0
      KOUNT=0
      STATE='110 '
 110  READ(KFILDI,111,IOSTAT=IOS,ERR=900,END=140)INX,MODX,NAMX
 111  FORMAT(2I3,1X,A60)
      IF(INX.EQ.99)GO TO 140
      IF(INX.EQ.0)GO TO 140
C        DON'T ACCEPT A UNIT NUMBER OF ZERO.  THIS CAN BE CAUSED
C        BY A BAD FORMAT.  THE END TRANSSFER TO 140 WORKS FOR
C        AN EMPTY RECORD.
      IF(J+1.LE.ND6)GO TO 120
C        NUMBER OF INPUTS, EXCLUSIVE OF THE TERMINATOR, EXCEEDS ND6.
      WRITE(KFILDO,115)ND6,(INUNIT(M),NAM(M),M=1,ND6)
 115  FORMAT(/,' ****TOO MANY INPUT DATA SETS.  ONLY',I3,' ALLOWED.',
     1         '  UNIT NUMBERS AND NAMES READ:',/,
     1        ('     ',I2,2X,A60))
      IER=29
C        ATTEMPT TO SKIP OVER THE EXTRA NAMES IN ORDER FOR THE CALLING
C        PROGRAM TO CONTINUE AS FAR AS DESIRED.  IF NO TERMINATOR IS
C        PRESENT, THERE COULD BE A MAJOR PROBLEM.  THEREFORE LIMIT
C        THE REPEATS TO 50.
      STATE='116 '
 116  READ(KFILDI,117,IOSTAT=IOS,ERR=900,END=118)IDUMMY
 117  FORMAT(I3)
 118  IF(IDUMMY.EQ.99)GO TO 140
      KOUNT=KOUNT+1
      IF(KOUNT.LE.50)GO TO 116
      WRITE(KFILDO,119)
 119  FORMAT(/,' ****CANNOT FIND TERMINATOR.')
      IER=30
      GO TO 140
C
C         CHECK TO MAKE SURE AN INPUT UNIT NUMBER IS NOT THE SAME
C         AS SPECIFIED FOR AN OUTPUT IN IP( ).
C
 120  DO 123 L=1,25
      IF(INX.NE.IP(L))GO TO 123
      WRITE(KFILDO,121)INX
 121  FORMAT(/,' ****AN ATTEMPT IS MADE TO USE AN INPUT UNIT NO. =',I4,
     1         ' THE SAME AS AN OUTPUT NUMBER IN IP( ).',/,
     2         '     THE IP( ) VALUE IS SET TO KFILDO.  CONTINUING.')
      IP(L)=KFILDO
      IER=31
 123  CONTINUE
C      
      J=J+1
      INUNIT(J)=INX
      MODNUM(J)=MODX
      NAM(J)=NAMX
      JFOPEN(J)=2
C
      IF(INUNIT(J).EQ.KFILDI)GO TO 110
C        THE DEFAULT INPUT FILE IS NOT OPENED HERE.
C
      IF(J.GT.1.AND.INUNIT(J).EQ.INUNIT(J-1))GO TO 110
C        ONLY THE FIRST FILE IN A SEQUENCE WITH THE SAME 
C        UNIT NUMBER IS TO BE OPENED.
C
C
      IF(STATUX(1:3).NE.'NEW'.AND.
     1   STATUX(1:3).NE.'OLD'.AND.
     2   STATUX(1:7).NE.'SCRATCH')GO TO 110
C        A STATUS OF "NEW", "OLD", OR "SCRATCH" IS NECESSARY FOR
C        FILE TO BE OPENED.
C
C     IF(STATUX(1:3).EQ.'NEW')CALL SYSTEM('rm -f '//NAM(J))
C***THE ABOVE STATEMENT REMOVED 2/25/98    
C        IF THE FILE IS "NEW" IT IS DELETED IN CASE IT ALREADY
C        EXISTS, BECAUSE AN ERROR OCCURS WHEN AN ATTEMPT IS MADE
C        TO OPEN AN EXISTING FILE AS "NEW."
      JFOPEN(J)=1
CINTEL
C
C        CHECK THE FORM OF THE FILE.
C
      IF(FORMX(1:11).EQ.'UNFORMATTED')THEN
C           FILE IS UNFORMATTED.  NEED TO CHECK ITS ENDIANNESS
         IF(STATUX(1:3).EQ.'OLD')THEN
C              FILE IS OLD. CALL CKFILEND TO DETERMINE
C              IT ENDIANNESS
            CALL CKFILEND(KFILDO,INUNIT(J),NAM(J),ISYSEND,IFILEND,
     1                    CONVERTX,IER)
         ELSEIF(STATUX(1:3).EQ.'NEW')THEN
C              FILE IS NEW. SET CONVERTX
            CONVERTX='BIG_ENDIAN'
         ENDIF
C          OPEN THE FILE WITH CONVERT= SPECIFIER
         OPEN(UNIT=INUNIT(J),FORM=FORMX,STATUS=STATUX,
     1        IOSTAT=IOS,CONVERT=CONVERTX,ERR=125)
C
      ELSEIF(FORMX(1:9).EQ.'FORMATTED')THEN
C          FORMATTED. NOT ENDIANNESS CHECK NEEDED. OPEN
C          FILE WITHOUT CONVERT= SPECIFIER
         OPEN(UNIT=INUNIT(J),FORM=FORMX,STATUS=STATUX,
     1        IOSTAT=IOS,ERR=125)
      ENDIF
CINTEL
      IF(IP(23).EQ.0)GO TO 110
C
C        WRITE OPENING FILE STATEMENT.
C
      IF(IBLANK.NE.0.AND.IP(23).NE.0)WRITE(IP(23),1244)
 1244 FORMAT(' ')
      IBLANK=0
      IF(IP(23).NE.0)WRITE(IP(23),1245)INUNIT(J),NAM(J)
 1245 FORMAT(' OPENING FILE ON UNIT NO.',I3,', FILE = ',A60)
      GO TO 110
C
 125  WRITE(KFILDO,126)INUNIT(J),IOS,NAM(J)
      IF(IP(23).NE.0.AND.IP(23).NE.KFILDO)
     1      WRITE(IP(23),126)INUNIT(J),IOS,NAM(J)
 126  FORMAT(/,' ****TROUBLE OPENING FILE ON UNIT NO.',I3,
     1         '.  IOSTAT =',I5,3X,A60)
      IER=31
C
C        IF THIS FILE WAS DESIGNATED AS 'OLD', IT MAY BE IT DOESN'T EXIST.
C        TRY IT AS A NEW FILE.  THIS SHOULD BE HELPFUL FOR AN OUTPUT FILE.
C        IER IS STILL NOT RETURNED AS 0, BECAUSE THIS MAY BE AN ERROR.
C        PROCEEDING HERE WILL ALLOW CHECKOUT OF MORE OF THIS RUN.  IT
C        SHOULD NOT BE DANGEROUS TO WRITE TO A FILE WITH NAME NAM(J),
C        BECAUSE IT IS KNOWN TO NOT EXIST AS AN OLD FILE.
C
CINTEL
C        INTEL FORTRAN IOSTAT:
C             10 = A FILE OPENED WITH STATUS 'NEW' ALREADY
C                  EXISTS
C             29 = A FILE WITH THE SPECIFIED NAME COULD NOT BE
C                  FOUND DURING AN OPEN OPERATION
C
C      IF(IOS.NE.908.OR.STATUX(1:3).NE.'OLD')GO TO 110
      IF(IOS.EQ.10.AND.STATUX(1:3).EQ.'NEW')THEN
         WRITE(KFILDO,127)
 127     FORMAT('     FILE OPENED WITH STATUS=NEW ALREADY EXISTS. ',
     1          'STOP IN RDSNAM AT 127')
         STOP 127
C
      ELSEIF(IOS.EQ.29.AND.STATUX(1:3).EQ.'OLD')THEN
         CONVERTX='BIG_ENDIAN'
C           OPEN FILE DIFFERENTLY ACCORDING TO ITS FORM
         IF(FORMX(1:11).EQ.'UNFORMATTED')THEN
            OPEN(UNIT=INUNIT(J),FORM=FORMX,STATUS='NEW',
     1           IOSTAT=IOS,CONVERT=CONVERTX,ERR=135)
         ELSEIF(FORMX(1:9).EQ.'FORMATTED')THEN
            OPEN(UNIT=INUNIT(J),FORM=FORMX,STATUS='NEW',
     1           IOSTAT=IOS,ERR=135)
         ENDIF
      ENDIF 
CINTEL
      WRITE(KFILDO,130)
      IF(IP(23).NE.0.AND.IP(23).NE.KFILDO)WRITE(IP(23),130)
 130  FORMAT('     IT HAS BEEN OPENED AS A NEW FILE RATHER THAN',
     1       ' AN OLD FILE.')
      GO TO 110
C
 135  WRITE(KFILDO,136)
      IF(IP(23).NE.0.AND.IP(23).NE.KFILDO)WRITE(IP(23),130)
 136  FORMAT('     COULD NOT OPEN IT AS A NEW FILE EITHER.')      
      GO TO 110
C
 140  NUMIN=J
      RETURN
C
 900  CALL IERX(KFILDO,KFILDO,IOS,'RDSNAM',STATE)
      RETURN
      END
