      SUBROUTINE RDI(KFILDO,IPN,KFIL,IDATA,ND,ITEMP,NT,FMT,NVAL,ITERM,
     1               IER) 
C 
C        JANUARY  1994   GLAHN   TDL   MOS-2000 
C        FEBRUARY 1999   GLAHN   CHARACTER TEST REMOVED
C        APRIL    2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                     CONFORM TO FORTRAN 90 STANDARDS
C                                     ON THE IBM SP
C
C        PURPOSE 
C            TO READ A LIST OF INTEGER DATA ACCORDING TO A SPECIFIED
C            FORMAT AND RETURN THE LIST IN ARRAY IDATA( ).
C            THE LIST IS TERMINATED BY A TERMINATOR ITERM.
C            IT IS ASSUMED THAT FORTRAN WILL READ A BLANK FIELD AS A 0
C            WITH AN I FORMAT; THIS ZERO OR BLANK, AS WELL AS ANY ZEROS
C            IN THE FIELD, WILL BE ELIMINATED FROM THE LIST.    
C            THIS ROUTINE WORKS ONLY FOR A 4-BYTE INTEGER VARIABLE.
C            FMT MUST BE DECLARED CHARACTER OR BE A CHARACTER STRING 
C            IN THE CALLING PROGRAM.  FMT WILL INDICATE HOW MANY VALUES
C            TO READ PER RECORD (NT OR LESS); HOWEVER, THE RECORD NEED 
C            NOT HAVE THAT MANY VALUES.  FOR CHARACTER DATA, SUBROUTINE 
C            RDC SHOULD BE USED.  
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            IPN    - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            KFIL   - UNIT NUMBER FROM WHICH TO READ DATA.  (INPUT) 
C 
C        VARIABLES 
C 
C            INPUT 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. 
C                       DIAGNOSTICS WILL BE WRITTEN HERE.
C                 IPN = UNIT NUMBER FOR OPTIONAL OUTPUT (PRINT) FILE.
C                       WHEN DIAGNOSTICS ARE WRITTEN TO KFILDO, THEY WILL
C                       ALSO BE WRITTEN TO UNIT NO. IPN UNLESS IPN = KFILDO
C                       OR IPN <= 0.
C                KFIL = UNIT NUMBER FROM WHICH TO READ DATA. 
C                  ND = SIZE OF ARRAY IDATA( ). 
C            ITEMP( ) = TEMPORARY ARRAY THAT MUST BE OF AT LEAST SIZE NT. 
C                  NT = NUMBER OF WORDS PER RECORD INDICATED IN FMT( ). 
C              FMT( ) = CONTAINS FORMAT OF DATA.  THIS MUST PERTAIN TO
C                       INTEGER DATA.  (CHARACTER*(*)) 
C                NVAL = COUNT OF ELEMENTS IN ARRAY RETURNED. 
C               ITERM = TERMINATOR. 
C                 IER = STATUS RETURN.
C                       0  = GOOD RETURN.
C                       20 = ERROR OR END OF FILE ON UNIT KFIL.
C                       21 = LIST TOO LONG FOR DIMENSION ND ON UNIT KFIL.
C 
C            OUTPUT 
C            IDATA(J) = ARRAY IN WHICH DATA ARE RETURNED (J=1,NVAL). 
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE. 
C
      CHARACTER FMT*(*)
C 
      DIMENSION IDATA(ND),ITEMP(NT)
C 
      IER=0
      NVAL=0 
C
C        READ A RECORD.
C      
 115  READ(KFIL,FMT,IOSTAT=IOS,ERR=116,END=116)ITEMP 
C        MORE VALUES IN THE RECORD THAN NT DOES NOT CAUSE AN ERROR.
C        ONLY NT WILL BE READ.  A RECORD TOO LONG MAY CAUSE THE
C        TERMINATOR TO BE SKIPPED.
      GO TO 119 
C 
 116  IER=20 
      WRITE(KFILDO,117)KFIL,IOS,IER 
      IF(IPN.NE.KFILDO.AND.IPN.GT.0)WRITE(IPN,117)KFIL,IOS,IER 
 117  FORMAT(/,' ****ERROR OR END OF FILE ON UNIT NO.',I3,
     1         '.  IOSTAT =',I5,/,
     2         '     RETURN FROM RDI AT 117 WITH IER =',I3)
      GO TO 135 
C
C        PROCESS DATA IN REOCRD.
C 
 119  DO 125 K=1,NT 
D     WRITE(KFILDO,120)K,NT,ITEMP(K),ITERM,NVAL,ND
D120  FORMAT(' ****K, NT, ITEMP(K), ITERM, NVAL, ND',2I6,I6,3I6)
      IF(ITEMP(K).EQ.ITERM)GO TO 135
C        THE LIST IS TERMINATED WITH THE TERMINATOR TERM. 
      IF(ITEMP(K).EQ.0)GO TO 125
C        THE ABOVE STATEMENT ELIMINATES BLANKS AND ZEROS WHEN AN 
C        I FORMAT HAS BEEN USED. 
      NVAL=NVAL+1 
      IF(NVAL.GT.ND)GO TO 130 
      IDATA(NVAL)=ITEMP(K) 
 125  CONTINUE 
C
C        READ ANOTHER RECORD.
      GO TO 115 
C 
C        THE LIST IS TOO LONG FOR THE ARRAY DATA( ).
C
 130  NVAL=NVAL-1 
      IER=21 
      WRITE(KFILDO,131)KFIL,IER 
      IF(IPN.NE.KFILDO.AND.IPN.GT.0)WRITE(IPN,131)KFIL,IER 
 131  FORMAT(/,' ****LIST TOO LONG ON UNIT NO.',I3,
     1         '.  RETURN FROM RDI AT 131 WITH IER =',I3,'.') 
C***      WRITE(KFILDO,132)(IDATA(J),J=1,NVAL),ITEMP 
C*** 132  FORMAT(' ',10I12)      
C***C        IF DATA ARE BEING READ WITH AN A FORMAT, THEN THIS PRINT WILL
C***C        NOT APPEAR TO BE CORRECT.
 135  RETURN 
      END 
