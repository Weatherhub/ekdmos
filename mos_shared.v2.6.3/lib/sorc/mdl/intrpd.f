      SUBROUTINE INTRPD(KFILDO,KFIL10,IP12,
     1                  ID,IDPARS,JD,NDATE,
     2                  KFILRA,RACESS,NUMRA,
     3                  CCALL,ICALLD,CCALLD,NAME,STALAT,STALON,NELEV,
     4                  ISDATA,SDATA,DIR,ND1,NSTA,
     5                  NGRIDC,NGRID,ND11,NSLAB,
     6                  IPACK,IWORK,ND5,
     7                  LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8                  NBLOCK,LASTD,NSTORE,NFETCH,
     9                  ND7,
     A                  P,IXSAVE,JYSAVE,EDSAVE,DSAVE,ND2X3,NX,NY,
     B                  L3264B,L3264W,ISTOP,IER)
C 
C        JULY      2003   GLAHN   TDL   MOS-2000 
C        AUGUST    2003   GLAHN   ELIMINATED /**** IN FORMATS 120
C                                 AND 140; ELIMINATED PRINTS AT 131
C                                 AND 141
C
C        PURPOSE 
C            TO FURNISH A VALUE AT EACH OF NSTA STATIONS FROM THE
C            CLOSEST GRIDPOINT TO THAT STATION WITHIN THE 4-GRIDPOINT
C            SQUARE IN WHICH THE STATION IS LOCATED SUCH THAT THE
C            GRIDPOINT:
C              1)  VALUE IS NOT MISSING (=9999.),
C              2)  IS NOT DEFINED AS UNUSABLE WITH A GRID MASK, AND
C              3)  IS NOT MORE THAN ECONST FT DIFFERENT IN ELEVATION
C                  FROM THE STATION.
C            THIS IS A SUBROUTINE FOR U201 TO USE IN NDFD VERIFICATION.
C            THE TERRAIN GRID AND MASK ARE READ FROM AN EXTERNAL RANDOM
C            ACCESS FILE WITH UNIT NUMBER 44 WITH SUBROUTINE CONST1.
C            THE CCC'S OF THE TERRAIN ARE TAKEN FROM IDV(3,L) AND
C            THE FFF'S OF THE TERRAIN ARE TAKEN FROM IDV(4,L).
C            IN LIKE MANNER, THE MASK CCC'S AND FFF'S ARE TAKEN FROM
C            IDV(5,L) AND IDV(6,L).  TO FIND THE COLUMN L IN IDV( ,L) TO
C            USE, IDPARS(1) AND IDPARS(2) ARE MATCHED WITH IDV(1, ) AND
C            IDV(2, ).  THE "Y" IN THE TERRAIN AND MASK FFF'S
C            DESIGNATES THE GRID MESH IN FRACTIONS OF BEDIENTS.  IT IS
C            DETERMINED FROM SUBROUTINE ACTNOM AND IS INSERTED IN THE
C            FFF'S; IT IS 4 FOR A NOMINAL 5-KM GRID.  IF A MATCH OF
C            IDPARS(1) AND IDPARS(2) IS NOT FOUND, A "GENERIC" SET OF
C            IDS AND ELEVATION THRESHOLD IS USED FROM IDV( ,NDIM)
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C            IP12   - INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                     STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                     THE FILE WHOSE UNIT NUMBER IS IP12.
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C                IP12 = INDICATES WHETHER (>0) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE EXTERNAL RANDOM ACCESS FILES
C                       WILL BE LISTED TO UNIT IP12.  (INPUT)
C               ID(J) = THE VARIABLE ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE VARIABLE
C                       ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C             JD(J,N) = THE BASIC INTEGER VARIABLE ID (J=1,4) (N=1,NPRED).
C                       THIS IS THE SAME AS ID(J,N), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3, ),
C                       T = IDPARS(8, ),
C                       I = IDPARS(13, ),
C                       S = IDPARS(14, ),
C                       G = IDPARS(15, ), AND
C                       THRESH( ).
C                       JD( , ) IS USED TO FOR INPUT TO CONSTG, BECAUSE
C                       INTERPOLATION INTO THE GRID MAY BE REQUIRED,
C                       BUT IS NOT PART OF THE BASIC ID.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH VARIABLE IS NEEDED.  (INPUT)
C           KFILRA(J) = THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                       ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES ASSOCIATED WITH KFILRA(J) (J=1,NUMRA).
C                       (CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF VALUES IN KFILRA( ) AND RACESS( ).
C                       (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ).  EQUIVALENCED TO ICALL( , , ). 
C                       (CHARACTER*8)  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).  THIS ARRAY IS USED 
C                       TO READ THE STATION DIRECTORY FROM A MOS-2000
C                       EXTERNAL FILE.  EQUIVALENCED TO CCALLD( ). 
C                       (CHARACTER*8)  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS ARRAY IS USED 
C                       IN CONST TO READ THE STATION DIRECTORY.  EQUIVALENCED 
C                       TO ICALLD( , ).  (CHARACTER*8)  (INTERNAL)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C            NELEV(K) = ELEVATION OF STATIONS (K=1,NSTA).  (INPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C            SDATA(K) = "INTERPOLATED" DATA.  THESE ARE THE "CLOSEST"
C                       GRIDPOINT VALUES (SEE PURPOSE) (K=1,NSTA).  (OUTPUT)
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN NGRIDC( ,M).
C                       (INPUT/OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                       (INPUT/OUTPUT)
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN DIR( , , ),
C                       MAXIMUM OF ND11.  (INPUT/OUTPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ) AND DIR( , , ).  (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , ) AND
C                       IN NGRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THE GRID IN P( , ).  SEE LSTORE(10, ).  FOR
C                       THE COMPUTATION ROUTINES RETURNING A GRID, THIS
C                       VALUE MUST BE OUTPUT BY GFETCH.  (OUTPUT) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), DATA( ) AND
C                       CCALLD( ).  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE VARIABLE IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C               LASTL = THE LAST LOCATION IN CORE( ) USED FOR MOS-2000 INTERNAL
C                       STORAGE.  INITIALIZED TO 0 ON FIRST ENTRY TO GSTORE.
C                       ALSO INITIALIZED IN U201 IN CASE GSTORE IS NOT ENTERED.
C                       MUST BE CARRIED WHENEVER GSTORE IS TO BE CALLED.
C                       (INPUT/OUTPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR MOS-2000
C                       INTERNAL STORAGE.  MUST BE CARRIED WHENEVER GSTORE
C                       IS TO BE CALLED.  (INPUT)
C              NSTORE = THE NUMBER OF TIMES GSTORE HAS BEEN ENTERED.  GSTORE
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.  GFETCH
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C            P(IX,NY) = INPUT GRID FROM WHICH TO TAKE VALUES TO RETURN
C                       IN SDATA( ) (IX=1,NX) (JY=1,NY).  (INPUT)
C           IXSAVE(K) = THE IX POSITION FOR EACH STATION OF THE GRID
C                       VALUE RETURNED IN SDATA(K) IN THE IX DIRECTION.
C                       USED FOR /D PRINTING ONLY.  (INTERNAL) 
C           JYSAVE(K) = THE JY POSITION FOR EACH STATION OF THE GRID
C                       VALUE RETURNED IN SDATA(K) IN THE JY DIRECTION.
C                       USED FOR /D PRINTING ONLY.  (INTERNAL) 
C           EDSAVE(K) = THE ELEVATION DIFFERENCE BETWEEN THE STATION
C                       AND THE GRIDPOINT USED FOR DATA RETURNED IN 
C                       SDATA(K).  USED FOR /D PRINTING ONLY.  (INTERNAL) 
C            DSAVE(K) = THE DISTANCE FROM THE STATION TO THE GRIDPOINT 
C                       USED FOR DATA RETURNED IN SDATA(K) IN 
C                       GRIDLENGTHS.  USED FOR /D PRINTING ONLY.
C                       (INTERNAL) 
C               ND2X3 = THE ARRAY SIZE OF SEVERAL ARRAYS.  (INPUT)
C                  NX = THE X EXTENT OF THE GRID IN P( , ).  (INPUT)
C                  NY = THE Y EXTENT OF THE GRID IN P( , ).  (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.
C                       (INPUT)
C               ISTOP = INCREMENTED WHEN A SPECIFIC TERRAIN AND 
C                       ELEVATION ID SET CANNOT BE FOUND.  ALL OTHER
C                       "ERRORS" SET IER NE 0, AND ISTOP IS INCREMENTED
C                       IN THE CALLING PROGRAM.  THIS IS CONSIDERED
C                       AN ERROR, BUT PROCESSING PROCEEDS WITH A "GENERIC"
C                       SET.  (INPUT/OUTPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                        60 = MAP PROJECTION NOT 3, 5, OR 7 (FROM ACTNOM).
C                       100 = GRIDS DO NOT MATCH.
C                       160 = CANNOT FIND A UNIT NUMBER AND NAME IN THE
C                             LIST OF MOS-2000 EXTERNAL FILES TO ACCESS
C                             (FROM CONST VIA CONST1).
C                       164 = NUMBER OF INDEX VALUES FROM GFETCH DOES
C                             NOT AGREE WITH THE NUMBER OF VALUES RETURNED
C                             BY RDTDLM OR WITH NSTA (FROM CONST VIA
C                             CONST1).
C                       188 = GRID LENGTH OF GRID IN P( , ) NOT SAME AS
C                             GRID LENGTH IN TERR( , ) OR RMASK( , )
C                             (FROM SAMEGR)
C                       189 = DYNAMIC MEMORY ALLOCATION FAILED.
C                       SEE CALLED ROUTINES FOR OTHER VALUES.
C                       (OUTPUT)
C         TERR(IX,JY) = TERRAIN FIELD READ FROM EXTERNAL RANDOM ACCESS
C                       (IX=1,NXT) (JY=1,NYT).  (ALLOCATABLE)  (INTERNAL) 
C        RMASK(IX,JY) = GRID MASK READ FROM EXTERNAL RANDOM ACCESS
C                       (IX=1,NXM) (JY=1,NYM).  A "1" MEANS THE POINT CAN
C                       BE USED; A "0" MEANS DON'T USE THE POINT.
C                       (ALLOCATABLE)  (INTERNAL) 
C              ECONST = THE VALUE IN FT WHICH WHEN THE DIFFERENCE OF 
C                       THE ELEVATION OF THE STATION AND THE ELEVATION
C                       OF THE GRIDPOINT EXCEEDS, THE GRIDPOINT VALUE
C                       WILL NOT BE USED.  SET BY DATA STATEMENT.
C                       (INTERNAL)
C            XIADD(J) = VALUES ASSOCIATED WITH GRIDPOINTS SURROUNDING
C                       THE STATION IN ORDER: UR, LR, LL, UL TO AID
C                       IN DETERMINING THE CLOSEST GRIDPOINT IN THE IX
C                       DIRECTION (J=1,4).  THIS ELIMINATES HAVING
C                       4 SECTIONS OF ESSENTIALLY DUPLICATIVE CODE. 
C                       VALUES SET BY DATA STATEMENT.  (INTERNAL) 
C            YJADD(J) = ESSENTIALLY THE SAME AS XIADD( ), EXCEPT IN THE
C                       JY DIRECTION.  (INTERNAL)
C                IFYF = THE Y IN CCC FYF DEFINING THE GRIDLENGTH.
C                       FOR INSTANCE, IFYF = 4 FOR A NOMINAL 5-KM GRID.
C                       RETURNED FROM ACTNOM.  (INTERNAL)
C           IDSAVT(J) = THE IDS OF THE TERRAIN FIELD IN TERR( ) (J=1,4).
C                       (INTERNAL)
C           IDSAVM(J) = THE IDS OF THE MASK IN RMASK( ) (J=1,4).
C                       (INTERNAL)
C            IDV(J,L) = CCC AND FFF PORTIONS OF FORECAST IDS AND
C                       ASSOCIATED TERRAIN IDS, MASK IDS, AND
C                       ELEVATION THRESHOLDS (J=1,7) (L=1,NDIM).
C                       THE 7 ELEMENTS IN IDV(J, ) ARE FOR J=1,7:
C                         1 -- CCC TO MATCH WITH IDPARS(1)
C                         2 -- FFF TO MATCH WITH IDPARS(2)
C                         3 -- CCC OF THE TERRAIN GRID
C                         4 -- FFF OF THE TERRAIN GRID
C                         5 -- CCC OF THE MASK
C                         6 -- FFF OF THE MASK
C                         7 -- ELEVATION THRESHOLD
C                       THE IDS CURRENTLY ACCOMMODATED  ARE:
C                         202 050 - NDFD SURFACE TEMP IN DEGREES F 
C                         202 130 - NDFD DAYTIME MAX IN DEGREES F 
C                         202 230 - NDFD NIGHTTIME MIN IN DEGREES F 
C                         203 030 - NDFD DEWPOINT IN DEGREES F 
C                         002 301 - RUC TEMPERATURE IN DEGREES F 
C                         003 301 - RUC DEWPOINT IN DEGREES F 
C                         204 330 - NDFD SURFACE WIND IN KT 
C                         203 520 - NDFD POP
C                       (INTERNAL)
C             NXT,NYT = NX,NY EXTENTS OF GRID READ INTO TERR( ).  THESE
C                       MUXT BE EQUAL TO NX,NY TO PROCEED.  (INTERNAL)
C             NXM,NYM = NX,NY EXTENTS OF GRID READ INTO RMASK( ).  THESE
C                       MUXT BE EQUAL TO NX,NY TO PROCEED.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES CALLED 
C            CONST1, ACTNOM, SAMEGR
C
      PARAMETER (NDIM=8)
C
      CHARACTER*8 CCALL(ND1,6),
     1            CCALLD(ND5)
      CHARACTER*20 NAME(ND1)
      CHARACTER*60 RACESS(NUMRA)
C
      DIMENSION ISDATA(ND1),SDATA(ND1),STALAT(ND1),STALON(ND1),
     1          NELEV(ND1)
      DIMENSION DIR(ND1,2,ND11),NGRIDC(6,ND11)
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION IPACK(ND5),IWORK(ND5),ICALLD(L3264W,ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
C        IS0( ), IS1( ), IS2( ), AND IS4( ) ARE AUTOMATIC ARRAYS.
      DIMENSION LSTORE(12,ND9)
      DIMENSION P(NX,NY),IXSAVE(ND2X3),JYSAVE(ND2X3),
     1          EDSAVE(ND2X3),DSAVE(ND2X3)
      DIMENSION CORE(ND10)
      DIMENSION KFILRA(NUMRA)
      DIMENSION IDV(7,NDIM)
      DIMENSION KD(4),KDPARS(15),XIADD(4),YJADD(4),IDSAVT(4),IDSAVM(4)
C
      ALLOCATABLE TERR(:)
      ALLOCATABLE RMASK(:)
C
      DATA KDPARS/15*0/
      DATA ECONST/500./
      DATA XIADD/1.,1.,0.,0./,
     1     YJADD/1.,0.,0.,1./
      DATA IDSAVT/4*0/,
     1     IDSAVM/4*0/
      DATA IDV/202, 054, 409, 300, 400, 301, 500,
     2         202, 134, 409, 300, 400, 301, 500,
     3         202, 234, 409, 300, 400, 301, 500,
     4         203, 034, 409, 300, 400, 301, 500,
     5         204, 230, 409, 300, 400, 301, 500,
     6         204, 334, 409, 300, 400, 301, 500,
     7         203, 520, 409, 300, 400, 301, 500,
     8         000, 000, 409, 300, 400, 301, 500/
C        THE ABOVE ARE FOR 
C        1    NDFD SFC TEMP (K)
C        2    NDFD MAX TEMP (K)
C        3    NDFD MIN TEMP (K)
C        4    NDFD DEWPT (K)
C        5    NDFD WND DIR (DEG)
C        6    NDFD WIND SPD (MPS)
C        7    NDFD 12H POP (DECIMAL)
C        8    GENERIC (USE IF OTHERS FAIL)
      DATA IFIRST/0/
C
      SAVE IDSAVT,IDSAVM,NXT,NYT,NSLABT,NXM,NYM,NSLABM
      SAVE TERR,RMASK
C
D     WRITE(KFILDO,100)
D100  FORMAT(/,' ENTERING INTRPD')
D     WRITE(KFILDO,101)IDPARS(1),IDPARS(2),ND5,NSLAB,NDIM,
D    1         NGRIDC(1,NSLAB),NGRIDC(2,NSLAB)
D101  FORMAT('     IDPARS(1),IDPARS(2),ND5,NSLAB,NDIM,',
D    1       'NGRIDC(1,NSLAB),NGRIDC(2,NSLAB)',7I7)
C
      IER=0
C
C        THE FIRST TIME ENTERED, ALLOCATE TERR( ) AND RMASK( ).
C   
      IF(IFIRST.EQ.0)THEN
         ALLOCATE (TERR(ND5),RMASK(ND5),STAT=IOS)
C
         IF(IOS.EQ.0)THEN
            WRITE(KFILDO,104)ND5
 104        FORMAT(/' ALLOCATION OF ',I7,' WORDS STORAGE IN EACH',
     1              ' OF TERR( ) AND RMASK( ) COMPLETE IN INTPRD.')
         ELSEIF(IOS.EQ.1)THEN
            WRITE(KFILDO,105)
 105        FORMAT(/' ****ALLOCATION OF TERR( ) AND RMASK( ) FAILED IN',
     1              ' INTPRD AT 105.  ARRAYS ALREADY ALLOCATED.',
     2              '  THIS SHOULD NOT HAPPEN.  PROCEEDING.')
         ELSEIF(IOS.EQ.2)THEN
            WRITE(KFILDO,106)ND5
 106        FORMAT(/' ****ALLOCATION OF TERR( ) AND RMASK( ) FAILED IN',
     1              ' INTRPD AT 106.  ALLOCATION OF ',I12,
     2              ' WORDS REQUESTED.  ARRAYS NOT ALLOCATED.')
            IER=189
            GO TO 290
         ENDIF
C
      ENDIF
C
      IFIRST=IFIRST+1
C        IFIRST KEEPS TRACK OF NUMBER OF ENTRIES IN CASE IT IS NEEDED
C        FOR DIAGNOSTICS.
C
C        GET THE Y FOR FYF DENOTING THE GRID MESH FOR THE IDS.
C        FOR INSTANCE, IFY = 4 FOR A NOMINAL 5 KM GRID.
C
      CALL ACTNOM(KFILDO,NGRIDC(1,NSLAB),NGRIDC(2,NSLAB),
     1            MTR,XTR,IFYF,IER)
C        MTR AND XTR ARE TRASH FOR THIS ROUTINE RETURNED FROM ACTNOM.
C
      IF(IER.NE.0)THEN
         GO TO 290
      ENDIF
C
C        FIND THE COLUMN L IN IDV( , )TO TAKE THE TERRAIN AND MASK
C        IDS AND THE ELEVATION THRESHOLD.
C
      DO 110 LL=1,NDIM
        IF(IDPARS(1).EQ.IDV(1,LL).AND.
     1     IDPARS(2).EQ.IDV(2,LL))GO TO 115
 110  CONTINUE
C
C        A DROP THROUGH HERE MEANS A SPECIFIC COLUMN IN IDV( , ) 
C        HAS NOT BEEN IDENTIFIED.  IN THIS CASE, USE THE "GENERIC"
C        COL LL = NDIM.  NOTIFY THE USER OF THIS SITUATION.
C
      LL=NDIM
      WRITE(KFILDO,114)(IDV(J,LL),J=3,7)
 114  FORMAT(/' ****SPECIFIC TERRAIN AND MASK FIELDS AND ELEVATION',
     1        ' THRESHOLD WERE NOT IDENTIFIED IN INTRPD.  THE',
     2        ' GENERIC SET BELOW WAS USED:'/
     3        '       TERRAIN CCC  FFF, MASK CCC  FFF, THRESHOLD'/
     4        '     ',I13,I5,I10,I5,I11)
      ISTOP=ISTOP+1
C        THIS IS CONSIDERED AN ERROR EVEN THOUGH PROCESSING 
C        CONTINUES.  OTHER ERRORS ARE INCREMENTED IN THE CALLING
C        ROUTINE.
C
C        READ THE TERRAIN GRID UNLESS IT ALREADY EXISTS IN TERR( )
C        AS DEFINED BY THE 4 IDS IN IDSAVT( ).  CONST1 WILL CALL
C        CONSTG WHEN KFILRA( ) CONTAINS UNIT NO. 44 AND KDPARS(1)
C        CONTAINS A VALUE BETWEEN 400 AND 499 INCLUSIVE.  THE VALUES
C        OF CCCFFF ARE TAKEN FROM IDV(3,LL) AND IDV(4,LL). FOR INSTANCE,
C        CCCFFF = 409340 REPRESENTS TERRAIN ON A LAMBERT CONFORMAL
C        MAP (3FF) AT A 5-KM RESOLUTION (F4F).
C
 115  KD(1)=IDV(3,LL)*1000000+IDV(4,LL)*1000+IFYF*10000
      KD(2)=0
      KD(3)=0
      KD(4)=0
      KDPARS(1)=IDV(3,LL)
      KDPARS(2)=IDV(4,LL)+IFYF*10
C
      IF(KD(1).NE.IDSAVT(1).OR.
     1   KD(2).NE.IDSAVT(2).OR.
     2   KD(3).NE.IDSAVT(3).OR.
     3   KD(4).NE.IDSAVT(4))THEN
         CALL CONST1(KFILDO,KFIL10,IP12,
     1               KD,KDPARS,KD,NDATE,
     2               KFILRA,RACESS,NUMRA, 
     3               CCALL,ICALLD,CCALLD,NAME,STALAT,STALON,
     4               ISDATA,SDATA,DIR,ND1,NSTA,
     5               NGRIDC,NGRID,ND11,NSLABT, 
     6               IPACK,IWORK,TERR,ND5,
     7               LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8               NBLOCK,LASTD,NSTORE,NFETCH,
     9               IS0,IS1,IS2,IS4,ND7,
     A               ISTAG,L3264B,L3264W,IER)
C           CONST1 FILLS OR CHECKS NGRIDC( ,NSLABT), WHERE NSLABT SHOULD 
C           EQUAL NSLAB.  
C
         IF(IER.NE.0)THEN
            WRITE(KFILDO,120)
 120        FORMAT('     THE TERRAIN FIELD IS NOT AVAILABLE IN THE',
     1             ' EXTERNAL RANDOM ACCESS FILE.  INTERPOLATION IN',
     2             ' INTRPD CANNOT BE DONE.')
C              THIS DIAGNOSTIC FOLLOWS ONE IN CONST1.
            GO TO 290
         ENDIF
C
C           SAVE THE IDS OF THE TERRAIN DATA IN TERR( ).  THIS WILL SIGNAL
C           WHETHER OR NOT A NEW TERRAIN GRID NEEDS BE READ IN.  THIS IS
C           UNLIKELY.  ALSO SAVE THE GRID EXTENTS.  NSLABT IS SAVED
C           WITH A SAVE STATEMENT.
C
         IDSAVT(1)=KD(1)
         IDSAVT(2)=KD(2)
         IDSAVT(3)=KD(3)
         IDSAVT(4)=KD(4)
         NXT=IS2(3)
         NYT=IS2(4)
C           NXT AND NYT ARE THE NX AND NY EXTENTS OF THE TERRAIN GRID.
C
C           CONVERT THE TERRAIN IN METERS TO FEET.  THE STATION 
C           IN NELEV( ) ARE IN FEET.  USE CONVERSION 1 FT = .30480 M.
C
         F=1./.30480
C           MULTIPLY RATHER THAN DIVIDE FOR EFFICIENCY.
C
         DO 130 J=1,NXT*NYT  
            TERR(J)=TERR(J)*F
 130     CONTINUE
C
C****         WRITE(KFILDO,131)KD,ID
C**** 131     FORMAT(/' TERRAIN FIELD WITH IDS =',I11.9,2I11,I5.3,
C****     1           ' READ FOR VARIABLE =',I11.9,2I11,I5.3)
      ENDIF
C
      CALL SAMEGR(KFILDO,KD,ID,NSLABT,NSLAB,NXT,NX,NYT,NY,
     1            'TERRAIN FIELD','INPUT GRID','INTRPD',
     2             NGRIDC,ND11,IER)
      IF(IER.NE.0)GO TO 290
C        IF IER NE 0, SAMEGR WILL HAVE PROVIDED A DIAGNOSTIC.
C
C        READ THE MASK GRID UNLESS IT ALREADY EXISTS IN RMASK( )
C        AS DEFINED BY THE 4 IDS IN IDSAVM( ).  CONST1 WILL CALL
C        CONSTG WHEN KFILRA( ) CONTAINS UNIT NO. 44 AND KDPARS(1)
C        CONTAINS A VALUE BETWEEN 400 AND 499 INCLUSIVE.  THE VALUES
C        OF CCCFFF ARE TAKEN FROM IDV(5,LL) AND IDV(6,LL).  FOR INSTANCE,
C        CCCFFF = 400341 DEFINES A MASK ON A LAMBERT CONFORMAL
C        MAP (3FF) AT A 5-KM NOMINAL RESOLUTION (F4F).  IN THE MASK,
C        "1" MEANS A GOOD POINT; "0" MEANS A POINT TO NOT USE.  NOTE
C        THESE ARE FLOATING POINT.
C
      KD(1)=IDV(5,LL)*1000000+IDV(6,LL)*1000+IFYF*10000
      KD(2)=0
      KD(3)=0
      KD(4)=0
      KDPARS(1)=IDV(5,LL)
      KDPARS(2)=IDV(6,LL)+IFYF*10
C
      IF(KD(1).NE.IDSAVM(1).OR.
     1   KD(2).NE.IDSAVM(2).OR.
     2   KD(3).NE.IDSAVM(3).OR.
     3   KD(4).NE.IDSAVM(4))THEN
         CALL CONST1(KFILDO,KFIL10,IP12,
     1               KD,KDPARS,KD,NDATE,
     2               KFILRA,RACESS,NUMRA, 
     3               CCALL,ICALLD,CCALLD,NAME,STALAT,STALON,
     4               ISDATA,SDATA,DIR,ND1,NSTA,
     5               NGRIDC,NGRID,ND11,NSLABM, 
     6               IPACK,IWORK,RMASK,ND5,
     7               LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8               NBLOCK,LASTD,NSTORE,NFETCH,
     9               IS0,IS1,IS2,IS4,ND7,
     A               ISTAG,L3264B,L3264W,IER)
C           CONST1 FILLS OR CHECKS NGRIDC( ,NSLABM), WHERE NSLABM SHOULD 
C           EQUAL NSLAB.
C
         IF(IER.NE.0)THEN
            WRITE(KFILDO,140)
 140        FORMAT('     THE GRID MASK FIELD IS NOT AVAILABLE IN THE',
     1             ' EXTERNAL RANDOM ACCESS FILE.  INTERPOLATION IN',
     2             ' INTRPD CANNOT BE DONE.')
C              THIS DIAGNOSTIC FOLLOWS ONE IN CONST1.
            GO TO 290
         ENDIF
C
C           SAVE THE IDS OF THE TERRAIN DATA IN TERR( ).  THIS WILL SIGNAL
C           WHETHER OR NOT A NEW TERRAIN GRID NEEDS BE READ IN.  THIS IS
C           UNLIKELY BUT POSSIBLE IF DIFFERENT MASKS ARE USED FOR A
C           DIFFERENT VARIABLES.  ALSO SAVE THE GRID EXTENTS.  NSLABT IS
C           SAVED WITH A SAVE STATEMENT.
C
         IDSAVM(1)=KD(1)
         IDSAVM(2)=KD(2)
         IDSAVM(3)=KD(3)
         IDSAVM(4)=KD(4)
         NXM=IS2(3)
         NYM=IS2(4)
C           NXM AND NYM ARE THE NX AND NY EXTENTS OF THE MASK GRID.
C
C****         WRITE(KFILDO,141)KD,ID
C**** 141     FORMAT(' MASK    FIELD WITH IDS =',I11.9,2I11,I5.3,
C****     1          ' READ FOR VARIABLE =',I11.9,2I11,I5.3)
      ENDIF
C
      CALL SAMEGR(KFILDO,KD,ID,NSLABM,NSLAB,NXM,NX,NYM,NY,
     1            'MASK GRID','INPUT GRID','INTRPD',
     2             NGRIDC,ND11,IER)
      IF(IER.NE.0)GO TO 290
C        IF IER NE 0, SAMEGR WILL HAVE PROVIDED A DIAGNOSTIC.
C    
C        DATA ARE AVAILABLE FOR COMPUTATIONS.  DO IN  SUBROUTINE
C        TRPD TO UTILIZE DOUBLE SUBSCRIPTING.  THE EXTENT OF THE
C        TERR( ) AND RMASK( ) GRIDS ARE NOT KNOWN UNTIL THEY ARE
C        READ.
C
      CALL TRPD(KFILDO,P,TERR,RMASK,NX,NY,
     1          CCALL,NAME,STALAT,STALON,NELEV,
     2          SDATA,DSAVE,EDSAVE,IXSAVE,JYSAVE,
     3          DIR,ND1,NSTA,ECONST,IER)
C  
      GO TO 300
C
C        ERROR LOOP.  SET ALL OF SDATA( ) TO MISSING.
C
 290  DO 295 K=1,ND1
         SDATA(K)=9999.
 295  CONTINUE
C
 300  RETURN
      END
