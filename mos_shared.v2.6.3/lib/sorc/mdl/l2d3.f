      SUBROUTINE L2D3(KFILDO,L1DATA,SDATA,SDATA1,ND1,NSTA,
     1                THRES1,THRES2,PROB,
     2                NTHRS1,NTHRS2,NREG,NOCAT1,NOCAT2,IER)
C
C        MAY  1995   GLAHN   TDL   MOS-2000
C        JUNE 1995   GLAHN   D COMPILE OPTION REMOVED
C 
C        PURPOSE 
C            TO COMPUTE THE TRANSFORMED VARIABLE FOR L1D.
C
C        DATA SET USE 
C            KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C           L1DATA(J) = REGION INDEX LIST (J=1,ND1).  (INPUT)
C            SDATA(J) = ON INPUT, THE SECOND OF THE VARIABLE PAIR TO 
C                       LINEARIZE; ON OUTPUT, THE LINEARIZED VARIABLE.
C                       (INPUT-OUTPUT)
C           SDATA1(K) = THE FIRST VARIABLE OF THE VARIABLE PAIR TO
C                       LINEARIZE (K=1,NSTA).  (INPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF L1DATA( ) AND SDATA( ).  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         THRES1(M,N) = THE THRESHOLD VALUES FOR CATEGORY M (M=1,NTHRS1),
C                       AND REGION N (N=1,NREG) FOR THE FIRST OF THE 
C                       VARIABLE PAIR.  (INPUT)
C         THRES2(M,N) = THE THRESHOLD VALUES FOR CATEGORY M (M=1,NTHRS2),
C                       AND REGION N (N=1,NREG) FOR THE SECOND OF THE
C                       VARIABLE PAIR.  (INPUT)
C         PROB(L,M,N) = THE PROBABILITY VALUES FOR CATEGORY L OF THE FIRST
C                       OF THE VARIABLE PAIR (L=1,NTHRS1), CATEGORY M OF
C                       THE SECOND OF THE VARIABLE PAIR (M=1,NTHRS2),
C                       AND REGION N (N=1,NREG)  (INPUT)
C              NTHRS1 = THE MAXIMUM NUMBER OF THRESHOLDS THAT CAN BE 
C                       USED FOR THE FIRST OF THE VARIABLE PAIR FOR
C                       THE TRANSFORMATION DEFINED BY THIS
C                       ROUTINE.  (INPUT)
C              NTHRS2 = THE MAXIMUM NUMBER OF THRESHOLDS THAT CAN BE 
C                       USED FOR THE SECOND OF THE VARIABLE PAIR FOR
C                       THE TRANSFORMATION DEFINED BY THIS
C                       ROUTINE.  (INPUT)
C                NREG = THE MAXIMUM NUMBER OF REGIONS THAT CAN BE
C                       USED FOR THE TRANSFORMATION DEFINED BY THIS
C                       ROUTINE.  (INPUT)
C              NOCAT1 = THE NUMBER OF CATEGORIES ACTUALLY BEING DEALT WITH
C                       FOR THE FIRST OF THE VARIABLE PAIR.  MUST BE LE NTHRS1.
C                       (INPUT)
C              NOCAT2 = THE NUMBER OF CATEGORIES ACTUALLY BEING DEALT WITH
C                       FOR THE SECOND OF THE VARIABLE PAIR.  MUST BE LE NTHRS2.
C                       (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                         
C        NON SYSTEM SUBROUTINES CALLED 
C            NONE.
C
      DIMENSION L1DATA(ND1),SDATA(ND1),SDATA1(ND1)
      DIMENSION THRES1(NTHRS1,NREG),
     1          THRES2(NTHRS2,NREG),
     2          PROB(NTHRS1,NTHRS2,NREG)
C
C        COMPUTE THE VARIABLE.  THIS ALGORITHM ASSUMES THAT
C        THRES1(NOCAT1, ) AND THRES2(NOCAT2, ) HOLD A NUMBER
C        LARGER THAN WILL BE ENCOUNTERED IN THE DATA.  THIS
C        HAS BEEN ASSURED IN THE CALLING PROGRAM.
C
      IER=0
C
      DO 270 K=1,NSTA
      LOC=L1DATA(K)
      IF(LOC.NE.0)GO TO 262
C        THIS STATION WAS NOT IN THE THRESHOLD LIST.
      SDATA(K)=9999.
      GO TO 270
C
 262  DO 265 L=1,NOCAT1
      IF(SDATA1(K).GT.THRES1(L,LOC))GO TO 265
C
      DO 264 J=1,NOCAT2
      IF(SDATA(K).GT.THRES2(J,LOC))GO TO 264
      SDATA(K)=PROB(J,L,LOC)
      GO TO 270
C
 264  CONTINUE
C
C        DROP THROUGH HERE IS AN ERROR.  POSSIBLY THRES2(NOCAT2,LOC)
C        IS NOT LARGE ENOUGH.
C
      WRITE(KFILDO,2640)
 2640 FORMAT(/' ****ERROR IN ALGORITHM IN L2D3 AT 264.')
      IER=116
      GO TO 270
C
 265  CONTINUE
C
C        DROP THROUGH HERE IS AN ERROR.  POSSIBLY THRES1(NOCAT1,LOC)
C        IS NOT LARGE ENOUGH.
C
      WRITE(KFILDO,2650)
 2650 FORMAT(/' ****ERROR IN ALGORITHM IN L2D3 AT 265.')
      IER=115
 270  CONTINUE
C
      RETURN
      END
