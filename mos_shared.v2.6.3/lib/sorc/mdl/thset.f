      SUBROUTINE THSET(KFILDO,ID,IDPARS,NVRBL,TRESHL,TRESHU,IER)
C 
C       JULY  1995   GLAHN   TDL   MOS-2000 
C       MARCH 1998   GLAHN   REMOVED TEST ON NTYPVR (SEE COMMENT)
C       MAY   1998   GLAHN   REMOVED NTYPVR FROM ARGUMENT LIST
C       JULY  1998   GLAHN   COMMENTS CHANGED TO ACCOMMODATE
C                            IDPARS = 8 AND 9.
C       JUNE  2004   GLAHN   MODIFIED TO ACCOMODATE CASE WHEN 
C                            ADJACENT VARIABLES MATCHED AND UPPER
C                            CATEGORY OF FIRST ONE IS 99999
C
C        PURPOSE 
C            TO USE THE SINGLE THRESHOLD PROVIDED IN TRESHL( ), 
C            TO SET A LOWER AND AND UPPER THRESHOLD IN TRESHL( )
C            AND TRESHU( ), RESPECTIVELY.  FOR VALUES OF IDAPRS(3):
C               (0)  FOR A NON-BINARY, TRESHL( ) AND TRESHU( ) ARE
C                    SET TO ZERO.
C               (1)  FOR A BINARY CUMULATIVE FROM ABOVE (IDPARS=1),
C                    THE UPPER THRESHOLD IS SET = 99999.  (THIS IS
C                    THE (ALMOST) MANDATORY BINARY FOR PREDICTORS.
C                    HOWEVER, THE PROGRAM DOES NOT STOP, BUT A
C                    DIAGNOSTIC IS PROVIDED TO THE DEFAULT OUTPUT 
C                    FILE.)
C               (2)  FOR A BINARY CUMULATIVE FROM BELOW (IDPARS=2),
C                    THE LOWER THRESHOLD IS SET -99999.
C               (3)  FOR A DISCRETE BINARY, THE THRESHOLD PROVIDED
C                    IS THE UPPER ONE, AND THE LOWER ONE IS SET TO
C                    THE UPPER ONE OF THE NEXT LOWER DISCRETE BINARY
C                    WITH THE SAME ID'S.  IN CASE THIS IS THE UPPERMOST
C                    DISCRETE BINARY WITH THE SAME ID'S, THE THRESHOLD
C                    PROVIDED SHOULD BE 99999 IF ALL REMAINING CASES
C                    ARE TO BE INCLUDED.  (ACTUALLY, ONLY 4 PLACES OF
C                    ACCURACY CAN BE PROVIDED, SO THE THRESHOLD SHOULD
C                    BE 9999 AS IT WOULD BE IN U201; IT WILL BE SET
C                    IN THIS ROUTINE TO 99999 FOR SYMMETRY.)  IN THE
C                    CASE THIS IS THE LOWERMOST DISCRETE BINARY WITH
C                    THE SAME ID'S, THE LOWER THRESHOLD IS SET -99999.
C                    IF A DISCRETE BINARY IS INDICATED AND THERE IS ONLY 
C                    ONE WITH THE SAME ID'S, THEN THE RESULTING
C                    BINARY WILL PERFORM THE SAME AS (2).
C               (4)  NOT USED, NOR CHECKED.
C               (5)  INDICATES GRID BINARY.  THE LOWER THRESHOLD IS 
C                    NOT CHANGED AND THE UPPER IS SET TO 0.
C               (6)  NOT USED. 
C               (7)  NOT USED. 
C               (8)  INDICATES A DISCRETE, MATCHING VARIABLE.
C                    SET UPPER TRESHOLD = 0. 
C               (9)  INDICATES A DISCRETE, MATCHING VARIABLE.
C                    SET UPPER TRESHOLD = 0. 
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C             ID(J,N) = THE INTEGER VARIABLE ID'S (J=1,4) (N=1,NVRBL).
C         IDPARS(J,N) = THE PARSED, INDIVIDUAL COMPONENTS OF THE VARIABLE
C                       ID'S CORRESPONDING TO ID( ,N) (J=1,15), (N=1,NVRBL).
C                       (OUTPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                          0 = NOT BINARY,
C                          1 = CUMULATIVE FROM ABOVE, VALUES GE LOWER THRESHOLD
C                              TRESHL = 1,
C                          2 = CUMULATIVE FROM BELOW, VALUES LT UPPER THRESHOLD
C                              TRESHU = 1.
C                          3 = DISCRETE BINARY.  VALUES GE LOWER THRESHOLD AND
C                              LT UPPER THRESHOLD = 1.
C                          ONLY THE VALUE OF 0 OR 1 SHOULD BE USED FOR PREDICTORS;
C                          0, 1, 2, OR 3 CAN BE USED FOR PREDICTANDS.
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               NVRBL = THE NUMBER OF VARIABLES TO PROCESS.  (INPUT)
C           TRESHL(N) = ON INPUT, THE SINGLE BINARY THRESHOLD CORRESPONDING 
C                       TO IDPARS( ,N) (N=1,NVRBL).  WHEN THE VARIABLE IS
C                       THE UPPERMOST IN A SERIES OF DISCRETE BINARIES,
C                       THIS INPUT THRESHOLD MUST BE 99999.
C                       ON OUTPUT, THE LOWER THRESHOLD FOR THE SAME VARIABLE.
C                       (INPUT-OUTPUT)
C           TRESHU(N) = ON OUTPUT, THE UPPER THRESHOLD FOR THE VARIABLE 
C                       IN IDPARS( ,N) (N=1,NVRBL).  (OUTPUT)
C                 IER = STATUS RETURN
C                       0 = GOOD RETURN.
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      DIMENSION ID(4,NVRBL),IDPARS(15,NVRBL),TRESHL(NVRBL),TRESHU(NVRBL)
C
      IER=0
C
      DO 140 N=1,NVRBL
      IF(IDPARS(3,N).EQ.0)THEN
C           THIS IS NOT A BINARY.
         TRESHL(N)=0.
         TRESHU(N)=0.
C
      ELSEIF(IDPARS(3,N).GE.5)THEN
C           THIS IS A GRID BINARY OR DISCRETE BINARY FOR
C           MATCHING VARIABLE.  TRESHL( ) NEED NOT BE CHANGED.
C           THE UPPER THRESHOLD HAS NO MEANING.  SET IT = 0.
         TRESHU(N)=0.
C
      ELSEIF(IDPARS(3,N).EQ.1)THEN
C           THIS IS A CUMULATIVE BINARY FROM ABOVE, THE NORMAL
C           PREDICTOR TYPE.  TRESHL( ) NEED NOT BE CHANGED.
         TRESHU(N)=99999.
C
      ELSEIF(IDPARS(3,N).EQ.2)THEN
C           THIS IS A CUMULATIVE BINARY FROM BELOW.
         TRESHU(N)=TRESHL(N)
         TRESHL(N)=-99999.
C
      ELSEIF(IDPARS(3,N).EQ.3)THEN
C           THIS IS A DISCRETE BINARY.  THE LOWER LIMIT
C           DEPENDS ON THE PREVIOUS VARIABLE.
D        WRITE(KFILDO,120)N,TRESHL(N),TRESHU(N)
D120     FORMAT(' THSET,N,TRESHL,TRESHU'I4,2F10.3)
         IF(N.EQ.1)THEN
C              THERE IS NO PREVIOUS VARIABLE.
            TRESHU(N)=TRESHL(N)
            TRESHL(N)=-99999.
C
         ELSEIF(ID(1,N).EQ.ID(1,N-1).AND.
     1          ID(2,N).EQ.ID(2,N-1).AND.
     2          ID(3,N).EQ.ID(3,N-1).AND.
     3          IDPARS(13,N).EQ.IDPARS(13,N-1).AND.
     4          IDPARS(14,N).EQ.IDPARS(14,N-1))THEN
C              PREVIOUS VARIABLE MATCHES.  THIS TAKES CARE OF
C              THE CASE WHEN THE DISCRETE BINARY IS THE UPPERMOST
C              OF THE GROUP AND HAS AN INPUT THRESHOLD OF 9999
C              CONSISTENT WITH U201.
C
            IF(TRESHU(N-1).EQ.99999.)THEN
C                 THIS IS FOR THE CASE WHEN THE IDS MATCH, BUT 
C                 THE UPPER THRESHOLD OF THE FIRST OF THE PAIR
C                 (N-1) HAS A THRESHOLD OF 99999 AS SET IN
C                 THE LOOP FOR N-1.  THIS MAY HAPPEN FOR U171.      
               TRESHU(N)=TRESHL(N)
               TRESHL(N)=-99999.
            ELSE
               TRESHU(N)=TRESHL(N)
               IF(TRESHU(N).EQ.9999.OR.
     1            TRESHU(N).EQ.99990.)TRESHU(N)=99999.
C                    INPUT ALLOWS ONLY 4 SIGNIFICANT PLACES.
C                    THE ABOVE STATEMENT HANDLES .9999E04 AND .9999E05.
               TRESHL(N)=TRESHU(N-1)
            ENDIF
C
         ELSE
C              PREVIOUS VARIABLE DOES NOT MATCH.
            TRESHU(N)=TRESHL(N)
            TRESHL(N)=-99999.
         ENDIF
C
      ENDIF           
C
 140  CONTINUE
C
      RETURN
      END 
