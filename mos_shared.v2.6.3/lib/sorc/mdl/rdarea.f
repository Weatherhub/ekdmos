      SUBROUTINE RDAREA(KFILDO,IP4,IP5,KFILD,CCALL,STNAME,NSUBNO,NWFO,
     1                  NGP,GPNAME,KGP,ND14,IER)
C 
C        OCTOBER   2002   GLAHN   TDL   NDFD-2002
C        DECEMBER  2002   GLAHN   CHANGED STNAME( ) TO CHARACTER*20
C                                 FROM CHARACTER*30; MODIFIED FORMATS
C                                 150, 1500, 154, 155
C        JANUARY   2003   TRIMARCO  ADDED COMMAS TO FORMAT STATEMENTS
C                                 FOR IBM COMPILATION
C
C        PURPOSE 
C            TO READ AN AREA LIST AND ASSOCIATED INFORMATION FROM AN
C            AREA DIRECTORY IN ONE OR MORE GROUPS AND RETURN THE
C            LIST IN THE ORDER READ BY GROUP.  FOR EACH GROUP, A GROUP
C            NAME IS READ.  THE AREA LIST IS PUT INTO CCALL( ).
C            NORMALLY, THE AREA IS IDENTIFIED BY A WFO'S CALL LETTERS.
C            THE SUBCENTER NUMBER IS EXPECTED TO MATCH THE WFO'S
C            AREA IN THE NDFD GRID.  WFOS CAN BE COMBINED INTO  
C            GROUPS, GIVING FLEXIBILITY TO VERIFY BY NWS REGION, ETC.
C            A SPECIAL AREA MAP COULD BE MADE WITH SUBCENTER NUMBERS
C            OUTSIDE THE WFO RANGE (POSSIBLY AN RFC SUBCENTER NUMBER),
C            CORRESPONDING ENTRIES MADE IN THE AREA DIRECTORY, AND
C            VERIFICATION MADE ON THAT BASIS, IF MORE FLEXIBILITY
C            IS NEEDED THAN COMBINATIONS OF WFOS.  THE CALL TO RDC
C            IS SET TO READ UP TO NT = 7 VALUES PER RECORD ACCORDING 
C            TO THE FORMAT (7(A8,1X)); HOWEVER, THE RECORD NEED NOT
C            HAVE THAT MANY VALUES.  THIS ROUTINE IS USED FOR READING
C            A COMPLETE LIST COMPOSED OF ONE OR MORE GROUPS.
C            READING STOPS WHEN AN EMPTY SET IN ENCOUNTERED.  A
C            DUPLICATE AREA WILL BE KEPT, BUT A DIAGNOSTIC IS OUTPUT.
C            THE ARRAYS TO BE FILLED ARE INITIALIZED.  RDAREA IS 
C            PRINCIPALLY USED FOR U855.
C
C        DATA SET USE 
C            KFILDO   - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (OUTPUT) 
C            IP4      - UNIT NUMBER FOR OUTPUT OF AREA LETTERS.
C                       (OUTPUT)
C            IP5      - UNIT NUMBER FOR OUTPUT OF DIRECTORY INFORMATION
C                       (OUTPUT) 
C            KFILD(J) - UNIT NUMBER FROM WHICH TO READ AREA LIST
C                       (J=1) AND AREA DIRECTORY (J=2).  IT IS
C                       ASSUMED FILES HAVE BEEN OPENED.  (INPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C                 IP4 = INDICATES WHETHER (>0) OR NOT (=0) THE AREA 
C                       LIST (AREA LETTERS ONLY) WILL BE WRITTEN TO UNIT
C                       IP4.  IF THERE ARE INPUT ERRORS, THE AREA 
C                       LIST WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE
C                       UNIT KFILDO AS WELL AS TO UNIT IP4.  (INPUT)
C                 IP5 = INDICATES WHETHER (>0) OR NOT (=0) THE AREA 
C                       DIRECTORY INFORMATION WILL BE WRITTEN TO UNIT
C                       IP5.  IF THERE ARE INPUT ERRORS, THE AREA
C                       LIST WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE
C                       UNIT KFILDO AS WELL AS TO UNIT IP5.  (INPUT) 
C            KFILD(J) = UNIT NUMBER FROM WHICH TO READ AREA LIST
C                       (J=1) AND AREA DIRECTORY (J=2).  IT IS
C                       ASSUMED FILES HAVE BEEN OPENED.  (INPUT) 
C                       (INPUT)
C            CCALL(K) = 8-CHARACTER AREA LETTERS, NORMALLY WFOS
C                       (K=1,ND14).  (CHARACTER*8)  (OUTPUT)
C           STNAME(K) = STATION (WFO) NAME (K=1,ND14).  (CHARACTER*20)
C                       (OUTPUT)
C           NSUBNO(K) = WFO SUBCENTER NUNBER (K=1,ND14).  (OUTPUT)
C                NWFO = THE TOTAL NUMBER OF AREAS (WFOS) RETURNED IN
C                       CCALL( ) AND NSUBNO( ).  (OUTPUT)
C              NGP(J) = SIZE OF KGP GROUPS OF AREAS (J=1,KGP), MAX
C                       OF ND14.  (OUTPUT)
C           GPNAME(J) = THE NAMES OF THE GROUPS (J=1,KGP) CORRESPONDING
C                       TO NGP(J).  (CHARACTER*30)  (OUTPUT)
C                 KGP = THE NUMBER OF VALUES RETURNED IN NGP( )
C                       AND GPNAME( ). (OUTPUT) 
C                ND14 = SIZE OF CCALL( ), GPNAME( ), STANAM( ),
C                       NGP( ), AND NSUBNO( ).  MAXIMUM NUMBER OF
C                       GROUPS, NORMALLY WFOS.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0   = GOOD RETURN.
C                       20  = ERROR OR EOF READING KFILD(1) IN RDC
C                             (ABORTS)
C                       21  = TOO MANY AREAS FOR CCALL(ND14) IN RDC
C                             (ABORTS)
C                       33  = ERROR ON UNIT KFILD(2) WHEN READING THE
C                             DIRECTORY (ABORTS WITH PRINT)
C                       34  = TOO MANY AREAS IN DIRECTORY TO BE 
C                             ACCOMMODATED BY ND14 WHEN USING ALL
C                             AREAS FROM THE DIRECTORY (ABORTS
C                             WITH PRINT)
C                       35  = ONE OR MORE AREAS NOT FOUND IN THE
C                             DIRECTORY (NORMAL RETURN)
C                       107 = KFILP(1) = KFILP(2). (ABORTS
C                             WITH PRINT)
C                         NOTE THAT DUPLICATE AREAS DO NOT HAVE
C                         AN IER CODE, BUT A DIAGNOSTIC IS WRITTEN.
C            CTEMP( ) = TEMPORARY ARRAY THAT MUST BE OF AT LEAST
c                       SIZE NT.  (CHARACTER*8)  (INTERNAL) 
C                  NT = NUMBER OF WORDS PER RECORD INDICATED IN FORMAT.
C                       SET BY PARAMETER, BECAUSE IS IS A DIMENSION.  
C                       (INTERNAL)
C              CCALLT = TO READ AREA LETTERS INTO FROM DIRECTORY.
C                       (CHARACTER*8)  (INTERNAL)
C               NAMET = TO READ NAME INTO FROM DIRECTORY.
c                       (CHARACTER*20) (INTERNAL)
C               NWFO1 = THE NUMBER OF AREAS READ IN A GROUP, RETURNED
C                       FROM RDC.  (INTERNAL)
C              BLANK8 = 8 BLANKS.  (CHARACTER*8)  (INTERNAL)
C               BLANK = 20 BLANKS   (CHARACTER*20)  (INTERNAL)
C           IFOUND(K) = USED TO KEEP TRACK OF THE AREAS FOUND IN THE
C                       DIRECTORY (K=1,NWFO).  (INTERNAL) 
C                       0 = NOT YET FOUND.
C                       1 = FOUND.
C                       7 = DUPLICATE.
C                       (AUTOMATIC) (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            RDC. 
C
      PARAMETER (NT=7)
C
      CHARACTER*8 CCALL(ND14),CTEMP(NT),BLANK8,CCALLT
      CHARACTER*20 NAMET,BLANK,STNAME(ND14)
      CHARACTER*30 GPNAME(ND14)
C
      DIMENSION NGP(ND14),NSUBNO(ND14)
      DIMENSION IFOUND(ND14)
C        IFOUND( ) IS AN AUTOMATIC ARRAY.
      DIMENSION KFILD(2)
C
      DATA BLANK8/' '/
      DATA BLANK/' '/
      DATA CCALLT/' '/
      DATA NAMET/' '/
C
      IER=0
      KGP=1
      NWFO=0
C
C        INITIALIZE ARRAYS.
C
      DO 103 K=1,ND14
      IFOUND(K)=0
      CCALL(K)=BLANK8
      GPNAME(K)=BLANK
      STNAME(K)=BLANK
      NGP(K)=0
 103  CONTINUE
C
C        KFILD(1) CANNOT EQUAL KFILD(2).
C
      IF(KFILD(1).EQ.KFILD(2))THEN
         WRITE(KFILDO,104)KFILD(1)
 104     FORMAT(/' ****KFILD(1) AND KFILD(2) ARE BOTH =',I3,
     1           '.  CHANGE ONE OR THE OTHER.')
         IER=107
         GO TO 160
      ENDIF
C
C        READ AREA LIST FROM UNIT NUMBER KFILD(1).
C     
 105  CALL RDC(KFILDO,IP4,KFILD(1),CCALL(NWFO+1),ND14-NWFO,CTEMP,NT,
     1         '(7(A8,1X))',NWFO1,'99999999',IER)
C        NT = 7 WITH FMT = '(7(A8,1X))' MEANS THAT UP TO 7 VALUES WILL
C        BE READ PER RECORD.  FEWER CAN BE PRESENT.  IF THE LAST
C        VALUE IS NOT COMPLETE, IT WILL BE BLANK FILLED ON THE RIGHT.
C        THAT IS, 'OKC     ' COULD BE 'OKC' OR 'OKC '.
      IF(IER.NE.0.AND.NWFO1.NE.0)
     1      WRITE(KFILDO,110)NWFO1,KGP,
     2                       (CCALL(K),K=NWFO+1,NWFO+NWFO1)
 110  FORMAT(/,' ',I4,' AREAS INPUT FOR GROUP NO.',I4,/,
     1      (14(1X,A8)))
C        IER FROM RDC WILL BE OVERWRITTEN IF THERE ARE DUPLICATE
C        AREAS IN THE LIST, OR IF AN AREA CANNOT BE FOUND
C        IN THE DIRECTORY. WHEN IER NE 0, A DIAGNOSTIC WILL
C        HAVE BEEN WRITTEN BY RDC.
      IF(NWFO1.EQ.0)GO TO 120
C        AN EMPTY SET TERMINATES READING.
      IF(IP4.EQ.0)GO TO 115
      IF(IP4.EQ.KFILDO.AND.IER.NE.0.AND.NWFO1.NE.0)GO TO 115
      WRITE(IP4,110)NWFO1,KGP,(CCALL(K),K=NWFO+1,NWFO+NWFO1)
C        AREA LIST MAY BE WRITTEN TWICE WHEN THERE ARE ERRORS
C        DETECTED, ONCE TO THE DEFAULT OUTPUT FILE AND ONCE TO UNIT
C        IP4.
 115  IF(KGP.LE.ND14)GO TO 117
      WRITE(KFILDO,116)ND14
 116  FORMAT(/,' ****TOO MANY GROUPS IN RDAREA FOR DIMENSION ND14 =',
     1       I7)
      IER=34
      KGP=KGP-1
      GO TO 160
C
 117  NGP(KGP)=NWFO1
      NWFO=NWFO+NWFO1
      IF(IER.NE.0)GO TO 160
C        WHEN IER NE 0 FROM RDC, NO USE OF ENTERING RDC AGAIN.
C
C        READ GROUP NAME.
C
      READ(KFILD(1),118)GPNAME(KGP)
 118  FORMAT(A30)
      KGP=KGP+1
      GO TO 105
C
C        MUST DECREASE KGP, BECAUSE IS IS THE VALUE OF THE NEXT
C        ENTRY INTO NGP( ).
C
 120  KGP=KGP-1
C
C        READ DIRECTORY.
C
 121  CCALLT=BLANK8
      NAMET(1:20)=BLANK
      READ(KFILD(2),122,IOSTAT=IOS,ERR=123,END=1405)CCALLT,
     1      NSUBNT,NAMET(1:17),NAMET(19:20)
 122  FORMAT(A8,1X,I8,1X,A17,4X,A2)
      GO TO 130
C
C        ERROR READING DIRECTORY.
C
 123  WRITE(KFILDO,124)IOS
      IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,124)IOS
      IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)WRITE(IP5,124)IOS
 124  FORMAT(/,' ****ERROR READING AREA DIRECTORY IN RDAREA.',
     1         '  IOSTAT =',I5)
      IER=33
      GO TO 1405
C
C        AN AREA LIST RESIDES IN CCALL( ).  DETERMINE WHETHER THIS
C        DIRECTORY ENTRY MATCHES AN AREA IN CCALL( ) AND
C        IS TO BE KEPT.  DUPLICATE AREAS WILL BE KEPT.
C        AREAS NOT IN THE DIRECTORY WILL RETAIN THEIR PLACE AS INPUT.
C
 130  JP1=0
C        JP1 IS SET TO 0 FOR EVERY DIRECTORY ENTRY READ.
C
      ISTART=0
C
      DO 140 M=1,KGP
C
      DO 139 KL=ISTART+1,ISTART+NGP(M)
      IF(IFOUND(KL).GT.0)GO TO 139
C        ABOVE TEST GUARDS AGAINST DUPLICATES IN THE DIRECTORY.
C        IF THERE IS A DUPLICATE, THE FIRST ENTRY IS USED.
C
C        CHECK FOR AREAS.
C
      IF(CCALLT.NE.CCALL(KL))GO TO 139
C
      JP1=JP1+1
      IF(KL.LE.ND14)GO TO 135
C        ND14 ABOUT TO BE EXCEEDED.  (THIS SHOULD HAVE BEEN DISCERNED
C        EARLIER.)
      WRITE(KFILDO,132)ND14
      IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,132)ND14
      IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)WRITE(IP5,132)ND14
 132  FORMAT(/' ****TOO MANY AREAS IN LIST FOR DIMENSION ND14 =',
     1       I7,'.')
      IER=34
      GO TO 1405
C
C        THIS IS THE AREA (WFO) LETTERS.  PLACE THE NAME IN
C        STNAME( ) AND THE SUBCENTER NUMBER OF THE WFO IN NSUBNO( ).
C        MSTA COUNTS THE STATIONS.
C
 135  STNAME(KL)(1:17)=NAMET(1:17)
      STNAME(KL)(19:20)=NAMET(19:20)
      NSUBNO(KL)=NSUBNT
      MSTA=MSTA+1
C
      IF(JP1.EQ.1)THEN
         IFOUND(KL)=1
      ELSE
         IFOUND(KL)=7
      ENDIF
C
C        IFOUND( ) CORRESPONDS TO THE ORIGINAL LIST AS READ.
C        EVEN THOUGH THIS DIRECTORY ENTRY HAS BEEN USED AND ENTERED
C        INTO THE LIST, THE SEARCH IS CONTINUED IN CASE THERE
C        ARE DUPLICATE AREAS IN THE LIST.
C
 139  CONTINUE
C
      ISTART=ISTART+NGP(M)
 140  CONTINUE
C
      GO TO 121
C
C        ARRAYS FOR AREAS NOT IN THE DIRECTORY MUST BE
C        GIVEN VALUES.  DUPLICATE AREAS AND AREAS NOT 
C        FOUND IN THE DIRECTORY ARE LISTED ACCORDING TO THE
C        ORDER INPUT.
C
 1405 JER=0
      ISTART=0
C
      DO 146 M=1,KGP
C      
      DO 145 KL=ISTART+1,ISTART+NGP(M)
c
      IF(IFOUND(KL).EQ.0)THEN
C
         IF(JER.EQ.0)THEN
            WRITE(KFILDO,141)CCALL(KL)
            IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,141)CCALL(KL)
            IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1             WRITE(IP5,141)CCALL(KL)
 141        FORMAT(/' ****AREA ',A8,' NOT FOUND IN DIRECTORY')
         ELSE
            WRITE(KFILDO,1410)CCALL(KL)
            IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,1410)CCALL(KL)
            IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1             WRITE(IP5,1410)CCALL(KL)
 1410       FORMAT(' ****AREA ',A8,' NOT FOUND IN DIRECTORY')
         ENDIF
C
         JER=1
      ELSE
C
         IF(IFOUND(KL).EQ.7)THEN
C
            IF(JER.EQ.0)THEN
               WRITE(KFILDO,142)CCALL(KL)
               IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,142)CCALL(KL)
               IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1                WRITE(IP5,142)CCALL(KL)
 142           FORMAT(/' ****AREA ',A8,' IS A DUPLICATE.')
            ELSE
               WRITE(KFILDO,1420)CCALL(KL)
               IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,1420)CCALL(KL)
               IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1                WRITE(IP5,1420)CCALL(KL)
 1420          FORMAT(' ****AREA ',A8,' IS A DUPLICATE.')
            ENDIF
C
            JER=1
         ENDIF
C    
      ENDIF
C
 145  CONTINUE      
C 
      ISTART=ISTART+NGP(M)
 146  CONTINUE
C 
C        WRITE AREA LISTS TO KFILDO WHEN THERE HAS BEEN AN 
C        ERROR AND TO IP5 AS DESIRED.  WRITING IS BY GROUP UNLESS
C        SINGLE AREA EQUATIONS ARE BEING PRODUCED, IN
C        WHICH CASE KGP = NWFO.  NOTE THAT ONLY ONE GROUP
C        OF TWO AREAS WILL CAUSE LISTING BY GROUP.
C
      ISTART=0
      KIP5=0
      KDO=0
C
C        KIP5 AND KDO ARE COUNTERS THAT KEEP THE HEADING
C        FROM PRINTING MORE THAN ONCE ON IP5 AND KFILDO,
C        RESPECTIVELY.  FORMATS 150 AND 1500 BELOW ARE USED
C        REPETIVELY IN THE PRINT LOOP.
C
 150  FORMAT(/' ',I4,
     1        ' AREAS (WFOS) AND DIRECTORY INFORMATION (IF NOT',
     2        ' LISTED BY GROUP, IMPLIES ONE AREA PER GROUP)'//
     3        '               WFO       WFO NAME      ',
     4        '        SUBCENTER NO.')
 1500 FORMAT(/' ',I4,
     1        ' AREAS (WFOS) AND DIRECTORY INFORMATION'//
     3        '               WFO       WFO NAME      ',
     4        '        SUBCENTER NO.')
C
      DO 158 M=1,KGP
C
      DO 157 KL=ISTART+1,ISTART+NGP(M)
C
      IF(IER.EQ.0)THEN
C
         IF(IP5.NE.0)THEN
C
            IF(KIP5.EQ.0)THEN
C
               IF(KGP.EQ.NWFO)THEN
                  WRITE(IP5,150)NWFO
               ELSE
                  WRITE(IP5,1500)NWFO
               ENDIF
C
               KIP5=1
            ENDIF
C
            IF(KL.EQ.ISTART+1.AND.KGP.NE.NWFO)THEN
               WRITE(IP5,153)M,NGP(M),GPNAME(M)
 153           FORMAT(/' GROUP',I4,',',I5,' AREAS   ',A30)
               WRITE(IP5,154)CCALL(KL),STNAME(KL),NSUBNO(KL)
 154           FORMAT(/' ',14X,A8,2X,A20,I9)
            ELSE
               WRITE(IP5,155)CCALL(KL),STNAME(KL),NSUBNO(KL)
 155           FORMAT(' ',14X,A8,2X,A20,I9)
            ENDIF
C
         ENDIF
C
      ELSE
C
         IF(KDO.EQ.0)THEN
C
            IF(KGP.EQ.NWFO)THEN
               WRITE(KFILDO,150)NWFO
            ELSE
               WRITE(KFILDO,1500)NWFO
            ENDIF
C
            KDO=1
         ENDIF
C
         IF(KL.EQ.ISTART+1.AND.KGP.NE.NWFO)THEN
            WRITE(KFILDO,153)M,NGP(M),GPNAME(M)
            WRITE(KFILDO,154)CCALL(KL),STNAME(KL),NSUBNO(KL)
         ELSE
            WRITE(KFILDO,155)CCALL(KL),STNAME(KL),NSUBNO(KL)
         ENDIF
C   
         IF(IP5.NE.0.AND.IP5.NE.KFILDO)THEN
C
            IF(KL.EQ.ISTART+1.AND.KGP.NE.NWFO)THEN
               WRITE(IP5,153)M,NGP(M),GPNAME(M)
               WRITE(IP5,154)CCALL(KL),STNAME(KL),NSUBNO(KL)
            ELSE
               WRITE(IP5,155)CCALL(KL),STNAME(KL),NSUBNO(KL)
            ENDIF
C
         ENDIF
C
      ENDIF
C
 157  CONTINUE
C
      ISTART=ISTART+NGP(M)
 158  CONTINUE
C
 160  RETURN
      END
