      SUBROUTINE TPCP3(KFILDO,KFIL10,IDPARS,JD,NDATE,
     1                NGRIDC,ND11,NSLAB,IPACK,IWORK,FDTP,ND5,
     2                LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3                IS0,IS1,IS2,IS4,ND7,
     4                FD1,ND2X3,
     5                ISTAV,L3264B,MISTOT,IER)
C
C        SEPTEMBER 1998   SFANOS   TDL   MOS-2000
C        OCTOBER   1998   SFANOS   MODIFIED FOR CONV PRECIP
C        MAY       1999   SFANOS   RESTRUCTURED PROGRAM
C        AUGUST    2002   SFANOS   OLDER VERSION IN LIBRARY; GOT
C                                  RID OF FD2 AND JUST USED FD1;
C                                  GOT RID OF CALL TO NONCNVP
C        OCTOBER   2002   WEISS    CHANGED ND5 TO ND2X3
C        MAY       2003   GLAHN    DIAGNOSTICS; WHITE SPACE;
C                                  ICCCFFF( ) IN DATA STATEMENT;
C                                  REARRANGED TYPE STATEMENTS;
C                                  REPLACED FD1 BY FDTP IN SOME
C                                  CALLS TO GFETCH AND ELIMINATED
C                                  LOOPS SETTING FDTP( ) = FD1( );
C                                  CHANGED DIMENSIONS OF IPACK( ),
C                                  IWORK( ), FDTP( ) TO ND5;
C                                  CHANGED CALCULATION LOOP FROM
C                                  I=1,ND2X3 TO I=1,MDX*MWX
C        JUNE       2003   GLAHN   REMOVED INITIALIZING MDX, MDY,
C                                  NSLABT AFTER FIRST CALL TO GFETCH
C        NOVEMBER   2003   SMB     ADDED TWO OTHER POSSIBILITIES
C                                  FOR USE WITH ETA32 DATA.  CORRECTED
C                                  I=1,MDX*MWX TO I=1,MDX*MDY.  ADDED
C                                  CHECK FOR NEGATIVE CALCULATIONS.
C
C        PURPOSE
C            TO COMPUTE THE 3-HR TOTAL OR CONVECTIVE PRECIP 
C            AMOUNT FOUR POSSIBILITIES ARE TRIED IN ORDER:
C              1)  A 3-HR AMOUNT DIRECTLY AT PROJECTION IDPARS(12)
C              2)  A 6-HR AMOUNT AT PROJECTION IDPARS(12) MINUS
C                  A 3-HR AMOUNT AT PROJECTION IDAPRS(12)-3
C              3)  A 9-HR AMOUNT AT PROJECTION IDPARS(12) MINUS
C                  A 6-HR AMOUNT AT PROJECTION IDAPRS(12)-3
C              4)  A 12HR AMOUNT AT PROJECTION IDPARS(12) MINUS
C                  A 9-HR AMOUNT AT PROJECTION IDAPRS(12)-3
C                   
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C
C               003 205 - 3-HR TOT PRECIP AMT
C                         VARIABLE ON AN ISOBARIC SURFACE
C               003 235 - 3-HR CONVECTIVE PRECIP AMT
C                         VARIABLE ON AN ISOBARIC SURFACE
C
C        DATA SET USE
C            KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT(PRINT) FILE.
C                     (OUTPUT)
C            KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM
C                     ACCESS.(INPUT-OUTPUT)
C
C        VARIABLES
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA
C                       IDENTIFIED IN LSTORE(,) (J=1,ND10).
C                       WHEN CORE() IS FULL DATA ARE STORED ON DISK.
C                       (INPUT)
C              FD1(J) = WORK ARRAY TO HOLD THE 3 HOUR TOTAL OR
C                       CONVECTIVE PRECIPITATION. (J=1,ND2X3)
C                       (INTERNAL)
C             FDTP(J) = ARRAY TO HOLD RETURNED DATA WHEN THE DATA ARE
C                       AT GRIDPOINTS. IN THIS CASE, 3 HOUR
C                       TOTAL PRECIPITATION(J=1,ND5). (OUTPUT)
C                   I = LOOP CONTROL VARIABLE
C           ICCCFFF() = CONTAINS IDPARS(1) AND IDPARS(2) ID FOR THE
C                       METEOROLOGICAL PARAMETERS BEING USED.
C                       COLUMN 1 CONTAINS ID FOR ISOBARIC SURFACE.
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID() (J=1,15).
C                       (INPUT)
C                       J=1--CCC      (CLASS OF VARIABLE),
C                       J=2--FFF      (SUBCLASS OF VARIABLE),
C                       J=3--B        (BINARY INDICATOR),
C                       J=4--DD       (DATA SOURCE, MODEL NUMBER),
C                       J=5--V        (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY
C                                      1 LAYER)
C                       J=7--LTLTLTLT (TOP OF LAYER)
C                       J=8--T        (TRANSFORMATION)
C                       J=9--RR       (RUN TIME OFFSET, ALWAYS +
C                                      AND BACK IN TIME)
C                       J=10-OT       (TIME APPLICATION)
C                       J=11-OH       (TIME PERIOD IN HOURS)
C                       J=12-TAU      (PROJECTION IN HOURS)
C                       J=13-I        (INTERPOLATION TYPE)
C                       J=14-S        (SMOOTHING INDICATOR)
C                       J=15-G        (GRID INDICATOR)
C                 IER = STATUS RETURN
C                         0 = GOOD RETURN
C                       100 = THE TWO GRIDS NEEDED ARE NOT THE SAME SIZE
C                       101 = GRID SIZE IS TOO BIG FOR ???(), WHOSE 
C                             DIMENSION IS ND5.
C                       103 = IDPARS(1) AND IDPARS(2) DO NOT INDICATE
C                             TPCP3.
C                       SEE GFETCH FOR OTHER VALUES.
C                       WHEN IER NE 0, DATA ARE RETURNED AS MISSING.
C                       (INTERNAL-OUTPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5). (INTERNAL)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       IS2(3) AND IS2(4) ARE USED BY THE CALLING
C                       PROGRAM AS THE GRID DIMENSIONS.
C                       (INTERNAL-OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (INTERNAL)
C                 ISO = 1 FOR ISOBARIC, 2 FOR CONSTANT PRESSURE 
C                       SURFACE,3 FOR SIGMA SURFACE(INTERNAL)
C               ISTAV = 0 SINCE THE DATA RETURNED ARE GRID DATA.
C                       (OUTPUT)
C            IWORK(J) = WORK ARRAY (J=1,ND5). (INTERNAL)
C                   J = LOOP CONTROL VARIABLE
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT
C                       THE PORTIONS PERTAINING TO PROCESSING
C                       ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       ID() IS USED TO HELP IDENTIFY THE BASIC MODEL
C                       FIELDS AS READ FROM THE ARCHIVE. (INPUT)
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT)
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64). (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE(,)
C                       THAT HAVE BEEN USED IN THIS RUN. (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE
C                       DATA STORED (L=1,12) (J=1,LITEMS).
C                       (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE() WHERE
C                              THE DATA START.  WHEN ON DISK,
C                              THIS IS MINUS THE RECORD NUMBER WHERE
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR(, ,L) AND
C                              IN NGRIDC(,L) DEFINING THE
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID(,N) (N=1,NPRED) FOR WHICH
C                              THIS VARIABLE IS NEEDED, WHEN IT IS
C                              NEEDED ONLY ONCE FROM LSTORE(,).
C                              WHEN IT IS NEEDED MORE THAN ONCE, THE 
C                              VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING
C                              MSTORE(,). LATER USED AS A WAY OF
C                              DETERMINING WHETHER TO KEEP THIS
C                              VARIABLE.
C             MDX,MDY = DIMENSIONS OF GRID RETURNED FOR 3 HOUR
C                       TOTAL PRECIPITATION AMOUNT (INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS
C                       0 FROM CALLING GFETCH WHEN THERE IS NO
C                       PRIMARY MISSING VALUE. (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS
C                       0 FROM CALLING GFETCH WHEN THERE IS NO
C                       PRIMARY MISSING VALUE.  (INTERNAL)
C              MISTOT = TOTAL NUMBER OF TIMES A MISSING INDICATOR
C                       HAS BEEN ENCOUNTERED IN UNPACKING GRIDS.
C                       (INPUT-OUTPUT)
C             MTP3(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED
C                       INTO FDTP() (J=1,4). (INTERNAL)
C             MTP6(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED
C                       INTO FDTP() (J=1,4). (INTERNAL)
C             MTP9(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED
C                       INTO FDTP() (J=1,4). (INTERNAL)
C            MTP12(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED
C                       INTO FDTP() (J=1,4). (INTERNAL)
C             MWX,MWY = DIMENSIONS OF GRID RETURNED FOR 6 HOUR
C                       TOTAL PRECIPITATION AMOUNT (INTERNAL)
C             MXX,MXY = DIMENSIONS OF GRID RETURNED FOR 9 HOUR
C                       TOTAL PRECIPITATION AMOUNT (INTERNAL)
C             MYX,MYY = DIMENSIONS OF GRID RETURNED FOR 12 HOUR
C                       TOTAL PRECIPITATION AMOUNT (INTERNAL)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE. (INPUT)
C               ND2X3 = DIMENSION OF SEVERAL VARIABLES.  THE SIZE OF
C                       THE GRID IS NOT KNOWN BEFORE FDTK AND FDDP
C                       ARE FETCHED.  (INPUT)
C                 ND5 = DIMENSION OF IPACK(), IWORK(), AND FDTP( ). 
C                       (INPUT)
C                 ND7 = DIMENSION OF IS0(),IS1(),IS2(), AND IS4().
C                       NOT ALL LOCATIONS ARE USED. (INPUT)
C                 ND9 = THE SECOND DIMENSION OF LSTORE(,). (INPUT)
C                ND10 = DIMENSION OF CORE(). (INPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN
C                       BE DEALT WITH ON THIS RUN.  LAST DIMENSION 
C                       OF NGRIDC(,). (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.
C                       (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING COUNT FROM THE BEGINNING OF
C                       THE PROGRAM.  THIS COUNT IS MAINTAINED IN
C                       CASE THE USER NEEDS IT(DIAGNOSTICS, ETC.).
C                       (OUTPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR
C                       EACH GRID COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=
C                            POLAR STEREOGRAPHIC).
C                       L=2--GRID LENGTH IN METERS.
C                       L=3--LATITUDE AT WHICH THE GRID LENGTH IS
C                            CORRECT *1000.
C                       L=4--GRID ORIENTATION IN DEGREES * 1000.
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *1000.
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES
C                            *1000.
C               NPACK = 2 FOR TDL GRIB PACKED DATA; 1 FOR NOT PACKED
C                       THIS IS RETURNED FROM GFETCH. (INTERNAL)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR(, ,) AND
C                       IN NGRIDC(,) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID. (OUTPUT)
C              NSLABL = SAME AS NSLAB.  RETURNED FROM CALLING GFETCH
C                       FOR 3-HR PRECIP. (INTERNAL)
C              NSLABN = SAME AS NSLAB.  RETURNED FROM CALLING GFETCH
C                       FOR 6-HR PRECIP. (INTERNAL)
C              NSLABP = SAME AS NSLAB.  RETURNED FROM CALLING GFETCH
C                       FOR 9-HR PRECIP. (INTERNAL)
C              NSLABQ = SAME AS NSLAB.  RETURNED FROM CALLING GFETCH
C                       FOR 12HR PRECIP. (INTERNAL)
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE,
C                       THAT THE RECORD HAS BEEN FETCHED.  THIS IS 
C                       STORED IN LSTORE(9,). (INTERNAL)
C              NWORDS = NUMBER OF WORDS RETURNED IN DATA().  THIS 
C                       IS RETURNED FROM GFETCH (INTERNAL)
C        1         2         3         4         5         6         7 X
C
C     NON SYSTEM SUBROUTINES USED
C        GFETCH
C
      IMPLICIT NONE
C
      INTEGER JD(4),IDPARS(15)
      INTEGER IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      INTEGER IPACK(ND5),IWORK(ND5) 
      INTEGER LSTORE(12,ND9)
      INTEGER NGRIDC(6,ND11)
      INTEGER ICCCFFF(12),MTP3(4),MTP6(4),MTP9(4),MTP12(4)
      INTEGER I,IER,ISTAV,J,KFILDO,KFIL10,L3264B,LITEMS,
     1        MDX,MDY,MISSP,MISSS,MISTOT,MWX,MWY,MXX,MXY,MYX,MYY,
     2        NBLOCK,ND2X3,ND5,ND7,ND9,ND10,
     3        ND11,NDATE,NFETCH,NPACK,
     4        NSLAB,NSLABL,NSLABN,NSLABP,NSLABQ,NTIMES,NWORDS
C
      REAL FDTP(ND5)
      REAL FD1(ND2X3)
      REAL CORE(ND10)
C
      DATA ICCCFFF/003205,
     1             003235,
     2             003265,
     3             003210,
     4             003240,
     5             003270,
     6             003215,
     7             003245,
     8             003275,
     9             003220,
     A             003250,
     B             003280/
C       ABOVE IDS ARE IN ORDER:
C        3-HR TOTAL
C        3-HR CONVECTIVE
C        3-HR NON-CONVECTIVE
C        6-HR TOTAL
C        6-HR CONVECTIVE
C        6-HR NON CONVECTIVE
C        9-HR TOTAL
C        9-HR CONVECTIVE
C        9-HR NON CONVECTIVE
C        12HR TOTAL
C        12HR CONVECTIVE
C        12HR NON CONVECTIVE
C
      IER=0
      ISTAV=0
C
C        MAKE SURE THIS SUBROUTINE DEALS WITH THE PREDICTOR.
C
      IF(IDPARS(1).NE.003.OR.(IDPARS(2).NE.205.AND.IDPARS(2).NE.
     1             235))THEN
         WRITE(KFILDO,101)(JD(J),J=1,4)
 101     FORMAT(/' ****IDPARS(1) AND IDPARS(2) DO NOT INDICATE ',
     1           '3-HOUR TOTAL OR CONVECTIVE PRECIP AMT.',
     2          /'     VARIABLE ',I9.9,2I10.9,I4.3,
     3           ' NOT COMPUTED IN TPCP3.')
         IER=103
         GOTO 800
      END IF
C
C        CHECK IF PROJECTION IS LESS THAN 3 HOURS.
C
      IF(IDPARS(12).LT.3)THEN
         WRITE(KFILDO,105)IDPARS(12),(JD(J),J=1,4)
 105     FORMAT(/' ****PROJECTION ENTERED IN THE CONTROL FILE =',I5,
     1           ' IS LESS THAN 3.',
     2          /'     VARIABLE ',I9.9,2I10.9,I4.3,
     3           ' NOT COMPUTED IN TPCP3.')
         IER=187
         GOTO 800
      END IF
C
C        CREATE ID FOR THE 3 HOUR PRECIP AMT ENDING AT IDPARS(12).
C
      MTP3(1)=JD(1)
      MTP3(2)=IDPARS(7)
      MTP3(3)=IDPARS(9)*1000000+IDPARS(12)
      MTP3(4)=0
C
C        READ THE 3-HR TOTAL OR CONVECTIVE PRECIP AMT.
C
      CALL GFETCH(KFILDO,KFIL10,MTP3,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDTP,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.EQ.0)GOTO 900
C        THE 3-HR AMOUNT HAS BEEN FOUND.
C
C        THE 3-HR AMOUNT WAS NOT THERE AT IDPARS(12), SO TRY
C        TO GET THE 6-HR AMOUNT ENDING AT IDPARS(12) AND SUBTRACT
C        THE 3-HR AMOUNT AT IDPARS(12)-3.
C
 200  IF(IDPARS(2).EQ.235)THEN
         MTP6(1)=ICCCFFF(5)*1000+IDPARS(4)
      ELSEIF(IDPARS(2).EQ.205)THEN
         MTP6(1)=ICCCFFF(4)*1000+IDPARS(4)
      END IF
C
      MTP6(2)=IDPARS(7)
      MTP6(3)=IDPARS(9)*1000000+IDPARS(12)
      MTP6(4)=0
C
C        READ 6-HR TOTAL OR CONVECTIVE AMOUNT.
C
      CALL GFETCH(KFILDO,KFIL10,MTP6,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDTP,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0) THEN      
C           AT THIS POINT, READING 3-HR AND 6-HR AMOUNTS ENDING AT
C           IDPARS(12) HAVE FAILED.  TRY THE 9-HR AMOUNT.
         GO TO 300
      END IF
C
      NSLABN=NSLAB
      MWX=IS2(3)
      MWY=IS2(4)
C
C        CHECK FOR A 3-HR AMOUNT NOW AND SUBTRACT FROM THE
C        6-HR TO GET A 3-HR.
C
      MTP3(1)=JD(1)
      MTP3(2)=IDPARS(7)
      MTP3(3)=IDPARS(9)*1000000+IDPARS(12)-3
      MTP3(4)=0
C
C        READ 3-HR AMOUNT IF CONV OR TOTAL.
C
      CALL GFETCH(KFILDO,KFIL10,MTP3,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,220)(JD(J),J=1,4)
 220     FORMAT(/' ****VARIABLE ',I9.9,2I10.9,I4.3,
     1           ' NOT COMPUTED IN TPCP3.  3-HR PRECIP',
     2           ' NOT FOUND BY GFETCH AT 220.')
         GO TO 800
      ENDIF
C
      NSLABL=NSLAB
      MDX=IS2(3)
      MDY=IS2(4)
C
C        CHECK THE NSLABS.
C
      IF(NSLABL.NE.NSLABN)THEN
         WRITE(KFILDO,230)NSLABL,NSLABN,(JD(J),J=1,4)
 230     FORMAT(/' ****THE GRID CHARACTERISTICS OF THE 3-HR',
     1           ' AND 6-HR PRECIPITATION AMOUNT GRIDS',
     2           ' ARE DIFFERENT.',I3,2X,I3,
     3          /'     VARIABLE ',I9.9,2I10.9,I4.3,
     4           ' NOT COMPUTED IN TPCP3.')
         IER=100
         GOTO 800
      END IF
C
C        COMPARE IF THE GRID SIZES ARE THE SAME.
C
      IF(MDX.NE.MWX.OR.MDY.NE.MWY)THEN
         WRITE(KFILDO,235)(MTP3(J),J=1,4),(NGRIDC(J,NSLABL),
     1                     J=1,6),MDX,MDY,
     2                    (MTP6(J),J=1,4),(NGRIDC(J,NSLABN),
     3                     J=1,6),MWX,MWY
 235     FORMAT(/' ****THE GRIDS NEEDED IN TPCP3 HAVE DIFFERENT',
     1           ' CHARACTERISTICS.  PREDICTOR NOT COMPUTED.',
     2           ' VALUES FROM NGRIDC( , ) AND MX,MY.'/
     3           (5X,I9.9,I10.9,I10.9,I4.3,4X,6I10,4X,2I5))
         IER=100
         GOTO 800
      END IF
C
C        CALCULATIONS.
C
      DO I=1,MDX*MDY
         FDTP(I)=FDTP(I)-FD1(I)
         IF(FDTP(I).LT.0.) FDTP(I)=0.
      END DO
C
C        3-HR PRECIP AMOUNT HAS BEEN CALCULATED BY SUBTRACTING
C        A 3-HR PRECIP AMOUNT AT IDPARS(12)-3 FROM A 6-HR 
C        AMOUNT AT IDPARS(12).
C
      GOTO 900
C
C        THE 6-HR AMOUNT WAS NOT THERE AT IDPARS(12), SO TRY
C        TO GET THE 9-HR AMOUNT ENDING AT IDPARS(12) AND SUBTRACT
C        THE 6-HR AMOUNT AT IDPARS(12)-3.
C
 300  IF(IDPARS(2).EQ.235)THEN
         MTP9(1)=ICCCFFF(8)*1000+IDPARS(4)
      ELSEIF(IDPARS(2).EQ.205)THEN
         MTP9(1)=ICCCFFF(7)*1000+IDPARS(4)
      END IF
C
      MTP9(2)=IDPARS(7)
      MTP9(3)=IDPARS(9)*1000000+IDPARS(12)
      MTP9(4)=0
C
C        READ 9-HR TOTAL OR CONVECTIVE AMOUNT.
C
      CALL GFETCH(KFILDO,KFIL10,MTP9,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDTP,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0) THEN      
C           AT THIS POINT, READING 3-HR, 6-HR, AND 9-HR AMOUNTS 
C           ENDING AT IDPARS(12) HAVE FAILED.  TRY THE 12HR AMOUNT.
         GO TO 400
      END IF
C
      NSLABP=NSLAB
      MXX=IS2(3)
      MXY=IS2(4)
C
C        CHECK FOR A 6-HR AMOUNT NOW AND SUBTRACT FROM THE
C        9-HR TO GET A 3-HR.
C
      IF(IDPARS(2).EQ.235)THEN
         MTP6(1)=ICCCFFF(5)*1000+IDPARS(4)
      ELSEIF(IDPARS(2).EQ.205)THEN
         MTP6(1)=ICCCFFF(4)*1000+IDPARS(4)
      END IF
C
      MTP6(2)=IDPARS(7)
      MTP6(3)=IDPARS(9)*1000000+IDPARS(12)-3
      MTP6(4)=0
C
C        READ 6-HR AMOUNT IF CONV OR TOTAL.
C
      CALL GFETCH(KFILDO,KFIL10,MTP6,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,320)(JD(J),J=1,4)
 320     FORMAT(/' ****VARIABLE ',I9.9,2I10.9,I4.3,
     1           ' NOT COMPUTED IN TPCP3.  6-HR PRECIP',
     2           ' NOT FOUND BY GFETCH AT 320.')
         GO TO 800
      ENDIF
C
      NSLABN=NSLAB
      MWX=IS2(3)
      MWY=IS2(4)
C
C        CHECK THE NSLABS.
C
      IF(NSLABN.NE.NSLABP)THEN
         WRITE(KFILDO,330)NSLABN,NSLABP,(JD(J),J=1,4)
 330     FORMAT(/' ****THE GRID CHARACTERISTICS OF THE 6-HR',
     1           ' AND 9-HR PRECIPITATION AMOUNT GRIDS',
     2           ' ARE DIFFERENT.',I3,2X,I3,
     3          /'     VARIABLE ',I9.9,2I10.9,I4.3,
     4           ' NOT COMPUTED IN TPCP3.')
         IER=100
         GOTO 800
      END IF
C
C        COMPARE IF THE GRID SIZES ARE THE SAME.
C
      IF(MWX.NE.MXX.OR.MWY.NE.MXY)THEN
         WRITE(KFILDO,335)(MTP6(J),J=1,4),(NGRIDC(J,NSLABN),
     1                     J=1,6),MWX,MWY,
     2                    (MTP9(J),J=1,4),(NGRIDC(J,NSLABP),
     3                     J=1,6),MXX,MXY
 335     FORMAT(/' ****THE GRIDS NEEDED IN TPCP3 HAVE DIFFERENT',
     1           ' CHARACTERISTICS.  PREDICTOR NOT COMPUTED.',
     2           ' VALUES FROM NGRIDC( , ) AND MX,MY.'/
     3           (5X,I9.9,I10.9,I10.9,I4.3,4X,6I10,4X,2I5))
         IER=100
         GOTO 800
      END IF
C
C        CALCULATIONS.
C
      DO I=1,MWX*MWY
         FDTP(I)=FDTP(I)-FD1(I)
         IF(FDTP(I).LT.0.) FDTP(I)=0.
      END DO
C
C
C        3-HR PRECIP AMOUNT HAS BEEN CALCULATED BY SUBTRACTING
C        A 6-HR PRECIP AMOUNT AT IDPARS(12)-3 FROM A 9-HR 
C        AMOUNT AT IDPARS(12).
C
      GOTO 900
C
C        THE 3-HR, 6-HR, AND 9-HR AMOUNTS WERE NOT THERE AT 
C        IDPARS(12), SO TRY TO GET THE 12HR AMOUNT ENDING AT 
C        IDPARS(12) AND SUBTRACT THE 9-HR AMOUNT AT IDPARS(12)-3.
C
 400  IF(IDPARS(2).EQ.235)THEN
         MTP12(1)=ICCCFFF(11)*1000+IDPARS(4)
      ELSEIF(IDPARS(2).EQ.205)THEN
         MTP12(1)=ICCCFFF(10)*1000+IDPARS(4)
      END IF
C
      MTP12(2)=IDPARS(7)
      MTP12(3)=IDPARS(9)*1000000+IDPARS(12)
      MTP12(4)=0
C
C        READ 12HR TOTAL OR CONVECTIVE AMOUNT.
C
      CALL GFETCH(KFILDO,KFIL10,MTP12,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDTP,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0) THEN      
C           AT THIS POINT, EVERY POSSIBILITY HAS FAILED. 
         GO TO 800
      END IF
C
      NSLABQ=NSLAB
      MYX=IS2(3)
      MYY=IS2(4)
C
C        CHECK FOR A 9-HR AMOUNT NOW AND SUBTRACT FROM THE
C        12HR TO GET A 3-HR.
C
      IF(IDPARS(2).EQ.235)THEN
         MTP9(1)=ICCCFFF(8)*1000+IDPARS(4)
      ELSEIF(IDPARS(2).EQ.205)THEN
         MTP9(1)=ICCCFFF(7)*1000+IDPARS(4)
      END IF
C
      MTP9(2)=IDPARS(7)
      MTP9(3)=IDPARS(9)*1000000+IDPARS(12)-3
      MTP9(4)=0
C
C        READ 9-HR AMOUNT IF CONV OR TOTAL.
C
      CALL GFETCH(KFILDO,KFIL10,MTP9,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,420)(JD(J),J=1,4)
 420     FORMAT(/' ****VARIABLE ',I9.9,2I10.9,I4.3,
     1           ' NOT COMPUTED IN TPCP3.  9-HR PRECIP',
     2           ' NOT FOUND BY GFETCH AT 420.')
         GO TO 800
      ENDIF
C
      NSLABP=NSLAB
      MXX=IS2(3)
      MXY=IS2(4)
C
C        CHECK THE NSLABS.
C
      IF(NSLABP.NE.NSLABQ)THEN
         WRITE(KFILDO,430)NSLABP,NSLABQ,(JD(J),J=1,4)
 430     FORMAT(/' ****THE GRID CHARACTERISTICS OF THE 9-HR',
     1           ' AND 12-HR PRECIPITATION AMOUNT GRIDS',
     2           ' ARE DIFFERENT.',I3,2X,I3,
     3          /'     VARIABLE ',I9.9,2I10.9,I4.3,
     4           ' NOT COMPUTED IN TPCP3.')
         IER=100
         GOTO 800
      END IF
C
C        COMPARE IF THE GRID SIZES ARE THE SAME.
C
      IF(MXX.NE.MYX.OR.MXY.NE.MYY)THEN
         WRITE(KFILDO,435)(MTP9(J),J=1,4),(NGRIDC(J,NSLABP),
     1                     J=1,6),MXX,MXY,
     2                    (MTP12(J),J=1,4),(NGRIDC(J,NSLABQ),
     3                     J=1,6),MYX,MYY
 435     FORMAT(/' ****THE GRIDS NEEDED IN TPCP3 HAVE DIFFERENT',
     1           ' CHARACTERISTICS.  PREDICTOR NOT COMPUTED.',
     2           ' VALUES FROM NGRIDC( , ) AND MX,MY.'/
     3           (5X,I9.9,I10.9,I10.9,I4.3,4X,6I10,4X,2I5))
         IER=100
         GOTO 800
      END IF
C
C        CALCULATIONS.
C
      DO I=1,MXX*MXY
         FDTP(I)=FDTP(I)-FD1(I)
         IF(FDTP(I).LT.0.) FDTP(I)=0.
      END DO
C
C        3-HR PRECIP AMOUNT HAS BEEN CALCULATED BY SUBTRACTING
C        A 9-HR PRECIP AMOUNT AT IDPARS(12)-3 FROM A 12-HR 
C        AMOUNT AT IDPARS(12).
C
      GOTO 900
C
C        SET OUTPUT FIELD TO MISSING WHEN AN ERROR HAS OCCURRED.
C 
 800  DO 801 J=1,ND2X3
         FDTP(J)=9999.
 801  CONTINUE
C
 900  RETURN
      END
