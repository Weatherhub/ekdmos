      SUBROUTINE BESEL(GRID,NX,NY,JY,IXMN,IXMX,IXMIN,IXMAX,JYMIN,JYMAX, 
     1                 ORIGIN,CONINT,LINEAR,LINE2,LINE3,LINE4) 
C 
C        MAY 1993   GLAHN   TDL   HP9000
C        MARCH 2000   DALLAVALLE   CHANGED ONE DATA STATEMENT
C                                  TO TRICK THE IBM SP COMPILER
C                                  SO AS TO GET NO WARNING MESSAGES.
C                                  HOWEVER, THIS SUBROUTINE SHOULD
C                                  BE MODIFIED TO AVOID PLACING
C                                  ALPHANUMERIC CHARACTERS INTO
C                                  INTEGER ARRAYS
C        APRIL 2001   MALONEY      CHANGED ARRAYS FROM INTEGER TO
C                                  CHARACTER TO AVOID SEGMENTATION
C                                  FAULTS ON IBM USING U201. 
C        PURPOSE 
C            TO INTERPOLATE FOR ALL NEEDED CHARACTER POSITIONS FOR 
C            PRTGR AND TO FILL LINE2( ), LINE3( ), AND LINE4( ) FOR 
C            PRINTING.  BESSEL'S BIQUADRATIC INTERPOLATION METHOD IS 
C            USED EXCEPT IN THE BORDER ROWS WHERE A BILINEAR 
C            INTERPOLATION IS DONE.  AS AN OPTION, BILINEAR 
C            INTERPOLATION CAN BE USED FOR ALL POINTS.  THIS ROUTINE
C            WAS WRITTEN BY PERROTTI FOR THE IBM 360/195 AND RUN ON
C            THE DG ECLIPSE AND MODIFIED BY GLAHN 1985-87.  CONVERTED
C            TO THE HP9000 IN 1993.
C
C        DATA SET USE 
C            NONE.       
C 
C        VARIABLES 
C 
C            INPUT 
C           GRID( , ) = INPUT DATA ARRAY.  DATA ARE STORED IN NMC 
C                       CONVENTION.  STARTING POINT GRID(1,1) ON THE MAP 
C                       IS LOWER LEFT WITH DATA STORED LEFT TO RIGHT 
C                       (THE IX DIRECTION), THEN THE NEXT ROW UP, ETC. 
C                  NX = FIRST DIMENSION OF GRID( , ). 
C                  NY = SECOND DIMENSION OF GRID( , ). 
C                  JY = THE NUMBER OF THE ROW (THE SECOND SUBSCRIPT) 
C                       IN GRID( , ) BEING REFERENCED. 
C                       BESEL IS ENTERED ONCE FOR EACH 
C                       VALUE OF JY = JYMIN+1, JYMAX FOR EACH PAGE 
C                       IN THE HORIZONTAL.  INTERPOLATION IS 
C                       DONE IN REVERSE ORDER, SO THE FIRST TIME BESEL 
C                       IS ENTERED, JY = JYMAX AND INTERPOLATION IS 
C                       DONE IN THE INTERVAL JY = JYMAX-1 TO JYMAX. 
C                IXMN = MINIMUM VALUE OF IX TO PLOT FOR THE PARTICULAR 
C                       PAGE BEING WORKED ON. 
C                IXMX = MAXIMUM VALUE OF IX TO PLOT FOR THE PARTICULAR 
C                       PAGE BEING WORKED ON. 
C               IXMIN = MINIMUM IX VALUE TO PLOT. 
C               IXMAX = MAXIMUM IX VALUE TO PLOT. 
C               JYMIN = MINIMUM JY VALUE TO PLOT. 
C               JYMAX = MAXIMUM JY VALUE TO PLOT. 
C              ORIGIN = VALUE WHERE CONTOURING IS TO BEGIN. 
C              CONINT = RECIPROCAL OF CONTOUR INTERVAL, IF CINT NE 0. 
C              LINEAR = TRUE FOR BILINEAR INTERPOLATION FOR ALL POINTS. 
C                       FALSE FOR BIQUADRATIC INTERPOLATION WHERE 
C                       POSSIBLE.  (LOGICAL) 
C 
C            OUTPUT 
C            LINE2( ) = HOLDS 1ST OF 3 ROWS OF CONTOURS. 
C            LINE3( ) = HOLDS 2ND OF 3 ROWS OF CONTOURS. 
C            LINE4( ) = HOLDS 3RD OF 3 ROWS OF CONTOURS. 
C 
C            INTERNAL 
C               NCHAR = KEEPS COUNT OF WHERE TO PLACE NEXT CHARACTER 
C                       IN LINE2( ), LINE3( ), AND LINE4( ). 
C                  IX = THE NUMBER THE GRIDPOINT IN THE IX DIRECTION 
C                       (THE FIRST SUBSCRIPT) BEING REFERENCED IN 
C                       GRID( , ). 
C             AA(K,L) = 3 VALUES (K=1,3) INTERPOLATED IN THE VERTICAL 
C                       BETWEEN THE ROWS JY AND JY-1 FOR EACH OF 4 
C                       COLUMNS (L=1,4).  ONCE QUADRATIC INTERPOLATION 
C                       FOR A PAGE IS STARTED, ONLY ONE VERTICAL 
C                       INTERPOLATION IS NEEDED, THE OTHER 3 HAVING BEEN 
C                       SAVED.  BECAUSE THE SUBROUTINE IS REENTRANT, 
C                       AA( , ) IS PUT IN COMMON BLOCK RAA.  (COMMON) 
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE. 
C 
      LOGICAL LINEAR 

      COMMON/RAA/AA 
C
      DIMENSION GRID(NX,NY)
      CHARACTER*1 LINE2(133),LINE3(133),LINE4(133) 
      CHARACTER*3 NALPHA(52), NBLK
      DIMENSION AA(3,4) 
C
      DATA NALPHA     /1HA,1H ,1HB,1H ,1HC,1H ,1HD,1H ,1HE,1H , 
     1 1HF,1H ,1HG,1H ,1HH,1H ,1HI,1H ,1HJ,1H ,1HK,1H ,1HL,1H , 
     2 1HM,1H ,1HN,1H ,1HO,1H ,1HP,1H ,1HQ,1H ,1HR,1H ,1HS,1H , 
     3 1HT,1H ,1HU,1H ,1HV,1H ,1HW,1H ,1HX,1H ,1HY,1H ,1HZ,1H / 
      DATA NBLK/1H / 
C 
      NCHAR=6 
C 
      DO 300 IX=IXMN,IXMX 
      IF(IX.EQ.IXMAX)GO TO 350 
      IF(LINEAR)GO TO 180 
C 
      IF(IX.EQ.IXMIN)GO TO 180 
      IF(IX.EQ.IXMAX-1)GO TO 180 
      IF(JY.EQ.JYMAX)GO TO 180 
      IF(JY.EQ.JYMIN+1)GO TO 180 
C        NOTE THAT BESEL IS NOT ENTERED IF JY = JYMIN. 
      IF(IX.EQ.IXMIN+1)GO TO 120 
      IF(IXMN.GT.IXMIN.AND.IX.EQ.IXMN)GO TO 120 
C        ABOVE TEST NECESSARY FOR SECOND AND SUBSEQUENT PAGES. 
C 
C        QUADRATIC INTERPOLATION HAS BEEN DONE IN THE VERTICAL FOR THIS 
C        PAGE BEFORE.  MOVE VALUES OVER ONE COLUMN. 
C 
      DO 115 K=1,3 
      DO 110 KK=1,3 
      AA(KK,K)=AA(KK,K+1) 
 110  CONTINUE 
 115  CONTINUE 
      GO TO 135 
C 
C        THIS IS THE SMALLEST IX FOR WHICH BIQUADRATIC INTERPOLATION CAN 
C        BE DONE FOR THIS PAGE.  THIS IS IX = IXMIN+1 FOR THE FIRST PAGE 
C        AND IX = IXMN FOR SUBSEQUENT PAGES.  DO VERTICAL INTERPOLATION 
C        FOR COLUMNS IX, IX-1, AND IX+1. 
C 
 120  IXM2=IX-2 
C 
      DO 130 K=1,3 
      RMU=(GRID(IXM2+K,JY-1)+GRID(IXM2+K,JY))*.5 
      DELT=GRID(IXM2+K,JY-1)-GRID(IXM2+K,JY) 
      RMUDEL=(GRID(IXM2+K,JY-2)-GRID(IXM2+K,JY-1) 
     1       -GRID(IXM2+K,JY)+GRID(IXM2+K,JY+1))*.25 
      S=.25 
C 
      DO 125 KK=1,3 
      AA(KK,K)=RMU+(S-.5)*DELT+S*(S-1.)*RMUDEL 
      S=S+.25 
 125  CONTINUE 
C 
 130  CONTINUE 
C 
C        DO BIQUADRATIC INTERPOLATION FOR COLUMN IX+2. 
C 
 135  RMU=(GRID(IX+2,JY-1)+GRID(IX+2,JY))*.5 
      DELT=GRID(IX+2,JY-1)-GRID(IX+2,JY) 
      RMUDEL=(GRID(IX+2,JY-2)-GRID(IX+2,JY-1)-GRID(IX+2,JY)+ 
     1        GRID(IX+2,JY+1))*.25 
      S=.25 
C 
      DO 140 KK=1,3 
      AA(KK,4)=RMU+(S-.5)*DELT+S*(S-1.)*RMUDEL 
      S=S+.25 
 140  CONTINUE 
C 
C        DO BIQUADRATIC INTERPOLATION IN THE HORIZONTAL. 
C 
      N56=5 
      IF(IX.EQ.IXMAX-1)N56=6 
C        ONLY 5 CHARACTERS ARE NEEDED PER GRIDPOINT EXCEPT FOR THE LAST, 
C        WHERE THE SIXTH IS NEEDED. 
C 
      DO 170 II=1,3 
      RMU=(AA(II,2)+AA(II,3))*.5 
      DELT=AA(II,3)-AA(II,2) 
      RMUDEL=(AA(II,1)-AA(II,2)-AA(II,3)+AA(II,4))*.25 
      R=0. 
C 
      DO 160 KK=1,N56 
      D=RMU+(R-.5)*DELT+R*(R-1.)*RMUDEL 
      R=R+.2 
C        CALCULATE INDEX 
      ZINC=(D-ORIGIN)*CONINT 
      INC=ZINC 
      IF(ZINC.LT.0.)INC=ZINC+.000001 
      IF(INC.GT.51)INC=MOD(INC,52) 
      IF(ZINC.LT.0.)INC=MOD(INC,-52)+51 
C        LOAD CONTOUR CHARACTER 
      IF(II.EQ.1)LINE2(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
      IF(II.EQ.2)LINE3(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
      IF(II.EQ.3)LINE4(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
 160  CONTINUE 
C 
 170  CONTINUE 
C 
      GO TO 290 
C 
C        DO BILINEAR INTERPOLATION. 
C 
 180  R=0. 
      N56=5 
      IF(IX.EQ.IXMAX-1)N56=6 
C        ONLY 5 CHARACTERS ARE NEEDED PER GRIDPOINT EXCEPT FOR THE LAST, 
C        WHERE THE SIXTH IS NEEDED. 
C 
      DO 200 KK=1,N56 
      S=.25 
C 
      DO 190 II=2,4 
      D=(1.-S)*((1.-R)*GRID(IX,JY)+R*GRID(IX+1,JY)) 
     1            +(S*((1.-R)*GRID(IX,JY-1)+R*GRID(IX+1,JY-1))) 
      S=S+.25 
C        CALCULATE INDEX 
      ZINC=(D-ORIGIN)*CONINT 
      INC=ZINC 
      IF(ZINC.LT.0.)INC=ZINC+.000001 
      IF(INC.GT.51)INC=MOD(INC,52) 
      IF(ZINC.LT.0.)INC=MOD(INC,-52)+51 
C        LOAD CONTOUR CHARACTER 
C     IF(II.EQ.2)LINE2(NCHAR+KK)=NALPHA(INC+1) 
C     IF(II.EQ.3)LINE3(NCHAR+KK)=NALPHA(INC+1) 
C     IF(II.EQ.4)LINE4(NCHAR+KK)=NALPHA(INC+1) 
      IF(II.EQ.2)LINE2(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
      IF(II.EQ.3)LINE3(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
      IF(II.EQ.4)LINE4(NCHAR+KK:NCHAR+KK+2)=NALPHA(INC+1) 
 190  CONTINUE 
C 
      R=R+.2 
 200  CONTINUE 
C 
 290  NCHAR=NCHAR+N56 
 300  CONTINUE 
C 
C        OVERWRITE ROW NUMBERS IN BEGINNING AND END OF LINE2( ) WITH 
C        BLANKS.  OVERWRITING AT THE END IS NECESSARY ONLY WHEN A 
C        SINGLE LINE IS ON THE PAGE. 
C 
 350  DO 351 KK=2,4 
      LINE2(KK:KK+2)=NBLK 
 351  CONTINUE 
      LINE2(NCHAR+1:NCHAR+3)=NBLK 
C 
      RETURN 
      END 
