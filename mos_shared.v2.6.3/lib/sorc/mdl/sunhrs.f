      SUBROUTINE SUNHRS(KFILDO,MDATE,IDPARS,XDATA,
     1                  DATA,ND1,ND5,NSTA)
C 
C        JULY 1999   RUDACK   TDL   MOS-2000
C
C        PURPOSE 
C            TO DETERMINE THE NUNBER OF HOURS OF DAYLIGHT FOR PARTICULAR
C            STATIONS AND THE EXTRA-TERRESTRIAL RADIATION(ETR).  
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C               MDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.
C                       (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN 
C                            TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C            XDATA(K) = THE STATION LATITUDES (INPUT), THEN THE
C                       TOTAL NUMBER OF HOURS OF SUN (K=1,NSTA).
C                       (OUTPUT)
C             DATA(K) = COMPUTED EXTRA-TERRESTRIAL RADIATION (ETR) IS
C                       RETURNED IN DATA(K) (K=1,NSTA).  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT 
C                       WITH.  DIMENSION OF XDATA( ).  (INPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ).
C                       (INPUT)
C                 NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT
C                       WITH.  (INPUT)
C           MTABLE(I) = TABLE CONTAINING THE NUMBER OF DAYS OF THE YEAR
C                       PRECEEDING EACH MONTH (I=1,13). (INTERNAL)
C           STABLE(I) = TABLE CONTAINING VALUES OF THE SOLAR DISTANCE
C                       FACTORS (I=1,13). (INTERNAL)
C                 IYR = YEAR IN MDATE(EXAMPLE:'1998') (INTERNAL).
C                 IMO = MONTH IN MDATE(EXAMPLE:'04') (INTERNAL).
C                IDAY = DAY IN MDATE(EXAMPLE:'17') (INTERNAL).
C                   K = JULIAN DAY MINUS ONE OF PROJECTION TIME.
C              DISFAC = SOLAR DISTANCE FACTOR. (INTERNAL)
C                 SIG = ANGLE OF REVOLUTION BETWEEN THE VERNAL EQUINOX
C                       AND MDATE (HAS BEEN CORRECTED FOR THE EARTH'S 
C                       ELLIPTICAL ORBIT).  (INTERNAL)
C              EFFDEC = EFFECTIVE DECLINATION OF THE SUN.  (INTERNAL)
C
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      PARAMETER(PI=3.1416)
      PARAMETER(RAD=57.2958)
      PARAMETER(PIOVTW=PI/2.)
      PARAMETER(RADPDG=PI/180.)
C
      DIMENSION IDPARS(15),XDATA(ND1),DATA(ND5)
      DIMENSION MTABLE(13),STABLE(13)
C
C        DATA TABLE
C
      DATA MTABLE/0,31,60,91,121,152,182,213,244,274,305,335,366/
      DATA STABLE/0.967,0.971,0.982,0.999,1.016,1.092,1.034,1.030,
     1            1.018,1.002,0.985,0.973,0.967/
C
C        STRIP THE YEAR, MONTH, DAY AND HOUR OF PROJECTION TIME.
C 
      IYR=MDATE/1000000
      IMO=MOD(MDATE,1000000)/10000
      IDAY=MOD(MDATE,10000)/100+((IDPARS(12)+12+MOD(MDATE,100))/24)
C
C        CALCULATE THE SOLAR DISTANCE FACTOR FOR A PARTICULAR DAY USING 
C        THE INTERPOLATED FACTOR "F".  NOTE: "F"=0 ON THE FIRST DAY OF
C        EACH MONTH IN WHICH CASE THE SOLAR DISTANCE VALUE EXACTLY 
C        CORRESPONDS TO THE VALUE IN "STABLE" FOR THAT PARTICULAR MONTH.
C
      F=FLOAT(IDAY-1)/FLOAT((MTABLE(IMO+1)-MTABLE(IMO)))
C   
      DISFAC=STABLE(IMO)+F*(STABLE(IMO+1)-STABLE(IMO))
C   
C        CALCULATING THE JULIAN DAY MINUS ONE.
C
      K=IDAY+MTABLE(IMO)-1
C
C        DETERMINING IF THE YEAR IS A LEAP YEAR.  IF IT IS NOT, SUBTRACT
C        ONE FROM THE JULIAN DAY FOR DAYS AFTER FEBRUARY 29.
C
      IF((IYR.NE.((IYR/4)*4)).AND.(K.GE.60)) K=K-1 
      DE=((360./365.242)*FLOAT(K))/RAD
      SIG=(279.9348+DE*RAD+1.914827*SIN(DE)-0.079525*COS(DE)+
     1     0.019938*SIN(DE*2)-0.00162*COS(DE*2))/RAD
C          
C        CALCULATE THE EFFECTIVE DECLINATION ANGLE OF THE SUN. 
C
      EFFDEC=ASIN(0.397850*SIN(SIG))
C
      DO 150 K=1,NSTA
C
C           IF THE LATITUDE FOR A PARTICULAR STATION IS UNAVAILABLE,
C           DO NOT CALCULATE THE AMOUNT OF SUN HOURS OR ETR FOR THAT
C           STATION.  RATHER, EVALUATE THE NEXT STATION FOR THE DESIRED
C           CALCULATION.
C
         IF(NINT(XDATA(K)).NE.9999) GOTO 120
         XDATA(K)=9999.
         DATA(K)=9999.
         GOTO 150
C
C           CONVERT LATITUDES FROM DEGREES TO RADIANS.
C 
 120     RLAT=XDATA(K)*RADPDG
C
C           CHECK AREAS FOR NO SUNLIGHT AND LABEL SUNLIGHT HOURS AS 0.
C
         IF((RLAT-EFFDEC).GE.PIOVTW) THEN
            XDATA(K)=0.0
C
C           CHECK FOR AREAS WITH 24 HOURS OF SUNLIGHT AND LABEL SUNLIGHT
C           HOURS AS 24.
C
         ELSEIF((RLAT+EFFDEC).GE.PIOVTW)THEN
            XDATA(K)=24.0
C
         ELSE
C
C              CALCULATE SUNLIGHT HOURS IN ALL OTHER SITUATIONS.
C
            XDATA(K)=(2.*ACOS(-TAN(RLAT)*TAN(EFFDEC)))*12./PI
C
         ENDIF     
C  
C           CALCULATE THE EXTRA-TERRESTRIAL RADIATION (ETR).
C     
         DHRS=XDATA(K)
         FACTR1=(DHRS/24.*PI)*SIN(RLAT)*SIN(EFFDEC)
         FACTR2=COS(RLAT)*COS(EFFDEC)*SIN(DHRS/24.*PI)
         DATA(K)=(1440./PI*1.95/DISFAC*(FACTR1+FACTR2))*0.011624
C             
 150  CONTINUE
C
      RETURN
      END     
                  
