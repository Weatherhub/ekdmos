      SUBROUTINE CONSTX(KFILDO,KFIL10,
     1                  ID,IDPARS,JD,NDATE,RACESS,
     2                  CCALL,ISDATA,XDATA,ND1,NSTA,
     3                  ICALLD,CCALLD,IPACK,IWORK,DATA,ND5,
     4                  LSTORE,ND9,LITEMS,CORE,ND10,
     5                  LASTL,LASTD,NBLOCK,NSTORE,NFETCH,
     6                  IS0,IS1,IS2,IS4,ND7,
     7                  L3264B,L3264W,IER)
C 
C        JANUARY 1997   GLAHN   TDL   MOS-2000 
C        JANUARY 1997   GLAHN   NELEV( ), STALAT( ), STALON( ) ELIMINATED
C        JANUARY 1997   GLAHN   LASTL, LASTD ADDED TO CALL
C        AUGUST  1998   GLAHN   COMMENTS UPDATED
C        MARCH   2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                    CONFORM TO FORTRAN 90 STANDARDS
C                                    ON IBM SP
C        OCTOBER 2012   ENGLE   CHANGED CALL FROM RDTDLM TO RDTDLMC WHEN
C                               READING STATION CALL LETTERS.
C
C        PURPOSE 
C            TO PROVIDE FOR U600, U700, AND OTHER ROUTINES CONSTANTS
C            THAT ARE AVAILABLE IN  MOS-2000 EXTERNAL RANDOM ACCESS
C            FILES.  THE MOS-2000 ID WILL HAVE A CCC BETWEEN 400 AND 499 
C            INCLUSIVE AND WILL INDICATE THE FREQUENCY WITH WHICH THE
C            CONSTANTS ARE AVAILABLE.  CONSTX AND COMPID WORK AS A PAIR.
C
C            THE FOLLOWING CONSTANTS ARE ACCOMMODATED IN CONSTX AND
C            COMPID:
C               40XXXX = TRULY CONSTANT VALUES; DO NOT DEPEND ON TIME
C               41XXXX = DAILY VALUES
C               422XXX = 5-DAY VALUES (JAN 5, JAN 10, ... 
C                        DEC 31 = 365TH DAY); INTERPOLATION ASSUMED
C               43XXXX = MONTHLY VALUES; INTERPOLATION ASSUMED
C               44XXXX = 6-MONTH SEASONS (APRIL-SEPTEMBER, ETC);
C                        NO INTERPOLATION
C               45XXXX = 3-MONTH SEASONS (MARCH-MAY, JUNE-AUGUST, ETC.);
C                        NO INTERPOLATION
C               46XXXX = YEARLY VALUES
C               47XXXX = MONTHLY VALUES; NO INTERPOLAATION
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 INTERNAL FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 INTERNAL FILE SYSTEM 
C                       ACCESS.  (INPUT)
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       JD( ) IS USED TO IDENTIFY THE BASIC MODEL FIELDS
C                       AS READ FROM THE ARCHIVE.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C              RACESS = THE FILE NAME OF THE EXTERNAL CONSTANT FILE.
C                       (CHARACTER*60)  (INPUT)
C            CCALL(K) = 8 STATION CALL LETTERS (K=1,NSTA).  (CHARACTER*8)
C                       (INPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  THE INDEX RECORD FOR THE
C                       CONSTANT FILE.  (INTERNAL)
C            XDATA(K) = CONSTANT DATA RETURNED (K=1,NSTA).  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).  THIS ARRAY IS USED 
C                       IN CONSTX TO READ THE STATION DIRECTORY FROM A
C                       MOS-2000 EXTERNAL FILE.  EQUIVALENCED TO CCALLD( ).
C                       (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS ARRAY IS USED 
C                       IN CONSTX TO READ THE STATION DIRECTORY.  EQUIVALENCED 
C                       TO CCALLD( ).  (CHARACTER*8)  (INTERNAL)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ) AND
C                       SECOND DIMENSION OF ICALLD( , ).  MUST BE GE ND1.
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MOSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.
C             CORE(J) = THE ARRAY TO STORE OR RETIREVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C               LASTL = THE LAST LOCATION IN CORE( ) USED.  THIS MAY BE
C                       MODIFIED, ALONG WITH ITEMS, IF COMPACTION IS
C                       DONE BY GCPAC.  INITIALIZED TO ZERO ON FIRST 
C                       ENTRY TO GSTORE.  (INPUT-OUTPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK.  INITIALIZED
C                       TO ZERO ON FIRST ENTRY TO GSTORE.  (INPUT-OUTPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              NSTORE = RUNNING COUNT OF NUMBER OF TIMES DATA ARE STORED BY 
C                       GSTORE.  GFETCH KEEPS TRACK OF THIS AND RETURNS
C                       THE VALUE.  (OUTPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.  GFETCH
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.
C                       (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING USED
C                       (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.  (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       SEE CALLED ROUTINES FOR OTHER VALUES.
C                       (INTERNAL-OUTPUT)
C               KFILX = THE FILE NUMBER ON WHICH USE THE MOS-2000
C                       EXTERNAL FILE FOR THIS VARIABLE.  UNIT NO. 95
C                       IS USED.  (INTERNAL)
C               KD(J) = IDS FOR DIRECTORY RECORD IN THE MOS-2000 INTERNAL
C                       STORAGE SYSTEM (STATION CALL LETTERS) AND IDS
C                       FOR THE 1ST SET OF CONSTANTS (J=1,4).  (INTERNAL)
C               LD(J) = IDS FOR CALL LETTERS RECORD IN MOS-2000
C                       EXTERNAL RANDOM ACCESS FILES (J=1,4) AND LATER
C                       THE IDS FOR THE SECOND SET OF CONSTANTS TO BE
C                       USED (IF NECESSARY) FOR INTERPOLATION.
C                       (INTERNAL)
C                   R = INTERPOLATION FACTOR, THE FRACTION OF THE WAY
C                       MDOY IS FROM THE FIRST OF THE TWO VALUES TO BE
C                       USED IN INTERPOLATION.  (INTERNAL)
C
C        NONSYSTEM SUBROUINES USED 
C            GSTORE, GFETCH, RDTDLM, DOY, UNPACK
C
      CHARACTER*8 CCALL(ND1)
      CHARACTER*8 CCALLD(ND5)
      CHARACTER*60 RACESS
C
      DIMENSION ISDATA(ND1),XDATA(ND1)
      DIMENSION ID(4),IDPARS(15),JD(4),KD(4),LD(4)
      DIMENSION ICALLD(L3264W,ND5),IPACK(ND5),
     1          IWORK(ND5),DATA(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
C
      DATA NCOMBO/9999/
      DATA NRRDAT/2100010100/
C
D     WRITE(KFILDO,100)(IDPARS(J),J=1,15)
D100  FORMAT(' *********** IN CONSTX *************'/' '15I5)
C
      IER=0
      KFILX=95
C        THE DEFAULT UNIT NUMBER 95 IS USED.
C
C        RETIREVE THE DIRECTORY RECORD IF IT IS AVAILABLE.
C        THIS IS THE CORRESPONDENCE BETWEEN THE NSTA STATIONS IN
C        CCALL( ) AND THE STATIONS IN THE MOS-2000 RANDOM ACCESS
C        FILE.  THE ID FOR THE RECORD IS JUST THE FILE UNIT 
C        NUMBER IN KD(2) ALONG WITH THE CALL LETTERS DESIGNATOR
C        IN ID(1); ALL VARIABLES ON THE FILE WILL, OF COURSE, HAVE
C        THE SAME STATION INDEX AND THE CALL LETTERS DIRECTORY
C        WILL HAVE THE SAME IDS REGARDLESS OF THE FILE.
C
      KD(1)=400001000
      KD(2)=KFILX
      KD(3)=0
      KD(4)=0
      CALL GFETCH(KFILDO,KFIL10,KD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,ISDATA,ND1,
     2            NWORDS,NPACK,0,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
C        THE INDEX VARIABLE IS TO BE IN ISDATA( ).
C        THE RETURNED VALUES NTIMES, MISSS, AND NWORDS ARE NOT
C        NEEDED OR USED.  IT IS ASSUMED NWORDS = NSTA, SINCE THIS
C        ROUTINE STORED THE DATA.  THE DATE OF SUCH RECORDS IS 0.
C        NOTE THAT ND1 IS DIMENSION OF IPACK( ), ETC., NOT ND5,
C        BECAUSE ISDATA( ) IS DIMENSIONED ND1.  THIS SHOULD BE
C        SUFFICIENT FOR THIS CALL BECAUSE ND5 IS GE ND1.
      IF(IER.EQ.0)GO TO 260
      IF(IER.NE.47)GO TO 300
C        IF IER NE 0 NOR 47, A FATAL ERROR HAS OCCURRED FOR THIS
C        VARIABLE.  THE DATA ARRAY SDATA( ) WILL BE SET TO MISSING
C        AND ISTOP INCREMENTED IN PRED1 OR PRED2.

C        INDEX RECORD NOT AVAILABLE, BECAUSE IER EQ 47.  GET THE
C        STATION INDEX FROM THE MOS-2000 EXTERNAL FILE.  NOTE THAT 
C        LD(1) = KD(1).
C
      LD(1)=400001000
      LD(2)=0
      LD(3)=0
      LD(4)=0
CINTEL
C      CALL RDTDLM(KFILDO,KFILX,RACESS,LD,ICALLD,ND5*L3264W,NVALUE,
C     1               L3264B,IER)
      CALL RDTDLMC(KFILDO,KFILX,RACESS,LD,CCALLD,ND5*L3264W,NVALUE,
     1               L3264B,IER)
CINTEL
      NVALUE=NVALUE/L3264W
C        THE CALL LETTERS ARE 8 BYTES EACH.  THIS IS TWO WORDS
C        ON A 32-BIT MACHINE.  THE NUMBER OF WORDS WRITTEN AND
C        READ MUST ACCOUNT FOR THIS.  THE ACTUAL NUMBER OF CALL
C        LETTERS IS NVALUE/L3264W.
D     WRITE(KFILDO,150)NVALUE,NSTA,IER
D150  FORMAT(' NVALUE, NSTA, IER AT 150 IN CONSTX ='3I6)
      IF(IER.NE.0)GO TO 300
C
C        FIND THE LOCATION OF THE STATIONS IN CALL( ) IN THE ARRAY
C        CCALLD( ), WHICH IS EQUIVALENCED TO ICALLD( , ).
C        ZERO OUT ISDATA( ) SO THAT DUPLICATES CAN BE DETECTED.
C
      DO 152 K=1,NSTA
      ISDATA(K)=0
 152  CONTINUE
C
      DO 160 K=1,NSTA
      DO 155 J=1,NVALUE
      IF(CCALLD(J).NE.CCALL(K))GO TO 155
      IF(ISDATA(K).EQ.0)THEN
         ISDATA(K)=J
      ELSE
         WRITE(KFILDO,153)CCALL(K),RACESS
 153     FORMAT(/,' ****DUPLICATE STATION ',A8,' IN DIRECTORY',
     1            ' IN RANDOM ACCESS FILE ',A60,/,
     2            '     THE FIRST OCCURRENCE IS USED.')
C        THIS IS NOT TREATED AS AN IER ERROR.
      ENDIF
C
 155  CONTINUE
C
      IF(ISDATA(K).NE.0)GO TO 160
C
      WRITE(KFILDO,156)CCALL(K),RACESS
 156  FORMAT(/,' ****STATION ',A8,' NOT FOUND IN DIRECTORY IN',
     1         ' RANDOM ACCESS FILE ',A60)
C        THIS IS NOT TREATED AS AN IER ERROR.
      ISDATA(K)=9999
 160  CONTINUE
C
C        STORE INTO INTERNAL STORAGE SYSTEM.  NCOMBO IS NOT
C        MEANINGFUL, BUT IS SET = 1.
C
      NCOMBO=1
D     WRITE(KFILDO,165)(CCALL(J),J=1,NSTA)
D165  FORMAT(' STATIONS IN CCALL'/(' 'A8))
D     WRITE(KFILDO,166)(CCALLD(J),J=1,NVALUE)
D166  FORMAT(' STATIONS IN CCCALL'/(' 'A8))
D     WRITE(KFILDO,167)(ISDATA(J),J=1,NSTA)
D167  FORMAT(' INDEX RECORD'/(' 'I4))
      CALL GSTORE(KFILDO,KFIL10,KD,NCOMBO,LSTORE,ND9,LITEMS,
     1            ISDATA,NSTA,1,NRRDAT,0,
     2            CORE,ND10,LASTL,NBLOCK,LASTD,NSTORE,L3264B,IER)
C        NOTE THAT ISDATA( ) IS INTEGER.  ALTHOUGH THE CORRESPONDING
C        VARIABLE IN GSTORE IS REAL, THIS IS OK.  ALSO NOTE
C        THE DATA ARE STORED UNPACKED.
C
C        COMPUTE THE IDS FOR THE CONSTANTS AS THEY RESIDE IN
C        THE MOS-2000 EXTERNAL FILES.
C
 260  CALL COMPID(KFILDO,ID,IDPARS,NDATE,KD,LD,MDOY,R,IER)
      IF(IER.NE.0)GO TO 300
C
C        RETRIEVE THE FIRST DATA RECORD AND UNPACK.
C
      CALL RDTDLM(KFILDO,KFILX,RACESS,KD,IPACK,ND5,NVALUE,
     1               L3264B,IER)
C        THE DATA RETURNED BY RDTDLM IN IPACK( ) AND FURNISHED TO
C        UNPACK ARE REAL.
      IF(IER.NE.0)GO TO 300
C
      CALL UNPACK(KFILDO,IPACK,IWORK,DATA,ND5,
     1            IS0,IS1,IS2,IS4,ND7,MISSP,MISSS,3,L3264B,
     2            IER)
      NVALUE=IS4(3)     
      IF(IER.NE.0)GO TO 300
C        IER NE 0 SHOULD BE TREATED AS A FATAL ERROR.  A DIAGNOSTIC
C        WILL HAVE BEEN PRINTED BY UNPACK.
c
      DO 270 K=1,NSTA
C
      IF(ISDATA(K).EQ.9999)THEN
         XDATA(K)=9999.
      ELSE
         XDATA(K)=DATA(ISDATA(K))
      ENDIF
C
 270  CONTINUE
D     WRITE(KFILDO,271)MDOY,(ISDATA(J),XDATA(J),J=1,NSTA)
D271  FORMAT(' MDOY, ISDATA, XDATA IN CONSTX'I4/(I4,F10.6))
C
C        AT THIS POINT ONE SET OF CONSTANTS HAVE BEEN RETIREVED
C        AND STORED IN XDATA( ).  WHEN INTERPOLATION IS NEEDED,
C        A SECOND RETRIEVAL IS NECESSARY, AS WELL AS THE 
C        INTERPOLATION.  DO THAT NOW WHEN NECESSARY.  THE 
C        INTERPOLATION FACTOR HAS BEEN RETURNED FROM COMPID.
C
      IF(IDPARS(1).EQ.422.OR.
     1   IDPARS(1)/10.EQ.43)GO TO 275
      GO TO 300
C
 275  IF(KD(2).EQ.LD(2))GO TO 300
C        THE ABOVE TEST IS RELEVANT TO 5-DAY TEMPERATURES.
C      
      CALL RDTDLM(KFILDO,KFILX,RACESS,LD,IPACK,ND5,NVALUE,
     1               L3264B,IER)
C        THE DATA RETURNED BY RDTDLM IN IPACK( ) AND FURNISHED TO
C        UNPACK ARE REAL.
      IF(IER.NE.0)GO TO 300
C
      CALL UNPACK(KFILDO,IPACK,IWORK,DATA,ND5,
     1            IS0,IS1,IS2,IS4,ND7,MISSP,MISSS,3,L3264B,
     2            IER)
      NVALUE=IS4(3)     
      IF(IER.NE.0)GO TO 300
C        IER NE 0 SHOULD BE TREATED AS A FATAL ERROR.  A DIAGNOSTIC
C        WILL HAVE BEEN PRINTED BY UNPACK.
C
      DO 280 K=1,NSTA
C
      IF(ISDATA(K).EQ.9999.OR.
     1   XDATA(K).EQ.9999..OR.
     2   DATA(ISDATA(K)).EQ.9999.)THEN
         XDATA(K)=9999.
      ELSE
         XDATA(K)=(DATA(ISDATA(K))-XDATA(K))*R+XDATA(K)
      ENDIF
C
 280  CONTINUE
D     WRITE(KFILDO,281)(ISDATA(K),DATA(ISDATA(K)),
D    1                             XDATA(K),K=1,NSTA)
D281  FORMAT(' ISDATA, DATA(ISDATA), XDATA IN CONSTX'/(I4,2F10.6))
C
 300  RETURN
      END
