      SUBROUTINE OBSPOPO3(KFILDO,KFIL10,IDPARS,JD,NDATE,
     1                    SDATA,ND1,NSTA,IPACK,IWORK,FD1,ND2X3,
     2                    LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3                    IS0,IS1,IS2,IS4,ND7,
     4                    ISTAV,L3264B,IER)
C
C        JULY      2000   RUDACK   TDL   MOS-2000
C        SEPTEMBER 2000   SU       MODIFIED TO ACCOMMODATE MORE
C                                  WEATHER PHENOMENA.
C        DECEMBER  2002   WEISS    CHANGED ND5 TO ND2X3
C        MAY       2003   GLAHN    CHANGED .AND. TEST TO .OR. ON 
C                                  IDPARS( ) ABOVE 100; CHANGED FORMAT
C                                  100; INDENTION; COSMETICS
C
C        PURPOSE 
C           THIS SUBROUTINE WILL CONVERT THE OBSERVATIONAL "PRESENT WEATHER" 
C           CODES INTO A PRECIPITATION OCCURENCE CODE OF 0 ("ZERO",
C           NOT PRECIPITATING), 1 (PRECIPITATING), OR 9999 (MISSING DATA) OVER
C           A THREE HOUR PERIOD.
C
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C
C               708 564 - PRECIPITATION OCCURRENCE OVER 3 HOURS                 
C
C           THE HOURLY DATA ARCHIVES CHANGED FROM SAO TO METAR ON DECEMBER 1,
C           1996. THE OBSERVATIONAL "PRESENT WEATHER" CODES IN THE SAO DATA
C           FILES ARE DIFFERENT FROM THOSE IN THE METAR DATA FILES.
C           THIS ROUTINE CONTAINS TWO SEPARATE ALGORITHMS TO PROCESS THE
C           DATA DEPENDING ON NDATE.
C 
C           ***** THE USER CAN NOT PROCESS DATES THAT CROSS THE BARRIER 
C           OF DECEMBER 1, 1996 BECAUSE THE MASS STORE WILL NOT BE
C           INITIALIZED PROPERLY.  THE USER MUST PROCESS ONE RUN THAT
C           GOES THROUGH NOV. 30, 1996, AND THEN START A NEW RUN FOR ANY
C           DATE FROM DECEMBER 1, 1996 AND ON. 
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE 
C                     (OUTPUT). 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS
C                     (INPUT-OUTPUT). 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE
C                       (INPUT). 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM 
C                       ACCESS (INPUT).
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID(J) (J=1,15)
C                       DEFINED IN THE CALLING PROGRAM (INPUT).
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 
C                            LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET -- PREVIOUS CYCLE.  
C                            ALWAYS + AND COUNTED BACKWARDS IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE 
C                       PORTIONS PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND THRESH.
C                       JD( ) IS USED TO IDENTIFY THE BASIC MODEL 
C                       FIELDS AS READ FROM THE ARCHIVE (INPUT).
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED
C                       (INPUT).
C            SDATA(K) = DATA TO RETURN (K=1,NSTA).  THE ARRAY CONTAINS
C                       THE CODES OF PRECIPITATION OCCURRENCE OVER A
C                       THREE HOUR PERIOD. (OUTPUT) 
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT 
C                       WITH.  (INPUT).
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH
C                       (INPUT).
C            IPACK(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL).  
C            IWORK(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL).  
C              FD1(J) = WORK ARRAY (J=1,ND2X3). FD1 CONTAINS THE
C                       OBSERVATIONAL "PRESENT WEATHER" DATA. (INTERNAL)
C                 ND5 = FORMER DIMENSION OF IPACK( ),IWORK( ), FD1( ).
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS), (INPUT-OUTPUT).
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR 
C                              NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN 
C                              RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE 
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE 
C                              SORTED LIST IN ID( ,N) (N=1,NPRED)
C                              FOR WHICH THIS VARIABLE IS NEEDED, WHEN 
C                              IT IS NEEDED ONLY ONCE FROM 
C                              LSTORE( , ).  WHEN IT IS NEEDED MORE
C                              THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING 
C                              MSTORE( , ). LATER USED AS A WAY OF 
C                              DETERMINING WHETHER TO KEEP THIS 
C                              VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ), (INPUT). 
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , )
C                       THAT HAVE BEEN USED IN THIS RUN (INPUT).  
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA 
C                       IDENTIFIED IN LSTORE( , ) (J=1,ND10). WHEN
C                       CORE( ) IS FULL, DATA ARE STORED ON DISK
C                       (OUTPUT).
C                ND10 = DIMENSION OF CORE( ), (INPUT).
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE (INPUT).  
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING  COUNT FROM THE BEGINNING OF 
C                       THE PROGRAM.  THIS COUNT IS MAINTAINED IN 
C                       CASE THE USER NEEDS IT (DIAGNOSTICS, ETC.). 
C                       (OUTPUT).  
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3)
C                       (INTERNAL).
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+)  
C                       (INTERNAL).
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12)
C                       (INTERNAL).
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4)
C                       (INTERNAL).
C                 ND7 = DIMENSION OF IS0, IS1, IS2, AND IS4. NOT ALL
C                       LOCATIONS ARE USED (INPUT).
C               ISTAV = 1 WHEN THE DATA RETURNED ARE STATION DATA
C                       0 THE DATA RETURNED ARE GRID DATA OR
C                       DATA ARE NOT AVAILABLE FOR RETURN. 
C                       (OUTPUT).
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64) (INPUT).
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                        52 = NWORDS DOES NOT EQUAL NSTA.
C                        57 = SUBROUTINE DOES NOT PROCESS DATA
C                             BECAUSE OF DEC 1996 CUTOFF.
C                       103 = IDPARS(1) AND IDPARS(2) NOT ACCOMMODATED
C                             IN THIS SUBROUTINE.
C                       SEE GFETCH FOR OTHER VALUES WHEN IER.NE.0 AND
C                       DATA ARE RETURNED AS MISSING (INTERNAL-OUTPUT)
C
C      ADDITIONAL VARIABLES 
C              IENTER = NUMBER OF TIMES THIS SUBROUTINE IS ENTERED.
C                       (INTERNAL).   
C                IPWX = HOLDING A VARIABLE FOR THE OBSERVATIONAL 
C                       "PRESENT WEATHER" CODE. (INTERNAL)
C                 IRR = HOLDS THE RUN TIME OFFSET IN HOURS.  (INTERNAL) 
C            JWORD(J) = ARRAY JWORD(1:3) IS USED TO HOLD THE FFF PORTION
C                       OF THE VARIABLE ID FOR 3 GROUPS(J=1,3) OF
C                       WEATHER EVENT FROM EACH OBSERVATION. (INTERNAL)
C               LD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       FD1( ) "PRESENT WEATHER GROUPS" (J=1,4) (INTERNAL).
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS
C                       ZERO FROM CALLING GFETCH WHEN THERE IS NO
C                       SECONDARY MISSING VALUE (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS
C                       ZERO FROM CALLING GFETCH WHEN THERE IS NO 
C                       SECONDARY MISSING VALUE. (INTERNAL)
C               NPACK = 2 FOR TDL GRIB PACKED DATA; 1 FOR NOT PACKED.
C                       THIS IS RETURNED FROM CALLING GFETCH (INTERNAL)
C               NSLAB = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ) FOR THE FIRST FIELD.  THIS IS THE
C                       VALUE OF NSLAB RETURNED.  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED (OUTPUT).  
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE, THAT 
C                       THE RECORD HAS BEEN FETCHED. THIS IS STORED 
C                       IN LSTORE(9, ) (INTERNAL).
C              NWORDS = NUMBER OF WORDS RETURNED IN FD1( ).
C                       (INTERNAL).
C
C               XDATA = HOLDS THE PRECIPITATION OCCURRENCE CODE OF A
C                       STATION.
C                          1. -- PRECIPITATING.
C                          0. -- NO PRECIPITATING.
C                       9999. -- OBSERVATIONAL DATA MISSING. 
C                       (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            GFETCH
C
      IMPLICIT NONE
C
      INTEGER JD(4),IDPARS(15)
      INTEGER IPACK(ND2X3),IWORK(ND2X3)
      INTEGER IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      INTEGER LSTORE(12,ND9)
      INTEGER JWORD(3),LD(4)
      INTEGER KFILDO,KFIL10,I,J,K,ND1,ND2X3,ND7,ND9,ND10,
     1        IENTER,IRR,ISTAV,LITEMS,L3264B,MISSP,MISSS,
     2        NBLOCK,NDATE,NFETCH,NPACK,NSLAB,NSTA,NTIMES,NWORDS,
     3        IPWX,IER
C
      REAL SDATA(ND1)
      REAL FD1(ND2X3)
      REAL CORE(ND10)
      REAL XDATA
C
      DATA IENTER/0/,JWORD/500,510,520/
      SAVE IENTER
C
C          INITIALIZE THE NECESSARY VARIABLES
C
      IER=0
      ISTAV=1
C
      IENTER=IENTER+1
C
      DO 25 K=1,ND1
        SDATA(K)=9999.
 25   CONTINUE
C
C        CHECK IF THE VARIABLE ID REQUESTED IN THE CALL IS FOR 
C        THE PRECIPITATION OCCURANCE OVER A THREE HOUR PERIOD 
C
      IF((IDPARS(1).NE.708).OR.(IDPARS(2).NE.564))THEN
         IER=103
         WRITE(KFILDO,100)(JD(J),J=1,4),IER
 100     FORMAT(/,' ****IDPARS(1) AND IDPARS(2) DO NOT INDICATE ',
     1            'PRECIPITATION OCCURENCE.',
     2          /,'     VARIABLE ',I9.9,2I10.9,I4.3,
     3            ' NOT ACCOMMODATED IN SUBROUTINE ',
     4            'OBSPOPO3.  IER=',I3)
         GO TO 900
      ENDIF
C
C        FIRST CHECK THAT THE USER IS NOT TRYING TO SPAN THE DECEMBER
C        1, 1996 CUTOFF BETWEEN THE OLD WEATHER DATA AND THE NEW WEATHER
C        DATA.  IF SO, WRITE A WARNING MESSAGE TO THE OUTPUT FILE
C        INDICATING THAT ALL THE OUTPUT DATA WILL BE GIVEN AS "MISSING".
C
      IF((NDATE.EQ.1996120100).AND.(IENTER.NE.0))THEN
        IER=57
        WRITE(KFILDO,120)IER
 120    FORMAT(/,' ****OBSPOPO3 CAN NOT PROCESS DATA BECAUSE OF THE',
     1         ' DECEMBER 1,1996 CUTOFF.  SEE WRITEUP.  IER= ',I4)
        GO TO 900
      ENDIF
C
C        SET THE CONSTANT WORDS OF THE VARIABLE ID.
C        LD(1) IS SET HERE FOR "ALGORITHM TWO" , AND WILL BE REPLACED IN
C        "ALGORITHM ONE". (SEE THE NEXT COMMENT BLOCK.)
C
      LD(1)=708500000+IDPARS(4) 
      LD(2)=IDPARS(7)
      LD(4)=0
C
C        NOW CHECK THE INPUT DATE TO DECIDE WHETHER "ALGORITHM ONE" FOR
C        DATES FROM DECEMBER 1, 1996 AND ON, OR "ALGORITHM 2" FOR
C        DATES FROM MAY 16,1979 THROUGH NOVEMBER 30,1996 IS TO BE USED.
C
      IF(NDATE.GE.1996120100)THEN
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                   *****  ALGORITHM ONE  *****
C        FOR THE DATA OF DECEMBER 1, 1996 AND LATER DATES.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        THE OUTER LOOP INCREMENTS THROUGH THE FOUR OBSERVATIONAL
C        HOURS WHILE THE INNER LOOP WILL GO THROUGH THE ALGORITHM  
C        FOR EACH OF THE 3 WEATHER OBSERVATION GROUPS.
C
        DO 300 I=1,4
C
          IRR=I-1
C
          LD(3)=(IDPARS(9)+IRR)*1000000+IDPARS(12)
C
        DO 200 J=1,3

C           COMPLETE THE LD() ARRAY TO HOLD THE ID FOR THE
C             OBSERVATIONAL WEATHER CODE THAT WILL BE FETCHED AND
C            PROCESSED.
C
          LD(1)=708000000+JWORD(J)*1000+IDPARS(4)
C
C            CALL GFETCH TO GET THE OBSERVATIONAL WEATHER DATA AND 
C            STORE IT IN FD1.
C
          CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1                IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2                NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3                NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,
     4                IER)
C
C            CHECK TO SEE IF IER NE 0, AND IF THIS IS THE FIRST
C            PROCESS DATE.  PRINT ERROR MESSAGE IF NEEDED.
C
          IF((IER.NE.0).AND.(IENTER.EQ.1)) THEN
            WRITE(KFILDO,140)NDATE
 140        FORMAT(/,' ****ERROR FROM GFETCH OCCURRED ON',
     1               ' 1ST PROCESS DATE =',I12,
     2               '.  OBSPOPO3 CANNOT RUN, PLEASE READ WRITE UP.',
     3             /,'     YOU MAY NEED A DUMMY CCC = 799 RECORD',
     4               ' WITH AN RR = 3.')
            GO TO 900
          ENDIF
C
          IF(IER.NE.0) GO TO 900
C
C            IF NWORDS DOES NOT EQUAL NSTA, SET ALL VALUES TO MISSING.
C
          IF(NWORDS.NE.NSTA)THEN
            IER=52
            WRITE(KFILDO,160)NWORDS,NSTA,IER
 160        FORMAT(/,' ****NWORDS =',I6,' RETURNED FROM GFETCH',
     1             ' NOT EQUAL TO NSTA =',I6,
     2             ' IN OBSPOPO3. DATA SET TO MISSING.',
     3             ' IER=',I3)
            GO TO 900
          ENDIF
C
C            SEARCH THROUGH THE GIVEN STATIONS TO ASSIGN PRECIPITATION 
C            OCCURRENCE CODE BASED ON THE OBSERVATIONAL WEATHER CODE.
C
          DO 170 K=1,NSTA
C
            IPWX = NINT(FD1(K))
C
            SELECT CASE(IPWX)
C
C              PRECIPITATING 
C
            CASE(51,53,55,56,57,58,59,61,63,
     1           65,66,67,68,69,71,73,75,76,77,79,80,81,83,84,
     2           85,86,88,90,95,96,97,121,156,
     3           166,174,176,183,187,196)
                XDATA=1.
C
C              NOT PRECIPITATING
C
            CASE(0,4:11,16:19,31,34,36:41,44,45,
     1           98,204,207,208)
                XDATA=0.
C
            CASE(9999) 
                XDATA=9999.
C
            END SELECT
C
          IF((J.EQ.1.AND.(IRR.EQ.0.OR.XDATA.EQ.9999.))
     &         .OR.(XDATA.EQ.1..AND.SDATA(K).NE.9999.))
     &         SDATA(K)=XDATA
C
 170      CONTINUE
C
 200    CONTINUE
C
 300    CONTINUE
C
        GO TO 910
C   
      ELSEIF((NDATE.GE.1979051600).AND.(NDATE.LE.1996113023))THEN
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                     *****  ALGORITHM TWO  *****
C        FOR THE DATA OF MAY 16,1979 THROUGH NOVEMBER 30, 1996.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C          MAKE A LOOP TO GO THROUGH THE FOUR OBSERVATIONAL HOURS.
C
        DO 500 I=1,4
C
          IRR=I-1
C
C            COMPLETE THE LD() ARRAY TO HOLD THE ID FOR THE OBSERVATIONAL
C            WEATHER CODE THAT WILL BE FETCHED AND PROCESSED 
C        
          LD(3)=(IDPARS(9)+IRR)*1000000+IDPARS(12)
C
C            CALL GFETCH TO GET THE OBSERVATIONAL WEATHER DATA AND 
C            STORE IT IN FD1.
C
          CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1                IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2                NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3                NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,
     4                IER)
C
C            CHECK TO SEE IF IER NE 0, AND IF THIS IS THE FIRST
C            PROCESS DATE.  PRINT ERROR MESSAGE IF NEEDED.
C
          IF((IER.NE.0).AND.(IENTER.EQ.1)) THEN
            WRITE(KFILDO,340)NDATE
 340        FORMAT(/,' ****ERROR FROM GFETCH OCCURRED ON',
     1               ' 1ST PROCESS DATE =',I12,
     2               '.  OBSPOPO3 CANNOT RUN, PLEASE READ WRITE UP.',
     3             /,'     YOU MAY NEED A DUMMY CCC = 799 RECORD',
     4               ' WITH AN RR = 3.')
            GO TO 900
          ENDIF
C
          IF(IER.NE.0) GO TO 900
C
C            IF NWORDS DOES NOT EQUAL NSTA, SET ALL VALUES TO MISSING.
C
          IF(NWORDS.NE.NSTA)THEN
            IER=52
            WRITE(KFILDO,360)NWORDS,NSTA,IER
 360        FORMAT(/,' ****NWORDS =',I6,' RETURNED FROM GFETCH',
     1             ' NOT EQUAL TO NSTA =',I6,
     2             ' IN OBSPOPO3. DATA SET TO MISSING.',
     3             ' IER=',I3)
            GO TO 900
          ENDIF
C
C            SEARCH THROUGH THE GIVEN STATIONS TO ASSIGN PRECIPITATION
C            OCCURRENCE CODE BASED ON THE OBSERVATIONAL WEATHER CODE.
C        
          DO 400 K=1,NSTA
C
            IPWX = NINT(FD1(K))
            SELECT CASE(IPWX)
C
C              PRECIPITATING
C
            CASE(1:28)
                XDATA=1.
C
C              NOT PRECIPITATING 
C
            CASE(0)
                XDATA=0.
C
            CASE(9999) 
                XDATA=9999.
            END SELECT
C
            IF(IRR.EQ.0.OR.XDATA.EQ.9999.
     &         .OR.(XDATA.EQ.1..AND.SDATA(K).NE.9999.))
     &             SDATA(K)=XDATA
C
 400      CONTINUE
C
 500    CONTINUE     
C
        GO TO 910 
C
      ENDIF
C
C        NOTE THAT IT DROPS THROUGH HERE FOR DATES BEFORE 1979051600.
C
C        IF THERE WAS AN ERROR IN GFETCH, THE CODE WILL JUMP TO
C        HERE.  SET SDATA TO MISSING BEFORE RETURNING. 
C
 900  DO 905 K=1,ND1
        SDATA(K)=9999.
 905  CONTINUE
C
 910  RETURN
      END     
