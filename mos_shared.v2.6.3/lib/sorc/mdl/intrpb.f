      SUBROUTINE INTRPB(KFILDO,P,NX,NY,DIR,ND1,NSTA,SDATA) 
C 
C        JUNE      1994   GLAHN   TDL   MOS-2000
C        AUGUST    2002   GLAHN   RESTRICTED NBX TO RANGE 1 TO NX-1 AND
C                                 NBY TO RANGE 1 TO NY-1; ELIMINATED
C                                 STATEMENT NO. 130.
C        SEPTEMBER 2002   GLAHN   MODIFIED TO SET RETURNED VALUE TO 9999
C                                 IF ANY ONE OF THE 4 POINTS IS 9999
C        SEPTEMBER 2002   GLAHN   CHANGED 9999 TEST FROM INTEGER TO FP
C        DECEMBER  2002   GLAHN   MODIFIED DIAGNOSTIC AT 159
C 
C        PURPOSE 
C            TO INTERPOLATE INTO FIELD P(NX,NY) FOR ALL STATION
C            LOCATIONS.  BILINEAR INTERPOLATION IS USED INSIDE THE GRID; 
C            LINEAR EXTRAPOLATION IS USED FOR POINTS OUTSIDE THE GRID.
C            IF ANY ONE OF THE 4 POINTS NEEDED FOR INTERPOLATION IS 
C            MISSING (9999), THE RETURNED VALUE IS MISSING.
C 
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            P(IX,JY) = FIELD TO INTERPOLATE INTO (IX=1,NX) (JY=1,NY).
C                       (INPUT)
C                  NX = THE IX (WEST-EAST) EXTENT OF P( , ).  (INPUT)
C                  NY = THE JY (SOUTH-NORTH) EXTENT OF P( , ).  (INPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA).  (INPUT)
C                 ND1 = THE MAXIMUM NUMBER OF STATIONS.  FIRST DIMENSION
C                       OF DIR( , ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH.
C                       (INPUT)
C            SDATA(K) = THE INTERPOLATED VALUES FOR ALL STATIONS 
C                       (K=1,NSTA).  (OUTPUT)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION DIR(ND1,2)
      DIMENSION P(NX,NY)
      DIMENSION SDATA(NSTA)
C
      DO 160 K=1,NSTA
      BX=DIR(K,1)
      BY=DIR(K,2) 
      NBX=BX 
      NBY=BY
C        
C        MAKE SURE GRID IS ACCESSED WITHIN BOUNDARIES.
C   
      IF(NBX.LT.1)THEN
         NBX=1
      ELSEIF(NBX.GT.NX-1)THEN
         NBX=NX-1
      ENDIF
C
      IF(NBY.LT.1)THEN
         NBY=1
      ELSEIF(NBY.GT.NY-1)THEN
         NBY=NY-1
      ENDIF 
C
C        START BI-LINEAR INTERPOLATION-EXTRAPOLATION. 
C 
      NBXP1=NBX+1 
      NBYP1=NBY+1
C
C        MAKE SURE NO VALUE NEEDED IN INTERPOLATION IS MISSING.
C
      IF(P(NBX,NBY)    .NE.9999..AND.
     1   P(NBXP1,NBY)  .NE.9999..AND.
     2   P(NBXP1,NBYP1).NE.9999..AND.
     3   P(NBX,NBYP1)  .NE.9999.)THEN
         DX=BX-FLOAT(NBX) 
         DY=BY-FLOAT(NBY) 
         SDATA(K)=P(NBX,NBY)
     1      +(P(NBXP1,NBY)-P(NBX,NBY))*DX
     2      +(P(NBX,NBYP1)-P(NBX,NBY))*DY
     3      +(P(NBX,NBY)+P(NBXP1,NBYP1)-P(NBX,NBYP1)-P(NBXP1,NBY))*DX*DY
      ELSE
         SDATA(K)=9999.
      ENDIF
C
D     IF(SDATA(K).LT.0.)THEN
D        WRITE(KFILDO,159)K,BX,BY,NBX,NBY,
D    1            P(NBX,NBY),P(NBXP1,NBY),P(NBX,NBYP1),P(NBXP1,NBYP1),
D    2            SDATA(K)
D159     FORMAT(' 'I4,2F12.2,2I4,5F12.2)
D     ENDIF
C
 160  CONTINUE
C
      RETURN 
      END 
