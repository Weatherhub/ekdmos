      SUBROUTINE GRCOMB(KFILDO,IP12,IS2,ND7,NGRIDC,ND11,NGRID,NSLAB,
     1                  CCALL,NAME,STALAT,STALON,DIR,ND1,NSTA,IER)
C
C        APRIL     1994   GLAHN   TDL   MOS-2000
C        SEPTEMBER 1997   GLAHN   ADDED /D TO PRINT STATEMENT 135
C        OCTOBER   1998   GLAHN   REMOVED COMMENT REFERENCE TO IS1( )
C        FEBRUARY  2002   GLAHN   ADDED PRINT OF NGRIDC( , ) WHEN FULL
C        JUNE      2002   GLAHN   ADDED COMMENT FOR MERCATOR
C        APRIL     2003   GLAHN   COMMA IN /D FORMAT; COMMENTS
C        MAY       2003   GLAHN   REARRANGED DIMENSION STATEMENTS
C        JULY      2003   GLAHN   MODIFIED TO ACCEPT A TOLERANCE
C                                 OF 5 FOR NGRID(2), (FOR GRIDLENGTH
C                                 =.000005 KM), NGRID(5), AND NGRID(6) 
C                                 (FOR CORNER POINTS = .05 KM) 
C
C        PURPOSE
C            TO DETERMINE WHETHER OR NOT THE GRID CHARACTERISTICS
C            IN AND IS2( ) ARE THE SAME AS ARE ALREADY STORED
C            IN NGRIDC( , ).  IF NOT, THEY ARE STORED AND NGRID IS
C            INCREASED BY 1.  IN ANY CASE, NSLAB IS THE NUMBER OF
C            THE DEFINITION OF THE GRID IN NGRIDC( , ).  ALSO, THE 
C            XI AND YJ POSITIONS OF STATIONS ARE COMPUTED FOR THIS
C            GRID WHEN IT IS NEW.
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C                IP12 = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS AND THEIR I,J POSITIONS ON THE
C                       GRID WILL BE PRINTED TO THE FILE WHOSE UNIT
C                       NUMBER IS IP12.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC, 7=MERCATOR). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000, AND
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ).  (INPUT)
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN NGRIDC( , ),
C                       MAXIMUM OF ND11.  (INPUT-OUTPUT)
C               NSLAB = THE NUMBER OF THE COMBINATION OF THE GRID IN
C                       NGRIDC( , ).  (OUTPUT)
C            CCALL(K) = 8 STATION CALL LETTERS (K=1,NSTA).  USED FOR
C                       PRINTOUT ONLY.  (CHARACTER*8)  (INPUT)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN NGRIDC( ,M).
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       (INPUT)
C                NSTA = THE NUMBER OF STATIONS IN CCALL( ).  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       53 = WHEN ND11 IS ABOUT TO BE EXCEEDED.
C                       60 = MAP PROJECTION NUMBER NOT EXPECTED (FROM DIRCMP).
C
C        NONSYSTEM SUBROUTINES USED 
C            DIRCMP
C
      CHARACTER*8 CCALL(ND1)
      CHARACTER*20 NAME(ND1)
C
      DIMENSION STALAT(ND1),STALON(ND1)
      DIMENSION IS2(ND7)
      DIMENSION DIR(ND1,2,ND11)
      DIMENSION NGRIDC(6,ND11)
C
C     DETERMINE WHETHER THE GRID CHARACTERISTICS ARE STORED IN NGRIDC( , ).
C
      IER=0
C
      IF(NGRID.EQ.0)GO TO 130
C
      DO 120 J=1,NGRID
      NSLAB=J
      IF(ABS(IS2(2)-NGRIDC(1,J)).LE.5.AND.
     1   IS2(8).EQ.NGRIDC(2,J).AND.
     2   IS2(9).EQ.NGRIDC(3,J).AND.
     3   IS2(7).EQ.NGRIDC(4,J).AND.
     4   ABS(IS2(5)-NGRIDC(5,J)).LE.5.AND.
     5   ABS(IS2(6)-NGRIDC(6,J)).LE.5)GO TO 200
 120  CONTINUE
C
C        DROP THROUGH HERE MEANS THE GRID CHARACTERISTICS ARE NOT YET
C        IN NGRIDC( , ).  INCREASE NGRID AND SAVE THEM.
C
      IF(NGRID.LT.ND11)GO TO 130
C        ND11 ABOUT TO BE EXCEEDED.
      WRITE(KFILDO,125)ND11,((NGRIDC(J,N),J=1,6),N=1,NGRID)
 125  FORMAT(/' ****TOO MANY GRID CHARACTERISTICS COMBINATIONS'/
     1        '     INCREASE ND11 FROM',I4//(6I10))
      IER=53
      GO TO 200
C
 130  NGRID=NGRID+1
      NGRIDC(1,NGRID)=IS2(2)
      NGRIDC(2,NGRID)=IS2(8)
      NGRIDC(3,NGRID)=IS2(9)
      NGRIDC(4,NGRID)=IS2(7)
      NGRIDC(5,NGRID)=IS2(5)
      NGRIDC(6,NGRID)=IS2(6)
      NSLAB=NGRID
D     WRITE(KFILDO,135)(NGRIDC(J,NSLAB),J=1,6)
D135  FORMAT(' STORING GRID CHARACTERISTICS IN GRCOMB',6I10)
C
C        COMPUTE XI AND YJ POSITIONS OF STATIONS.  IF THE MAP
C        PROJECTION IS NOT ACCOMMODATED IN DIRCMP, DIR( , ,NSLAB)
C        IS SET TO 9999.  NGRID REMAINS INCREASED, BUT THERE IS
C        NO STATION CORRESPONDENCE.
C
      CALL DIRCMP(KFILDO,IP12,CCALL,NAME,STALAT,STALON,DIR(1,1,NSLAB),
     1            NGRIDC(1,NSLAB),ND1,NSTA,IER)
 200  RETURN
      END     
     
      
