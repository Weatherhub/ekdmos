      SUBROUTINE RDSTDR(KFILDO,IP4,IP5,KFILD,CCALL,
     1                  NAME,NELEV,IWBAN,STALAT,STALON,IFOUND,
     2                  ND1,NSTA,IER)
C 
C        JULY  1999   GLAHN   TDL   MOS-2000 
C     JANUARY  2003   TRIMARCO  ADDED COMMAS TO FORMAT STATEMENTS
C                             FOR IBM COMPILATION
C
C        PURPOSE 
C            TO READ A STATION DIRECTORY AND MATCH INFORMATION
C            WITH THE STATION LIST IN CCALL( ,1).  5 SUBSTITUTE
C            STATIONS ARE PUT INTO CCALL( ,J),J=2,6), SOME OF WHICH
C            MAY BE BLANK.  CCALL( , ) SHOULD BE DECLARED CHARACTER*8
C            IN THE CALLING PROGRAM.
C
C            NOTE:  THE DIFFERENCE BETWEEN THIS RDSTDR AND OTHER
C                   ROUTINES DEALING WITH THE DIRECTORY (E.G., RDSTAL) IS 
C                   THAT RDSTDR DOES NOT READ THE LIST, BUT ACCEPTS
C                   WHAT IS PROVIDED IN CCALL( ,1).
C    
C
C        DATA SET USE 
C            KFILDO   - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (OUTPUT) 
C            IP4      - UNIT NUMBER FOR OUTPUT OF CALL LETTERS.  (OUTPUT)
C            IP5      - UNIT NUMBER FOR OUTPUT OF DIRECTORY INFORMATION
C                       (OUTPUT) 
C               KFILD - UNIT NUMBER FROM WHICH TO READ STATION DIRECTORY.
C                       IT IS ASSUMED FILE HAS BEEN OPENED.  (INPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C                 IP4 = INDICATES WHETHER (>0) OR NOT (=0) THE STATION 
C                       LIST (CALL LETTERS ONLY) WILL BE WRITTEN TO UNIT
C                       IP4.  IF THERE ARE INPUT ERRORS, THE STATION LIST
C                       WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE UNIT
C                       KFILDO AS WELL AS TO UNIT IP4.  (INPUT)
C                 IP5 = INDICATES WHETHER (>0) OR NOT (=0) THE STATION 
C                       DIRECTORY INFORMATION WILL BE WRITTEN TO UNIT
C                       IP5.  IF THERE ARE INPUT ERRORS, THE STATION LIST
C                       WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE UNIT
C                       KFILDO AS WELL AS TO UNIT IP5.  (INPUT) 
C               KFILD = UNIT NUMBER FROM WHICH TO READ STATION DIRECTORY.
C                       IT IS ASSUMED FILE HAS BEEN OPENED.  (INPUT) 
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) (J=1) (INPUT) FOR
C                       TO PROVIDE OUTPUT (J=2,6) (K=1,NSTA).
C                       (CHARACTER*8)  (INPUIT/OUTPUT)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA)  (CHARACTER*20)
C                       (OUTPUT)
C            NELEV(K) = ELEVATION OF STATIONS (K=1,NSTA).  (OUTPUT)
C            IWBAN(K) = WBAN NUMBERS OF STATIONS (K=1,NSTA).  (OUTPUT) 
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (OUTPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (OUTPUT)
C           IFOUND(K) = USED TO KEEP TRACK OF THE STATIONS FOUND IN THE
C                       DIRECTORY (K=1,NSTA).  (INTERNAL) 
C                       0 = NOT YET FOUND,
C                       1 = FOUND,
C                 ND1 = SIZE OF ARRAYS CCALL( , ), NELEV( ), IWBAN( ), 
c                       STALAT( ), STALON( ) AND IFOUND( ).
C                       THIS IS THE MAXIMUM NUMBER OF STATIONS IN THE
C                       LIST TO BE RETURNED.  (INPUT) 
C                NSTA = COUNT OF ELEMENTS IN ARRAY RETURNED.  (OUTPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0  = GOOD RETURN.
C                       33 = ERROR OR END OF FILE ON UNIT KFILD
C                            WHEN READING THE DIRECTORY.
C                       35 = ONE OR MORE STATIONS NOT FOUND IN THE DIRECTORY.
C                       36 = ONE OR MORE DUPLICATES READ IN.
C                       37 = BOTH IER 35 AND 36 HAVE OCCURRED.
C           CCALLT(J) = TO READ CALL LETTERS INTO FROM DIRECTORY (J=1,6).
C                       (CHARACTER*8)  (INTERNAL)
C               NAMET = TO READ NAME INTO FROM DIRECTORY.  (CHARACTER*20) 
C                       (INTERNAL)
C              NELEVT = TO READ ELEVATION INTO FROM DIRECTORY.  (INTERNAL)
C              IWBANT = TO READ WBAN NUMBER INTO FROM DIRECTORY.  (INTERNAL)
C              ITIMEX = TO READ TIME ZONE INDICATOR INTO FROM DIRECTORY.
C                       (INTERNAL)
C              SIGNLA = SIGN OF THE LATITUDE AS READ FROM THE DIRECTORY.
C                       WILL BE "N" FOR NORTH LATITUDE OR "S" FOR SOUTH
C                       LATITUDE.  WHEN "S", THE LATITUDE WILL BE STORED
C                       AS NEGATIVE.  (CHARACTER*1)  (INTERNAL)
C              XLATDD = LATITUDE IN DEGREES.  (INTERNAL)
C              SIGNLO = SIGN OF THE LONGITUDE AS READ FROM THE DIRECTORY.
C                       WILL BE "E" FOR EAST LONGITUDE OR "W" FOR WEST
C                       LONGITUDE.  WHEN "E", THE LONGITUDE IS ADJUSTED
C                       SO THAT ALL VALUES ARE WEST.  (CHARACTER*1)  (INTERNAL)
C              XLONDD = LONGITUDE IN DEGREES.  (INTERNAL)
C              BLANK8 = 8 BLANKS.  (CHARACTER*8)  (INTERNAL)
C               BLANK = 20 BLANKS   (CHARACTER*20)  (INTERNAL)
C                MSTA = COUNTS THE STATIONS IN THE CCALL( , ) LIST FOUND
C                       IN THE DIRECTORY.  WHEN ALL NSTA STATIONS
C                       HAVE BEEN FOUND, THE READING OF THE DIRECTORY
C                       CAN STOP.
C                 JER = CONTROLS LINE SPACING WHEN PRINTING DIAGNOSTIC
C                       FOR STATIONS NOT FOUND IN DIRECTORY OR DUPLICATE
C                       STATIONS.
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE. 
C
      CHARACTER*1 SIGNLA,SIGNLO
      CHARACTER*4 STATE
      CHARACTER*8 CCALL(ND1,6),BLANK8,CCALLT(6)
      CHARACTER*20 NAME(ND1),NAMET,BLANK
C
      DIMENSION NELEV(ND1),IWBAN(ND1),ITIMEZ(ND1),
     1          STALAT(ND1),STALON(ND1),IFOUND(ND1)
C
      DATA BLANK8/' '/
      DATA BLANK/' '/
      DATA CCALLT/6*' '/
C
      IER=0
      JER=0
      MSTA=0
C
C        INITIALIZE ARRAYS.  NOTE THAT CCALL( ,1) HOLDS THE LIST.
C
      DO 105 K=1,ND1
C
      DO 102 L=2,6
      CCALL(K,L)=BLANK8
 102  CONTINUE
C     
      NAME(K)=BLANK
      NELEV(K)=0
      IWBAN(K)=0
      STALAT(K)=0.
      STALON(K)=0.
      IFOUND(K)=0
 105  CONTINUE
C
C        READ DIRECTORY.  IT IS ASSUMED THE DIRECTORY IS ALPHABATIZED
C        BY CALL LETTERS.  FOR THE CURRENT DIRECTORY, THIS IS BY THE
C        ICAO CALL LETTERS.
C 
 120  STATE='120 '
      J=0
 121  CCALLT(1)=BLANK8
      CCALLT(2)=BLANK8
      NAMET(1:20)=BLANK
C        THE ABOVE STATEMENTS ARE AS A PRECAUTION AND ARE PROBABLY
C        NOT REALLY NEEDED.
      READ(KFILD,122,IOSTAT=IOS,ERR=123,END=140)CCALLT(1),
     1     CCALLT(2),NAMET(1:17),NAMET(19:20),NBLOCK,
     2     NELEVT,SIGNLA,XLATDD,SIGNLO,XLONDD,ITIMEX,ITYPE,
     3     OPEN,(CCALLT(K),K=3,6),IDATE,IWBANT
 122  FORMAT(A8,1X,A8,1X,A17,4X,A2,1X,I6,1X,I5,1X,A1,F7.4,1X,A1,
     1       F8.4,1X,I3,1X,I1,1X,A1,4(1X,A8),1X,I10,1X,I5)
      GO TO 130
C
 123  WRITE(KFILDO,124)IOS
      IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,124)IOS
      IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)WRITE(IP5,124)IOS
 124  FORMAT(/' ****ERROR READING STATION DIRECTORY IN RDSTAR.',
     1        '  IOSTAT =',I5)
      IER=33
      GO TO 160
C
C        A STATION LIST ALREADY RESIDES IN CCALL( ,1).  IF THE
C        STATION EXISTS IN THE DIRECTORY AS ANY OF THE 6 POSSIBLE
C        VALUES, FILL NAME( ), IWBAN( ), NELEV( ), STALAT( ),
C        STALON( ), AND (CCALL( ,L), L=2,6).  THE VALUE IN
C        CCALL( ,1) IS LEFT INTACT.
C        
 130  JP1=0
C
      DO 139 K=1,NSTA
      IF(IFOUND(K).GT.0)GO TO 139
C        ABOVE TEST GUARDS AGAINST DUPLICATES IN THE DIRECTORY.
C        IF THERE IS A DUPLICATE, THE FIRST ENTRY IS USED.C
C
C        CHECK FOR STATIONS.  CCALLT(1) AND CCALLT(2) WILL (ALMOST)
C        NEVER BE BLANK.  HOWEVER, BLANKS ARE MORE COMMON AFTER 
C        THAT THAN NOT.
C
      DO 133 J=1,6
      IF(CCALLT(J).EQ.CCALL(K,1))GO TO 134
 133  CONTINUE
C
      GO TO 121
C
 134  DO 135 J=2,6
      CCALL(K,J)=CCALLT(J)
 135  CONTINUE
C
      IF(J.NE.1)THEN
         CCALL(K,J)=CCALLT(1)
      ENDIF
C
      NAME(K)=NAMET
      IWBAN(K)=IWBANT
      ITIMEZ(K)=ITIMEX
      NELEV(K)=NELEVT
      STALAT(K)=XLATDD
      IF(SIGNLA.EQ.'S')STALAT(K)=-STALAT(K)
C        ABOVE STATEMENT MAKES SOUTH LATITUDE NEGATIVE.
      STALON(K)=XLONDD
      IF(SIGNLO.EQ.'E')STALON(K)=360.-STALON(K)     
C        ABOVE STATEMENT MAKES ALL LONGITUDES WEST, RANGE 0-360.
      JP1=JP1+1
      IF(JP1.EQ.1)GO TO 139
      IER=36
C****      WRITE(KFILDO,132)K,CCALLT(1),CCALLT(2),CCALL(K,1),IFOUND(K),JP1
C
      IF(JER.EQ.0)THEN
         WRITE(KFILDO,136)CCALL(K,1)
         IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,136)CCALL(K,1)
         IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1             WRITE(IP5,136)CCALL(K,1)
 136     FORMAT(/' ****STATION ',A8,' IS A DUPLICATE')
      ELSE
         WRITE(KFILDO,137)CCALL(K,1)
         IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,137)CCALL(K,1)
         IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1             WRITE(IP5,137)CCALL(K,1)
 137     FORMAT(' ****STATION ',A8,' IS A DUPLICATE')
      ENDIF
C
      JER=36
C        EVEN THOUGH THIS DIRECTORY ENTRY HAS BEEN USED,
C        THE SEARCH IS CONTINUED IN CASE THERE
C        ARE DUPLICATE STATIONS IN THE LIST.
 139  CONTINUE
C
C        EITHER THE END OF THE DIRECTORY, OR HAVING FOUND
C        ALL THE NSTA STATIONS CAN HALT READING OF THE
C        DIRECTORY.
C
      IF(MSTA.LT.NSTA)GO TO 121
C
C        DETERMINE AND PRINT WHETHER THERE ARE STATIONS
C        IN THE LIST THAT WERE NOT FOUND IN THE DIRECTORY.
C        SET IER TO THE VALUES INDICATED IN THE PROLOGUE.
C
 140  DO 145 K=1,NSTA
      IF(IFOUND(K).NE.0)GO TO 145
C
      IF(JER.EQ.0)THEN
         WRITE(KFILDO,141)CCALL(K,1)
         IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,141)CCALL(K,1)
         IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1          WRITE(IP5,141)CCALL(K,1)
 141     FORMAT(/' ****STATION ',A8,' NOT FOUND IN DIRECTORY')
      ELSE
         WRITE(KFILDO,142)CCALL(K,1)
         IF(IP4.NE.0.AND.IP4.NE.KFILDO)WRITE(IP4,142)CCALL(K,1)
         IF(IP5.NE.0.AND.IP5.NE.KFILDO.AND.IP4.NE.IP5)
     1          WRITE(IP5,142)CCALL(K,1)
 142     FORMAT(' ****STATION ',A8,' NOT FOUND IN DIRECTORY')
      ENDIF
C
      JER=35
      CCALL(K,2)=BLANK8
      CCALL(K,3)=BLANK8
      CCALL(K,4)=BLANK8
      CCALL(K,5)=BLANK8
      CCALL(K,6)=BLANK8
C        ABOVE STATEMENTS NECESSARY TO OBLITERATE ANY INFO
C        LEFT OVER IN CCALL( , ).
145   CONTINUE      
C 
C        SET ERROR CODE IER.  WHEN IER = 33 OR 34, LEAVE IT.
C        OTHERWISE, SET IER = 37 AS APPROPRIATE.
C
      IF(IER.EQ.33.OR.IER.EQ.34)GO TO 148
C
      DO 147 K=1,NSTA
      IF(IFOUND(K).EQ.0)THEN
         IF(IER.GT.35)THEN
            IER=37
            GO TO 148
         ELSE
            IER=35
         ENDIF
      ELSEIF(IFOUND(K).EQ.2)THEN
         IF(IER.EQ.0.OR.IER.EQ.36)THEN
            IER=36
         ELSE
            IER=37
            GO TO 148
         ENDIF
      ENDIF
 147  CONTINUE
C 
 148  IF(IER.NE.0)WRITE(KFILDO,151)NSTA,(CCALL(J,1),NAME(J),IWBAN(J),
     1     STALAT(J),STALON(J),NELEV(J),ITIMEZ(J),
     2     (CCALL(J,I),I=2,6),J=1,NSTA)
 151  FORMAT(/' ',I4,' STATIONS AND DIRECTORY INFORMATION',/,
     1        ' STA           NAME             WBAN',
     2        '    LAT     LON   ELEV  ZONE  SUBSTITUTE STATIONS',/,
     3        (1X,A8,1X,A20,1X,I5,2F8.2,I6,I5,2X,5(1X,A8)))
      IF(IP5.EQ.0)GO TO 160
      IF(IP5.EQ.KFILDO.AND.IER.NE.0)GO TO 160
      WRITE(IP5,151)NSTA,(CCALL(J,1),NAME(J),IWBAN(J),
     1      STALAT(J),STALON(J),NELEV(J),ITIMEZ(J),
     2      (CCALL(J,I),I=2,6),J=1,NSTA)
C        STATION DIRECTORY MAY BE WRITTEN TWICE WHEN THERE ARE ERRORS
C        DETECTED, ONCE TO THE DEFAULT OUTPUT FILE AND ONCE TO UNIT
C        IP5.
 160  RETURN
      END
