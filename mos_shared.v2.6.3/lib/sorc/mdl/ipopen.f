      SUBROUTINE IPOPEN(KFILDO,CALING,IPINIT,IP,IER)
C 
C        SEPTEMBER 1994   GLAHN   TDL   MOS2000 
C        AUGUST    1996   GLAHN   VARIABLE IFIRST ADDED
C        OCTOBER   1996   GLAHN   ADDED CHECK FOR UNIT NOS. 10 AND 80
C        SEPTEMBER 1999   GLAHN   DELETED CHECK FOR UNITS 10 AND 80;
C                                 ADDED CHECK FOR UNITS 97, 99 AND 45-49
C        MAY       2000   GLAHN   ADDED COMMAS TO FORMAT STATEMENTS
C        MARCH     2003   GLAHN   ADDED CHECK IP( ) AGAINST 5
C        JANUARY   2005   GLAHN   ADDED CHECK IP( ) AGAINST 42-44
C        MARCH     2005   GALHN   CORRECTION TO DIAGNOSTIC
C        JUNE      2005   RLC     MERGED NEW DEVELOPMENTAL VERSION WITH
C                                 MCE 5/2000 OPERATIONAL CHANGES OF NOT
C                                 REMOVING FILES AND TAKING NAME OUT OF  
C                                 OPEN STATEMENTS.
C
C        PURPOSE  
C            TO OPEN FOR OUTPUT THE FILES INDICATED IN IP( ), EXCEPT 
C            WHEN IP( ) = KFILDO WHICH IS CONSIDERED TO BE THE DEFAULT
C            PRINT FILE.  THE FILE NAMES WILL BE 4 CHARACTERS FROM
C            CALING, THEN 4 CHARACTERS FROM IPINIT, THEN 2 CHARACTERS
C            FROM IP(J).  WHEN IP(1) NE KFILDO, KFILDO IS SET = IP(1)
C            SO THAT DIFFERENT RUNS CAN HAVE DIFFERENT "DEFAULT" OUTPUT
C            FILES.  IN THIS CASE, TIMPR IS CALLED SO THAT THE "DEFAULT"
C            (PRINT) OUTPUT WILL BE TIME STAMPED AT THE TOP.  NOTE 
C            THAT ALL FILES ARE NOT TIME STAMPED BECAUSE SOME OF THEM
C            COULD BE BINARY AND A TIME STAMP MIGHT NOT BE WANTED.
C            ALSO, A CHECK IS MADE TO MAKE SURE UNIT NOS. 97, 99 AND 
C            45 THRU 49 ARE NOT IN IP( ), BECAUSE THEY ARE RESERVED 
C            FOR MOS-2000 PROGRAMS.  UNIT NO. 99 IS FOR THE MOS-2000
C            INTERNAL RANDOM ACCESS FILE.  NO. 97 IS FOR READING BY
C            1-D AND 2-D COMPUTATIONAL ROUTINES IN U201; THIS MAY
C            BE CHANGED LATER.
C
C        DATA SET USE 
C           KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C           IP( )  - UNIT NUMBERS FOR OUTPUT FILES, WHEN NON ZERO.
C                    (OPENED FOR OUTPUT) 
C
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       WHEN IP(1) NE 0, KFILDO IS SET = IP(1). 
C                       (INPUT-OUTPUT)
C              CALING = 4 CHARACTERS REPRESENTING THE CALLING PROGRAM
C                       NAME.  (CHARACTER*4)  (INPUT)
C              IPINIT = 4 CHARACTERS TO HELP DEFINE OUTPUT FILE NAME.
C                       THESE ARE APPENDED TO THE 4 CHARACTERS IN CALING.
C                       (CHARACTER*4)  (INPUT)
C               IP(J) = INDICATES WHETHER (>0) OR NOT (=0) PARTICULAR
C                       SEGMENTS OF PRINT WILL BE DONE AND IF SO, THE
C                       UNIT NUMBER FOR THE PRINT FILE (J=1,25).  THIS
C                       VALUE WILL BE THE LAST 2 CHARACTERS OF THE FILE
C                       NAME.  WHEN IP( ) = 0 OR KFILDO, THE FILE IS NOT
C                       OPENED.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       31 = ERROR OPENING FILE. 
C 
C        NONSYSTEM SUBROUINES USED 
C            IERX 
C
      CHARACTER*(*) CALING
      CHARACTER*4 IPINIT,STATE
      CHARACTER*10 FLNAM
      CHARACTER*20 CTIMPR
C
      DIMENSION IP(25)
C
      DATA CTIMPR/'START               '/
      DATA IFIRST/0/
C
      IER=0
      STATE='110 '
C
      DO 120 J=1,25
      IF(IP(J).EQ.0)GO TO 120
 102  IF(IP(J).EQ.KFILDO)GO TO 120
C        KFILDO IS THE DEFAULT PRINT FILE AND DOESN'T NEED TO BE OPENED.
C
C        CHECK FOR RESERVED UNIT NOS. 99, 97, 45-49, AND 5.
C        UNIT 99 IS KFIL10 FOR THE INTERNAL STORAGE SYSTEM.
C        UNIT 97 IS FOR LINEARIZING ROUTINES AND INTERNAL TO U830.
C        UNITS 45-49 ARE FOR RANDOM ACCESS FILES.
C        UNIT 5 IS KFILDI INPUT IN ALL MOS PROGRAMS.
C
      IF(IP(J).EQ.99.OR.
     1   IP(J).EQ.97.OR.
     2   IP(J).EQ.42.OR.
     3   IP(J).EQ.43.OR.
     4   IP(J).EQ.44.OR.
     5   IP(J).EQ.45.OR.
     6   IP(J).EQ.46.OR.
     7   IP(J).EQ.47.OR.
     8   IP(J).EQ.48.OR.
     9   IP(J).EQ.49.OR.
     A   IP(J).EQ.5)THEN
         WRITE(KFILDO,103)J,IP(J),KFILDO
 103     FORMAT(/,' ****AN ATTEMPT IS MADE TO USE IP(',I2,') NO. =',I4,
     1           ' WHICH IS RESERVED.  DO NOT USE UNIT NOS. 97, 99,',
     2           ' 5, OR 42-49.'/,
     3           '     THIS IP( ) IS SET TO THE DEFAULT OUTPUT FILE',
     4           ' NUMBER',I3)
         IP(J)=KFILDO
      ENDIF
         
      IF(J.EQ.1)GO TO 105
C
      DO 104 M=1,J-1
      IF(IP(J).EQ.IP(M))GO TO 120
C        OPEN A FILE ONLY ONCE.
 104  CONTINUE
C    
 105  FLNAM(1:4)=CALING(1:4)
      FLNAM(5:8)=IPINIT(1:4)
      WRITE(FLNAM(9:10),'(I2.2)')IP(J)
C     CALL SYSTEM('rm -f '//FLNAM)
C        REMOVE AN EXISTING FILE OF NAME FLNAM IF IT EXISTS. 
C        AN ERROR WILL OCCUR IF THE FILE ALREADY EXISTS.
      OPEN(UNIT=IP(J),STATUS='NEW',IOSTAT=IOS,ERR=900)
      IF(J.GT.1)GO TO 109
C
      IF(IP(1).NE.KFILDO)THEN
         KFILDO=IP(1)
         CTIMPR(7:10)=CALING(1:4)
         CALL TIMPR(KFILDO,KFILDO,CTIMPR)
C        CONTROL DOES NOT COME HERE UNLESS IP(1) NE 0.
      ENDIF
C
 109  IFIRST=IFIRST+1
      IF(IFIRST.EQ.1)WRITE(KFILDO,1090)
 1090 FORMAT(' ')
      WRITE(KFILDO,110)FLNAM
 110  FORMAT(' OPENING FILE',1X,A10,' FOR OUTPUT.')
 120  CONTINUE
C
      RETURN
C
 900  CALL IERX(KFILDO,KFILDO,IOS,'IPOPEN',STATE)
      IER=31
      RETURN
      END
                                    
