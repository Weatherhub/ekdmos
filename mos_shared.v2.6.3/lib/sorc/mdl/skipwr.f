      SUBROUTINE SKIPWR(KFILDO,KFILIO,KSKIP,KWRITE,KCHECK,
     1                  CCALL,ND1,NSTA,
     2                  CCALLD,NDX,IPACK,ND5,
     3                  NTOTBY,NTOTRC,L3264B,L3264W,IER)
C
C        JUNE      1995   GLAHN   TDL   MOS-2000
C        AUGUST    1996   GLAHN   ELIMINATED MISS AND CMISS
C        OCTOBER   1996   GLAHN   ADDED KWRITE AND KCHECK
C        JANUARY   1997   GLAHN   CHANGED CALL, ND1 TO ND5 FOR CCALLD( )
C        JANUARY   1997   GLAHN   CHANGED CALL TO INCLUDE NDX
C        JANUARY   1997   GLAHN   CALL TO UNPKBG CHANGED TO INCLUDE L3264B
C        JANUARY   1997   GLAHN   A FEW COMMENTS CHANGED; NO CHANGE TO CODE
C        JANUARY   1997   GLAHN   ELIMINATED NBYTES FROM CALL AND INSERTED
C                                 CALL TO TRAIL
C        JANUARY   1997   GLAHN   COMMENT CHANGED, NO CODE CHANGE
C        FEBRUARY  1997   GLAHN   A NUMBER OF CHANGES REGARDING CHECKING
C                                 AND WRITING OF DIRECTORY AND TRAILER.
C        JANUARY   1998   GLAHN   2ND DIMENSION ADDED TO CCALL( , ); ACCEPTS
C                                 A DIRECTORY WITH SUBSTITUTE STATIONS
C        SEPTEMBER 1998   GLAHN   ELIMINATED SKIPPING WHEN KFILIO = 0
C        OCTOBER   1998   GLAHN   SEVERAL CHANGES TO ACCOMMODATE ALL
C                                 POSSIBILITIES THAT MIGHT BE ENCOUNTERED
C        OCTOBER   1998   GLAHN   SOME COMMENTS CHANGED
C        NOVEMBER  1998   GLAHN   MOVED IF(KSKIP.EQ.0)GO TO 222
C        FEBRUARY  1999   GLAHN   ADDED TEST FOR ISHORT LE ND5
C        APRIL     2000   DALLAVALLE   MODIFIED FORMAT STATMENTS TO 
C                                 CONFORM TO FORTRAN 90 STANDARDS
C                                 ON THE IBM SP
C        MAY       2006   GLAHN   MODIFIED WRITE STATEMENTS 225 AND 235
C        JULY      2006   DALLAVALLE   CORRECTED WRITE STATEMENT 230 TO
C                                      REFER TO CORRECT FILE NAME
C
C        PURPOSE
C           TO SKIP RECORDS ON THE OUTPUT FILE AND TO WRITE THE
C           CALL LETTERS RECORD.  WHEN KSKIP NE 0, CALL LETTERS AND
C           DATA RECORDS WILL BE READ UNTIL THE DATE/TIME IN KSKIP
C           IS PASSED OR AN EOF IS FOUND.  WHEN KWRITE NE 0, THE CALL
C           LETTERS RECORD IN CCALL( ,1) WILL BE WRITTEN FOLLOWING THE 
C           DATA FOR THE DATE/TIME KSKIP.  WHEN KCHECK NE 0, ANY CALL
C           LETTERS RECORD READ WILL BE CHECKED WITH THE ONES IN CCALL( , )
C           AND AN ERROR RETURNED IF THEY ARE NOT THE SAME.  WHEN
C           KSKIP = 0, THE CALL LETTERS WILL ALWAYS BE WRITTEN, 
C           REGARDLESS OF KWRITE.  A CALL LETTERS RECORD WILL EXIST
C           AS THE FIRST RECORD ON THE FILE AND ANOTHER MAY EXIST
C           FOLLOWING A 32-BYTE RECORD IN WHICH THE "DATE" = 9999.
C           SKIPWR IS FOR VECTOR DATA; SKIPR PERFORMS A SIMILIAR
C           FUNCTION FOR GRIDPOINT DATA.  NOTE THAT WHILE THE PRIMARY
C           STATIONS ARE IN CCALL( ,1), THE STATION LIST READ IS
C           ALSO MATCHED WITH THE SUBSTITUTE STATIONS IN CCALL( ,J)
C           (J=2,5), IF NECESSARY, AND IF ANY MATCH, THE DIRECTORY
C           RECORD IS USED, BUT WITH A DIAGNOSTIC.  ONLY THE LAST
C           CALL LETTERS RECORD IS READ IS MATCHED; OTHER PROGRAMS
C           WILL HAVE ASSURED ALL MATCH UP TO THAT POINT.
C
C        COMMENT
C           UNUSUAL CONDITIONS CAUSE A **** DIAGNOSTIC.  USUAL
C           CONDITIONS CAUSE A **** DIAGNOSTIC ONLY WITH /D COMPILE.
C   
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILIO    - UNIT NUMBER OF OUTPUT FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILIO = UNIT NUMBER OF OUTPUT FILE.  IF KFILIO = 0,
C                       THIS IS A DO NOTHING ROUTINE.  (INPUT)
C               KSKIP = 0 IF THE CALL LETTERS RECORD IS TO BE WRITTEN
C                       AS THE FIRST RECORD ON THE FILE.
C                       NE 0 INDICATES THAT THE OUTPUT FILE
C                       IS TO BE MOVED FORWARD UNTIL ALL DATA FOR
C                       DATE/TIME KSKIP HAVE BEEN SKIPPED.  KSKIP IS 
C                       INPUT AS YYYYMMDDHH.  (INPUT)
C              KWRITE = 0 IF CALL LETTERS RECORD IS NOT TO BE WRITTEN.
C                       NE 0 OTHERWISE.  (INPUT)
C              KCHECK = 0 IF CALL LETTERS RECORD READ IS NOT TO BE CHECKED
C                       WITH THE ONE IN CCALL( , ).
C                       NE O OTHERWISE.  IF KCHECK = 0 WHEN KSKIP NE 0
C                       AND KWRITE EQ 0, KCHECK IS TREATED AS NE 0
C                       FOR SAFETY.  (INPUT)
C          CCALL(K,J) = 8 STATION CALL LETTERS (K=1,NSTA).  THE PRIMARY
C                       STATIONS ARE IN CCALL( ,1), BUT ANY SUBSTITUTE
C                       STATION IN J=2,5 WILL BE ACCEPTED.
C                       (CHARACTER*8)  (INPUT)
C                 ND1 = 1ST DIMENSION OF CCALL( , ).  (INPUT)
C                NSTA = THE NUMBER OF CALL LETTERS IN CCALL( , ).  (INPUT)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,NDX).  THIS VARIABLE 
C                       HOLDS THE LIST OF STATIONS ALREADY ON THE OUTPUT
C                       FILE WHEN DATA ARE SKIPPED ON IT.  IT IS USED
C                       TO CHECK THE LIST TO WRITE WHEN DESIRED.  WHEN
C                       THE LISTS DO NOT AGREE, THE PROGRAM RESPONDS
C                       TO KWRITE.  (CHARACTER*8)  (INTERNAL)
C                 NDX = DIMENSION OF CCALLD( ).  THIS MAY BE ND1, ND5, OR
C                       SOME OTHER VALUE IN CALLING PROGRAMS.  GENERALLLY,
C                       PROGRAMS WILL HAVE CCALLD( ), IPACK( ) AND 
C                       OTHER VARIABLES DIMENSIONED ND5.  (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ).  (INPUT)
C              NTOTBY = THE TOTAL NUMBER OF BYTES ON THE FILE.  IT IS
C                       INITIALIZED, AND THE BYTES ARE COUNTED WHEN 
C                       SKIPPING RECORDS AND WRITING CALL LETTERS RECORDS.
C                       (OUTPUT)
C              NTOTRC = THE TOTAL NUMBER OF RECORDS ON THE FILE.  IT IS
C                       INITIALIZED, AND THE RECORDS ARE COUNTED WHEN 
C                       SKIPPING RECORDS AND WRITING CALL LETTERS RECORDS.
C                       (OUTPUT)
C              L3264B = WORD LENGTH IN BITS OF MACHINE BEING USED.
C                       (INPUT)
C              L3264W = THE NUMBER OF 32-BIT "WORDS" TO CONTAIN 64 BITS.
C                       2 FOR 32-BIT MACHINE, 1 FOR 64-BIT MACHINE.
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       140 = ERROR READING CALL LETTERS RECORD.
C                       141 = CCALLD( ) ARRAY NOT LARGE ENOUGH.
C                       142 = CALL LETTERS READ FROM FILE DO NOT MATCH
C                             CCALL( , ).
C                       143 = ERROR WHEN SKIPPING RECORDS.
C                       144 = ERROR WRITING CALL LETTERS RECORD.
C                       147 = IMPOSSIBLE SITUATION.
C                       148 = ND5 TOO SMALL.
C                       OTHER RETURNS FROM UNPKBG OR PKBG.
C                       (OUTPUT)
C           NBYTES(L) = USED TO ACCESS DATA AS EITHER 2 WORDS FOR A 32-BIT
C                       MACHINE OR 1 WORD FOR A 64-BIT MACHINE (L=1,2).
C                       (INTERNAL)
C               IDATE = DATE/TIME OF RECORD READ.  (INTERNAL)
C               NSTA1 = NUMBER OF STATIONS IN THE LIST READ FROM THE
C                       OLD OUTPUT FILE.  (INTERNAL)
C              BLANK8 = 8 BLANKS.  USED TO BLANK OUT ARRAY SO IF
C                       THE LISTS DON'T MATCH AND ARE PRINTED, THE
C                       CHARACTERS WILL BE PRINTABLE.  (CHARACTER*8)
C                       (INTERNAL)
C              NTOTCL = COUNTS NUMBER OF CALL LETTERS RECORDS READ. 
C                       (INTERNAL)
C              ISHORT = THE NUMBER OF WORDS IN A TRAILER RECORD,
C                       FOLLOWING THE INITIAL 8 BYTES.  (INTERNAL)
C                IWRT = 0 WHEN CALL LETTERS RECORD IS CHECKED AND
C                         AGREES WITH THE ONE PROVIDED IN CCALL( , ).
C                       1 WHEN THERE IS NOT AGREEMENT, OR DIRECTORY
C                         IS TO BE WRITTEN.
C                       (INTERNAL)
C           CCALLX(K) = 8 STATION CALL LETTERS (K=1,NDX).  THIS VARIABLE 
C                       HOLDS THE PREVIOUSLY READ LIST OF STATIONS
C                       ALREADY ON THE OUTPUT FILE WHEN DATA ARE SKIPPED 
C                       ON IT.  IT IS USED TO CHECK THE LIST TO 
C                       WRITE WHEN DESIRED.  WHEN THE LISTS DO NOT
C                       AGREE, THE PROGRAM RESPONDS TO KWRITE.  THIS
C                       VARIABLE, IN ADDITION TO CCALLD( ), IS NECESSARY
C                       TO KEEP A DIRECTORY AND TRAILER TO BE KEPT
C                       WITH NO DATA BETWEEN.  THIS MIGHT HAPPEN WITH
C                       MULTIPLE RUNS WHERE THE SAME KSKIP WERE USED WITH
C                       KWRITE = 1 AND THE STATIONS IN CCALL( , )
C                       DIFFERENT ON THE RUNS.
C                       (CHARACTER*8)  (AUTOMATIC-INTERNAL)
C               NSTAP = THE NUMBER OF CALL LETTERS IN CCALLX( ).
C                       (INTERNAL)
C                LAST = 9999 WHEN THE LAST RECORD READ WAS A TRAILER.
C                       0 OTHERWISE.  (INTERNAL)
C
C        NONSYSTEM SUBROUINES USED 
C            UNPKBG, TRAIL
C
      CHARACTER*8 CCALL(ND1,6)
      CHARACTER*8 CCALLD(NDX)
      CHARACTER*8 BLANK8
      CHARACTER*8 CCALLX(NDX)
C        CCALLX( ) IS AN AUTOMATIC ARRAY.
C
      DIMENSION IPACK(ND5)
      DIMENSION NBYTES(2)
C
      DATA BLANK8/' '/
C
      IER=0
C
C        SET NBYTES( ) = 0 AND COUNT RECORDS.
C
      NBYTES(1)=0
      NBYTES(2)=0
      ICOUNT=0
C
C        INITIALIZE NTOTCL, NTOTBY, NTOTRC, AND ISHORT.
C
      NTOTCL=0
C        NTOTCL IS THE TOTAL NUMBER OF CALL LETTERS RECORDS READ
C        AND WRITTEN.  NOTE THAT NTOTCL, NTOTBY, AND NTOTRC ARE
C        INITIALIZED HERE INSTEAD OF IN A DATA STATEMENT IN CASE
C        SKIPWR IS ENTERED MORE THAN ONCE.
      NTOTBY=0
C        NTOTBY IS THE TOTAL NUMBER OF BYTES ON THIS FILE.
      NTOTRC=0
C        NTOTRC IS THE TOTAL NUMBER OF RECORDS ON THIS FILE.
      ISHORT=192/L3264B
C        ISHORT IS THE NUMBER OF MACHINE WORDS IN A TRAILER,
C        AFTER THE INITAL 8 BYTES.  IT IS ALSO THE NUMBER OF
C        BYTES THAT MUST BE READ TO FIND THE DATE IN SKIPPING
C        RECORDS.
C
      IF(ISHORT.GT.ND5)THEN
         WRITE(KFILDO,101)ND5,ISHORT
 101     FORMAT(/,' ****ND5 =',I4,' TOO SMALL IN SKIPWR.',
     1            '  INCREASE TO GE',I4)
         IER=148
         GO TO 300
      ENDIF
C
      LAST=0
      NSTA1=0
      JBYTES=0
C
      IF(KFILIO.EQ.0)GO TO 300
      IF(KSKIP.EQ.0)GO TO 222
C
C        INITIALIZE CCALLD( ).
C
      DO 102 K=1,NDX
      CCALLD(K)=BLANK8
 102  CONTINUE
C
C        THIS SECTION FOR READING CALL LETTERS RECORD.
C        FIRST, TRANSFER CALL LETTERS IN CCALLD( ) TO
C        CCALLX( ) AND BLANK OUT CCALLD( ).
C
 105  DO 106 K=1,NDX
      CCALLX(K)=CCALLD(K)
      CCALLD(K)=BLANK8
 106  CONTINUE
C
      NSTAP=NSTA1
C        NSTAP IS THE NUMBER OF CALL LETTERS IN CCALLX( ).
      READ(KFILIO,IOSTAT=IOS,ERR=110,END=115)(NBYTES(J),J=1,L3264W),
     1                   (CCALLD(K),K=1,MIN(NDX,NBYTES(L3264W)/8))
      NTOTRC=NTOTRC+1
C        NTOTRC IS THE TOTAL NUMBER OF DIRECTORY RECORDS READ.
      NTOTCL=NTOTCL+1
C        NTOTCL IS THE TOTAL NUMBER OF RECORDS READ.
      LSTCLB=NBYTES(L3264W)+8
C        LSTCLB IS THE BYTES IN THIS DIRECTORY, SAVED IN CASE
C        IT HAS TO BE BACKSPACED OVER.
      NTOTBY=NTOTBY+LSTCLB
      GO TO 120
C
 110  NTOTP1=NTOTCL+1
      WRITE(KFILDO,111)NTOTP1,IOS
 111  FORMAT(/,' ****ERROR READING CALL LETTERS RECORD, NO. ',I5,
     1         ' IN SKIPWR AT 110, IOSTAT =',I5)
      IER=140
      GO TO 300
C
C     *********************************************************
C
C        THIS SECTION FOR AN END OF FILE WHEN READING DIRECTORY.
C
C        THIS CAN HAPPEN ONLY AT THE BEGINNING OR FOLLOWING A 
C        TRAILER.
C
C     *********************************************************
C
 115  IF(NTOTRC.EQ.0)THEN
         WRITE(KFILDO,116)KFILIO
 116     FORMAT(/,' ****THIS IS A NEW DATA SET ON UNIT ',I2,
     1            '.  START WRITING AT THE BEGINNING.')
         REWIND KFILIO
         IWRT=1
         GO TO 222
C
      ELSE
         BACKSPACE KFILIO
C           IN ORDER TO WRITE, MUST BACKSPACE OVER THE EOF.
C
         IF(LAST.EQ.9999)THEN
D           WRITE(KFILDO,1165)KFILIO,KSKIP
D1165       FORMAT(/,' ****SKIPPED ALL DATA ON UNIT ',I2,
D    1               ' LOOKING FOR DATE/TIME',I12)   
            BACKSPACE KFILIO
C              IF THE LAST RECORD READ WAS A TRAILER, BACKSPACE
C              OVER IT.  THIS IS THE ONLY POSSIBILITY; A
C              NEW DIRECTORY RECORD IS READ ONLY AFTER A TRAILER.
            NTOTRC=NTOTRC-1
            NTOTBY=NTOTBY-32
C              32 IS THEN NUMBER OF BYTES IN THE TRAILER RECORD.
         ELSE
            WRITE(KFILDO,117)
C              THIS SHOULD BE AN IMPOSSIBLE SITUATION.
 117        FORMAT(/,' ****CODE ERROR IN SKIPWR AT 117.')
            IER=147
            GO TO 300
         ENDIF             
C
C           REINITIALIZE CCALLD( ).
C
         DO 118 K=1,NDX
         CCALLD(K)=CCALLX(K)
 118     CONTINUE
C
         NSTA1=NSTAP
C***D        WRITE(KFILDO,119)LAST,NTOTRC,NTOTBY,(CCALL(K,1),CCALLD(K),
C***D    1                    K=1,MIN(NDX,ND1))
C***D119     FORMAT(/,' LAST,NTOTRC,NTOTBY,CCALL(K,1),CCALLD(K)',
C***D    1           3I8,/,(' ',2A8))
C           THE ABOVE MAY PRINT A LARGE NUMBER OF BLANKS.
         GO TO 200
C
      ENDIF
C
C        **********************************************************
C
C        GOOD READ.  CHECK FOR SUFFICIENCY OF ARRAY SIZE. 
C        WITH THE READ ABOVE, OVERFLOW WILL NOT OCCUR.
C
 120  NSTA1=NBYTES(L3264W)/8
      IF(NDX.GE.NSTA1)GO TO 150
      WRITE(KFILDO,121)NDX,NSTA1
 121  FORMAT(/,' ****ARRAY CCALLD( ) NOT LARGE ENOUGH TO HOLD CALL',
     1         ' LETTERS IN FILE.',/,
     2         '     INCREASE NDX IN SKIPWR FROM',I5,' TO GE',I5)
      IER=141
      GO TO 300
C
C        READ PACKED RECORD.  THE CALL LETTERS READ ARE RETAINED
C        IN CCALLD( ) FOR LATER CHECKING AS NECESSARY.  ONLY
C        THE LAST CALL LETTERS RECORD READ NEED BE CHECKED.
C
 150  READ(KFILIO,IOSTAT=IOS,ERR=151,END=155)(NBYTES(J),J=1,L3264W),
     1                                       (IPACK(J),J=1,ISHORT)
      NTOTRC=NTOTRC+1
      JBYTES=NBYTES(L3264W)+8
      NTOTBY=NTOTBY+JBYTES
      GO TO 160
C
C        ERROR READING FILE WHEN SKIPPING RECORDS.
C
 151  WRITE(KFILDO,152)KSKIP,IOS
 152  FORMAT(/,' ****ERROR IN SKIPWR AT 151 WHEN SKIPPING RECORDS',
     1         ' ON OUTPUT FILE WHILE LOOKING FOR DATE KSKIP =',
     2       I12,'.',/,'     IOSTAT =',I5,'.')
C        IF PROCESSING WERE TO CONTINUE, THE OUTPUT FILE MIGHT NOT
C        BE POSITIONED PROPERLY AND DATA WOULD BE OVERWRITTEN.
      IER=143
      GO TO 300
C
C     *********************************************************
C
C        THIS SECTION FOR AN END OF FILE WHEN SKIPPING RECORDS.
C
C     *********************************************************
C
C        END OF FILE ENCOUNTERED WHEN SKIPPING RECORDS.  THIS MAY BE OK.
C        THAT IS, THE DATE TO BE SKIPPED MAY BE THE LAST ONE ON THE FILE.
C
 155  BACKSPACE KFILIO
C        THIS BACKSPACES OVER THE EOF.
C
      IF(ICOUNT.EQ.0)THEN
C           THERE WERE NO DATA AFTER THE LAST DIRECTORY.  THIS IS
C           NOT NORMAL.
C
         IF(NTOTCL.EQ.1)THEN
C              THERE WAS ONLY ONE DIRECTORY RECORD AND NO DATA READ.
C              START WRITING AT THE BEGINNING.
            WRITE(KFILDO,156)KFILIO,KSKIP
 156        FORMAT(/,' ****EOF FOUND.  NO DATA TO SKIP ON UNIT ',I2,
     1              ' FOLLOWING A SINGLE DIRECTORY WHILE LOOKING FOR',
     2              ' DATE/TIME',I12,'.',/,
     3              '     TREATING AS A NEW FILE.')
            REWIND KFILIO
            IWRT=1
            NTOTRC=0
            NTOTCL=0
            NTOTBY=0
            GO TO 222
         ELSE
            WRITE(KFILDO,157)KFILIO,KSKIP
 157        FORMAT(/,' ****EOF FOUND.  NO DATA TO SKIP ON UNIT ',I2,
     1               ' AFTER THE LAST DIRECTORY WHILE LOOKING FOR',
     2               ' DATE/TIME',I12,'.',/,
     3               '     CHECKING THE PREVIOUS DIRECTORY.')
            BACKSPACE KFILIO
C              BACKSPACE OVER DIRECTORY.
            NTOTBY=NTOTBY-LSTCLB
            BACKSPACE KFILIO
C              BACKSPACE OVER TRAILER.
            NTOTBY=NTOTBY-32
            NTOTRC=NTOTRC-2
            NTOTCL=NTOTCL-1
C
C              TRANSFER THE PREVIOUS DIRECTORY INTO CCALLD( ).
C
            DO 158 K=1,NDX
            CCALLD(K)=CCALLX(K)
 158        CONTINUE
C
            NSTA1=NSTAP
            GO TO 200
         ENDIF
C
      ELSE
         IF(LAST.EQ.9999)THEN
D           WRITE(KFILDO,1165)KFILIO,KSKIP
            BACKSPACE KFILIO
C              BACKSPACE OVER TRAILER.
            NTOTBY=NTOTBY-32
         ENDIF
C              THIS WOULD HAPPEN ONLY ON UNSUCCESSFUL COMPLETION
C              OF PREVIOUS RUN.  THE TRAILER SHOULD ALWAYS HAVE
C              BEEN WRITTEN.
            WRITE(KFILDO,159)KFILIO
 159        FORMAT(/,' ****NO TRAILER AFTER DATA ON UNIT ',I2,
     1               '.  PREVIOUS RUN MUST HAVE NOT COMPLETED',
     2               'SUCCESSFULLY.  PROCEEDING')
            GO TO 200
      ENDIF
C
      GO TO 200
C
C     *********************************************************
C
 160  IF(L3264B.EQ.32)THEN
         IDATE=IPACK(5)
C           IPACK(5) HOLDS THE DATE/TIME OF THE RECORD FOR A 32-BIT
C           MACHINE.
      ELSE
         LOC=3
         IPOS=1
         CALL UNPKBG(KFILDO,IPACK,ND5,LOC,IPOS,IDATE,32,L3264B,IER,*170)
C           THE LEFT HALF OF IPACK(3) HOLDS THE DATE/TIME OF THE RECORD
C           FOR A 64-BIT MACHINE.
      ENDIF
C
      GO TO 180
C
 170  WRITE(KFILDO,171)
 171  FORMAT(/,' ****ERROR IN UNPKBG IN SKIPWR AT 171.')
      GO TO 300
C
 180  CONTINUE
C
D     WRITE(KFILDO,181)KSKIP,IDATE,NTOTRC,NTOTCL,NTOTBY,
D    1                 ICOUNT,NSTA,NSTA1,NSTAP,LAST
D181  FORMAT(' KSKIP,IDATE,NTOTRC,NTOTCL,NTOTBY,',
D    1       'ICOUNT,NSTA,NSTA1,NSTAP,LAST',2I11,8I6)
C
      LAST=0
      IF(IDATE.EQ.9999)THEN
         LAST=9999
         GO TO 105
      ENDIF
C
C        WHEN THE ABOVE TEST IS MET, THE RECORD JUST READ WAS A
C        TRAILER, AND ANOTHER CALL LETTERS RECORD MAY FOLLOW.
C 
      IF(IDATE.LE.KSKIP)THEN
         ICOUNT=ICOUNT+1
C           ICOUNT GT 0 INDICATES DATA RECORDS TO BE SKIPPED
C           FOLLOWING LAST DIRECTORY RECORD.
         GO TO 150
C
C     *********************************************************
C
C        THIS SECTION FOR A GOOD RECORD (NON TRAILER) READ PAST
C        THE RECORD TO SKIP.
C
C     *********************************************************
C
      ELSE
         IF(ICOUNT.EQ.0)THEN
C              WHEN ICOUNT = 0, A DIRECTORY WAS READ BUT NO DATA
C              RECORDS TO SKIP FOLLOWED.  THEREFORE, GO BACK TO
C              PREVIOUS DIRECTORY IF THERE WAS ONE.
            IF(NTOTCL.EQ.1)THEN
C                 THERE WAS NO PREVIOUS DIRECTORY.  REWIND AND
C                 WRITE.
               WRITE(KFILDO,184)KFILIO,KSKIP
 184           FORMAT(/,' ****NO DATA TO SKIP ON UNIT ',I2,
     1                  ' FOLLOWING A SINGLE DIRECTORY LOOKING',
     2                  ' FOR DATE/TIME',I12,'.',
     3                  '  TREATING AS A NEW FILE.')

               REWIND KFILIO
               IWRT=1
               NTOTRC=0
               NTOTCL=0
               NTOTBY=0
               GO TO 222
C
            ELSE
C                 THERE WAS A PREVIOUS DIRECTORY.
               WRITE(KFILDO,185)KFILIO,KSKIP
 185           FORMAT(/,' ****NO DATA TO SKIP ON UNIT ',I2,
     1                  ' AFTER THE LAST DIRECTORY LOOKING FOR',
     2                  ' DATE/TIME',I12,
     3                  '.  CHECKING THE PREVIOUS ONE.')
C
               DO 186 K=1,NDX
               CCALLD(K)=CCALLX(K)
 186           CONTINUE
C
               NSTA1=NSTAP
               BACKSPACE KFILIO
C                 BACKSPACE OVER RECORD JUST READ.
               NTOTBY=NTOTBY-JBYTES
               BACKSPACE KFILIO
C                 BACKSPACE OVER THE LAST DIRECTORY.
               NTOTBY=NTOTBY-LSTCLB
               BACKSPACE KFILIO
C                 BACKSPACE OVER THE TRAILER.
               NTOTBY=NTOTBY-32
               NTOTRC=NTOTRC-3
               NTOTCL=NTOTCL-1
            ENDIF
C
         ELSE
D           WRITE(KFILDO,167)KFILIO,KSKIP
D167        FORMAT(/,' ****SUCCESSFUL SKIP ON UNIT ',I2,
D    1               ' PAST DATE/TIME',I12)
            BACKSPACE KFILIO
C              MUST BACKSPACE OVER THE RECORD JUST READ.
            NTOTRC=NTOTRC-1
            NTOTBY=NTOTBY-JBYTES
         ENDIF
C
C     *********************************************************
C
      ENDIF
C
C        AT THIS POINT, CONSIDER THE FOLLOWING:
C        KSKIP  KCHECK  KWRITE  ACTION
C          0       0       0               WRITE DIRECTORY
C          0       0       1               WRITE DIRECTORY
C          0       1       0               WRITE DIRECTORY
C          0       1       1               WRITE DIRECTORY
C         DATE     0       0    CHECK DIR, ERROR IF NO MATCH
C                                 NOT REASONABLE, TREAT KCHECK = 1
C         DATE     0       1               WRITE TRAILER AND DIR
C         DATE     1       0    CHECK DIR, ERROR IF NO MATCH
C         DATE     1       1    CHECK DIR, WRITE TRAILER AND DIR
C                                          ONLY IF NO MATCH 
C         NOS. 5 AND 7 ABOVE HAVE BEEN TREATED IN THE READING
C         PORTION.  AT STATEMENT 200, KSKIP NE 0.
C
 200  IF(KCHECK.EQ.0.AND.KWRITE.NE.0)GO TO 220
C        KCHECK = 0 AND KWRITE NE 0 DOES NOT MAKE SENSE.
C
C        CHECK NEW STATION LIST WITH ONE IN CCALL( , ).
C
      IWRT=0
C        IWRT = 0 UNITL A NON-MATCH OCCURRS.
      ISUB=0
C        ISUB = 0 UNITL A SUBSTITUTE STATION IS USED, THEN
C        IT BECOMES 1.
      IF(NSTA1.NE.NSTA)GO TO 210
C
      DO 205 K=1,NSTA
      IF(CCALL(K,1).EQ.CCALLD(K))GO TO 205
C
      DO 203 J=2,6
      IF(CCALL(K,J).EQ.CCALLD(K))THEN
         IF(ISUB.EQ.0)THEN
            WRITE(KFILDO,202)CCALLD(K),(CCALL(K,L),L=1,6)
 202        FORMAT(/,' ****AT LEAST ONE SUBSTITUTE STATION',
     1               ' WAS ACCEPTED BY SKIPWR WHILE SKIPPING',
     2               ' RECORDS',/,
     3               '     STATION ON FILE         ',A8,/,
     4               '     CORRESPONDING STATIONS ',6(1X,A8))
            ISUB=1
C              NOTE THAT ONLY THE FIRST STATION IS PRINTED
C              TO REDUCE POSSIBLY VOLUMINOUS PRINT.
         ENDIF
C
         GO TO 205
C
      ENDIF
C
 203  CONTINUE
C      
      GO TO 210   
C
 205  CONTINUE
C
      GO TO 220
C
 210  WRITE(KFILDO,211)NSTA1,NSTA,(K,CCALLD(K),(CCALL(K,J),J=1,6),
     1                 K=1,MIN(ND1,NDX,MAX(NSTA1,NSTA)))
 211  FORMAT(/,' ****OLD STATION LIST OF',I5,
     1         ' STATIONS DOES NOT MATCH NEW LIST OF',I5,' STATIONS',
     2         ' IN SKIPWR AT 211.',//,
     3         '   NO.  OLD LIST   NEW LIST',//,
     4         (' ',I4,3X,A8,2X,6(1X,A8)))
C        THE LIST WILL PROBABLY BE PARTIAL WHEN THE LIST ON THE
C        FILE IS SHORTER THAN THE ONE TO WRITE.  THIS IS BECAUSE
C        CCALLD( ) MAY CONTAIN NON PRINTABLE CHARACTERS.
      IWRT=1
C
      IF(KWRITE.EQ.0)THEN
         IER=142
         GO TO 300
C
      ENDIF      
C
C        CHECK TO SEE WHETHER TRAILER NEED BE WRITTEN.
C        CONTROL WILL NOT COME HERE WHEN KSKIP = 0.
C
 220  IF((KCHECK.EQ.0.AND.KWRITE.NE.0).OR.
     1   (KCHECK.NE.0.AND.KWRITE.NE.0.AND.IWRT.EQ.1))THEN
C
C           MUST PREPARE AND WRITE TRAILER RECORD PRIOR TO
C           CALL LETTERS RECORD.
C
         CALL TRAIL(KFILDO,KFILIO,L3264B,L3264W,NTOTBY,NTOTRC,IER)
         IF(IER.NE.0)GO TO 300
C
      ENDIF
C
C        CHECK TO SEE WHETHER TO WRITE DIRECTORY RECORD.
C        ALWAYS WRITE WHEN KSKIP = 0 AND FOR THE TWO SITUATIONS
C        WHEN THE TRAILER IS WRITTEN.  THE PRIMARY STATION
C        CALL LETTERS ARE WRITTEN.
C
 222  IF((KSKIP.EQ.0).OR.
     1   (KSKIP.NE.0.AND.KCHECK.EQ.0.AND.KWRITE.NE.0).OR.
     2   (KSKIP.NE.0.AND.KCHECK.NE.0.AND.KWRITE.NE.0.AND.IWRT.EQ.1))THEN  
         NBYTES(L3264W)=NSTA*8
         WRITE(KFILIO,IOSTAT=IOS,ERR=230)(NBYTES(J),J=1,L3264W),
     1                  (CCALL(K,1),K=1,NSTA)
         NTOTRC=NTOTRC+1
         NTOTCL=NTOTCL+1
         NTOTBY=NTOTBY+NSTA*8+8
         WRITE(KFILDO,225)NSTA,NTOTRC,NTOTCL,KFILIO,NTOTBY
 225     FORMAT(/,' WRITING CALL LETTERS RECORD',/,
     1            '    NUMBER OF STATIONS =               ',I11,/,
     2            '    NUMBER OF RECORDS ON FILE =        ',I11,/,
     3            '    NUMBER OF CALL LETTERS RECORDS =   ',I11,/,
     4            '    TOTAL BYTES IN FILE UNIT NO. = ',I4, I11)
         GO TO 300
C
 230     WRITE(KFILDO,231)KFILIO,IOS
 231     FORMAT(/,' ****ERROR WRITING CALL LETTERS RECORD IN SKIPWR',
     1            ' ON UNIT NO.',I3,' AT 230.  IOSTAT =',I5)
         IER=144
C
      ELSE
C
C           WHEN CALL LETTERS RECORD IS NOT WRITTEN,
C           RECORD STATISTICS HERE.
C
         WRITE(KFILDO,235)KSKIP,NTOTRC,NTOTCL,KFILIO,NTOTBY
 235     FORMAT(/,' RECORDS PREVIOUS TO AND INCLUDING DATE/TIME',I12,
     1            ' SKIPPED ON OUTPUT FILE.',/,
     2            '    NUMBER OF RECORDS SKIPPED =        ',I11,/,
     3            '    NUMBER OF CALL LETTERS RECORDS =   ',I11,/,
     4            '    TOTAL BYTES IN FILE UNIT NO. = ',I4, I11)
C
      ENDIF
C
300   RETURN
      END
