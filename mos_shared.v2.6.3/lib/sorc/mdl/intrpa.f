      SUBROUTINE INTRPA(KFILDO,P,NX,NY,DIR,ND1,NSTA,SDATA) 
C 
C        MAY 1994   GLAHN   TDL   MOS-2000 
C 
C        PURPOSE 
C            TO INTERPOLATE INTO FIELD P(NX,NY) FOR ALL STATION
C            LOCATIONS.  BIQUADRATIC INTERPOLATION IS USED 
C            WITHIN GRID EXCEPT BILINEAR IS USED IN OUTSIDE BORDER. 
C            LINEAR EXTRAPOLATION IS USED FOR POINTS OUTSIDE THE GRID. 
C 
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            P(IX,JY) = FIELD TO INTERPOLATE INTO (IX=1,NX) (JY=1,NY).
C                       (INPUT)
C                  NX = THE IX (WEST-EAST) EXTENT OF P( , ).  (INPUT)
C                  NY = THE JY (SOUTH-NORTH) EXTENT OF P( , ).  (INPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA).  (INPUT)
C                 ND1 = THE MAXIMUM NUMBER OF STATIONS.  FIRST DIMENSION
C                       OF DIR( , ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH.  (INPUT)
C            SDATA(K) = THE INTERPOLATED VALUES FOR ALL STATIONS (K=1,NSTA).
C                       (OUTPUT)
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION DIR(ND1,2)
      DIMENSION P(NX,NY)
      DIMENSION SDATA(NSTA)
      DIMENSION B(4) 
C
      DO 160 K=1,NSTA
      BX=DIR(K,1)
      BY=DIR(K,2) 
      NBX=BX 
      NBY=BY 
      NXM1=NX-1 
      NYM1=NY-1
C
C        DETERMINE WHETHER QUADRATIC OR LINEAR INTERPOLATION IS TO BE DONE.
C
      IF(NBX-1)114,120,111 
 111  IF(NBX-NXM1)112,120,115 
 112  IF(NBY-1)121,130,113 
 113  IF(NBY-NYM1)140,130,123 
 114  NBX=1 
      GO TO 120 
 115  NBX=NXM1 
 120  IF(NBY-1)121,130,122 
 121  NBY=1 
      GO TO 130 
C 
 122  IF(NBY-NY)130,123,123 
 123  NBY=NYM1 
C 
C        START BI-LINEAR INTERPOLATION-EXTRAPOLATION. 
C 
 130  NBXP1=NBX+1 
      NBYP1=NBY+1 
      DX=BX-FLOAT(NBX) 
      DY=BY-FLOAT(NBY) 
      BB=P(NBX,NBY)
     1   +(P(NBXP1,NBY)-P(NBX,NBY))*DX
     2   +(P(NBX,NBYP1)-P(NBX,NBY))*DY
     3   +(P(NBX,NBY)+P(NBXP1,NBYP1)-P(NBX,NBYP1)-P(NBXP1,NBY))*DX*DY 
      GO TO 150 
C 
C        START BI-QUADRATIC INTERPOLATION. 
C 
 140  DX=BX-FLOAT(NBX) 
      DY=BY-FLOAT(NBY) 
      NBYP2=NBY+2 
      NBYP1=NBY+1 
      NBYM1=NBY-1 
      FCT=(DY**2-DY)/4. 
      FET=(DX**2-DX)/4. 
C 
      DO 145 J=1,4 
      N=NBX-2+J 
      B(J)=P(N,NBY)
     1   +(P(N,NBYP1)-P(N,NBY))*DY
     2   +(P(N,NBYM1)+P(N,NBYP2)-P(N,NBY)-P(N,NBYP1))*FCT 
 145  CONTINUE 
C 
      BB=B(2)+(B(3)-B(2))*DX+(B(1)+B(4)-B(2)-B(3))*FET 
 150  SDATA(K)=BB
 160  CONTINUE
C
      RETURN 
      END 
