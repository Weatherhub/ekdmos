      SUBROUTINE SETUP(LPT,IOPT,NX,NY,IXMIN,IXMAX,JYMIN,JYMAX,IPAGE, 
     1                 LINEAR) 
C 
C        MAY 1993   GLAHN   TDL   HP9000 
C 
C        PURPOSE 
C            ESTABLISHES DEFAULTS AND SETS VARIOUS OPTIONS DEPENDING 
C            ON VALUES IN OPTION TABLE FOR PRTGR.  ADJUSTMENTS ARE MADE IF 
C            IOPT(2), IOPT(3), IOPT(4), AND IOPT(5) ARE NOT CONSISTENT. 
C            THIS ROUTINE WAS WRITTEN BY PERROTTI FOR THE IBM 360/195
C            IN 1980 AND WAS MODIFIED BY GLAHN FOR THE DG ECLIPSE 1985-87.
C            PORTED TO THE VAX, THEN FROM THERE TO THE HP.
C 
C        DATA SET USE 
C            NONE. 
C 
C        VARIABLES 
C 
C            INPUT 
C                 LPT = UNIT NUMBER FOR OUTPUT.  NOT CURRENTLY USED. 
C             IOPT( ) = ADDITIONAL OPTIONS TABLE. 
C                       1 - 0 OPTION TABLE NOT USED/1 USED. 
C                       2 - SUB ARRAY MIN IX VALUE. 
C                       3 - SUB ARRAY MAX IX VALUE. 
C                       4 - SUB ARRAY MIN JY VALUE. 
C                       5 - SUB ARRAY MAX JY VALUE. 
C                       6 - 1 IF ALL INTERPOLATION IS TO BE BILINEAR. 
C                           OTHERWISE, INTERPOLATION IS TO BE 
C                           BIQUADRATIC WHERE POSSIBLE. 
C                       7 - NOT USED. 
C                       8 - PAGE WIDTH IN GRID POINTS. 
C                  NX = FIRST DIMENSION OF DATA TO CONTOUR. 
C                  NY = SECOND DIMENSION OF DATA TO CONTOUR. 
C 
C            OUTPUT 
C               IXMIN = MINIMUM IX VALUE TO PLOT.  DEFAULT = 1. 
C               IXMAX = MAXIMUM IX VALUE TO PLOT.  DEFAULT = NX. 
C               JYMIN = MINIMUM JY VALUE TO PLOT.  DEFAULT = 1. 
C               JYMAX = MAXIMUM JY VALUE TO PLOT.  DEFAULT = NY. 
C               IPAGE = PAGE WIDTH IN GRID POINTS.  DEFAULT = 24. 
C              LINEAR = TRUE FOR BILINEAR INTERPOLATION FOR ALL POINTS. 
C                       FALSE FOR BIQUADRATIC INTERPOLATION WHERE 
C                       POSSIBLE.  DEFAULT = FALSE.  (LOGICAL) 
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE. 
C 
      LOGICAL LINEAR 
      DIMENSION IOPT(8) 
C 
C          SET DEFAULT VALUES 
C 
      IXMIN=1 
      JYMIN=1 
      IXMAX=NX 
      JYMAX=NY 
      IPAGE=24 
      LINEAR=.FALSE. 
C 
C          CHECK IF OPTION TABLE USED 
C 
      IF(IOPT(1).EQ.0)GO TO 150 
C          SET OPTIONS WHERE APPROPRIATE 
      IXMIN=MIN(MAX(IOPT(2),1),NX) 
      IXMAX=MAX(MIN(IOPT(3),NX),IXMIN) 
      JYMIN=MIN(MAX(IOPT(4),1),NY) 
      JYMAX=MAX(MIN(IOPT(5),NY),JYMIN) 
      LINEAR=IOPT(6).EQ.1 
      IF(IOPT(8).NE.0)IPAGE=IOPT(8) 
 150  RETURN 
      END 
