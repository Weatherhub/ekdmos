      SUBROUTINE GSITB(KFILDO,ID,IDPARS,THRESH,DIR,
     1                 SDATA,NSTA,ND1,DATA,ND5,
     2                 IS2,ND7,FD1,ND2X3,IER)
C
C        DECEMBER 1996   GLAHN   MOS-2000
C        JUNE     1997   GLAHN   D COMPILE OPTION REMOVED
C
C        PURPOSE
C            TO PROVIDE A ROUTINE THAT PERFORMS 5 BASIC FUNCTIONS THAT
C            CAN BE CALLED BY COMPUTATION ROUTINES THAT NEED TO DEAL WITH
C            STATION (POINT, AS OPPOSED TO GRID) DATA.  THE FUNCTIONS ARE:
C               GRID BINARY
C               SMOOTHING
C               INTERPOLATION
C               TRANSFORMATION
C               POINT BINARY
C            WHILE THESE SAME FUNCTIONS ARE CODED INTO PRED21 AND TWICE
C            INTO PRED22, THAT CODE IS LEFT INTACT BECAUSE OF EFFICIENCY
C            OF PROCESSING (THE CODE THERE IS NOT ALWAYS PERFORMED AS
C            A BLOCK; THERE ARE ENTRY POINTS INTO THE BLOCK.)
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C              THRESH = THE BINARY THRESHOLD ASSOCIATED WITH IDPARS( ).
C                       (INPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA).  (INPUT)
C            SDATA(K) = RETURNED DATA (K=1,NSTA).  (OUTPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH.
C                       (INPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       (INPUT)
C             DATA(J) = INPUT GRID DATA (J=1,ND5).  (INPUT)
C                 ND5 = DIMENSION OF DATA( ).  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       IS2(3) AND IS2(4) ARE THE ARRAY SIZE.  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  (INPUT)
C              FD1(J) = WORK ARRAY (J=1,ND2X3).  (INTERNAL)
C               ND2X3 = DIMENSION OF FD1( ).  (INPUT)
C                 IER = STATUS RETURN.
C                        0 = GOOD RETURN.
C                       46 = INTERPOLATION VALUE IN IDPARS(13) NOT
C                            1, 2, OR 3.
C                       SEE OTHER ROUTINES FOR OTHER VALUES.
C                       (INTERNAL-OUTPUT)
C 
C        NONSYSTEM SUBROUINES USED 
C            GRIDB, SMTH5, SMTH9, SMTH25, INTRP, INTRPA,
C            INTRPB, TRANS, BINARY, TIMPR
C
      DIMENSION ID(4),IDPARS(15)
      DIMENSION SDATA(ND1)
      DIMENSION DIR(ND1,2)
      DIMENSION DATA(ND5)
      DIMENSION FD1(ND2X3)
      DIMENSION IS2(ND7)
C
C        MAKE GRID BINARY IF DESIRED.
C
      IF(IDPARS(3).EQ.5)
     1    CALL GRIDB(KFILDO,ID,IDPARS(3),THRESH,
     2    DATA,FD1,IS2(3),IS2(4),IER)
      IF(IER.NE.0)GO TO 298
C
C        SMOOTH FIELD IF DESIRED.
C
      IF(IDPARS(14).EQ.1)CALL SMTH5 (KFILDO,DATA,FD1,IS2(3),IS2(4))
      IF(IDPARS(14).EQ.2)CALL SMTH9 (KFILDO,DATA,FD1,IS2(3),IS2(4))
      IF(IDPARS(14).EQ.3)CALL SMTH25(KFILDO,DATA,FD1,IS2(3),IS2(4))
C
C        INTERPOLATE TO STATIONS.
C
      IF(IDPARS(13).EQ.1)THEN
         CALL INTRPA(KFILDO,DATA,IS2(3),IS2(4),
     1               DIR,ND1,NSTA,SDATA)
C        INTRPA IS BIQUADRATIC INTERPOLATION WHERE POSSIBLE, BILINEAR
C        OTHERWISE.
C
      ELSEIF(IDPARS(13).EQ.2)THEN
         CALL INTRPB(KFILDO,DATA,IS2(3),IS2(4),
     1               DIR,ND1,NSTA,SDATA)
C        INTRPB IS BILINEAR.
C
      ELSEIF(IDPARS(13).EQ.3)THEN
         CALL INTRP(KFILDO,DATA,FD1,IS2(3),IS2(4),
     1              DIR,ND1,NSTA,SDATA)
C        INTRP IS INTERPOLATION FOR PRECIPITATION AMOUNT.  THE PROCESS
C        IS BILINEAR AFTER PREPARATION OF THE FIELD TO PUT THE ZERO
C        LINE ABOUT HALFWAY BETWEEN POSITIVE AND ZERO GRIDPOINTS.
C
      ELSE
         IER=46
         GO TO 298
C
      ENDIF
C
C        TRANSFORM VARIABLE WHEN DESIRED.
C
      IF(IDPARS(8).NE.0)
     1    CALL TRANS(KFILDO,ID,IDPARS(8),SDATA,NSTA,FD1,ND2X3,
     2               IER)
C        WHEN IER NE O, ALL VALUES IN SDATA( ) HAVE BEEN SET TO 9999.
      IF(IER.NE.0)GO TO 300
C
C         MAKE POINT BINARY VARIABLE WHEN DESIRED.
C
      IF(IDPARS(3).EQ.1)
     1    CALL BINARY(KFILDO,ID,IDPARS(3),THRESH,SDATA,NSTA,
     2                IER)
C        WHEN IER NE 0, ALL VALUES IN SDATA( ) HAVE BEEN SET TO 9999.
      GO TO 300
C
 298  DO 299 K=1,NSTA
      SDATA(K)=9999.
 299  CONTINUE
C
 300  CONTINUE
      RETURN
      END
