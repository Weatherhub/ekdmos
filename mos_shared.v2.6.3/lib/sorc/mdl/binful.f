      SUBROUTINE BINFUL(KFILDO,ID,IDPARS,TRESHL,TRESHU,SDATA,NSTA,IER)
C
C        MAY      1994   GLAHN   TDL   MOS-2000 
C        DECEMBER 1997   GLAHN   ADDED IDPARS = 0 CONTINGENCY
C        MAY      1998   GLAHN   D/C COMMENT ADDED
C        MARCH    2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                     CONFORM TO FORTRAN 90 STANDARDS
C                                     ON THE IBM SP
C                                 
C 
C        PURPOSE 
C            TO MAKE A BINARY VARIABLE USING TWO THRESHOLDS.  WHEN
C            THE LOWER THRESHOLD TRESHL IS SMALL (E.G., -999999),
C            THIS IS A CUMULATIVE FROM BELOW.  WHEN THE UPPER THRESHOLD
C            TRESHU IS LARGE (E.G., 999999), THIS A CUMULATIVE FROM
C            ABOVE.  WHEN THE DATUM IS 9999, INDICATING MISSING,
C            IT IS UNCHANGED. 
C 
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C               ID(J) = 4-WORD VARIABLE ID (J=1,4).  USED ONLY FOR
C                       DIAGNOSTIC PRINT.  (INPUT)
C              IDPARS = SINGLE VALUE INDICATING THE TRANSFORMATION TO
C                       MAKE.  IT SHOULD BE NONZERO WHEN TRANS IS ENTERED.
C                       ONLY VALUES 1-4 ARE DEALT WITH, 5 AND GREATER
C                       BEING GRID BINARIES.  (INPUT)
C              TRESHL = THE LOWER THRESHOLD VALUE.  SDATA(K) BECOMES 1 WHEN
C                       THE INPUT SDATA(K) GE THRESL AND LT TRESHU,
C                       AND 0 OTHERWISE.  (INPUT)
C              TRESHU = THE UPPER THRESHOLD VALUE (SEE TRESHL).  (INPUT)
C            SDATA(K) = THE VALUES FOR ALL STATIONS (K=1,NSTA) TO BE
C                       BINARIED.  (INPUT-OUTPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH.  
C                       ALSO, TREATED AS DIMENSION OF SDATA( ).  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0  = GOOD RETURN.
C                       61 = THRESHOLD IS MISSING VALUE.
C                       62 = IDPARS VALUE NOT FOUND IN THIS ROUTINE. 
C                       63 = IDPARS = 0.
C                       WHEN IER = 61 OR 62, SDATA( ) IS SET TO 9999.
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION ID(4)
      DIMENSION SDATA(NSTA)
C
C
D     CALL TIMPR(KFILDO,KFILDO,'START BINFUL        ')
C
      IER=0
      IF(TRESHL.NE.9999.AND.
     1   TRESHU.NE.9999.)GO TO 110
C
C        AT LEAST ONE OF THE THRESHOLDS IS INDICATED AS MISSING.
C        SET THE VALUES TO MISSING.
C
      WRITE(KFILDO,101)TRESHL,TRESHU,(ID(J),J=1,4)
 101  FORMAT(/,' ****THRESHOLD(S) =',2F7.0,' FOR BINARY VARIABLE',
     1         1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,/,
     2         '     INDICATED AS MISSING IN SUBROUTINE BINFUL.',
     3         '  ALL VALUES SET TO MISSING.')
      IER=61
      GO TO 805
C
 110  IF(IDPARS.GE.4)GO TO 800
      IF(IDPARS.EQ.0)GO TO 850
C
C        MAKE THE BINARIES.  THE BINARY IS SET 1 WHEN THE DATA
C        VALUE IS GE TRESHL AND LT TRESHU.
C
      DO 160 K=1,NSTA
C***D     WRITE(KFILDO,159)K,SDATA(K),TRESHL,TRESHU
C***D159  FORMAT(' IN BINFUL--K,SDATA(K),TRESHL,TRESHU'I4,3F15.9)
      IF(SDATA(K).EQ.9999.)THEN
         GO TO 160
C
      ELSEIF(SDATA(K).GE.TRESHL.AND.
     1       SDATA(K).LT.TRESHU)THEN
         SDATA(K)=1.
C
      ELSE
         SDATA(K)=0.
      ENDIF
C
 160  CONTINUE
C
      GO TO 900
C
C        AT THIS POINT, THE BINARY PARAMETER HAS NOT BEEN
C        FOUND.  PRINT DIAGNOSTIC, AND SET ALL VALUES TO MISSING.
C
 800  WRITE(KFILDO,801)IDPARS,(ID(J),J=1,4)
 801  FORMAT(/,' ****THE BINARY PARAMETER IDPARS =',I3,
     1         ' IN PREDICTOR',1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,/,
     2         '     WAS NOT FOUND IN SUBROUTINE BINFUL.',
     3         '  ALL VALUES SET TO MISSING.')
      IER=62
C
 805  DO 810 K=1,NSTA
      SDATA(K)=9999.
 810  CONTINUE
C
      GO TO 900
C
 850  WRITE(KFILDO,851)IDPARS,(ID(J),J=1,4)
 851  FORMAT(/,' ****THE BINARY PARAMETER IDPARS =',I3,
     1         ' IN PREDICTOR',1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,/,
     2         '     IS ZERO IN SUBROUTINE BINFUL.',
     3         '  VALUES WERE NOT CHANGED.')
      IER=63
 900  RETURN
      END 
