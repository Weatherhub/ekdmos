      SUBROUTINE PAWLPM(KFILDO,KFIL10,NDATE,
     1                  ID,ITAUH,ITAUM,MODNO,NSEQ,ISCALD,
     2                  NPROJ,ALAT,ALON,ORIENT,MESHB,MESH,XLAT,NX,NY,
     3                  RECORD,IA,IC,IPACK,ND5,MINPK,
     4                  LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,
     5                  IS0,IS1,IS2,IS4,ND7,
     6                  IPLAIN,PLAIN,NCHAR,
     7                  XMISSP,XMISSS,LX,IOCTET,
     8                  L3264B,L3264W,IER)
C
C        JANUARY   2007   GLAHN   TDL   MOS-2000
C                                 MODIFIED FROM PAWING USED FOR LAMP
C        JULY      2007   GLAHN   CHANGED PAWING TO PAWLPM IN ALL PLACES
C        JUNE      2012   ENGLE   MODIFIED CALL TO INCLUDE PLAIN.
C                                 PLAIN LANGUAGE IS NOW PACKED BY
C                                 USING IACHAR FUNCTION.
C 
C        PURPOSE 
C            TO PREPARE FOR PACKING, PACK, AND WRITE A GRIDPOINT
C            RECORD TO THE MOS-2000 INTERNAL RANDOM ACCESS FILE.
c            HANDLES LAMBERT, POLAR STEREOGRAPHIC, AND MERCATOR 
C            PROJECTIONS FOR VALUES OF NPROJ 3, 5, AND 7, RESPECTIVELY.
C            (PAWING IN LAMP WAS SPECIFIC TO POLAR STEREOGRAPHIC.)
C
C        DATA SET USE 
C            KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT) 
C            KFIL10    - UNIT NUMBER FOR INTERMEDIATE PREDICTOR STORAGE.
C                        (OUTPUT)
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C              KFIL10 = UNIT NUMBER FOR INTERMEDIATE PREDICTOR STORAGE.
C                       WHEN = 0, THE PACKING WILL BE DONE, BUT THE
C                       DATA WILL NOT BE WRITTEN.  (INPUT)
C               NDATE = DATE OF DATA IN FORM YYYYMMDDHH.  (INPUT)
C               ID(J) = MOS ID OF VARIABLE TO WRITE (J=1,4).  (INPUT)
C               ITAUH = PROJECTION IN HOURS.  (INPUT)
C               ITAUM = PROJECTION IN MINUTES.  (INPUT)
C               MODNO = MODEL NUMBER.  (INPUT)
C                NSEQ = SEQUENCE NUMBER.  (INPUT)
C              ISCALD = DECIMAL SCALE FACTOR.  (INPUT)
C               NPROJ = MAP PROJECTION NUMBER.  (INPUT)
C                ALAT = LATITUDE OF LL CORNER OF GRID IN DEG.  (INPUT)
C                ALON = LONGITUDE OF LL CORNER OF GRID IN DEG.  (INPUT)
C              ORIENT = LONGITUDE OF GRID ORIENTATION IN DEG.  (INPUT)
C               MESHB = THE NOMINAL MESH LENGTH OF 1/4 BEDIENT GRID.
C                       (INPUT)
C                MESH = NOMINAL MESH LENGTH OF GRID IN KM.  (INPUT)
C                XLAT = LATITUDE AT WHICH XMESH IS CORRECT IN DEG.
C                       (INPUT)
C                  NX = X EXTENT OF THE GRID.  (INPUT)
C                  NY = Y EXTENT OF THE GRID.  (INPUT)
C           RECORD(J) = GRID OF DATA FOR WRITING (J=1,ND5).  (INPUT)
C               IA(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C               IC(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IPACK(J) = SCRATCH ARRAY USED IN PACKING THE DATA
C                       (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF RECORD( ), IA( ), IC( ), AND
C                       IPACK( ).  ND5 = ND2X3 IN CALLING PROGRAM.
C                       (INPUT)
C               MINPK = VALUES ARE PACKED IN GROUPS OF MINIMUM SIZE
C                       MINPK.  ONLY WHEN THE NUMBER OF BITS TO HANDLE
C                       A GROUP CHANGES WILL A NEW GROUP BE FORMED.  
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,11) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDLPACK, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --FOR LAMP, THE NOMINAL MESH LENGTH OF
C                              AN 80-KM GRID.  NSLAB IS NOT USED, NOR IS 
C                              DIR( , ,L) AND NGRIDC( ,L).  THIS IS USED
C                              TO DETERMINE WHAT TO SAVE IN LSTORE( , ).
C                       L=11 --THE NUMBER OF THE FIRST VARIABLE IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT DOES NOT NEED
C                              TO BE STORED AFTER DAY 1.  WHEN THE VARIABLE
C                              MUST BE STORED (TO BE ACCESSED THROUGH OPTION)
C                              FOR ALL DAYS, ID(11,N) IS 7777 + THE NUMBER
C                              OF THE FIRST VARIABLE IN THE SORTED LIST
C                              FOR WHICH THIS VARIABLE IS NEEDED.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       ARE CURRENTLY IN USE.  (INPUT/OUTPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (INPUT-OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              IS0(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 0 (L=1,ND7).  (INTERNAL)
C              IS1(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 1 (L=1,ND7).  (INTERNAL)
C              IS2(L) = HOLDS THE VALUES FOR GRIB SECTION 2 (L=1,ND7).
C                       NOT ACTUALLY USED.  (INTERNAL)
C              IS4(L) = HOLDS THE VALUES FOR GRIB SECTION 4.  NONE OF 
C                       THE VALUES NEED BE FURNISHED BY THE USER.
C                       IS4(2) IS SET TO INDICATE NON-GRIDPOINT DATA,
C                       COMPLEX PACKING, ORIGINAL SCALED VALUES TO BE
C                       PACKED (NOT SECOND ORDER SPATIAL DIFFERENCES),
C                       AND MISSING VALUES OR NOT DEPENDING ON WHETHER 
C                       OR NOT XMISS NE OR EQ ZERO, RESPECTIVELY.
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ),AND IS4( ).
C                       (INPUT)
C               PLAIN = THE PLAIN LANGUAGE DESCRIPTION OF THE PREDICTORS
C                       EQUIVALENCED TO IPLAIN( , ). (CHARACTER*32)
C         IPLAIN( , ) = NAME OF VARIABLE TO PACK.  THIS IS CHARACTER
C                       DATA IN AN INTEGER ARRAY.  (INPUT)
C               NCHAR = THE NUMBER OF CHARACTERS OF PLAIN LANGUAGE 
C                       IN PLAIN TO PACK WITH THE DATA.  (INPUT)
C              XMISSP = PRIMARY MISSING VALUE INDICATOR.  (INPUT)
C              XMISSS = SECONDARY MISSING VALUE INDICATOR.  (INPUT)
C                  LX = THE NUMBER OF GROUPS (THE NUMBER OF 2ND ORDER 
C                       MINIMA).  WHILE NEEDED ONLY IN SUBROUTINE PACK,
C                       IT IS OUTPUT IN THE ARGUMENT LIST OF PAWLPM IN
C                       CASE THE USER WANTS TO KNOW IT.  (OUTPUT)
C              IOCTET = THE TOTAL MESSAGE SIZE IN OCTETS.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2). 
C                       (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD VALUE.
C                        51 = ND5 LT NX*NY.
C                       OTHER VALUES COME FROM CALLED SUBROUTINES AND 
C                       SHOULD BE TREATED AS FATAL IN CALLING PROGRAM.
C                       (OUTPUT)
C               XMESH = ACTUAL MESH LENGTH OF GRID IN KM.  (INTERNAL)
C               LASTL = THE LAST LOCATION IN CORE( ) USED.  THIS IS THE
C                       MAXIMUM VALUE THAT HAS BEEN USED, EVEN THOUGH
C                       THE SLOT(S) AT THE END HAVE BEEN RELEASED.  THIS
C                       IS MODIFIED, ALONG WITH LITEMS, IF COMPACTION IS
C                       DONE OR IF THE ITEM RELEASED IS THE LAST ITEM
C                       IN THE LIST.  INITIALIZED TO 0 ON FIRST ENTRY TO
C                       GSTORE.  THE USER NEED NOT WORRY ABOUT THIS. 
C                       (INTERNAL)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR
C                       MOS-2000 INTERNAL STORAGE.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NON SYSTEM SUBROUTINES CALLED 
C           PACK2D, UNPKBG, GSTORE, DATPRS
C
      CHARACTER*32 PLAIN
      DIMENSION RECORD(ND5)
      DIMENSION IA(ND5),IC(ND5),IPACK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION IPLAIN(L3264W,4)
      DIMENSION ID(4)
C
      IER=0
D     CALL TIMPR(KFILDO,KFILDO,'START PAWLPM        ')
D     WRITE(KFILDO,100)(ID(J),J=1,4),MESH,ALAT,ALON,ORIENT,XLAT,NX,NY
D100  FORMAT(/' IN PAWLPM--ID(J),MESH,ALAT,ALON,ORIENT,XLAT,NX,NY'/
D    1        '          ',3I11,I6,I5,4F15.9,2I5)
C
C        CHECK SIZE OF ND5.
C
      IF(ND5.LT.NX*NY)THEN
         NXY=NX*NY
         WRITE(KFILDO,105)ND5,NXY
 105     FORMAT(/' ****ND5 = ',I8,'  MUST BE GE NX*NY =',I8,
     1           ' IN PAWLPM.  DATA NOT PACKED AND WRITTEN.'/
     2           '     RETURN WITH IER = 51 AT 105.')
         IER=51
         GO TO 900
      ENDIF
C
C        ZERO ARRAYS
C
      DO 110 J=1,ND7
      IS0(J)=0
      IS1(J)=0
      IS2(J)=0
      IS4(J)=0
 110  CONTINUE
C
C        FILL IS1( ).  IS1(1) IS FILLED BY PACK2D.
C
      IS1(2)=1
C        IS1(2) = 1 INDICATES NO BIT MAP AND GRIDPOINT DATA.
      CALL DATPRS(KFILDO,NDATE,IS1(3))
C        IS1(3-6) HOLDS DATE--YYYY, MM, DD, HH.
      IS1(7)=0
C        IS1(7) INDICATES 0 MINUTES.
      IS1(8)=NDATE
C        IS1(8) IS FULL DATE YYYYMMDDHH.
      IS1(9)=ID(1)
      IS1(10)=ID(2)
      IS1(11)=ID(3)
      IS1(12)=ID(4)
C        IS1(9-12) IS THE ID.
      IS1(13)=ITAUH
C        IS1(13) IS THE PROJECTION IN HOURS.
      IS1(14)=ITAUM
C        IS1(14) IS THE PROJECTION IN MINUTES.
      IS1(15)=MODNO
C        IS1(15) IS THE MODEL OR PROCESS NUMBER.
      IS1(16)= NSEQ
C        IS1(16) IS THE MODEL SEQUENCE NUMBER.
      IS1(17)=ISCALD
C        IS1(17) IS THE DECIMAL SCALE FACTOR.
      IS1(18)=0
C        IS1(18) IS THE BINARY SCALE FACTOR (NOT IMPLEMENTED).
      IS1(22)=NCHAR
C
C        PUT PLAIN LANGUAGE INTO IS1( ) FOR PACKING.
C
      LOC=1
C        LOC = WORD POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
      IPOS=1
C        IPOS = BIT POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
C
CINTEL
C
C        USE IACHAR FUNCTION TO PUT ONE CHARACTER (BYTE) FROM
C        PLAIN INTO ONE IS1( ) WORD.
C
      DO J=1, NCHAR
         IS1(J+22)=IACHAR(PLAIN(J:J))
      END DO
C      DO 120 J=1,NCHAR
C      CALL UNPKBG(KFILDO,IPLAIN(1,1),4*L3264W,LOC,IPOS,
C     1            IS1(J+22),8,L3264B,IER,*910)
CC        NOTE THAT THIS PUTS ONE BYTE PER IS1( ) WORD.
C 120  CONTINUE
CINTEL
C
C        FILL ID2( ).  ID2(1) IS FILLED BY PACK2D.
C
      IS2(2)=NPROJ
C        IS2(2) IS THE MAP PROJECTION (E.G., 5 = POLAR STEREOGRAPHIC).
      IS2(3)=NX
C        IS2(3) IS THE X EXTENT OF THE GRID. 
      IS2(4)=NY
C        IS2(4) IS THE Y EXTENT OF THE GRID. 
      IS2(5)=NINT(ALAT*10000.)
C        IS2(5) IS THE LATITUDE*10000 OF THE LL CORNER OF THE GRID.
      IS2(6)=NINT(ALON*10000.)
C        IS2(6) IS THE LONGITUDE*10000 OF THE LL CORNER OF THE GRID.
      IS2(7)=NINT(ORIENT*10000.)
C        IS2(7) IS THE GRID ORIENTATION*10000.
      CALL ACTUAL(KFILDO,MESH,XMESH,XMESHN,NPROJ,IER)
C        SUBROUTINE ACTUAL COMPUTES XMESH FROM MESH.
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,125)
 125     FORMAT(' ****ERROR IN PAWLPM CALLING ACTUAL.',
     1          '  TREAT AS FATAL AT 125 IN PAWLPM.')
         STOP 125
      ENDIF
C
      IS2(8)=NINT(XMESH*1000000.)
C        IS2(8) IS THE MESH LENGTH*1000000.
      IS2(9)=NINT(XLAT*10000.)
C        IS2(9) IS THE LATITUDE*10000 WHERE XMESH IS CORRECT.
C
C        PACK FROM RECORD( ) TO IPACK( ).
C
      CALL PACK2D(KFILDO,RECORD,IA,IC,NX,NY,IS0,IS1,IS2,IS4,
     1            ND7,XMISSP,XMISSS,IPACK,ND5,
     2            MINPK,LX,IOCTET,L3264B,IER)
      IF(IER.NE.0)GO TO 900
C        IER NE 0 FROM PACK2D TREATED AS FATAL WITH RETURN TO CALLING
C        PROGRAM
C
      IF(KFIL10.EQ.0)GO TO 900
C        WHEN KFIL10 = 0, THE DATA WILL BE PACKED, BUT WILL NOT
C        BE WRITTEN.  THIS ALLOWS PAWLPM TO BE USED FOR PROGRAMS
C        THAT NEED PACKING DONE, BUT OPTIONAL WRITING.
      CALL GSTORE(KFILDO,KFIL10,IS1(9),MESHB,LSTORE,ND9,LITEMS,
     1            IPACK,(IOCTET*8)/L3264B,2,0,IS1(8),
     2            CORE,ND10,LASTL,NBLOCK,LASTD,NSTORE,L3264B,IER)
D     WRITE(KFILDO,150)(IS1(J),J=9,12),NSTORE,IOCTET,MINPK,ISCALD
D150  FORMAT(' IN PAWLPM--(IS1(J),J=9,12),NSTORE,IOCTET,MINPK,ISCALD',
D    1       4I11,4I6)
C        IS1(9) IS THE FIRST OF 4 ID WORDS.  THE NUMBER OF WORDS
C        WRITTEN IS (IOCTET*8)/L3264B, WHERE IOCTET IS FROM PACK2D.
C        THE LOCATION USED FOR NSLAB IN U201 IS USED FOR MESHB HERE.
C***D     CALL TIMPR(KFILDO,KFILDO,'END   PAWLPM        ')
D     WRITE(KFILDO,151)(IS2(J),J=1,20)
D151        FORMAT(' IN PAWLPM--(IS2(J),J=1,20)--IS2(J),J=1,20)',
D    1              /(10I10))
 900  RETURN
C        
C        ERROR RETURN SECTION FROM UNPKBG.  OTHER ROUTINES
C        PROVIDE DIAGNOSTICS.
C
 910  WRITE(KFILDO,911)IER
 911  FORMAT(/' ****ERROR IN UNPKBG ROUTINE, IER =',I4)
      RETURN
      END
      
