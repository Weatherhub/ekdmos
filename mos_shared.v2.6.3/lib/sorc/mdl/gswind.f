      SUBROUTINE GSWIND(KFILDO,XMESHL,SWIND,FDSIN,FDM,FDH,NX,NY)
C
C        FEBRUARY 1995   GLAHN   TDL   MOS-2000
C        AUGUST   2003   GLAHN   ADDED NOTE TO PURPOSE 
C
C        PURPOSE 
C            TO COMPUTE THE GEOSTROPHIC WIND SPEED IN M/SEC FROM
C            HEIGHTS FOR EACH GRIDPOINT.
C
C            NOTE:  IF HEIGHTS ARE SCALED TO WHOLE METERS, THE
C                   RESULT WILL NOT BE VERY ACCURATE.
C
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C
C               004 212 - GEOSTROPHIC WIND SPEED FROM HEIGHTS
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
c                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
c                       (INPUT) 
C              XMESHL = GRID LENGTH IN M AT LATITUDE XLAT.  (INPUT)
C        SWIND(IX,JY) = COMPUTED WIND SPEED IN N/SEC (IX=1,NX) (JY=1,NY).
C                       (OUTPUT)
C        FDSIN(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).
c                       (INPUT)
C          FDM(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (INPUT)
C          FDH(IX,JY) = GEOPOTENTIAL HEIGHTS IN M (IX=1,NX) (JY=1,NY).
C                       (INPUT)
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C              OMEGA2 = EARTH'S ROTATION PERIOD IN RADIANS/SEC *2.
C                       SET BY PARAMETER.
C               AGRAG = ACCELERATION OF GRAVITY IN M/SEC/SEC.
C                       SET BY PARAMETER.
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            EXTRAP
C
      PARAMETER (OMEGA2=14.584E-5)
      PARAMETER (AGRAV=9.81)
C
      DIMENSION SWIND(NX,NY),FDSIN(NX,NY),FDM(NX,NY),FDH(NX,NY)
C
      F=AGRAV/(OMEGA2*XMESHL*2.)
C
      DO 270 JY=2,NY-1
      DO 269 IX=2,NX-1
      U=FDH(IX,JY+1)-FDH(IX,JY-1)
      V=FDH(IX+1,JY)-FDH(IX-1,JY)
      H=SQRT(U*U+V*V)
      SWIND(IX,JY)=H*F*FDM(IX,JY)/FDSIN(IX,JY)
 269  CONTINUE
 270  CONTINUE
C
C        EXTRAPOLATE LINEARLY TO BOUNDARY POINTS.
C
      CALL EXTRAP(KFILDO,SWIND,NX,NY)
C
      RETURN
      END
