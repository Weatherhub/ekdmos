      SUBROUTINE TSLOP(KFILDO,KFIL10,ID,IDPARS,JD,NDATE,
     1                 KFILRA,RACESS,NUMRA,
     2                 CCALL,NAME,SDATA,STALAT,STALON,DIR,ND1,NSTA,
     3                 NGRIDC,NGRID,ND11,NSLAB,IPACK,IWORK,DATA,ND5,
     4                 LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     5                 IS0,IS1,IS2,IS4,ND7,
     6                 FD1,FD2,FD3,FD4,FD5,FD6,FD7,ND2X3,
     7                 ISTAV,L3264B,MISTOT,IER)
C
C        JUNE      2002   GLAHN   TDL   MOS-2000
C        FEBRUARY  2003   WEISS   MODIFIED FORMAT STATEMENTS
C                                 TO CONFORM TO FORTRAN 90
C                                 STANDARDS ON THE IBM-SP
C        MAY       2003   GLAHN   REARRANGED DIMENSION STATEMENTS;
C                                 CHANGED ND5 TO ND1 ON ERROR RETURN
C        JUNE      2003   GLAHN   ADDED NAME TO CALL
C        AUGUST    2003   GLAHN   ADDED CHECK FOR FFF/100 GE 3; 
C                                 EMBELISHED DEFINITION OF CCCFFF
C        SEPTEMBER 2003   GLAHN   ADDED SAVING AND RETRIEVING DATA
C                                 RATHER THAN COMPUTING IT EACH TIME
C
C        PURPOSE 
C            TO COMPUTE THE WEST (POSITIVE) OR SOUTH (POSITIVE) SLOPE
C            OF A TERRAIN FIELD AT STATIONS AND RETURN DATA IN SDATA( ).
C            WHEN COMPUTED, THE DATA ARE SAVED IN INTERNAL STORAGE,
C            THEN ON SUBSEQUENT ENTRIES RETIREVED INSTEAD OF BEING
C            RECOMPUTED.
C
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C
C               CCCFFF = 409XYZ
C                  X = REPRESENTS THE MAP PROJECTION
C                      3 = LAMBERT
C                      5 = POLAR STEREOGRAPHIC
C                      7 = MERCATOR
C                  Y = REPRESENTS THE MESH LENGTH IN BEDIENTS
C                      0 = 1/4 BEDIENT
C                      1 = 1/8    "
C                      2 = 1/16   "
C                      3 = 1/32   "
C                      4 = 1/64   " ABOUT 5.9 KM AT 60 N
C                      5 = 1/128  "
C                      6 = 1/256  "
C                  Z = 1 FOR WEST
C                      2 FOR SOUTH
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C               ID(J) = THE VARIABLE WANTED (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       PREDICTOR ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE
C                       PORTIONS PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       JD( ) IS USED TO HELP IDENTIFY THE BASIC MODEL
C                       FIELDS AS READ FROM THE ARCHIVE.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.
C                       (INPUT)
C           KFILRA(J) = HOLDS THE UNIT NUMBERS FOR ACCESSING THE 
C                       MOS-2000 EXTERNAL RANDOM ACCESS FILES 
C                       (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES CORRESPONDING TO KFILRA(J)
C                       (J=1,NUMRA).  (CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF UNIT NUMBERS AND NAMES IN
C                       KFILRA( ) AND RACESS( ).  (INPUT)
C            CCALL(K) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRIDPOINT DEVELOPMENT (K=1,NSTA).
C                       USED FOR PRINTOUT ONLY.  (INPUT)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C            SDATA(K) = TERRAIN SLOPE VALUES TO RETURN (K=1,NSTA).
C                       (OUTPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN 
C                       NGRIDC( ,M).  (INPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       FIRST DIMENSION OF DIR( , , ).  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT
C                       WITH.  (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH
C                       GRID COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS 
C                            CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN DIR( , , ),
C                       MAXIMUM OF ND11.  (INPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ).  (INPUT)
C               NSLAB = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ).  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED.  (OUTPUT) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(K) = THE TERRAIN FIELD READ FROM THE EXTERNAL RANDOM
C                       ACCESS FILE.  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ),IWORK( ), AND DATA( ).
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH 
C                              THIS VARIABLE IS NEEDED, WHEN IT IS
C                              NEEDED ONLY ONCE FROM LSTORE( , ).
C                              WHEN IT IS NEEDED MORE THAN ONCE, THE 
C                              VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING 
C                              MSTORE( , ).  LATER USED AS A WAY OF 
C                              DETERMINING WHETHER TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.  (INPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (INPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.  IT
C                       IS A RUNNING  COUNT FROM THE BEGINNING OF THE 
C                       PROGRAM.  THIS COUNT IS MAINTAINED IN CASE THE
C                       USER NEEDS IT (DIAGNOSTICS, ETC.).  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       IS2(3) AND IS2(4) ARE USED AS THE GRID 
C                       DIMENSIONS.  (INTERNAL-OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C  FD1(J),FD2(J), ETC = WORK ARRAYS (J=1,ND2X3).  (INTERNAL)
C               ND2X3 = DIMENSION OF SEVERAL VARIABLES.  FOR THIS
C                       ROUTINE, THE COMPUTATIONS ARE BY STATION,
C                       AND NX2X3 MUST BE GE NSTA.  (INPUT)
C               ISTAV = 1 SINCE THE DATA RETURNED ARE VECTOR DATA.
C                       (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64).  (INPUT)
C              MISTOT = TOTAL NUMBER OF TIMES A MISSING INDICATOR
C                       HAS BEEN ENCOUNTERED IN UNPACKING GRIDS.
C                       (INPUT-OUTPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       103 = IDPARS(1) DOes NOT INDICATE TERRAIN
C                             UPSLOP.  COMPUTATION NOT DONE.
C                       SEE GFETCH AND MAPLAT FOR OTHER VALUES.
C                       WHEN IER NE 0, DATA ARE RETURNED AS MISSING.
C                       (INTERNAL-OUTPUT)
C                 NXT = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INTERNAL).
C                 NYT = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INTERNAL).
C               LD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       DATA( ) (J=1,4).  (INTERNAL)
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT. 
C                       (INTERNAL)
C                MAPP = THE MAP PROJECTION OF THE GRID ACCESSED.
C                       (INTERNAL)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES
C                       WEST.  DO NOT USE NEGATIVE.  (INTERNAL)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS
C                       CORRECT.  DO NOT USE NEGATIVE.  (INTERNAL)
C              ALATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES NORTH.  (INTERNAL)
C              ALONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES WEST.  DO NOT USE NEGATIVE.
C                       (INTERNAL)
C                 IWS = SLOPE INDICATOR; 1 = WEST, 2 = SOUTH.
C                       (INTERNAL)
C              MSTIME = SET TO ZERO FOR CALL TO GFETCH.  (INTERNAL)
C              NCOMBO = SET TO 1 FOR CALL TO GSTORE.  (INTERNAL)
C               NPACK = SET TO 1 TO INDICATE UNPACKED DATA FOR
C                       GSTORE.  (INTERNAL)
C              NRRDAT = DATE FAR INTO THE FUTURE TO FORCE SAVING OF
C                       STATION UPSLOPE VALUES.  (INTERNAL)
C        NOTE:  VARIABLES KFIL10, NDATE, LSTORE, ND9, LITEMS, CORE, 
C               ND10, NBLOCK, NFETCH, FD3, FD4, FD5, FD6, FD7, AND 
C               MISTOT IN THE CALL SEQUENCE NOT USED.
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            CONSTG, GRCOMB, UPSLCM, SMTH5, SMTH9, SMTH25, SMTH2X,
C            SMGH3X
C
      CHARACTER*8 CCALL(ND1)
      CHARACTER*20 NAME(ND1)
      CHARACTER*60 RACESS(NUMRA)
C
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION SDATA(ND1),STALAT(ND1),STALON(ND1)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5)
      DIMENSION FD1(ND2X3),FD2(ND2X3),FD3(ND2X3),FD4(ND2X3),
     1          FD5(ND2X3),FD6(ND2X3),FD7(ND2X3)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION DIR(ND1,2,ND11),NGRIDC(6,ND11)
      DIMENSION KFILRA(NUMRA)
      DIMENSION LD(4)
C
      DATA NRRDAT/2100010100/
C
      IER=0
      ISTAV=0
C
C        MAKE SURE THIS SUBROUTINE DEALS WITH THE PREDICTOR.
C        A MAP PROJECTION OF GE 3 IS NEEDED.
C
      IF(IDPARS(1).EQ.409.AND.IDPARS(2)/100.GE.3)GO TO 103
      WRITE(KFILDO,101)(JD(J),J=1,4)
 101  FORMAT(/' ****ID DOES NOT INDICATE TERRAIN USLOPE.',/,
     1        '     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2        ' NOT COMPUTED IN TSLOP.')
      IER=103
      GO TO 800
C
C        RETRIEVE DATA IF THEY EXIST IN INTERNAL STORAGE.
C
 103  MSDATE=0
C        DATE/TIME ON CONSTANT RECORD = 0.
      MSTIME=0
C        TIME ADJUSTMENT NOT TO BE MADE.
      CALL GFETCH(KFILDO,KFIL10,ID,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,SDATA,ND1,
     2            NWORDS,NPACK,MSDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,MSTIME,IER)
C        FOR UNPACKED DATA, WORK ARRAYS ARE NOT USED.  IF CONSTANT
C        DATA ARE AVAILABLE, THEY WILL BE EXACTLY AS NEEDED; NO
C        FURTHER PROCESSING IS TO BE DONE.  THE RETURNED VALUES
C        NTIMES, MISS, AND NWORDS ARE NOT NEEDED OR USED.  IT IS
C        ASSUMED NWORDS = NSTA, SINCE THIS PROGRAM STORED THE DATA.
C        THE DATE OF SUCH RECORDS IS 0.  NOTE THAT THE DIMENSION 
C        ND1 IS FURNISHED RATHER THAN ND5.
C
      IF(IER.EQ.0)THEN
D        WRITE(KFILDO,104)
D104     FORMAT(/' RETRIEVED TERRAIN SLOPE DATA IN TSLOP.')
         GO TO 900
      ENDIF
C
C        IF DATA CANNOT BE FOUND, THEN COMPUTE THE VARIABLE.
C
C        GET THE TERRAIN IN DATA( ).  FIRST, FIND THE RANDOM
C        ACCESS UNIT, NO. 44.
C
      DO 340 J=1,NUMRA
         IF(KFILRA(J).NE.44)GO TO 340
         GO TO 345
 340  CONTINUE
C
C        FALL THROUGH HERE MEANS A RANDOM ACCESS UNIT 44 IS NOT
C        AVAILABLE.
C
      WRITE(KFILDO,343)(JD(J),J=1,4)
 343  FORMAT(/' ****RANDOM ACCESS UNIT 44 NOT AVAILABLE.',/,
     1        '     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2        ' NOT COMPUTED IN TSLOP.')
      IER=181
      GO TO 800
C
 345  LD(1)=IDPARS(1)*1000000+(IDPARS(2)/10)*10000
C        CCC, MAP PROJECTION, AND MESH LENGTH REPRESENTATION ARE
C        RETAINED.
      LD(2)=0
      LD(3)=0
      LD(4)=0
      CALL CONSTG(KFILDO,KFILRA(J),RACESS(J),LD,
     1            IPACK,IWORK,DATA,ND5,
     2            IS0,IS1,IS2,IS4,ND7,
     3            ISTAV,L3264B,IER) 
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,350)(ID(J),J=1,4)
 350     FORMAT('     TERRAIN HEIGHT UNAVAILABLE.',/,
     1          '     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2          ' NOT COMPUTED IN TSLOP.')
C           CONSTANT GRID IS NOT AVAILABLE.  THIS DIAGNOSTIC
C           FOLLOWS ONE IN CONSTG.
         GO TO 800
      ENDIF
C
      IF(ISTAV.NE.0)THEN
         WRITE(KFILDO,352)(ID(J),J=1,4)
 352     FORMAT(/' ****RECORD FROM RANDOM ACCESS FILE FOR VARIABLE',
     1             I9.9,1X,I9.9,1X,I9.9,1X,I10.3,/,
     2           '     SHOULD BE GRIDPOINT, BUT IS INDICATED AS',
     3           ' VECTOR.  NOT USED.')
         IER=180
         GO TO 800
      ENDIF
C
C        AT THIS POINT, DATA( ) CONTAINS HEIGHT IN METERS.
C
      CALL GRCOMB(KFILDO,0,IS2,ND7,NGRIDC,ND11,NGRID,NSLAB,
     1            CCALL,NAME,STALAT,STALON,DIR,ND1,NSTA,IER)
C        UPON RETURN FROM GRCOMB, NSLAB IS THE NUMBER OF THE GRID
C        COMBINATION IN NGRIDC OF THE GRID TO STORE.  NOTE THAT "IP12"
C        IS USED AS ZERO HERE.
C
      IF(IER.NE.0)THEN
C           IER NE 0 IS TREATED AS FATAL WITH RETURN TO CALLING
C           PROGRAM.
         GO TO 800
C
      ENDIF
C
C        ESTABLISH TERRAIN GRID PARAMETERS FOR USE IN SUBROUTINE
C        TSLCM.
C
      NXT=IS2(3)
      NYT=IS2(4)
      MAPP=NGRIDC(1,NSLAB)
      XMESHL=NGRIDC(2,NSLAB)/1000.
      XLAT=NGRIDC(3,NSLAB)/10000.
      ORIENT=NGRIDC(4,NSLAB)/10000.
      ALATLL=NGRIDC(5,NSLAB)/10000.
      ALONLL=NGRIDC(6,NSLAB)/10000.
C        THE ABOVE PERTAINS TO THE TERRAIN GRID.
C
C        SMOOTH TERRAIN IF DESIRED.
C
      IF(IDPARS(14).EQ.0)GO TO 400
C
      IF(IDPARS(14).EQ.1)THEN
C           IWORK( ) IS DIMENSIONED ND5 LIKE DATA( ).  IT IS
C           USED AS FLOATING POINT IN SMOOTHING ROUTINES, BUT
C           THAT IS OK.
         CALL SMTH5 (KFILDO,DATA,IWORK,NXT,NYT)
      ELSEIF(IDPARS(14).EQ.2)THEN
         CALL SMTH9 (KFILDO,DATA,IWORK,NXT,NYT)
      ELSEIF(IDPARS(14).EQ.3)THEN
         CALL SMTH25(KFILDO,DATA,IWORK,NXT,NYT)
      ELSEIF(IDPARS(14).EQ.4)THEN
         CALL SMTH2X(KFILDO,DATA,IWORK,NXT,NYT)
      ELSEIF(IDPARS(14).EQ.5)THEN
         CALL SMTH3X(KFILDO,DATA,IWORK,NXT,NYT)
      ELSE
         WRITE(KFILDO,399)IDPARS(14),(ID(J),J=1,4)
 399     FORMAT(/' ****INCORRECT SMOOTHING VALUE IN IDPARS(14)',
     1           ' IN TSLOP.',/,
     2           '     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     3           ' NOT COMPUTED IN TSLOP.')
         IER=45
         GO TO 800
      ENDIF
C
C        CALCULATE SLOPE BY STATION.  DO IN SUBROUTINE TO
C        GET INDEXING OF ARRAY.
C
 400  IWS=IDPARS(2)-(IDPARS(2)/10)*10
C
      IF(IWS.EQ.1.OR.IWS.EQ.2)THEN
         CALL TSLCM(KFILDO,SDATA,DIR(1,1,NSLAB),FD1,FD2,ND1,NSTA,
     1              DATA,NXT,NYT,MAPP,XMESHL,ORIENT,XLAT,
     2              ALATLL,ALONLL,IWS,IER)
      ELSE
         WRITE(KFILDO,405)IWS,(ID(J),J=1,4)
 405     FORMAT(/,' ****INCORRECT VALUE IN IDPARS(2) =',I3,
     1           ' INDICATING SLOPE.  MUST BE 1 OR 2.',/,
     2           '     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     3           ' NOT COMPUTED IN TSLOP.')
         IER=182
         GO TO 800
      ENDIF
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,407)(ID(J),J=1,4)
 407     FORMAT(/'     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     1           ' NOT COMPUTED IN TSLOP.')
C           THE ABOVE DIAGNOSTIC AUGMENTS THAT IN TSLCM.
         ISTAV=1
C           ISTAV = 1 INDICATES VECTOR DATA ARE RETURNED EVEN
C           THOUGH THEY ARE MISSING.     
         GO TO 800
      ENDIF
C      
      ISTAV=1
C        ISTAV = 1 INDICATES VECTOR DATA ARE RETURNED.     
C
      GO TO 900
C
C        SET OUTPUT FIELD TO MISSING WHEN AN ERROR HAS OCCURRED.
C        THE VALUES ARE INDICATED AS VECTOR, SO ND1 IS USED.
C
 800  DO 801 J=1,ND1
      DATA(J)=9999.
 801  CONTINUE
C
C        WRITE THE DATA TO INTERNAL STORAGE SO THEY WON'T HAVE TO
C        BE COMPUTED AGAIN.  EVEN MISSING DATA ARE WRITTEN;
C        IF THEY COULDN'T BE COMPUTED THE FIRST TIME, THEY CAN'T
C        BE ON SUBSEQUENT ENTRIES.
C
      NCOMBO=1
C        NCOMBO HAS NO MEANING FOR VECTOR DATA.
      NPACK=1
C        THE DATA ARE NOT PACKED.
      MSDATE=0
C        THE DATE TO STORE IS ZERO.
      CALL GSTORE(KFILDO,KFIL10,ID,NCOMBO,LSTORE,ND9,LITEMS,
     1            SDATA,NSTA,NPACK,NRRDAT,MSDATE,
     2            CORE,ND10,LASTL,NBLOCK,LASTD,
     3            NSTORE,L3264B,IER) 
 900  RETURN
      END      
