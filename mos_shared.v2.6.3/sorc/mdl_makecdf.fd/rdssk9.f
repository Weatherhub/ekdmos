      SUBROUTINE RDSSK9(KFILDO,KFILAI,NDATE,ID,CCALLSP,XB0,
     1                  XB1,XMIN,XMAX,ND5,NCALL,IER)
C
C     VEENHUIS  FEBRUARY  8, 2011  THIS IS A NEW VERSION. I CHANGED THE FORMAT
C                                  OF THE U714 OUTPUT FILE TO JUST GIVE THE
C                                  SLOPE INTERCEPT PARAMETERS AND SUPPORT.
C     VEENHUIS  FEBRUARY 28, 2011  ADDED ND5 TO INPUT CALL LIST. FIXED ARRAY
C                                  DIMENSIONS TO BE ND5. 
C     VEENHUIS  MARCH    8,  2011  ADDED CHECK TO MAKE SURE DATE FALLS WITHIN
C                                  THE RANGE SPECIFIED BY THE HEADER OF THE
C                                  SPREAD SKILL FILE.
C     VEENHUIS  APRIL     5, 2011  ADDED A CHECK TO MAKE SURE KFILAI IS GREATER
C                                  THAN ZERO. IT COULD BE ZERO IF THE SPREAD-SKILL
C                                  FILE IS MISSING.
C
C     PURPOSE
C        RDSSK9 - THIS IS THE OPERATIONAL VERSION OF RDSSK7.
C                 THE DIFFERENCE IS THAT RDSSK9 CHECKS TO MAKE
C                 SURE THE DATE IS WITHIN THE RANGE SPECIFIED BY
C                 SPREAD-SKILL FILE HEADER.
C                 READ SPREAD SKILL - READS AND RETRIEVES
C                 INFORMATION FOR THE ASCII INPUT
C                 SPREAD-SKILL RELATIONSHIP FILE PRODUCED
C                 BY U714. THE ASCII INPUT FILE,KFILAI, 
C                 HAS ALREADY BEEN OPENED BY INT715.
C
C     DATA SET USE
C         KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C         KFILAI    - UNIT NUMBER OF THE ASCII INPUT SREAD-SKILL
C                     FILE PRODUCED BY U714 (INPUT) 
C          ID(J)    - THE VARIABLE ID(J=1,4)TO RETRIEVE THE RELATIONSHIP
C                     FOR. (INPUT)
C            ND5    - MAXMIM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                     (INPUT) 
C     CCALLSP(K)    - CALL LETERS READ FROM SPREAD-SKILL RELATIONSHIP
C                     FILE ON KFILAI (K=1,ND5). (OUTPUT)
C         XB0(K)    - THE INTERCEPT COMPONENT OF THE LINEAR
C                     SPREAD SKILL RELATIONSHIP (K=1,ND5). (OUTPUT)    
C         XB1(K)    - THE SLOPE COMPONENT OF THE LINEAR
C                     SPREAD SKILL RELATIONSHIP (K=1,ND5). (OUTPUT)
C        XMIN(K)    - THE MINIMUM VALUE OF ENSEMBLE SPREAD
C                     THAT WILL BE USED WHEN EVAULATING THE
C                     LINEAR SPREAD-SKILL RELATIONSHIP. THIS 
C                     THIS IS USED TO RESTRICT THE RANGE OF 
C                     VALUES TO BE WITHIN THOSE USED TO 
C                     DEVELOP THE RELATIONSHIP, I.E. TO PREVENT
C                     EXTRAPOLATION (K=1,ND5). (OUTPUT)
C        XMAX(K)    - THE MAXIMUM VALUE OF ENSEMBLE SPREAD
C                     THAT WILL BE USED WHEN EVAULATING THE
C                     LINEAR SPREAD-SKILL RELATIONSHIP. THIS
C                     THIS IS USED TO RESTRICT THE RANGE OF
C                     VALUES TO BE WITHIN THOSE USED TO
C                     DEVELOP THE RELATIONSHIP, I.E. TO PREVENT
C                     EXTRAPOLATION (K=1,ND5). (OUTPUT)
C          NCALL    - NUMBER OF CALL LETTERS READ FROM KFILAI.
C                     (OUTPUT) 
C            IER    - THE ERROR STATUS. (OUTPUT)
C
C
      CHARACTER*8 CCALLSP(ND5),CTEMP1,CTEMP2
      CHARACTER*9 CID(4),CTEST(4),CBUF
C
      DIMENSION ID(4) 
      DIMENSION XB0(ND5),XB1(ND5),XMIN(ND5),XMAX(ND5)
      DIMENSION XTEMP1(5)
C
C         SET IER TO 99. IF THE ID IS SUCCESSFULLY FOUND
C         AND LOADED THEN SET IER TO 0.
C
      IER=99
C
C 
      IF(KFILAI.LE.0) THEN
        IER=99
        RETURN
      ENDIF
C
C         SET FILE TO BEGINING OF RECORD
      REWIND(KFILAI)
C
      IDFOUND=0
C
C         PROCESS IDS UNTIL WE FIND THE ID WE ARE LOOKING FOR
C
      WRITE(CID,100) ID
 100  FORMAT(I9)
C
      DO WHILE (IDFOUND.EQ.0)
        READ(KFILAI,150,IOSTAT=INFO) CTEST(1:4)
 150    FORMAT(4(X,A9))
       IF(INFO.NE.0) THEN
        IF(INFO.EQ.-1)THEN
          WRITE(KFILDO,160) ID(1:4),ID(1:4) 
 160      FORMAT(/," ****IN SUBROUTINE RDSPSK: REACHED END OF",
     1             " SPREAD SKILL FILE LOOKING FOR",
     2             " ID: ",4(X,I9),/,
     3             " ****SPREAD SKILL RELATION NOT FOUND FOR ID:",
     4             4(X,I9))
          IER=99
          RETURN      
        ELSE
          WRITE(KFILDO,170) INFO
 170      FORMAT(/," ****ERROR READING SPREAD SKILL INPUT FILE.",
     1             " IOSTAT = ",I9)
        ENDIF
       ENDIF  
C
        IF((CID(1).EQ.CTEST(1)).AND.
     1     (CID(2).EQ.CTEST(2)).AND.
     2     (CID(3).EQ.CTEST(3)).AND.
     3     (CID(4).EQ.CTEST(4)))THEN
C
C            THE ID HAS BEEN FOUND. BACKSPACE TO CHECK THAT
C            IDATE IS IN THE RANGE SPECIFIED BY THE HEADER 
C
          BACKSPACE KFILAI
          BACKSPACE KFILAI
          READ(KFILAI,175,IOSTAT=IOS) IHOUR,MONDAS,MONDAE
 175      FORMAT(1X,I4,5X,I4,1X,I4) 
C
C           CHECK THE HEADER TO MAKE SURE THE DATA BEING
C           PROCESSED IS IN THE RANGE.
C
          CALL DOY(NDATE,JY,JM,JDD,JH,MDAY)          
          IF(IHOUR.EQ.JH*100)THEN
C
            IF(MONDAS.LE.MONDAE)THEN
               IF(JM*100+JDD.GE.MONDAS.AND.
     1            JM*100+JDD.LE.MONDAE) IDFOUND=1
            ELSE
               IF(JM*100+JDD.LE.MONDAE.OR.
     1            JM*100+JDD.GE.MONDAS) IDFOUND=1
            ENDIF
         ENDIF
C
C          READS ONE LINE TO CORRECTLY POSITION ITSELF
C
         READ(KFILAI,*) CBUF
        ENDIF
      ENDDO
C
C         THE ID HAS BEEN FOUND NOW CONTINUE READING THE FILE.
C         PROCESS EACH GROUP OF CALL LETTERS AND SPREAD
C         SKILL BINS UNTIL THE END OF THE RECORD FOR THIS CURRENT
C         ID.
C
      NIDEND=0
      NCALL=0
      DO WHILE (NIDEND.EQ.0)
C
C         READ CALL LETTERS AND SPD-SKILL DATA
C
        XTEMP1(:)=99999999.
        READ(KFILAI,180,IOSTAT=INFO) CTEMP1,XTEMP1
 180    FORMAT(X,A8,5(X,F10.5))
C      
        IF(INFO.NE.0) THEN
          WRITE(KFILDO,190) ID,INFO 
 190      FORMAT(/," ****IN SUBROUTINE RDSPSK:",
     1             "ERROR READING SPREAD SKILL ",
     2             "FOR ID",4(X,I9),/," ****",
     3             "IOSTAT = ",I8,/," ****",
     4             "ABORTING ATTEMPT TO READ") 
         IER=99
         RETURN 
        ENDIF
C
C
C         CHECK FOR TERMINATOR
C
        IF(CTEMP1.EQ."99999999") THEN
          WRITE(KFILDO,*) "REACHED END OF CALL LETTERS"
          NIDEND=1 
        ELSE
C
          NCALL=NCALL+1
          CCALLSP(NCALL)=CTEMP1
          XB0(NCALL)=XTEMP1(1)
          XB1(NCALL)=XTEMP1(2)  
          XMIN(NCALL)=XTEMP1(4)
          XMAX(NCALL)=XTEMP1(5) 
          IF(NCALL.EQ.1) PRINT *,CTEMP1
C
        ENDIF
      ENDDO
C
      IF(NCALL.GT.0) THEN
        IER=0
      ENDIF
C
      END SUBROUTINE
