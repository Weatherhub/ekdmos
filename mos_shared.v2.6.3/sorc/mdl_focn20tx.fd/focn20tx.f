C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***                             
C                                                                       
C MAIN PROGRAM: MDL_FOCN20TX
C   PRGMMR: COSGROVE         ORG: OST22       DATE: 2002-04-06
C                                                                       
C ABSTRACT:  GENERATES THE AVN TX BULLETIN CONTAINING AVN BASED         
C            CALIBRATED OBJECTIVE MAX/MIN TEMPERATURE GUIDANCE FOR  
C            THE FORECAST ELEMENTS IN THE MESSAGE ARE MAX/MIN TEMPS,    
C            FOR 24 CANADIAN STATIONS. FORECASTS ARE GIVEN EVERY            
C            12 HOURS FROM 24 TO 72 HOURS. THIS BULLETIN IS SENT OUT
C            AS A COLLECTIVE UNDER WMO HEADER FECN20 KWBC.
C            **** WITH THE CONVERSION IN APRIL 2002, THE HEADER IS 
C                 CHANGING TO FOCN20 KWNO
C
C PROGRAM HISTORY LOG:
C   94-08-08  GILBERT
C   96-01-30  WEISS   - HOBBS (H-P PLATFORM) AND CRAY C90 CONVERSION.
C                       THIS CODE INCORPORATES I/O DEVELOPED TO ACCESS
C                       FORECAST MOS 2000 DATA FILES USED ON THE HOBBS
C                       AND CRAY C90. NOTE: THIS CODE ORIGINATES FROM
C                       THE AVNFEXTX CODE. SEE THE AVNFEXTX CODE FOR
C                       ANY ADDITIONAL PROGRAM HISTORY INFORMATION.
C   97-01-07  GILBERT - CHANGED THE HEXIDECIMAL CODE FOR A VERTICAL BAR 
C                       FROM 7C TO CB.
C   97-01-08  GILBERT - CHANGED THE HEXIDECIMAL CODE FOR A VERTICAL BAR 
C                       BACK TO THE ASCII REPRESENTATION JUST BEFORE 
C                       PRINTING.
C   98-07-10  SHIREY  - CHANGED GTDATE/CHNGDT TO GETDATE (5X) AND
C                       CHNGDATE (3X) FOR Y2K.      
C   98-07-21  SHIREY  - CHANGED CALLS W3FS13 AND W3FS05 TO 
C                       W3DOXDAT FOR Y2K AND F90.
C   98-08-13  SHIREY  - CHANGED THE 5TH PARAMETER IN CALL PUTMOS 
C                       MADE 1 TO A 0 TO PRINT OUT 00 FOR YR 2000
C   98-08-18  ALLEN   - ADDED A COMMA TO FORMAT STATEMENT 101 FOR CONVERSION
C                       TO IBM SP.
C   02-01-30  COSGROVE- CONVERTED CODE TO RUN ON NEW MOS2000 RANDOM 
C                       ACCESS FILES, AND TO USE NEW MOS2000 SITES
C   03-04-16  COSGROVE- INCREASED MAXSTA TO 3500 TO ACCOMMODATE NEW MARINE
C                       SITES THAT WERE ADDED TO THE DICTIONARY, AND THE 
C                       NEW MOS SITES WE ARE ADDING IN THE FALL
C   03-11-17  COSGROVE  AS PART OF THE GFS TRANSITION, CHANGED AVN TO GFS IN 
C                       TITLE LINE.
C   05-08-18  RLC     - WE ARE TRANSITIONING FROM SENDING THE PRODUCT 
C                       VIA THE STATFILE TO VIA NTC.  CHANGES NEED TO BE
C                       MADE.  A NEW VERSION OF PUTQ IS USED, WRMSG 
C                       WAS REDONE.  SOME CHARACTERS ARE ALSO CHANGED.
C   06-05-02  MALONEY - INCREASED MAXSTA TO 25000 FOR GRIDDED MOS
C                                                                       
C USAGE:                                                                
C                                                                       
C   SEE BELOW FOR MDL STANDARDS                                         
C                                                                       
C     PROGRAM FOCN20TX                                                  
C                                                                       
C     MAY  1992   GILBERT    MDL   NAS9000
C     JAN  1996   WEISS      RDC   HOBBS AND CRAY C90
C                                                                       
C        PURPOSE                                                        
C            GENERATES THE SHORT-RANGE GFS BULLETIN CONTAINING GFS BASED         
C            CALIBRATED OBJECTIVE MAX/MIN TEMPERATURE GUIDANCE FOR  
C            THE FORECAST ELEMENTS IN THE MESSAGE ARE MAX/MIN TEMPS,    
C            FOR 24 CANADIAN STATIONS. FORECASTS ARE GIVEN EVERY            
C            12 HOURS FROM 24 TO 72 HOURS. THIS BULLETIN IS SENT OUT
C            AS A COLLECTIVE UNDER WMO HEADER FOCN20 KWNO (BEFORE 4/2002
C            THE WMO HEADER WAS FECN20 KWBC).
C                                                                       
C
C        DATA SET USE                                                   
C        READ(5...      - STANDARD INPUT 
C        WRITE(6...     - STANDARD OUTPUT
C        FORT.05        - AVN STATION LIST
C        FORT.27        - STATION DICTIONARY FILE
C        FORT.10        - NCEP STANDARD DATE FILE (INPUT)
C        FORT.48        - MOS 2000 FCST FILE (INPUT)
C        FORT.60        - TELETYPE MESSAGE (OUTPUT)
C        FORT.65        - TRANSMISSION FILE (OUTPUT)
C
C        VARIABLES
C
C        SUBROUTINE GETDATE CALLING ARGUMENTS:
C                 IUNIT=FILE NUMBER(10) USED FOR THE NMC DATE FILE
C                  IERR=ERROR RETURN CODE.
C                 LDATE=CURRENT OPERATIONAL DATE IN MDL FORMAT
C                   MDG=CURRENT DAY, 2 DIGITS
C                   MHG=CURRENT HOUR, 2 DIGITS
C                   MOG=CURRENT MONTH, 2 DIGITS
C                   MYG=CURRENT YEAR, 4 DIGITS
C
C        SUBROUTINE RDWMO CALLING ARGUMENTS:
C                 JUNIT=FILE NUMBER(5) USED TO ACCESS AVN STATION
C                       LIST
C                  LIST=LIST OF NMOSTA CALL LETTERS OF STATIONS,
C                       LEFT JUSTIFIED (8 CHARACTERS)
C                MAXSTA=MAXIMUM NUMBER OF STATIONS (ACTUALLY NEEDS TO BE
C                       2 * THE NUMBER OF STATIONS IN THE DICTIONARY)
C                 NHEAD=NUMBER OF WMO STATION CALL LEADER HEADERS
C                       (EX. AVNFEX42AZ)
C                NMOSTA=NUMBER OF STATION CALL LETTERS READ FROM INPUT LIST 
C                  NWMO=NUMBER OF STATIONS IN WMO CALL LETTER ARRAY,
C                       WMO(I)
C                   WMO=LIST OF WMO HEADERS(CHARACTER *8)
C
C**       SUBROUTINE W3DOXDAT CALLING ARGUMENTS:
C**	     IDAT(8)=NCEP ABSOLUTE DATE AND TIME ARRAY
C                    1 - 4 DIGIT YEAR
C		     2 - MONTH OF YEAR
C                    3 - DAY OF MONTH
C                    4 - HHMM TIME ZONE DIFFERENCE FROM UTC (-0500)
C		     5 - HOUR OF DAYI 24-HR CLOCK
C                    6 - MINUTES OF HOUR
C                    7 - SECONDS OF MINUTE
C                    8 - MILLISECONDS OF SECOND
C                JDOW=INTEGER DAY OF WEEK (1 IS SUNDAY)
C		 JDOY=INTEGER DAY OF YEAR (1 IS JAN 1)
C                JDAY=INTEGER JULIAN DAY (FROM JAN 1, 4713BC)
C      
C
C        ADDITIONAL VARIABLES:
C
C**             CBULHD=BULLETIN HEADER  (CHAR*11)
C**            CDAY(3)=CHARACTER *3 ARRAY OF CALENDER DAYS
C**          CHARDY(7)=CHARACTER *3 ARRAY OF DAYS OF THE WEEK
C**                 IC=TIME CYCLE IDENTIFIER (IC=1 00Z, IC=2 12Z)
C**             IDA(3)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**              IDYWK=INTEGER DAY OF THE WEEK (1 - 7)
C**             IMO(3)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**                INC=TIME INCREMENT
C**                 IS=COUNTER VARIABLE (DEPENDENT ON IC)
C**             IYR(3)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**                JDY=JULIAN DATE USED TO HELP CALCULATE CDAY.
C**         KOUNT( , )=INITIALLY SET TO ZERO, INCREASED BY 1            
C**                    WHERE A FORECAST IS ENTERED IN THAT LINE.        
C**                    1ST DIMENSION = LINE                             
C**                    2ND DIMENSION = GREATER THAN OR EQUAL TO NMOSTA
C**             LBULHD=NUMBER OF CHARACTERS IN BULLETIN HEADER
C**           LOC(2,2)=MSG POSITION ARRAY
C**          LOCD(5,2)=MSG POSITION ARRAY
C**               LINE=NUMBER OF LINES REQUIRED PER STATION
C**              MM( )=ARRAY USED TO HOLD THE MDL IDENTIFIERS FOR THE
C**                    MAX/MIN TEMPERATURE FORECAST-BOTH 00Z AND 12Z
C**                    CYCLES.
C**              MO( )=ARRAY OF MONTH NUMBER(1-12) USED WITH W3SF13 FOR
C**                    CALCULATION OF THE JULIAN DAY
C**          MSG(80, )=AREA INTO WHICH TELETYPE MESSAGE IS BUILT        
C**                    LAST DIMENSION=NOLINE SHOULD BE GE LINE*NMOSTA+5
C**               MYGA=LAST TWO DIGITS OF CURRENT YEAR
C**                    GOOD FOR YEAR 2000 AND BEYOND 
C**              NBLAK=BLANK CHARACTER
C**              NBSTA=BEGINNING STATION NUMBER (USED TO DELINEATE
C**                    BULLETINS IN WRMSG)
C**               NCAT=CHARACTER*5 CATELOG NUMBER 
C**              NESTA=ENDING STATION NUMBER (USED TO DELINEATE
C**                    BULLETINS IN WRMSG)
C**              NFILL=CHARACTER USED AS FILL IN THE FOUS14--IS TRANS-
C**                    PARENT TO THE AFOS OR TTY SYSTEM
C**             NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS PREPARED
C**                    RETURNED FROM RDLSTA
C**             NOCHAR=1ST DIMENSION OF MSG( , )-MAX NUMBER OF CHARACTER
C**             NOHEAD=NUMBER OF LINES IN THE HEADER, INCLUDING BLANK
C**                    LINES.
C**             NOLINE=2ND DIMENSION OF MSG( , )-MAX NUMBER OF LINES
C**               NOUT=OUTPUT TRANSMISSION FILE UNIT NUMBER (=65)      
C**              NTEMP=NUMBER OF STATIONS RUN (USUALLY EQUAL TO NMOSTA)
C**              NUNIT=OUTPUT PRINT(READABLE) FILE UNIT NUMBER (=60)
C**               VBAR=ASCII CHARACTER "BAR" SEPARATOR LINE
C**                    NECESSARY FOR TRANSMISSION
C
C                                                                       
C   SUBPROGRAMS CALLED:                                                 
C      UNIQUE: RDWMO(CALLS RDLSTA),RDLNK
C
C     LIBRARY:                                                          
C       W3LIB: W3DOXDAT         
C       TDLLIB: CHNGDATE,PUTMOS,PUTCHAR,
C              PRMSG,WRMSG,RDLSTA,PUTQ
C       MDLLIB: GET_NCEPDATE,READ_MOSDA
C
C      PUTCHAR=SUBROUTINE WHICH COPIES CHARACTER STRINGS FROM
C              STRING TO ANOTHER; AVOIDS HAVING TO USE DO LOOPS.
C
C       PUTMOS=SUBROUTINE WHICH CONVERTS A NUMBER (INTEGER OR
C              REAL), CONVERTS THE NUMBER TO A CHARACTER STRING
C              WHICH CAN THEN BE COPIED TO THE BULLETIN MESSAGE.
C
C         PUTQ=SUBROUTINE WHICH ATTACHES CONTROL CHARACTERS AT
C              THE END OF EACH RECORD FOR AFOS TRANSMISSION
C              (CALLED BY WRMSG)
C
C        RDLNK=SUBROUTINE TO CHECK FOR UPDATED CALL LETTERS IN
C              THE STATION DICTIONARY
C
C       RDLSTA=TO READ CHARACTER DATA WITH A GIVEN FORMAT.
C              ASSUME A MAX RECORD LENGTH OF 80 BYTES
C
C   READ_MOSDA=SUBROUTINE TO GET FORECASTS FROM RANDOM ACCESS FILE
C
C        PRMSG=PRINTS AN ASCII(SEQUENTIAL FILE) OUTPUT OF AVN
C              STATION BULLETINS
C
C        WRMSG=GENERATES AN ASCII(DIRECT ACCESS "FORMATTED") FILE
C              WITH EBSCDIC CONTROL CHARACTERS (TRANSMISSION 
C              COMPONENTS) OF AVN STATION BULLETINS
C
C                                                                       
C   EXIT STATES:   (STOPS OCCURS IN MAIN UNLESS OTHERWISE STATED) 
C     COND =   0 - SUCCESSFUL RUN
C          =   3 - CATASTROPHIC ERROR IN READ_MOSDA 
C          =  30 - NUMBER OF STATIONS IN LIST ARE EXCEEDED
C                  STOP OCCURS IN RDWMO
C          =  70 - NO MOS FORECASTS WERE AVAILABLE          
C          = 115 - UNABLE TO READ THE STANDARD NCEP DATE FILE       
C                                                                       
C REMARKS:
C
C     3000 STATIONS MAX                                                 
C                                                                       
C ATTRIBUTES:                                                           
C   LANGUAGE: FORTRAN                                                
C   MACHINE:  IBM SP
C
C$$$
      PROGRAM FOCN20TX

      PARAMETER(NOCHAR=80,MAXSTA=25000,LINE=1,NUMDAY=3)
      PARAMETER(NOHEAD=3,ND7=54)

      INTEGER KOUNT(LINE,MAXSTA),IDA(NUMDAY),MO(NUMDAY),IYR(NUMDAY),
     * MM(5,2),LOC(2,2),LOCD(5,2),NWMO(MAXSTA),IOPT,JBLOCK(MAXSTA),
     * JNERR,KUNIT,JUNIT,IUNIT,NUNIT,NDATE,NSTA,ID(4),ISW,NERR,
     * NMOSTA,LDATE,KUT,IFLAG,IERR,MDG,MHG,MOG,MYG,NHEAD,
     * JDY,INC,JWBAN(MAXSTA),MYGA,IDYWK,IDAT(8),JDAY,IS0(ND7),
     * IS1(ND7),IS2(ND7),IS4(ND7),INDEX(MAXSTA,5)

      REAL FCST(MAXSTA)

      CHARACTER*1 MSG(80,MAXSTA*LINE+NOHEAD),NBLAK,NFILL,VBAR,MREC
      CHARACTER*3 CDAY(3),CHARDY(7)
      CHARACTER*5 CTYPE,NCAT
      CHARACTER*6 CDESC
      CHARACTER*8 LIST(MAXSTA),WMO(MAXSTA),CALLS(MAXSTA)
      CHARACTER*8 CCALL(MAXSTA,6)
      CHARACTER*11 CBULHD
      CHARACTER*20 CNAME(MAXSTA)
      CHARACTER*80 CFILE,BFILE

      DATA KUNIT/48/,JUNIT/5/,IUNIT/10/,NOUT/65/,NUNIT/60/
      DATA NBLAK/' '/,NFILL/' '/,LBULHD/11/,KFILDO/6/,KFILD/27/
      DATA KFILX/48/,IPTST/0/

      DATA CHARDY/'SUN','MON','TUE','WED','THU','FRI','SAT'/
      DATA MM/202120008,202220008,202120008,202220008,202120008,
     *        202220008,202120008,202220008,202120008,202220008/

      DATA LOC/14,22,15,23/
      DATA LOCD/13,17,21,25,29,9,13,17,21,25/

      DATA NEW/0/

C        HOUR PROJECTIONS ARE 30,42,54,66,78  
      DATA NCAT/'00000'/
      DATA CBULHD/'XXXXXX KWNO'/

      DATA IOPT/0/,NMOSTA/0/,ISW/1/,NDATE/0/,LDATE/0/
      DATA MDATE/0/,NHEAD/0/,IDYWK/0/,NESTA/0/
      DATA IDAT/0,0,0,-0500,0,0,0,0/

      CALL W3TAGB('MDL_FOCN20TX',2002,0096,0071,'OST22')                  

      ND5=MAXSTA

C        8/2005 - CHANGED VBAR FROM CHAR(203) FOR NTC
C                 ADDED MREC FOR RECORD SEPARATOR TO REPLACE $
      VBAR=CHAR(124)
      MREC=CHAR(30)

C        INITIALIZE ARRAYS
      DO 112 M=1,MAXSTA
	FCST(M)=0.
        NWMO(M)=0
      DO 112 K=1,LINE
        KOUNT(K,M)=0
  112 CONTINUE

C        READ IN THE MOS 2000 FORECAST DIRECT ACCESS DATA
C        FILE NAME "CFILE" (INPUT) AND THE TRANSMISSION BULLETIN
C        FILE "BFILE" (OUTPUT) -- THIS WAS ORIGINALLY PUT IN BUT IS
C        NOT USED ANYMORE.  IT'S REALLY JUST READING TWO BLANK LINES

	 READ(JUNIT,1000)CFILE
	 READ(JUNIT,1000)BFILE
 1000    FORMAT(A80)
C        READ BULLETIN HEADERS AND STATIONS                             
      CALL RDWMO(JUNIT,NMOSTA,LIST,WMO,NWMO,NHEAD,MAXSTA)
      WRITE(KFILDO,91) NMOSTA
 91   FORMAT(1X,'NMOSTA=',I10)
      NOLINE=(NMOSTA*LINE)+NOHEAD
      KUT=0
C
C        CALL RDLNK TO RETURN THE NEWEST CALL LETTER LINKS FOR THE
C        STATIONS IN LIST.  THE NEWEST CALL LETTER LIST WILL BE IN
C        IN CCALL(J,1) 
C
      CALL RDLNK(KFILD,KFILDO,NEW,LIST,CCALL,NMOSTA,MAXSTA)  
C
C        SET THE NUMBER OF STATIONS TO PROCESS;
C        USUALLY WILL BE NMOSTA
	 NTEMP=NMOSTA
         WRITE(KFILDO,91)NTEMP
         WRITE(KFILDO,95)NHEAD
 95      FORMAT(1X,'NHEAD=',I10)

C        FILL THE MESSAGE ARRAY WITH BLANKS EXCEPT FOR THE 1ST CHARACTER
      DO 110 M=1,NOLINE
      DO 110 K=1,NOCHAR
        MSG(K,M)=NBLAK
        IF(K.EQ.1)MSG(K,M)=NFILL
  110 CONTINUE

C        READ NCEP STANDARD DATE FILE
C        PUT DATE IN MDL FORMAT
C        LDATE=(MYG*1000000)+(MOG*10000)+(MDG*100)+MHG
      CALL GET_NCEPDATE(IUNIT,MYG,MOG,MDG,MHG,LDATE,IERR)
      IF(IERR.NE.0) THEN
        CALL W3TAGE('MDL_FOCN20TX') 
        STOP 115
      ENDIF

C        FIND VALID DATES FOR HEADINGS.
C        FOR 3 CALENDER DAYS
      MDATE=LDATE
C        CONVERT 1995 TO 95 (FOR DISPLAY PURPOSES)
      MYGA=MOD(MYG,100)
      IF (MHG.EQ.0) THEN
        IDA(1)=MDG
        MO(1)=MOG
C        FULL YEAR (EX. 1995, 2005) TO GO INTO IYR ARRAY
        IYR(1)=MYG
        M=0
	IDAT(1)=MYG
	IDAT(2)=MOG
	IDAT(3)=MDG
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(1)=CHARDY(IDYWK)
        IS=2
        IC=1
      ELSE
        IS=1
        IC=2
      ENDIF
      DO 116 I=IS,3
        CALL CHNGDATE(-MDATE,24,MDATE)
        IDA(I)=MOD(MDATE,10000)/100
        MO(I)=MOD(MDATE,1000000)/10000
        IYR(I)=MDATE/1000000
        M=0
	IDAT(1)=IYR(I)
	IDAT(2)=MO(I)
	IDAT(3)=IDA(I)
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(I)=CHARDY(IDYWK)
 116  CONTINUE


C*****
C*****   START CONSTRUCTION OF THE
C*****   BULLETIN
C*****

C        CREATE THREE LINE HEADER                                       
      CALL PUTCHAR(MREC,MSG(1,2),1)
      CALL PUTCHAR('GFS-BASED STATISTICAL MAX/MIN GUIDANCE',
     *     MSG(2,2),38)
      CALL PUTCHAR('/  /',MSG(44,2),4)

      CALL PUTMOS('MOG',FLOAT(MOG),0.,1.,1,12,MSG(42,2),
     *       2,0,'99',KX)
      CALL PUTMOS('MDG',FLOAT(MDG),0.,1.,1,31,MSG(45,2),
     *       2,2,'99',KX)
      CALL PUTMOS('MYG',FLOAT(MYGA),0.,1.,0,99,MSG(48,2),
     *       2,2,'99',KX)
      CALL PUTCHAR('00 UTC',MSG(54,2),6)
      CALL PUTMOS('MHG',FLOAT(MHG),0.,1.,0,12,MSG(52,2),
     *       2,2,'99',KX)

C        PLACE VALID TIMES  NOTE: MPOS=0(CANADIAN) VS MPOS=IC-1(US) 
      MPOS=0
      DO 119 J=1,3
        MPOS=MPOS+8
        CALL PUTCHAR(CDAY(J),MSG(MPOS,3),3)
        CALL PUTMOS('DAY',FLOAT(IDA(J)),0.,1.,0,31,MSG(MPOS+4,3),
     *       2,2,'99',KX)
 119  CONTINUE

C        PLACES SEPARATOR LINES IN MESSAGE FOR THE DATES(LINE 3) AND
C        EACH STATION REGARDLESS OF WHETHER DATA EXISTS FOR A GIVEN
C        STATION.  
	DO 130 LL=3,NOLINE
          DO 120 I=1,2
            CALL PUTCHAR(VBAR,MSG(LOC(I,1),LL),1)
 120      CONTINUE
 130    CONTINUE

C        PLACES STATION IDENTIFIER REGARDLESS OF WHETHER 
C        DATA EXISTS FOR A GIVEN STATION.  USE THE ORIGINAL LSIT
C        FROM FORT.5, NOT THE UPDATED CALL LETTERS IN CCALL(,1) 
      DO 140 K=1,NTEMP
        CALL PUTCHAR(MREC,MSG(1,(K-1)*LINE+4),1)
        CALL PUTCHAR(LIST(K),MSG(2,(K-1)*LINE+4),4)
        KOUNT(1,K)=1
C        ABOVE STATEMENT PROVIDES FOR BLANK LINE.                       
 140  CONTINUE

CCCCCCCCCC
CCCCCCCCCC

C        PLACE MIN/MAX FCSTS
      ID(1)=0
      ID(2)=0
      ID(3)=18
      ID(4)=0
      INC=12
      DO 160 K=1,5
	ID(1)=MM(K,IC)
	ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,
     *       INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        IF(IER.EQ.3) THEN
          CALL W3TAGE('MDL_FOCN20TX')
          STOP 3
        ENDIF
        DO 150 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(LOCD(K,IC)-2,
     *    (J-1)*LINE+4),3,0,'999',KOUNT(1,J))
 150    CONTINUE
 160  CONTINUE


      IF (KUT.LE.0) THEN
        WRITE(KFILDO,270)
 270    FORMAT(//' NO GFS MOS FORECASTS FOUND.')
      CALL W3TAGE('MDL_FOCN20TX') 
        STOP 70
      ENDIF

C        FOR EACH WMO HEADER, WRITE THE MESSAGE TO THE TRAN FILE.       
      DO 400 N=1,NHEAD
        NBSTA=NESTA+1
        NESTA=NESTA+NWMO(N)
        CBULHD(1:6)=WMO(N)(1:6)
        CALL WRMSG(MSG,NOCHAR,NOLINE,NBSTA,NESTA,LINE,NCAT,KOUNT,
     *  LINE,NMOSTA,CBULHD,LBULHD,MDG,MHG,NOHEAD,NOUT,BFILE,NHEAD)
 400  CONTINUE
C
C        CHANGE HEXIDECIMAL VALUE CB BACK TO THE ASCII REPRESENTATION
C        OF A VERTICAL BAR BEFORE PRINTING.
C
      DO 550 I=1,NOLINE
        DO 500 J=1,NOCHAR
          IF (MSG(J,I).EQ.VBAR) MSG(J,I)='|'
 500    CONTINUE
 550  CONTINUE
C
C        PRINT MESSAGE                                                  
C
      CALL PRMSG(MSG,NOCHAR,NOLINE,KOUNT,LINE,NMOSTA,NUNIT,NOHEAD)

      CALL W3TAGE('MDL_FOCN20TX') 
      STOP
      END
