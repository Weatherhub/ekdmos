C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***                             
C                                                                       
C MAIN PROGRAM: MDL_FECN21TX
C   PRGMMR: COSGROVE         ORG: OST22       DATE: 2002-04-06
C                                                                       
C ABSTRACT:  GENERATES THE GFS EXTENDED TEXT BULLETIN CONTAINING          
C            CALIBRATED OBJECTIVE MAX/MIN GUIDANCE FOR 24 CANADIAN      
C            STATIONS. FORECASTS ARE GIVEN EVERY 12 HOURS             
C            FROM 24 TO 192 HOURS.     
C            THIS BULLETIN IS SENT OUT UNDER WMO HEADER                 
C            FECN21 KWNO (IT USED TO HAVE THE HEADER FECN21 KWBC).           
C                                                                       
C PROGRAM HISTORY LOG:                                                  
C   94-05-16  GILBERT
C   96-01-23  WEISS    - HOBBS(HP PLATFORM) AND CRAY C90 CONVERSION.
C                        THIS CODE INCORPORATES I/O DEVELOPED TO ACCESS
C                        FORECAST MOS 2000 DATA FILE USED ON THE HOBBS
C                        AND CRAY C90. NOTE: THIS CODE ORIGINATES FROM
C                        THE MRFFOXTX CODE. SEE THE MRFFOXTX CODE FOR 
C                        ADDITIONAL PROGRAM HISTORY INFORMATION.
C   97-01-07  GILBERT  - CHANGED THE HEXIDECIMAL CODE FOR A VERTICAL BAR
C                        7C TO CB.
C   97-01-08  GILBERT  - CHANGED THE HEXIDECIMAL CODE FOR A VERTICAL BAR
C                        BACK TO THE ASCII REPRESENTATION JUST BEFORE
C                        PRINTING.
C                      - CHANGED STOPS TO EXITS
C   98-07-10  SHIREY   - CHANGED GTDATE/CHNGDT TO GETDATE (5X) AND
C                        CHNGDATE (3X) FOR Y2K.  
C   98-07-22  SHIREY   - CHANGED CALLS W3FS13 AND W3FS05 TO
C                        W3DOXDAT FOR Y2K AND F90.
C   98-07-29  SHIREY   - CHANGED GTNORM TO GETNORM FOR Y2K
C   98-08-28  SHIREY   - CHANGED BOUNDARY FOR YEAR IN CALL PUTMOS FROM
C                        1-99 TO 0-99 FOR Y2K
C   99-08-19  SHIREY   - ADDED COMMA TO FORMAT STATEMENT 93. 
C   02-02-06  COSGROVE - CONVERTED CODE TO RUN ON NEW MOS2000 RANDOM
C                        ACCESS FILES, AND TO USE NEW MOS2000 SITES.
C                        AT THIS TIME, THE CLIMO PART OF THE CODE HAS
C                        BASICALLY JUST BEEN COMMENTED OUT, NOT 
C                        CONVERTED.  ALSO CHANGED THE HEADER FROM 
C                        FECN21 KWBC to FECN21 KWNO.
C   03-04-16  COSGROVE - INCREASED MAXSTA TO 3500 TO ACCOMMODATE NEW
C                        MARINE SITES, AND NEW MOS SITES THAT WILL BE
C                        ADDED TO THE DICTIONARY IN THE FALL.
C   03-07-15  COSGROVE - AS PART OF THE GFS TRANSITION, CHANGED ALL
C                        MODEL NUMBER 9'S TO 8'S
C   03-11-17  COSGROVE - ALSO AS PART OF THE GFS TRANSITION, CHANGED
C                        MRF TO GFSX IN HEADING LINE
C   05-07-28  COSGROVE - ADDED ABILITY TO DO 12Z MESSAGE.  REMOVED
C                        CLIMO HEADER AND 8TH VERTICAL BAR.
C   05-08-19  COSGROVE - WE ARE TRANSITIONING FROM SENDING THE PRODUCT 
C                        VIA THE STATFILE TO VIA NTC.  CHANGES NEED TO BE
C                        MADE.  A NEW VERSION OF PUTQ IS USED, WRMSG 
C                        WAS REDONE.  SOME CHARACTERS ARE ALSO CHANGED.
C   06-05-01  MALONEY  - INCREASED MAXSTA TO HANDLE NEW STATION TABLE
C                                                                       
C USAGE:                                                                
C                                                                       
C   SEE BELOW FOR TDL STANDARDS                                         
C                                                                       
C     PROGRAM FECN21TX                                                  
C                                                                       
C     MAY  1992   GILBERT    TDL   NAS9000
C     JAN  1996   WEISS      RDC   HOBBS AND CRAY C90
C                                                                       
C        PURPOSE                                                        
C            GENERATES THE TX BULLETIN CONTAINING EXTENDED-RANGE GFS-         
C            BASED CALIBRATED OBJECTIVE MAX/MIN GUIDANCE FOR CANADIAN      
C            STATIONS. FORECASTS ARE GIVEN EVERY 12 HOURS             
C            FROM 24 TO 192 HOURS.    
C            THIS BULLETIN IS SENT OUT UNDER WMO HEADER                 
C            FECN21 KWNO.                                               
C                                                                       
C        DATA SET USE                                                   
C        READ(5...      - STANDARD INPUT 
C        WRITE(6...     - STANDARD OUTPUT
C        FORT.5         - MRF STATION LIST 
C        FORT.10        - NCEP STANDARD DATE FILE (INPUT)
C        FORT.27        - MDL STATION DICTIONARY 
C        FORT.45        - MDL MOS CONSTANT FILE (INPUT)
C        FORT.48        - MDL MOS 2000 FCST FILE (INPUT)
C        FORT.60        - TELETYPE MESSAGE (OUTPUT)
C        FORT.65        - TRANSMISSION FILE (OUTPUT)
C
C                                                                       
C        VARIABLES
C
C
C        SUBROUTINE GET_NCEPDATE CALLING ARGUMENTS:
C                 IUNIT=FILE NUMBER(10) USED FOR THE NCEP DATE FILE
C                  IERR=ERROR RETURN CODE.
C                 LDATE=CURRENT OPERATIONAL DATE IN TDL FORMAT
C                   MDG=CURRENT DAY, 2 DIGITS
C                   MHG=CURRENT HOUR, 2 DIGITS
C                   MOG=CURRENT MONTH, 2 DIGITS
C                   MYG=CURRENT YEAR, 4 DIGITS
C
C        SUBROUTINE RDWMO CALLING ARGUMENTS:
C                 JUNIT=FILE NUMBER(5) USED TO ACCESS MRF STATION
C                       DIRECTORY FILE
C                  LIST=LIST OF NMOSTA CALL LETTERS OF STATIONS,
C                       LEFT JUSTIFIED (8 CHARACTERS)
C                MAXSTA=MAXIMUM NUMBER OF STATIONS (ACTUALLY 2* THE
C                       NUMBER OF SITES IN THE DICTIONARY THE CODE READS)
C                 NHEAD=NUMBER OF WMO STATION CALL LEADER HEADERS
C                       (EX. MRFFOX42AZ)
C                NMOSTA=NUMBER OF STATION CALL LETTERS READ FROM INPUT LIST 
C                  NWMO=NUMBER OF STATIONS IN WMO CALL LETTER ARRAY,
C                       WMO(I)
C                   WMO=LIST OF WMO HEADERS(CHARACTER *8)
C
C**       SUBROUTINE W3DOXDAT CALLING ARGUMENTS:
C**	     IDAT(8)=NCEP ABSOLUTE DATE AND TIME ARRAY
C                    1 - 4 DIGIT YEAR
C		     2 - MONTH OF YEAR
C                    3 - DAY OF MONTH
C                    4 - HHMM TIME ZONE DIFFERENCE FROM UTC (-0500)
C		     5 - HOUR OF DAYI 24-HR CLOCK
C                    6 - MINUTES OF HOUR
C                    7 - SECONDS OF MINUTE
C                    8 - MILLISECONDS OF SECOND
C                JDOW=INTEGER DAY OF WEEK (1 IS SUNDAY)
C		 JDOY=INTEGER DAY OF YEAR (1 IS JAN 1)
C                JDAY=INTEGER JULIAN DAY (FROM JAN 1, 4713BC)
C      
C        ADDITIONAL VARIABLES:
C**             CBULHD=BULLETIN HEADER  (CHAR*11)
C**            CDAY(8)=CHARACTER *3 ARRAY OF CALENDER DAYS(DAYS OF WEEK)
C**          CHARDY(7)=CHARACTER *3 ARRAY OF DAYS OF THE WEEK
C**             IDA(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**              IDYWK=INTEGER DAY OF THE WEEK (1 - 7)
C**             IMO(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**                INC=TIME INCREMENT
C**             IYR(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**                JDY=JULIAN DATE USED TO HELP CALCULATE CDAY
C**         KOUNT( , )=INITIALLY SET TO ZERO, INCREASED BY 1            
C**                    WHERE A FORECAST IS ENTERED IN THAT LINE.        
C**                    1ST DIMENSION = LINE                             
C**                    2ND DIMENSION = GREATER THAN OR EQUAL TO NMOSTA
C**             LBULHD=NUMBER OF CHARACTERS IN BULLETIN HEADER
C**               LINE=NUMBER OF LINES REQUIRED PER STATION
C**             LOC(8)=MSG POSITION ARRAY
C**            LOCD(8)=MSG POSITION ARRAY
C**              MM( )=ARRAY USED TO HOLD THE TDL IDENTIFIERS FOR THE
C**                    MAX/MIN TEMPERATURE FORECAST-BOTH 00Z AND 12Z
C**                    CYCLES.
C**              MO( )=ARRAY OF MONTH NUMBER(1-12) USED WITH W3SF13 FOR
C**                    CALCULATION OF THE JULIAN DAY.  
C**          MSG(80, )=AREA INTO WHICH TELETYPE MESSAGE IS BUILT        
C**                    LAST DIMENSION=NOLINE SHOULD BE GE LINE*NMOSTA+5 
C**               MREC=RECORD SEPARATOR
C**               MYGA=LAST TWO DIGITS OF CURRENT YEAR 
C**                    GOOD FOR YEAR 2000 AND BEYOND
C**              NBLAK=BLANK CHARACTER 
C**              NBSTA=BEGINNING STATION NUMBER (USED TO DELINEATE 
C**                    BULLETINS IN WRMSG) 
C**              NESTA=ENDING STATION NUMBER (USED TO DELINEATE 
C**                    BULLETINS IN WRMSG) 
C**              NFILL=CHARACTER USED AS FILL IN THE FOUS14--IS TRANS- 
C**                    PARENT TO THE AFOS OR TTY SYSTEM.  
C**             NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS PREPARED
C**                    RETURNED FROM RDLSTA 
C**             NOCHAR=1ST DIMENSION OF MSG( , )-MAX NUMBER OF CHARACTER
C**             NOLINE=2ND DIMENSION OF MSG( , )-MAX NUMBER OF LINES
C**             NOHEAD=NUMBER OF LINES IN THE HEADER, INCLUDING BLANK
C**                    LINES.
C**               NOUT=OUTPUT TRANSMISSION FILE UNIT NUMBER (=65)
C**              NTEMP=NUMBER OF STATIONS RUN (USUALLY EQUAL TO NMOSTA)
C**              NUNIT=OUTPUT PRINT(READABLE) FILE UNIT NUMBER (=60)
C**               VBAR=ASCII CHARACTER "BAR" (SEPARATOR LINE)
C**                    NECESSARY FOR TRANSMISSION
C
C                                                                       
C   SUBPROGRAMS CALLED:                                                 
C      UNIQUE: RDWMO(CALLS RDLSTA),RDLNK
C
C     LIBRARY:                                                          
C       W3LIB: W3DOXDAT    
C       TDLLIB:CHNGDATE,PUTMOS,PUTCHAR,PRMSG,WRMSG,PUTQ,RDLSTA
C       MDLLIB:GET_NCEPDATE,READ_MOSDA
C       
C      PUTCHAR=SUBROUTINE WHICH COPIES CHARACTER STRINGS FROM
C              STRING TO ANOTHER; AVOIDS HAVEING TO US DO LOOPS.
C
C       PUTMOS=SUBROUTINE WHICH CONVERTS A NUMBER (INTEGER OR
C              REAL), CONVERTS THE NUMBER TO A CHARACTER STRING
C              WHICH CAN THEN BE COPIED TO THE BULLETIN MESSAGE.
C
C         PUTQ=SUBROUTINE WHICH ATTACHES CONTROL CHARACTERS AT THE
C              END OF EACH RECORD FOR AFOS TRANSMISSION.
C              (CALLED BY WRMSG)
C
C       RDLSTA=READS CHARACTER DATA WITH A GIVEN FORMAT.
C              ASSUME A MAX RECORD LENGTH OF 80 BYTES.
C
C        RDLNK=SUBROUTINE TO CHECK FOR UPDATED CALL LETTERS IN
C              THE STATION DICTIONARY
C
C   READ_MOSDA=SUBROUTINE TO GET FORECASTS FROM RANDOM ACCESS FILE
C
C        PRMSG=PRINTS AN ASCII(SEQUENTIAL FILE) OUTPUT OF MRF
C              STATION BULLETINS
C
C        WRMSG=GENERATES AN ASCII FILE FOR TRANSMISSION
C
C   EXIT STATES:   (STOPS OCCUR IN MAIN UNLESS OTHERWISE STATED)     
C     COND =   0 - SUCCESSFUL RUN
C          =   3 - CATASTROPHIC ERROR IN READ_MOSDA
C          =  30 - NUMBER OF STATIONS IN LIST ARE EXCEEDED
C                  STOP OCCURS IN RDWMO
C          =  70 - NO MOS FORECASTS WERE AVAILABLE (KUT=0)         
C          = 100 - PROBLEM READING MRF MOS 2000 FORECAST FILE
C          = 110 - PROBLEM READING CONSTANT FILE
C          = 115 - UNABLE TO READ THE STANDARD NCEP DATE FILE
C                                                                       
C REMARKS:                                                              
C     3000 STATIONS MAX    
C                                                                       
C ATTRIBUTES:                                                           
C   LANGUAGE: VS FORTRAN                                                
C   MACHINE:  IBM SP
C   
C$$$
      PROGRAM FECN21TX

      PARAMETER(NOCHAR=80,MAXSTA=25000,LINE=1,NUMDAY=8)
      PARAMETER(NOHEAD=3,ND7=54)

      INTEGER IOPT,JBLOCK(MAXSTA),JNERR,NDATE,NSTA,KUNIT,
     * KOUNT(LINE,MAXSTA),IDA(NUMDAY),MO(NUMDAY),IYR(NUMDAY),
     * MM(16),LOC(8),LOCD(16),ID(4),ISW,NERR,JUNIT,IUNIT,LUNIT,
     * NMOSTA,LDATE,KUT,IFLAG,IERR,MDG,MHG,MOG,MYG,NHEAD,
     * JDY,INC,JWBAN(MAXSTA),NWMO(MAXSTA),NUNIT,
     * NOUT,LBULHD,NESTA,NBSTA,IDYWK,IDAT(8),JDAY,IS0(ND7),
     * IS1(ND7),IS2(ND7),IS4(ND7),INDEX(MAXSTA,5)

      REAL FCST(MAXSTA),RNORM(MAXSTA)

      CHARACTER*1 MSG(NOCHAR,MAXSTA*LINE+NOHEAD),NBLAK,NFILL,VBAR,MREC
      CHARACTER*3 CDAY(8),CHARDY(7)
      CHARACTER*5 CTYPE,NCAT
      CHARACTER*6 CDESC
      CHARACTER*8 LIST(MAXSTA),CALLS(MAXSTA),WMO(MAXSTA),CCALL(MAXSTA,6)
      CHARACTER*11 CBULHD
      CHARACTER*20 CNAME(MAXSTA)
      CHARACTER*80 CFILE,AFILE,BFILE

      DATA KUNIT/48/,JUNIT/5/,IUNIT/10/,LUNIT/45/,LBULHD/11/,NOUT/65/
     * NBLAK/' '/,NFILL/' '/,NUNIT/60/,KFILD/27/,KFILX/48/,IPTST/0/
     * KFILDO/6/
      DATA CHARDY/'SUN','MON','TUE','WED','THU','FRI','SAT'/
      DATA CBULHD/'XXXXXX KWNO'/
      DATA NCAT/'00000'/,NEW/0/

      DATA MM/202240008,202140008,202240008,202140008,202240008,
     * 202140008,202240008,202140008,202240008,202140008,202240008,
     * 202140008,202240008,202140008,202240008,202140008/
      DATA LOC/14,22,30,38,46,54,62,70/
      DATA LOCD/9,13,17,21,25,29,33,37,41,45,49,53,57,61,65,69/

C        HOUR PROJECTIONS ARE: 
C        30,42,54,66,78,90,102,114,126,138,150,162,174,186

      DATA NDATE/0/,MDATE/0/,LDATE/0/,NHEAD/0/,IDYWK/0/
      DATA IOPT/0/,NMOSTA/0/,ISW/1/,NESTA/0/
      DATA IDAT/0,0,0,-0500,0,0,0,0/

      CALL W3TAGB('MDL_FECN21TX',2002,0096,0078,'OST22')                  

      ND5=MAXSTA
C        8/2005 - CHANGED VBAR FROM CHAR(203) FOR NTC
C                 ADDED MREC FOR RECORD SEPARATOR TO REPLACE $
      VBAR=CHAR(124)
      MREC=CHAR(30)


C        INITIALIZE ARRAYS
      DO 112 M=1,MAXSTA
	NWMO(M)=0
	FCST(M)=0.
      DO 112 K=1,LINE
        KOUNT(K,M)=0
  112 CONTINUE


C        READ IN THE TWO MOS 2000 DATA FILE DIRECT ACCESS 
C        FILES. FORECAST FILE "CFILE" AND CONSTANT FILE
C        "AFILE" (INPUT) AND THE BULLETIN "BFILE" OUTPUT 
C        FILENAME -- THIS WAS SET UP AT ONE TIME BUT IS NOT
C        USED ANYMORE.  RIGHT NOW IT JUST READ THREE BLANK
C        LINES IN THE STATION LIST FILE - FORT.5

      READ(JUNIT,1000) CFILE
      READ(JUNIT,1000) AFILE
      READ(JUNIT,1000) BFILE
 1000 FORMAT(A80)

C        READ BULLETIN HEADERS AND STATIONS                             
      CALL RDWMO(JUNIT,NMOSTA,LIST,WMO,NWMO,NHEAD,MAXSTA)
      WRITE(KFILDO,91)NMOSTA
 91   FORMAT(1X,'NMOSTA=',I10)
      NOLINE=(NMOSTA*LINE)+NOHEAD
      KUT=0

C
C        CALL RDLNK TO RETURN THE NEWEST CALL LETTER LINKS FOR THE
C        STATIONS IN LIST.  THE NEWEST CALL LETTER LIST WILL BE 
C        IN CCALL(J,1) 
C
      CALL RDLNK(KFILD,KFILDO,NEW,LIST,CCALL,NMOSTA,MAXSTA)  
C
C        FILL THE MESSAGE ARRAY WITH BLANKS EXCEPT FOR THE 1ST CHARACTER
      DO 110 M=1,NOLINE
      DO 110 K=1,NOCHAR
      MSG(K,M)=NBLAK
      IF(K.EQ.1)MSG(K,M)=NFILL
  110 CONTINUE

C        SET THE NUMBER OF STATIONS TO PROCESS;
C        USUALLY WILL BE NMOSTA
      NTEMP=NMOSTA
      WRITE(KFILDO,91)NTEMP
      WRITE(KFILDO,93)NHEAD
 93   FORMAT(1X,'NHEAD=',I5)

C        READ NCEP STANDARD DATE FILE
C        PUT DATE IN TDL FORMAT
C        LDATE=(MYG*1000000)+(MOG*10000)+(MDG*100)+MHG
      CALL GET_NCEPDATE(IUNIT,MYG,MOG,MDG,MHG,LDATE,IERR)
      IF(IERR.NE.0) THEN
      CALL W3TAGE('MDL_FECN21TX') 
        STOP 115 
      ENDIF

C        FIND VALID DATES FOR HEADINGS.
C        FOR 8 CALENDER DATES 
      MDATE=LDATE
C        CONVERT 1995 TO 95 (FOR DISPLAY PURPOSES)
        MYGA=MOD(MYG,100)
C
C        FOR 00Z SET FIRST DAY FOR HEADER TO TODAY.  FOR 12Z
C        WE'LL SET IDA(1) TO BE TOMORROW USING THE BOUNDS OF
C        THE 116 LOOP.
      IF(MHG.EQ.0)THEN
        IDA(1)=MDG
        MO(1)=MOG
C        FULL YEAR EX. 1995, 2005, 2025 TO GO INTO IYR ARRAY
        IYR(1)=MYG
        M=0
C        RETURNS JULIAN DAY
C        RETURNS INTEGER DAY OF WEEK 
	IDAT(1)=MYG
	IDAT(2)=MOG
	IDAT(3)=MDG
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(1)=CHARDY(IDYWK)
        IS=2
      ELSE
        IS=1
      ENDIF

      DO 116 I=IS,8
        CALL CHNGDATE(-MDATE,24,MDATE)
        IDA(I)=MOD(MDATE,10000)/100
        MO(I)=MOD(MDATE,1000000)/10000
        IYR(I)=MDATE/1000000
        M=0
	IDAT(1)=IYR(I)
	IDAT(2)=MO(I)
	IDAT(3)=IDA(I)
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(I)=CHARDY(IDYWK)
 116  CONTINUE

C*****
C*****   START CONSTRUCTION OF THE 
C*****   BULLETIN:
C*****

C        CREATE THREE LINE HEADER                                       
      CALL PUTCHAR(MREC,MSG(1,2),1)
      CALL PUTCHAR('GFSX-BASED STATISTICAL MAX/MIN GUIDANCE',
     *       MSG(2,2),39)
      CALL PUTCHAR('/  /',MSG(44,2),4)
      CALL PUTMOS('MOG',FLOAT(MOG),0.,1.,1,12,MSG(42,2),
     *       2,0,'99',KX)
      CALL PUTMOS('MDG',FLOAT(MDG),0.,1.,1,31,MSG(45,2),
     *       2,2,'99',KX)
      CALL PUTMOS('MYG',FLOAT(MYGA),0.,1.,0,99,MSG(48,2),
     *       2,2,'99',KX)
      CALL PUTCHAR('00 UTC',MSG(54,2),6)
      CALL PUTMOS('MHG',FLOAT(MHG),0.,1.,0,12,MSG(52,2),
     *       2,2,'99',KX)

C        PLACE VALID TIMES
        MPOS=0
        DO 117 J=1,8
	  MPOS=MPOS+8
	  CALL PUTCHAR(CDAY(J),MSG(MPOS,3),3)
	  CALL PUTMOS('DAY',FLOAT(IDA(J)),0.,1.,0,31,MSG(MPOS+4,3),
     *         2,2,'99',KX)
 117    CONTINUE

C        PLACE FORECAST ELEMENT NAME (ONLY CLIMO IS NEEDED)
C        REMOVED 7/2005
C	 CALL PUTCHAR('CLIMO',MSG(72,3),5)

C        PLACES SEPERATOR LINES IN MESSAGE FOR THE DATES(LINE 3) AND
C        EACH STATION REGARDLESS OF WHETHER DATA EXISTS FOR A GIVEN
C        STATION  (7/2005 - REMOVED 8TH VBAR)
	DO 130 LL=3,NOLINE
          DO 120 I=1,7
	    CALL PUTCHAR(VBAR,MSG(LOC(I),LL),1)
 120      CONTINUE
 130    CONTINUE

C        PLACES STATION IDENTIFIER REGARDLESS OF WHETHER
C        DATA EXISTS FOR A GIVEN STATION
      DO 140 K=1,NTEMP
        CALL PUTCHAR(MREC,MSG(1,(K-1)*LINE+4),1)
        CALL PUTCHAR(LIST(K),MSG(2,(K-1)*LINE+4),4)
        KOUNT(1,K)=1
C        ABOVE STATEMENT PROVIDES FOR BLANK LINE.                       
 140  CONTINUE

CCCCCCCCCCC
CCCCCCCCCCC

C        PLACE MIN/MAX FCSTS
C        FOR 00Z CYCLE, START WITH 2ND MAX/MIN ID AND LOCD
C        FOR 12Z CYCLE, START WITH 1ST MAX/MIN ID AND LOCD
      ID(1)=0
      ID(2)=0
      ID(3)=18
      ID(4)=0
      INC=12
C
      IF(MHG.EQ.00)THEN
        ISTART=2
        IEND=16
      ELSE
        ISTART=1
        IEND=15
      ENDIF
C
      DO 160 K=ISTART,IEND
	ID(1)=MM(K)
	ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,
     *       INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        IF(IER.EQ.3) THEN
          CALL W3TAGE('MDL_FECN21TX')
          STOP 3
        ENDIF
        DO 150 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(LOCD(K)-2,
     *         (J-1)*LINE+4),3,0,'999',KOUNT(1,J))
 150    CONTINUE
 160  CONTINUE

      IF (KUT.LE.0) THEN
        WRITE(KFILDO,270)
 270    FORMAT(//'NO GFSX MOS FORECASTS FOUND.')
      CALL W3TAGE('MDL_FECN21TX') 
        STOP 70
      ENDIF

C
C  ** AS OF 2/2002, THIS PORTION IS NOT YET WORKING AS THERE
C     ARE NO CLIMO VALUES IN THE MOS-2000 SYSTEM
C
C        START CLIMATOLOGY PORTION OF BULLETIN
C
C
C        NOTE: THE ARRAY OF CALL LETTERS (LIST) HAS NOT CHANGED
C
C        PLACE MIN TEMP NORMALS
C     ID(1)=402016000
C     ID(2)=0
C        NOTE: ID(3) IS BEING USED TO PIGGYBACK THE CORRECT HOUR
C              PROJECTION TO GETNORM & GETMON. THE ACTUAL VALUE
C              OF ID(3) IN THE "CONSTANT" FILE IS ZERO. (SEE THE
C              WRITEUPS FOR GETNORM AND GETMON 
C     ID(3)=108
C     ID(4)=0
C     CALL GETNORM(LUNIT,ID,LDATE,NMOSTA,RNORM,ISW,LIST,X,NERR)
C     IF (NERR.NE.0) CALL MISSNG(NMOSTA,RNORM)
C     DO 280 J=1,NTEMP
C       CALL PUTMOS(LIST(J),RNORM(J),0.,1.,-99,999,MSG(71,(J-1)*
C    *       LINE+4),3,0,'999',KX)
C280  CONTINUE
C
C        PLACE MAX TEMP NORMALS
C     ID(1)=402006000
C     ID(3)=120
C     CALL GETNORM(LUNIT,ID,LDATE,NMOSTA,RNORM,ISW,LIST,X,NERR)
C     IF (NERR.NE.0) CALL MISSNG(NMOSTA,RNORM)
C     DO 290 J=1,NTEMP
C       CALL PUTMOS(LIST(J),RNORM(J),0.,1.,-99,999,MSG(75,(J-1)*
C    *       LINE+4),3,0,'999',KX)
C290  CONTINUE
C
C**********************************************************
C        END OF BULLETIN
C**********************************************************

      DO 400 N=1,NHEAD
	NBSTA=NESTA+1
	NESTA=NESTA+NWMO(N)
        CBULHD(1:6)=WMO(N)(1:6)
	CALL WRMSG(MSG,NOCHAR,NOLINE,NBSTA,NESTA,LINE,NCAT,KOUNT,
     *       LINE,NTEMP,CBULHD,LBULHD,MDG,MHG,NOHEAD,NOUT,
     *       BFILE,NHEAD)
 400  CONTINUE
C
C        CHANGE THE HEXIDECIMAL VALUE CB BACK TO THE ASCII
C        REPRESENTATION FOR A VERTICAL BAR BEFORE PRINTING.
C
      DO 550 I=1,NOLINE
        DO 500 J=1,NOCHAR
          IF (MSG(J,I).EQ.VBAR) MSG(J,I)='|'
 500    CONTINUE
 550  CONTINUE
C
C        PRINT MESSAGE                                                  
C
      CALL PRMSG(MSG,NOCHAR,NOLINE,KOUNT,LINE,NMOSTA,NUNIT,NOHEAD)
C
      CALL W3TAGE('MDL_FECN21TX') 
      STOP
      END
