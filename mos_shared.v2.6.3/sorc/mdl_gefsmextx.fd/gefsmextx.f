C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***                             
C                                                                       
C MAIN PROGRAM: MDL_GEFSMEXTX
C   PRGMMR: ERICKSON         ORG: OST22       DATE: 2002-01-11
C                                                                       
C ABSTRACT:  GENERATES THE NEW MRF BULLETIN CONTAINING MEDIUM RANGE   
C            MOS GUIDANCE FOR STATIONS IN U.S. & PUERTO RICO.
C            THE FORECAST ELEMENTS IN THE MESSAGE ARE MAX/MIN TEMPS,    
C            6-HR TEMP & DEWPOINT FOR NOW. OTHER ELEMENTS WILL BE
C            ADDED AT A LATER DATE.
C   NOTE:  AS OF LATE 2003, THIS CODE IS ONLY USED FOR THE GFSX
C          ENSEMBLES.  THE "REAL" GFSX TEXT MESSAGE IS CREATED BY
C          MDL_GFSMEXTX.
C                                                                       
C PROGRAM HISTORY LOG:                                                  
C   00-05-22  ERICKSON - ADAPTED FROM MRFFOX CODE TO HANDLE 
C                        FORECASTS FROM THE MOS2000 SYSTEM,
C                        GETTING CALL LETTERS FROM DICTIONARY,
C                        AND NEW HEADERS W/2NCD LINE.
C   00-07-03  MCE      - MODIFIED TO ADD POP AND QPF LINES
C                        CHANGED LINE VALUE TO 12
C                        ALSO CHANGED INCREMENT FOR PUTTING
C                        IN VERTICAL BAR.
C                        ADDED LOGIC TO DEAL W/HAWAII PQPF
C   00-07-03  SHIREY   - CHANGED 24-H POP ID TO 203423109
C                        FOR CONSISTENCY CHECK  
C   00-09-19  MCE      - CHANGED TO ADD PTYPE AND CLOUDS
C                        REMOVED OLD COMMENTS
C   00-09-20  MCE      - FIXED PTYPE IDS AND VB'S FOR MORE LINES 
C   01-02-22  RLA      - CHANGED IDS TO CONFORM TO NEW CATEGORICAL
C                        AND CONSISTENCY ID SCHEME.
C   01-05-02  MCE      - ADDED 12 & 24 TSTM FCSTS; 
C                        CHANGED TO CALL NEW READ_MOSDA IN LIBRARY
C   01-08-27  MCE     -  MODIFIED TO READ IN THE NUMBER OF HAWAII
C                        STATIONS AS A VARIABLE. THIS WAS HARD WIRED
C                        IN CODE BEFORE.  REDUCED NUMBER OF BLANK LINES
C                        READ IN TO 1(BFILE).
C   01-11-28  RLC     -  INCREASED MAXSTA TO 3000 TO ACCOMMODATE THE NEW
C                        1432 MOS SITES
C   02-07-12  MCE     -  MODIFIED TO ADD CLIMATOLOGY TERMS
C   02-08-23  MCE     -  MODIFIED TO ADD WIND SPEED
C   03-04-16  RLC     -  INCREASED MAXSTA TO 3500 TO ACCOMMODATE THE NEW
C                        MARINE SITES AND SITES WE ARE ADDING IN THE FALL
C   05-08-18  RLC     -  WE ARE TRANSITIONING FROM SENDING THE PRODUCT 
C                        VIA THE STATFILE TO VIA NTC.  CHANGES NEED TO BE
C                        MADE.  A NEW VERSION OF PUTQ IS USED, WRMSGA 
C                        WAS REDONE.  SOME CHARACTERS ARE ALSO CHANGED.
C   06-04-25  JCM     -  INCREASED MAXSTA TO 25000 TO PARALLEL OTHER
C                        MESSAGE CODES
C   12-10-23  ENGLE   -  MAIN PROGRAM NAME CHANGED FROM MRFMEXTX TO
C                        GEFSMEXTX
C                                                                       
C USAGE:                                                                
C                                                                       
C        DATA SET USE                                                   
C        INPUT FILES:
C      FORT.10(IUNIT) - NMC STANDARD DATE FILE 
C      FORT.27(KFILD) - MDL STATION DICTIONARY /nwprod/fix/tdl_station.tbl
C      FORT.45(KFILC) - MDL MOS CONSTANT FILE 
C      FORT.48(KFILX) - MDL MOS 2000 FCST FILE 
C             PARM    - STANDARD IN, LIST OF WMO HEADERS AND CALL LETTERS
C
C        OUTPUT FILES:  (INCLUDING WORK FILES)
C             FORT.XX - INDICATE NAME AND PURPOSE
C             FORT.06 - STANDARD OUT
C      FORT.06(KFILDO)- STANDARD OUTPUT
C      FORT.60(FTN60) - TELETYPE MESSAGE 
C      FORT.65(FTN65) - TRANSMISSION FILE 
C
C                                                                       
C        VARIABLES
C
C        SUBROUTINE RDWMO CALLING ARGUMENTS:
C                 JUNIT=FILE NUMBER(5) USED TO ACCESS MRF STATION
C                       DIRECTORY FILE
C                  LIST=LIST OF NMOSTA CALL LETTERS OF STATIONS,
C                       LEFT JUSTIFIED (8 CHARACTERS)
C                MAXSTA=MAXIMUM NUMBER OF STATIONS (ACTUALLY NEEDS TO BE
C                       MAXIMUM # STATIONS IN DICT X 2 FOR READ_MOSDA)
C                 NHEAD=NUMBER OF WMO STATION CALL LEADER HEADERS
C                       (EX. MRFFOX42AZ)
C                  NWMO=NUMBER OF STATIONS IN WMO CALL LETTER ARRAY,
C                       WMO(I)
C                   WMO=LIST OF WMO HEADERS(CHARACTER *8)
C
C            SUBROUTINE RDLSTA CALLING ARGUMENTS:
C                      CFMT=CONTAINS FORMAT OF DATA
C                     DTEMP=TEMPORARY ARRAY USED TO READ ONE RECORD
C                DATA(LIST)=LIST OF NMOSTA CALL LETTERS OF STATIONS 
C                    IVALEN=LENGTH OF EACH CHARACTER STRING WHICH
C                           IS READ (8 IN THIS CASE)
C                     JUNIT=FILE NUMBER(5) SEE RDWMO:
C                  LEFT(ND)=SIZE OF ARRRAY NAME
C                        NT=CHARACTER STRINGS READ PER RECORD(9)
C                      NVAL=COUNT OF ELEMENTS IN ARRAY RETURNED
C                      TERM=CHARACTER STRING TO INDICATE THE 
C                           TERMINATOR OF A LIST OF STATION CALL
C                           LETTERS FOR A PARTICULAR WMO HEADER
C
C**       SUBROUTINE W3DOXDAT CALLING ARGUMENTS:
C**	     IDAT(8)=NCEP ABSOLUTE DATE AND TIME ARRAY
C                    1 - 4 DIGIT YEAR
C		     2 - MONTH OF YEAR
C                    3 - DAY OF MONTH
C                    4 - HHMM TIME ZONE DIFFERENCE FROM UTC (-0500)
C		     5 - HOUR OF DAYI 24-HR CLOCK
C                    6 - MINUTES OF HOUR
C                    7 - SECONDS OF MINUTE
C                    8 - MILLISECONDS OF SECOND
C                JDOW=INTEGER DAY OF WEEK (1 IS SUNDAY)
C		 JDOY=INTEGER DAY OF YEAR (1 IS JAN 1)
C                JDAY=INTEGER JULIAN DAY (FROM JAN 1, 4713BC)
C      
C
C        ADDITIONAL VARIABLES:
C
C**             CBULHD=BULLETIN HEADER  (CHAR*18) 
C**            CDAY(8)=CHARACTER *3 ARRAY OF CALENDER DAYS(DAYS OF WEEK)
C**          CHARDY(7)=CHARACTER *3 ARRAY OF DAYS OF THE WEEK
C**             IDA(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**              IDYWK=INTEGER DAY OF THE WEEK (1 - 7)
C**             IMO(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**                INC=TIME INCREMENT
C**             IYR(8)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**              JDATE=DATE CHECK FOR SNOW (CPOS)
C**                JDY=JULIAN DATE USED TO HELP CALCULATE CDAY
C**         KOUNT( , )=INITIALLY SET TO ZERO, INCREASED BY 1            
C**                    WHERE A FORECAST IS ENTERED IN THAT LINE.        
C**                    1ST DIMENSION = LINE                             
C**                    2ND DIMENSION = GREATER THAN OR EQUAL TO NMOSTA
C**                    NOTE THAT THIS IS ONE LESS THAN THE NUMBER
C**                    YOU ARE USING FOR MSG ARRAY
C**             LBULHD=NUMBER OF CHARACTERS IN BULLETIN HEADER
C**               LINE=NUMBER OF LINES REQUIRED PER STATION
C**             LOC(8)=MSG POSITION ARRAY
C**            LOCD(8)=MSG POSITION ARRAY
C**              MM( )=ARRAY USED TO HOLD THE MDL IDENTIFIERS FOR THE   
C**                    MAX/MIN TEMPERATURE FORECAST-BOTH 00Z AND 12Z    
C**                    CYCLES.
C**              MO( )=ARRAY OF MONTH NUMBER(1-12) USED WITH W3SF13 FOR
C**                    CALCULATION OF THE JULIAN DAY.
C**          MSG(80, )=AREA INTO WHICH TELETYPE MESSAGE IS BUILT
C**                    LAST DIMENSION=NOLINE SHOULD BE GE LINE*NMOSTA+5
C**               MYGA=CONVERTS 1995 TO 95 FOR DISPLAY PURPOSES 
C**              NBLAK=BLANK CHARACTER
C**              NBSTA=BEGINNING STATION NUMBER (USED TO DELINEATE
C**                    BULLETINS IN WRMSG)
C**               NCAT=CHARACTER*5 CATELOG NUMBER
C**              NESTA=ENDING STATION NUMBER (USED TO DELINEATE
C**                    BULLETINS IN WRMSG)
C**                    OF BULLETIN)
C**              NFILL=CHARACTER USED AS FILL IN THE FOUS14--IS TRANS-  
C**                    PARENT TO THE AFOS OR TTY SYSTEM.
C**             NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS PREPARED
C**                    RETURNED FROM RDLSTA                             
C**             NOCHAR=1ST DIMENSION OF MSG( , )-MAX NUMBER OF CHARACTER
C**             NOHEAD=NUMBER OF LINES IN THE HEADER, INCLUDING BLANK   
C**                    LINES.                                           
C**             NOLINE=2ND DIMENSION OF MSG( , )-MAX NUMBER OF LINES    
C**               NOUT=OUTPUT TRANSMISSION FILE UNIT NUMBER (=65) 
C**              NTEMP=NUMBER OF STATIONS RUN (USUALLY EQUAL TO NMOSTA) 
C**              NUNIT=OUTPUT PRINT (READABLE) FILE UNIT NUMBER (=60)
C**               VBAR=ASCII CHARACTER "BAR" (SEPARATOR LINE)
C**                    NECESSARY FOR TRANSMISSION
C
C                                                                       
C   SUBPROGRAMS CALLED:                                                 
C      UNIQUE: RDWMO(CALLS RDLSTA), READ_MOSDA, RDLNK, 
C              WRMSGA
C
C     LIBRARY:                                                          
C       W3LIB: W3AG15,W3DOXDAT,W3TAG                        
C       TDLLIB: PUTMOS,PUTCHAR,PRMSG,WRMSG,RDLSTA,PUTQ
C
C       CHNGDATE: SEE DOCUMENTATION WRITEUPS
C
C      PUTCHAR=SUBROUTINE WHICH COPIES CHARACTER STRINGS FROM
C              STRING TO ANOTHER; AVOIDS HAVEING TO US DO LOOPS.
C
C       PUTMOS=SUBROUTINE WHICH CONVERTS A NUMBER (INTEGER OR
C              REAL), CONVERTS THE NUMBER TO A CHARACTER STRING
C              WHICH CAN THEN BE COPIED TO THE BULLETIN MESSAGE.
C
C         PUTQ=SUBROUTINE WHICH ATTACHES CONTROL CHARACTERS AT
C              THE END OF EACH RECORD FOR AFOS TRANSMISSION
C              (CALLED BY WRMSG)
C
C       RDLSTA=TO READ CHARACTER DATA WITH A GIVEN FORMAT.
C              ASSIME A MAX RECORD LENGTH FO 80 BYTES
C
C        PRMSG=PRINTS AN ASCII(SEQUENTIAL FILE) OUTPUT OF MRF
C              STATION BULLETINS
C
C        WRMSG=GENERATES AN ASCII(DIRECT ACCESS "FORMATTED") FILE
C              WITH EBSCDIC CONTROL CHARACTERS (TRANSMISSION 
C              COMPONENTS) OF MRF STATION BULLETINS
C
C                                                                       
C   EXIT STATES:   (STOP OCCURS IN MAIN UNLESS OTHERWISE STATED)   
C     COND =   0 - SUCCESSFUL RUN
C          =   3 - PROBLEM READING MRF MOS 2000 FORECAST FILE
C          =  30 - NUMBER OF STATIONS IN LIST ARE EXCEEDED(STOP
C                  OCCURS IN RDWMO)
C          = 110 - PROBLEM READING CONSTANT FILE
C          = 115 - UNABLE TO READ THE STANDARD NMC DATE FILE
C          = 370 - NO MOS FORECASTS WERE AVAILABLE          
C COMMENTED= 475 - CAN NOT POST MESSAGE WITH W3AG15 
C    OUT                   
C                                                                       
C REMARKS:                                                              
C     2500 STATIONS MAX                                                 
C                                                                       
C                                                                       
C ATTRIBUTES:                                                           
C   LANGUAGE: XLF90                                                
C   MACHINE:  IBM SP
C   
C$$$ 

      PROGRAM GEFSMEXTX

      PARAMETER(NOCHAR=70,MAXSTA=25000,LINE=20,NUMDAY=8)
      PARAMETER(NOHEAD=1,ND7=54)

      INTEGER IOPT,JBLOCK(MAXSTA),JNERR,NDATE,NSTA,KUNIT,
     * KOUNT(LINE,MAXSTA),IDA(NUMDAY),MO(NUMDAY),IYR(NUMDAY),
     * MM(15),LOC(7),LOCD(15),ID(4),ISW,NERR,JUNIT,IUNIT,LUNIT,
     * NMOSTA,LDATE,IDATE,KUT,IFLAG,IERR,MDG,MHG,MOG,MYG,NHEAD,
     * JDY,INC,JWBAN(MAXSTA),JDATE,NWMO(MAXSTA),NUNIT,
     * NOUT,LBULHD,NESTA,NBSTA,IDYWK,IDAT(8),JDAY,KFILX,ND1,ND5,
     * INDEX(MAXSTA,15),IPTST,KFILC,KD(4),IS0(ND7),IS1(ND7),
     * IS2(ND7),IS4(ND7),NHI

      REAL FCST(MAXSTA),RNORM(MAXSTA)

      CHARACTER*1 MSG(NOCHAR,MAXSTA*LINE+NOHEAD),NBLAK,NFILL,VBAR,MREC
      CHARACTER*2 CLD(4),PTYP(5)
      CHARACTER*3 CDAY(8),CHARDY(7)
      CHARACTER*5 CTYPE,NCAT
      CHARACTER*6 CDESC
      CHARACTER*8 LIST(MAXSTA),CALLS(MAXSTA),ICAO(MAXSTA),
     *            CCALL(MAXSTA,6)
      CHARACTER*18 WMO(MAXSTA),CBULHD
      CHARACTER*20 CNAME(MAXSTA)
      CHARACTER*80 BFILE

      DATA KUNIT/12/,JUNIT/5/,IUNIT/10/,LUNIT/21/,LBULHD/11/,
     *     NOUT/65/,NBLAK/' '/,NFILL/' '/,NUNIT/60/,KFILD/27/,
     *     KFILDO/6/,KFILX/48/,IPTST/0/,KFILC/45/
      DATA CHARDY/'SUN','MON','TUE','WED','THU','FRI','SAT'/
      DATA NCAT/'00000'/
      DATA CLD/'CL','PC','OV','XX'/
      DATA PTYP/' Z',' S','RS',' R','XX'/

      DATA MM/202140009,202240009,202140009,202240009,
     * 202140009,202240009,202140009,202240009,202140009,202240009,
     * 202140009,202240009,202140009,202240009,202140009/
      DATA LOC/9,17,25,33,41,49,57/
      DATA LOCD/6,10,14,18,22,26,30,34,38,42,46,50,54,58,62/

C        HOUR PROJECTIONS ARE
C        24,36,48,60,72,84,96,108,120,132,144,156,168,180,192

      DATA NDATE/0/,MDATE/0/,IDATE/0/,LDATE/0/,JDATE/0/,NHI/0/,
     *     NHEAD/0/,NESTA/0/,IOPT/0/,NMOSTA/0/,ISW/1/,IDYWK/0/
      DATA IDAT/0,0,0,-0500,0,0,0,0/

      CALL W3TAGB('MDL_MRFMEXTX',2002,0011,0066,'OST22')                  

      ND5=MAXSTA

C        8/2005 - CHANGED VBAR FROM CHAR(203) FOR NTC
C                 ADDED MREC FOR RECORD SEPARATOR TO REPLACE $
      VBAR=CHAR(124)
      MREC=CHAR(30)

C        INITIALIZE ARRAYS
      DO 102 M=1,MAXSTA
	NWMO(M)=0
	FCST(M)=0.
      DO 102 K=1,LINE
        KOUNT(K,M)=0
  102 CONTINUE

C        READ IN THE BULLETIN "BFILE" OUTPUT FILENAME
      READ(JUNIT,104) BFILE
 103  FORMAT(A80)
      WRITE(6,104) BFILE
 104  FORMAT(' ',A80)
      READ(JUNIT,105) NHI
 105  FORMAT(I3)
      WRITE(6,106) NHI
 106  FORMAT(' HAWAII SITES:',I3)

C        READ BULLETIN HEADERS AND STATIONS                             
      CALL RDWMO(JUNIT,NMOSTA,LIST,WMO,NWMO,NHEAD,MAXSTA)
      WRITE(6,108)NMOSTA
 108  FORMAT(1X,'NMOSTA=',I10)
      WRITE(6,109)NHEAD
 109  FORMAT(1X,'NHEAD=',I5)
      NOLINE=(NMOSTA*LINE)+NOHEAD
      KUT=0
C
      NEW=0 
      CALL RDLNK(KFILD,KFILDO,NEW,LIST,CCALL,NMOSTA,MAXSTA)
C
C        FILL THE MESSAGE ARRAY WITH BLANKS EXCEPT FOR THE 1ST CHARACTER
C
      DO 110 M=1,NOLINE
      DO 110 K=1,NOCHAR
      MSG(K,M)=NBLAK
      IF(K.EQ.1)MSG(K,M)=NFILL
  110 CONTINUE
C
C        READ NCEP STANDARD DATE FILE
C        PUT DATE IN MDL FORMAT
C
      CALL GET_NCEPDATE(IUNIT,MYG,MOG,MDG,MHG,LDATE,IERR)
      WRITE(KFILDO,112) LDATE
  112 FORMAT(' LDATE RETURNED FROM GET_NCEPDATE:',I10)
      IF(IERR.NE.0) THEN
      CALL W3TAGE('MDL_MRFMEXTX') 
        STOP 115
      ENDIF

C        FIND VALID DATES FOR HEADINGS.
C        FOR 8 CALENDER DATES 
      MDATE=LDATE
      IDA(1)=MDG
      MO(1)=MOG
C        FULL YEAR EX. 1995, 2005, 2025 TO GO INTO IYR ARRAY
      IYR(1)=MYG
C        CONVERT 1995 TO 95 (FOR DISPLAY PURPOSES)
      MYGA=MOD(MYG,100)
      MYGC=MYG/100
      M=0
C        RETURNS JULIAN DAY
C        RETURNS INTEGER DAY OF WEEK 
      IDAT(1)=MYG
      IDAT(2)=MOG
      IDAT(3)=MDG
      CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
      CDAY(1)=CHARDY(IDYWK)
      DO 116 I=2,8
        CALL CHNGDATE(-MDATE,24,MDATE)
        IDA(I)=MOD(MDATE,10000)/100
        MO(I)=MOD(MDATE,1000000)/10000
        IYR(I)=MDATE/1000000
        M=0
	IDAT(1)=IYR(I)
	IDAT(2)=MO(I)
	IDAT(3)=IDA(I)
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(I)=CHARDY(IDYWK)
 116  CONTINUE

C*****
C*****   START CONSTRUCTION OF THE 
C*****   BULLETIN:
C*****
C        PLACE HEADER INFO FOR EACH STATION 
C        (CHANGED LOOP FROM NTEMP TO NMOSTA)
C
      DO 140 K=1,NMOSTA
C
C        BUILD 1ST LINE 
      CALL PUTCHAR(MREC,MSG(1,(K-1)*LINE+2),1)
      CALL PUTCHAR(CCALL(K,1),MSG(2,(K-1)*LINE+2),4)
      CALL PUTCHAR('MRF MOS GUIDANCE',MSG(9,(K-1)*LINE+2),16)
      CALL PUTCHAR('/  /',MSG(30,(K-1)*LINE+2),4)
      CALL PUTMOS('MOG',FLOAT(MOG),0.,1.,1,12,MSG(28,(K-1)*LINE+2),
     *       2,0,'99',KX)
      CALL PUTMOS('MDG',FLOAT(MDG),0.,1.,1,31,MSG(31,(K-1)*LINE+2),
     *       2,2,'99',KX)
      CALL PUTMOS('MYG',FLOAT(MYGC),0.,1.,19,99,MSG(34,(K-1)*LINE+2),
     *       2,2,'99',KX)  
      CALL PUTMOS('MYG',FLOAT(MYGA),0.,1.,0,99,MSG(36,(K-1)*LINE+2),
     *       2,2,'99',KX)
      CALL PUTCHAR('00 UTC',MSG(42,(K-1)*LINE+2),6)
      CALL PUTMOS('MHG',FLOAT(MHG),0.,1.,0,12,MSG(40,(K-1)*LINE+2),
     *       2,2,'99',KX)
      KOUNT(1,K)=1
C
C        PLACE FORECAST HOUR LINE (SECOND LINE)
      CALL PUTCHAR('FHR',MSG(2,(K-1)*LINE+3),3)
      IHR=MHG+12
      MPOS=6
      DO 117 J=1,15
        IHR=IHR+12
        CALL PUTMOS('HOUR',FLOAT(IHR),0.,1.,24,192,
     *  MSG(MPOS,(K-1)*LINE+3),3,0,'999',KX)
        MPOS=MPOS+4
 117  CONTINUE
      KOUNT(2,K)=1
C
C        PLACE VALID TIMES (THIRD LINE)                                             
      MPOS=2
      DO 118 J=1,8
        CALL PUTCHAR(CDAY(J),MSG(MPOS,(K-1)*LINE+4),3)
        IF(MPOS.EQ.2) THEN
          CALL PUTMOS('DAY',FLOAT(IDA(J)),0.,1.,0,31,MSG(MPOS+5,
     *         (K-1)*LINE+4),2,2,'99',KX)
          MPOS=MPOS+9
        ELSE
          CALL PUTMOS('DAY',FLOAT(IDA(J)),0.,1.,0,31,MSG(MPOS+4,
     *         (K-1)*LINE+4),2,2,'99',KX)
          MPOS=MPOS+8
        ENDIF
 118  CONTINUE
      KOUNT(3,K)=1
C
C        PLACE FORECAST ELEMENT NAMES                                   
      CALL PUTCHAR('CLIMO',MSG(66,(K-1)*LINE+4),5)
      CALL PUTCHAR('X/N',MSG(2,(K-1)*LINE+5),3)
      CALL PUTCHAR('TMP',MSG(2,(K-1)*LINE+6),3)
      CALL PUTCHAR('DPT',MSG(2,(K-1)*LINE+7),3)
      CALL PUTCHAR('CLD',MSG(2,(K-1)*LINE+8),3)
      CALL PUTCHAR('WND',MSG(2,(K-1)*LINE+9),3)
      CALL PUTCHAR('P12',MSG(2,(K-1)*LINE+10),3)
      CALL PUTCHAR('P24',MSG(2,(K-1)*LINE+11),3)
      CALL PUTCHAR('Q12',MSG(2,(K-1)*LINE+12),3)
      CALL PUTCHAR('Q24',MSG(2,(K-1)*LINE+13),3)
      CALL PUTCHAR('T12',MSG(2,(K-1)*LINE+14),3)
      CALL PUTCHAR('T24',MSG(2,(K-1)*LINE+15),3)
      CALL PUTCHAR('PZP',MSG(2,(K-1)*LINE+16),3)
      CALL PUTCHAR('PSN',MSG(2,(K-1)*LINE+17),3)
      CALL PUTCHAR('PRS',MSG(2,(K-1)*LINE+18),3)
      CALL PUTCHAR('TYP',MSG(2,(K-1)*LINE+19),3)
C
C        PLACES SEPERATOR LINES IN MESSAGE                              
C        NOTE: LL WILL NEED TO INCREMENT TO 20 WHEN
C        MESSAGE IS ITS COMPLETE SIZE
C        7/1/00 CHANGED DO LOOP 130 FROM 7 TO 11
C        9/20/00 CHANGED FROM 11 TO 16  
C        4/30/01 CHANGED FROM 16 TO 18
C        8/23/02 CHANGED FROM 18 TO 19
C
        DO 130 LL=3,19
          DO 120 I=1,7
            CALL PUTCHAR(VBAR,MSG(LOC(I),(K-1)*LINE+LL),1)
 120      CONTINUE
 130    CONTINUE
        KOUNT(LINE,K)=-1
C        ABOVE STATEMENT PROVIDES FOR BLANK LINE.                       
 140  CONTINUE
C
C***************************************************
C        BEGIN PLACING FORECASTS
C        PLACE MIN/MAX FCSTS
C        FCSTS ARE STORED AT TAU+6 
C
      ID(1)=0
      ID(2)=0
      ID(3)=18
      ID(4)=0
      INC=12
      DO 160 K=1,15
	ID(1)=MM(K)
	ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.EQ.3) THEN
          CALL W3TAGE('MDL_MRFMEXTX')
          STOP 3
        ENDIF
        IF(IER.LT.2) KUT=KUT+1
        DO 150 J=1,NMOSTA
C         WRITE(6,149) k,LOCD(k)
C149      FORMAT(' K=',i3,' LOCD=',i3)
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(LOCD(K),
     *         (J-1)*LINE+5),3,0,'999',KOUNT(4,J))
 150    CONTINUE
 160  CONTINUE
C
C        PLACE TEMP FORECASTS                                       
C 
      ID(1)=202020009
      ID(2)=0
      ID(3)=12
      INC=12
      DO 180 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 170 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(LOCD(K),
     *    (J-1)*LINE+6),3,0,'999',KOUNT(5,J))
 170    CONTINUE
 180  CONTINUE
C
C        PLACE DEW POINT FORECASTS                                       
C 
      ID(1)=203020009
      ID(2)=0
      ID(3)=12
      INC=12
      DO 200 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 190 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(LOCD(K),
     *    (J-1)*LINE+7),3,0,'999',KOUNT(6,J))
 190    CONTINUE
 200  CONTINUE
C
C        PLACE CLOUD FORECASTS                                       
C        (2/2001-ID CHANGED FROM 208290)
C
      ID(1)=208391009
      ID(2)=0
      ID(3)=12
      INC=12
      DO 202 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 201 J=1,NMOSTA
          IF(NINT(FCST(J)).EQ.9999) THEN
            FCST(J)=4.
          ELSE
            KOUNT(7,J)=1
          ENDIF
          CALL PUTCHAR(CLD(NINT(FCST(J))),MSG(LOCD(K)+1,
     *    (J-1)*LINE+8),2)
 201    CONTINUE
 202  CONTINUE
C
C        PLACE WIND SPEED FORECASTS                                       
C 
      ID(1)=204475009
      ID(2)=0
      ID(3)=12
      INC=12
      DO 204 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 203 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,999,MSG(LOCD(K),
     *    (J-1)*LINE+9),3,0,'999',KOUNT(8,J))
 203    CONTINUE
 204  CONTINUE
C
C
C        PLACE 12H POP FORECASTS 
C
      ID(1)=203320109
      ID(2)=0
      ID(3)=12
      ID(4)=0950052000
      KD(1)=203320109
      KD(2)=0
      KD(3)=12
      KD(4)=0950052000
      INC=12
      DO 210 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 205 J=NHI+1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+10),3,0,'999',KOUNT(9,J))
 205    CONTINUE
C          NOW PROCESS HAWAIIN STNS- PROJ 6HRS LATER
        KD(3)=ID(3)+6
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,KD,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        DO 206 J=1,NHI
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+10),3,0,'999',KOUNT(9,J))
 206    CONTINUE
 210  CONTINUE
C
C        PLACE 24H POP FORECASTS
C        (2/2001-ID CHANGED FROM 203423)
C
      ID(1)=203430109
      ID(2)=0
      ID(3)=24
      ID(4)=0950052000
      INC=24
      DO 220 K=3,15,2 
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 215 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+11),3,0,'999',KOUNT(10,J))
 215    CONTINUE
 220  CONTINUE
C
C        PLACE 12H QPF FORECASTS
C        (2/2001-CHANGED IDS FROM 203320)
C
      ID(1)=203321009
      ID(2)=0
      ID(3)=12
      ID(4)=0
      KD(1)=203321009
      KD(2)=0
      KD(3)=12
      KD(4)=0
      INC=12
      DO 230 K=1,12
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 225 J=NHI+1,NMOSTA
          IF(FCST(J).LT.9999.) THEN
            FCST(J)=FCST(J) - 1.
          ENDIF
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(LOCD(K)+2,
     *    (J-1)*LINE+12),1,1,'9',KOUNT(11,J))
 225    CONTINUE
C          NOW PROCESS 6 HAWAIIN STNS- PROJ 6HRS LATER
        KD(3)=ID(3)+6
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,KD,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        DO 226 J=1,NHI
          IF(FCST(J).LT.9999.) THEN
            FCST(J)=FCST(J) - 1.
          ENDIF
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(LOCD(K)+2,
     *    (J-1)*LINE+12),1,1,'9',KOUNT(11,J))
 226    CONTINUE
 230  CONTINUE
C
C        PLACE 24H QPF FORECASTS
C        (2/2001-CHANGED ID FROM 203420)
C
      ID(1)=203421009
      ID(2)=0
      ID(3)=24
      ID(4)=0
      INC=24
      DO 240 K=3,11,2
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 235 J=1,NMOSTA
          IF(FCST(J).LT.9999.) THEN
            FCST(J)=FCST(J) - 1.
          ENDIF
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(LOCD(K)+2,
     *    (J-1)*LINE+13),1,1,'9',KOUNT(12,J))
 235    CONTINUE
 240  CONTINUE
C
C
C        PLACE 12H TSTM FORECASTS
C
      ID(1)=207320109
      ID(2)=0
      ID(3)=12
      ID(4)=0950000000
      INC=12
      DO 245 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 242 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+14),3,0,'999',KOUNT(13,J))
 242    CONTINUE
 245  CONTINUE
C
C        PLACE 24H TSTM FORECASTS
C
      ID(1)=207430109
      ID(2)=0
      ID(3)=12
      ID(4)=0950000000
      INC=24
      DO 250 K=2,14,2
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 247 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+15),3,0,'999',KOUNT(14,J))
 247    CONTINUE
 250  CONTINUE
C
C        SKIP FREEZING RAIN, SNOW, RAIN/SNOW MIX, & TYPE
C        FORECASTS IF DATE IS AFTER MAY 31 AND BEFORE SEP 01.
      JDATE=(MOG*100)+MDG
      IF ((JDATE.GT.531).AND.(JDATE.LT.901)) GOTO 369 
C
C        PLACE PZP - PROB OF FREEZING RN FORECASTS
C
      ID(1)=208555309
      ID(2)=0
      ID(3)=12
      ID(4)=0150001000
      INC=12
      DO 265 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 262 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+16),3,0,'999',KOUNT(15,J))
 262    CONTINUE
 265  CONTINUE
C
C        PLACE PSN - PROB OF SNOW FORECASTS
C
      ID(1)=208555309
      ID(2)=0
      ID(3)=12
      ID(4)=0250001000
      INC=12
      DO 270 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 267 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+17),3,0,'999',KOUNT(16,J))
 267    CONTINUE
 270  CONTINUE
C
C        PLACE PRS - PROB OF RAIN/SNOW MIX FORECASTS
C
      ID(1)=208555309
      ID(2)=0
      ID(3)=12
      ID(4)=0350001000
      INC=12
      DO 275 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 273 J=1,NMOSTA
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(LOCD(K),
     *    (J-1)*LINE+18),3,0,'999',KOUNT(17,J))
 273    CONTINUE
 275  CONTINUE
C
C        PLACE TYP - PTYPE BEST CAT FORECASTS
C        (2/2001-CHANGED ID FROM 208555)
C
      ID(1)=208556009
      ID(2)=0
      ID(3)=12
      ID(4)=0
      INC=12
      DO 280 K=1,15
        ID(3)=ID(3)+INC
        CALL READ_MOSDA(KFILDO,KFILX,IPTST,ID,LDATE,CCALL,
     *       NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
        IF(IER.LT.2) KUT=KUT+1
        DO 277 J=1,NMOSTA
          IF(NINT(FCST(J)).EQ.9999) THEN
            FCST(J)=5.
          ELSE
            KOUNT(18,J)=1
          ENDIF
          CALL PUTCHAR(PTYP(NINT(FCST(J))),MSG(LOCD(K)+1,
     *    (J-1)*LINE+19),2)
 277    CONTINUE
 280  CONTINUE
C
C
 369  IF (KUT.LE.0) THEN
        WRITE(6,370)
 370    FORMAT(//'0NO MRF MOS FORECASTS FOUND.')
      CALL W3TAGE('MDL_MRFMEXTX') 
        STOP 370
      ENDIF

C        OPEN MOS 2000 CONSTANT FILE USING SUBROUTINE 
C        READMOSDA - UNIT NUMBER MUST BE 45
C        PLACE HOLDER FOR NOW
C
C        START CLIMATOLOGY PORTION OF BULLETIN
C
C        NOTE: THE ARRAY OF CALL LETTERS (LIST) HAS NOT CHANGED
C
C        PLACE MIN TEMP NORMALS
      ID(1)=422016000
      ID(2)=0
C        NOTE: ID(3) IS BEING USED TO PIGGYBACK THE CORRECT HOUR
C              PROJECTION TO GETNORM & GETMONTH. THE ACTUAL VALUE
C              OF ID(3) IN THE "CONSTANT" FILE IS ZERO. (SEE THE
C              WRITEUPS FOR GETNORM AND GETMONTH 
      ID(3)=108
      ID(4)=0
      CALL READ_MOSDA(KFILDO,KFILC,IPTST,ID,LDATE,CCALL,
     *     NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
C?    IF(IER.LT.2) KUT=KUT+1
      DO 380 J=1,NMOSTA
      CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(65,(J-1)*
     *       LINE+5),3,0,'999',KX)
 380  CONTINUE
C        PLACE MAX TEMP NORMALS
      ID(1)=422006000
      ID(2)=0
      ID(3)=120
      ID(4)=0
      CALL READ_MOSDA(KFILDO,KFILC,IPTST,ID,LDATE,CCALL,
     *     NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
      DO 385 J=1,NMOSTA
      CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(68,(J-1)*
     *       LINE+5),3,0,'999',KX)
 385  CONTINUE
C        PLACE 12H POP RELATIVE FREQUENCIES (0Z-12Z, 12Z-00Z)
      ID(1)=433624000
      ID(2)=0
      ID(3)=108
      ID(4)=0
      CALL READ_MOSDA(KFILDO,KFILC,IPTST,ID,LDATE,CCALL,
     *     NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
      DO 390 J=1,NMOSTA
      CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(65,(J-1)*
     *       LINE+10),3,0,'999',KX)
 390  CONTINUE
      ID(1)=433626000
      ID(2)=0
      ID(3)=120
      ID(4)=0
      CALL READ_MOSDA(KFILDO,KFILC,IPTST,ID,LDATE,CCALL,
     *     NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
      DO 395 J=1,NMOSTA
      CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(68,(J-1)*
     *       LINE+10),3,0,'999',KX)
 395  CONTINUE
C        PLACE 24H POP RELATIVE FREQUENCIES (00Z-00Z)
      ID(1)=433628000
      ID(2)=0
      ID(3)=120
      ID(4)=0
      CALL READ_MOSDA(KFILDO,KFILC,IPTST,ID,LDATE,CCALL,
     *     NMOSTA,FCST,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,INDEX,IER)
      DO 397 J=1,NMOSTA
      CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(68,(J-1)*
     *       LINE+11),3,0,'999',KX)
 397  CONTINUE

C**********************************************************
C        END OF BULLETIN
C**********************************************************
      DO 400 N=1,NHEAD
        NBSTA=NESTA+1
        NESTA=NESTA+NWMO(N)
          CBULHD=WMO(N)
          CALL WRMSGA(MSG,NOCHAR,NOLINE,NBSTA,NESTA,LINE,NCAT,KOUNT,
     *         LINE,NMOSTA,CBULHD,LBULHD,MDG,MHG,NOHEAD,NOUT,
     *         BFILE,NHEAD)
 400  CONTINUE
C
C        CHANGE THE HEXIDECIMAL VALUE CB BACK TO THE ASCII REPRESENTATIO
C        FOR A VERTICAL BAR BEFORE PRINTING.
C
      DO 550 I=1,NOLINE
        DO 500 J=1,NOCHAR
          IF (MSG(J,I).EQ.VBAR) MSG(J,I)='|'
 500    CONTINUE
 550  CONTINUE
C
C        PRINT MESSAGE                                                  
C
      CALL PRMSG(MSG,NOCHAR,NOLINE,KOUNT,LINE,NMOSTA,NUNIT,NOHEAD)
C
      CALL W3TAGE('MDL_MRFMEXTX') 
      STOP
      END
