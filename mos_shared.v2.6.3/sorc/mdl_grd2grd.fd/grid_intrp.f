      SUBROUTINE GRID_INTRP(KFILDO,ND2X3,ND7,NX,NY,
     1                      IS2,MPROJ,XLATLL,YLONLL,XMESH,
     2                      ORIENT,XLAT,XYDIR)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: DATA_INTRP
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2002-07-01
C
C ABSTRACT: TO REDEFINE THE "IX" AND "JY" COORDINATES RELATIVE TO 
C           THE OUTPUT GRID.  THE OUTPUT GRID USUALLY POSSESS 
C           DIFFERENT GRID CHARACTERISTICS THAN THE INPUT GRID. 
C           THE OUTPUT GRID CHARACTERISTICS ARE DEFINED AT THE 
C           TOP OF THE 'U365.CN' FILE.
C
C PROGRAM HISTORY LOG:
C   02-07-01  RUDACK
C   04-03-01  RUDACK  ADDED A LINE OF CODE TO CHANGE
C                     THE SIGN OF THE LONGITUDE VALUE
C                     RETURNED FROM SUBROUTINE 'PSIJLL'.
C   05-11-01  MALONEY ADDED NCEP DOCBLOCK.
C   07-09-11  RLC     COMMENTED OUT ALON=-ALON WHEN WE
C                     SWITCHED FROM REAP VERSION OF
C                     PSIJLL TO HRG VERSION
C
C USAGE:  CALLED BY GRIDCOMB
C
C        DATA SET USE:
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C               ND2X3 = THE DIMENSION OF SEVERAL ARRAYS (ND2*ND3).  SET 
C                       BY PARAMETER.  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  SET BY PARAMETER.  (INPUT)
C                  NX = X-EXTENT OF THE OUTPUT GRID.  (INPUT)
C                  NY = Y-EXTENT OF THE OUTPUT GRID.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INPUT)                 
C               MPROJ = MAP PROJECTION OF THE OUTPUT GRID.  (INPUT)
C              XLATLL = LATITUDE OF LOWER LEFT CORNER POINT OF THE
C                       OUTPUT GRID.  (INPUT)
C              YLONLL = WEST LONGITUDE OF LOWER LEFT CORNER POINT
C                       OF THE OUTPUT GRID.  DO NOT USE NEGATIVE.  (INPUT)
C               XMESH = MESH LENGTH OF OUTPUT GRID IN KM AT XLAT DEGREES 
C                       NORTH LATITUDE.  (INPUT)
C              ORIENT = ORIENTATION OF OUTPUT GRID IN DEGREES WEST 
C                       LONGITUDE.  DO NOT USE NEGATIVE.  (INPUT)
C                XLAT = LATITUDE OF OUTPUT GRID IN DEGREES AT WHICH 
C                       XMESH APPLIES. ALSO THE LATITUDE WHERE THE 
C                       PROJECTION CUTS THE EARTH.  DO NOT USE NEGATIVE.
C                       (INPUT)
C          XYDIR(K,J) = FOR EACH GRIDPOINT ON THE OUTPUT GRID THE POSITION
C                       (J=1 FOR IX -- J=2 FOR JY) ON THE INPUT GRID 
C                       (K=NX*NY).  (OUTPUT)
C             XMESHIN = MESH LENGTH OF INPUT IN METERS AT XLATIN DEGREES 
C                       NORTH LATITUDE.  (INTERNAL)
C              XLATIN = LATITUDE OF INPUT GRID IN DEGREES AT WHICH 
C                       XMESHIN APPLIES. ALSO THE LATITUDE WHERE THE 
C                       PROJECTION CUTS THE EARTH.  DO NOT USE NEGATIVE.
C                       (INTERNAL)
C            XLATLLIN = LATITUDE OF LOWER LEFT (1,1) CORNER POINT OF THE
C                       INPUT GRID.  (INTERNAL)
C            YLONLLIN = WEST LONGITUDE OF LOWER LEFT (1,1) CORNER POINT
C                       OF THE INPUTGRID.  DO NOT USE NEGATIVE.  (INTERNAL)
C              INPROJ = MAP PROJECTION OF THE INPUT GRID.  (INTERNAL) 
C              XMESHL = XMESH IN UNITS OF METERS.  (INTERNAL)
C                 IER = ERROR RETURN.  (INTERNAL)
C                         0 = GOOD RETURN.
C                       105 = THE REQUESTED XI,YJ POINT IS IN THE
C                             FORBIDDEN ZONE, I.E. OFF THE LAMBERT MAP
C                             IN THE OPEN SPACE WHERE THE CONE IS CUT.
C                             IN THIS CASE, ALAT=9999. AND ELON=9999.
C
C        SUBPROGRAMS CALLED:  PSIJLL, PSLLIJ, LMIJLL, LMLLIJ, 
C                             MCIJLL, MCLLIJ
C          UNIQUE: NONE
C          LIBRARY:
C           MOSLIB - PSIJLL, PSLLIJ, LMIJLL, LMLLIJ, MCIJLL, MCLLIJ
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  105 - REQUESTED XI,YJ IS OFF THE MAP
C                   SEE OTHER ROUTINES FOR OTHER VALUES.
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      DIMENSION XYDIR(ND2X3,2)
      DIMENSION IS2(ND7)  
C
      INPROJ=IS2(2)
C      
      XMESHL=XMESH*1000.
C            
      XMESHIN=FLOAT(IS2(8))/1000.
      ORIENTIN=FLOAT(IS2(7))/10000.
      XLATIN=FLOAT(IS2(9))/10000.
      XLATLLIN=FLOAT(IS2(5))/10000.
      YLONLLIN=FLOAT(IS2(6))/10000.
C
      IER=0
C
      K=1
C
      IF(MPROJ.EQ.3) THEN
C
C           THE OUTPUT GRID IS LAMBERT CONFORMAL.      
C
         DO 120 JY=1,NY
            DO 110 IX=1,NX
C
               YJ=JY
               XI=IX
C
C                 CONVERT THE XI AND YJ COORDINATES ON THE OUTPUT GRID
C                 TO LATITUDE AND LONGITUDE VALUES.
C
               CALL LMIJLL(KFILDO,XI,YJ,XMESHL,ORIENT,XLAT,
     1                     XLATLL,YLONLL,ALAT,ALON,IER)
C
C                 IF IER = 105, THE LATITUDE AND LONGITUDE ARE IN THE 
C                 "FORBIDDEN ZONE".  IN THIS CASE, DO NOT CALCULATE A 
C                 CORRESPONDING "XI" AND "YJ" VALUE BUT RATHER ASSIGN IT 
C                 A MISSING VALUE.  CONSEQUENTLY, IN THE SUBROUTINE 
C                 'DATA_INTRP, THE DATA AT THIS POINT WILL BE ASSIGNED A 
C                 MISSING VALUE OF 9999.  PROCESS THE NEXT LATITUDE AND 
C                 LONGITUDE POINT.
C 
               IF(IER.EQ.105) THEN
                  XYDIR(K,1)=9999.
                  XYDIR(K,2)=9999.
C                    INCREMENT TO THE NEXT POINT.
                  K=K+1
C                    PROCESS THE NEXT POINT.
                  GOTO 110
               ENDIF
C
C                 EXPRESS THE XI AND YJ VALUES OF EACH POINT
C                 ON THE OUTPUT GRID RELATIVE TO THE INPUT GRID.
C
               IF(INPROJ.EQ.3) THEN 
                  CALL LMLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.5) THEN
                  CALL PSLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.7) THEN
                  CALL MCLLIJ(KFILDO,ALAT,ALON,XMESHIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ENDIF
C
C                 STORE EACH GRID POINT COORDINATE FOR LATER USE.
C  
               XYDIR(K,1)=XI
               XYDIR(K,2)=YJ
C
               K=K+1
C
 110        CONTINUE
 120     CONTINUE
C
      ELSEIF(MPROJ.EQ.5) THEN
C
C           THE OUTPUT GRID IS NORTH POLAR STEREOGRAPHIC.
C
         DO 150 JY=1,NY
            DO 140 IX=1,NX
C
               YJ=JY
               XI=IX
C
C                 CONVERT THE XI AND YJ COORDINATES ON THE OUTPUT GRID
C                 TO LATITUDE AND LONGITUDE VALUES.
C
               CALL PSIJLL(KFILDO,XI,YJ,XMESHL,ORIENT,XLAT,
     1                     XLATLL,YLONLL,ALAT,ALON)
C
C                 THE LONGITUDE VALUE "ALON" RETURNED FROM 'PSIJLL'
C                 IS NEGATIVE.  THIS VALUE MUST BE CONVERTED INTO A
C                 POSITIVE VALUE.  THIS IS BECAUSE ALL OTHER MOS-2000
C                 GRID-MANIPULATION CODES REQUIRE THAT THE INPUT 
C                 LONGITUDE VALUE BE POSITIVE. 
C
c              ALON=-ALON            
C
C                 EXPRESS THE XI AND YJ VALUES OF EACH POINT
C                 ON THE OUTPUT GRID RELATIVE TO THE INPUT GRID.
C
               IF(INPROJ.EQ.3) THEN 
                  CALL LMLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.5) THEN
                  CALL PSLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.7) THEN 
                  CALL MCLLIJ(KFILDO,ALAT,ALON,XMESHIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ENDIF
C
C                 STORE EACH GRID POINT COORDINATE FOR LATER USE.
C  
               XYDIR(K,1)=XI
               XYDIR(K,2)=YJ
C
               K=K+1
C
 140        CONTINUE
 150     CONTINUE
C
      ELSEIF(MPROJ.EQ.7) THEN
C
C           THE OUTPUT GRID IS MERCATOR.      
C
         DO 180 JY=1,NY
            DO 170 IX=1,NX
C
               YJ=JY
               XI=IX
C
C                 CONVERT THE XI AND YJ COORDINATES ON THE OUTPUT GRID
C                 TO LATITUDE AND LONGITUDE VALUES.
C
               CALL MCIJLL(KFILDO,XI,YJ,XMESHL,XLAT,
     1                     XLATLL,YLONLL,ALAT,ALON)
C             
C                 EXPRESS THE XI AND YJ VALUES OF EACH POINT
C                 ON THE OUTPUT GRID RELATIVE TO THE INPUT GRID.
C
               IF(INPROJ.EQ.3) THEN 
                  CALL LMLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.5) THEN
                  CALL PSLLIJ(KFILDO,ALAT,ALON,XMESHIN,ORIENTIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ELSEIF(INPROJ.EQ.7) THEN
                  CALL MCLLIJ(KFILDO,ALAT,ALON,XMESHIN,XLATIN,
     1                        XLATLLIN,YLONLLIN,XI,YJ)
               ENDIF
C
C                 STORE EACH GRID POINT COORDINATE FOR LATER USE.
C  
               XYDIR(K,1)=XI
               XYDIR(K,2)=YJ
C
               K=K+1
C
 170        CONTINUE
 180     CONTINUE
C
      ENDIF
C           
      RETURN
      END
 


