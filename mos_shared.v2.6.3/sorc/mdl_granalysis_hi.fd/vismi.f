      SUBROUTINE VISMI(KFILDO,XDATA,NVAL,TLO,SETLO,THI,SETHI,
     1                 CONST,NSCAL,EX1,EX2,IER)
C
C        MARCH     2008   GLAHN   TDL   MOS-2000
C        MARCH     2008   GLAHN   SCALING UPPER CATEGORY
C
C        PURPOSE
C            TO POSTPROCESS A VISIBILITY CATEGORY INTO A
C            CONTINUOUS VARIABLE MILES.  THE VARIABLE IN XDATA( )
C            IS FIRST TRUNCATED TO VALUES SUCH THAT ALL VALUES LT TLO
C            ARE SET TO SETLO AND ALL VALUES GT THI ARE SET 
C            TO SETHI.  EX1 AND EX2 ARE FOR POSSIBLE FUTURE USE.
C            AT THE END OF SCALING, THE VARIABLE CAN BE FURTHER
C            SCALED BY FACTOR=CONST*10.**NSCAL.  THE LAST CATEGORY
C            (NOCAT=7) IS NOT REALLY UNLIMITED.  SCALE IT ALSO
C            BETWEEN 6.05 MI AND AN ARBITRAY NUMBER.
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            XDATA(K) = THE DATA TO SCALE (K=1,NVAL).  (INPUT-OUTPUT)
C                NVAL = THE NUMBER OF VALUES IN XDATA( ) BEING DEALT
C                       WITH.  (INPUT)
C                 TLO = LOW THRESHOLD.  WHEN A LAST PASS GRIDPOINT IS
C                       LT TLOD, IT IS SET TO SETLOD, THEN CONST
C                       AND NSCAL APPLIED.  (INPUT)
C               SETLO = SEE TLOD.  (INPUT)
C                 THI = HIGH THRESHOLD.  WHEN A LAST PASS  GRIDPOINT IS
C                       GT THID, IT IS SET TO SETHID, THEN CONST
C                       AND NSCAL APPLIED.  (INPUT)
C               SETHI = SEE THID.  (INPUT)
C               CONST = ADDITIVE CONSTANT TO FURNISH TO THRESHOLDING
C                       AND SCALING SUBROUTINE.  (INPUT)
C               NSCAL = SCALING CONSTANT TO FURNISH TO THRESHOLDING
C                       AND SCALING SUBROUTINE.  (INPUT)
C                 EX1 = EXTRA PARAMETER NOT YET USED FOR THRESHOLDING.
C                       (INPUT)
C                 EX2 = EXTRA PARAMETER NOT YET USED FOR THRESHOLDING.
C                       (INPUT)
C                 IER = ERROR RETURN.
C                       0 = GOOD RETURN.
C                       (OUTPUT)
C          TABLE(J,M) = HOLDS THE LOWER AND UPPER CATEGORY BREAKPOINTS
C                       FOR THE NOCAT CATEGORIES OF VISIBILITY
C                       (M=1,NOCAT), (J=1,2).  (INTERNAL)
C               NOCAT = THE NUMBER OF VISIBILITY CATEGORIES FORECAST
C                       BY LAMP.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      PARAMETER(NOCAT=7)
C
      DIMENSION XDATA(NVAL)
      DIMENSION TABLE(2,NOCAT)
C
      DATA TABLE/-.5,    .495,
     2           .495,   .95,
     3           .95,   1.95,
     4          1.95,   2.95,
     5          2.95,   5.05,
     6          5.05,   6.05,
     7          6.05,  30.0/
C        THSES TABLED VALUES BEING OFF-DIGIT BY .05 MAY SLIGHTLY
C        OFFSET VALUES IN MI IN THE HUNDREDTHS PLACE.  ROUNDED
C        TO TENTHS IS OK.  THIS IS CLOSE ENOUGH.
C
CD     CALL TIMPR(KFILDO,KFILDO,'START VISMI         ')
      IER=0
C
      WRITE(KFILDO,102)TLO,SETLO,THI,SETHI,CONST,NSCAL,EX1,EX2
 102  FORMAT(/' AT 102 IN VISMI--TLO,SETLO,THI,SETHI,CONST,NSCAL,',
     1        'EX1,EX2',5F10.4,I4,2F10.4)
C
CCCD     WRITE(KFILDO,105)(XDATA(K),K=1,NVAL)
CCCD105  FORMAT(/,' IN VISMI AT 105--XDATA(K)',/,(15F8.2))
C
      FACTOR=CONST*10.**NSCAL
      SETLOF=SETLO*FACTOR
      SETHIF=SETHI*FACTOR
C
      IF(TLO.LE.-99999.5.AND.
     1   THI.GE.+99998.5)GO TO 125
C
C         TRUNCATE AT HIGH AND/OR LOW ENDS.
C
      DO 120 K=1,NVAL
C
      IF(NINT(XDATA(K)).NE.9999)THEN
C
         IF(XDATA(K).LT.TLO)THEN
            XDATA(K)=SETLOF
         ELSEIF(XDATA(K).GT.THI)THEN
            XDATA(K)=SETHIF
         ENDIF
C
      ENDIF
C
 120  CONTINUE
C
C        TRUNCATION HAS BEEN DONE, IF NEEDED.  NOW TURN
C        ANALYZED CATEGORICAL VALUES INTO ACTUAL VISIBILITIES IN MI.
C
 125  DO 160 K=1,NVAL
C
      IF(XDATA(K).GT.9998.5)GO TO 160
C        MISSINGS HAVE BEEN INSERTED WITH CLIPPING FOR THE
C        ARCHIVE.  FOR THE DISPOSABLE, THE BORDERS ARE FIRST
C        GUESS = 0.
C
      J=XDATA(K)+.499
C        THIS TRUNCATES TO THE CATEGORY NUMBER J.  A VALUE OF
C        2.5 TO 3.5 SHOULD GO INTO CATEGORY 3.
C        NOTE THAT J CAN GO OUTSIDE THE RANGE 1 TO NOCAT FOR
C        ANALYZED VALUES.
C
      IF(J.GT.NOCAT)THEN
         XDATA(K)=888.
C           ANY ANALYZED CATEGORICAL GT NOCAT, SET VISIBILITY
C           TO UNLIMITED = 888.  NOTE THAT THIS IS NOT DONE
C           FOR J = NOCAT.
      ELSEIF(J.LT.1)THEN
         XDATA(K)=0.
C           ANY ANALYZED CATEGORICAL VALUE LT 1, SET VISIBILITY TO
C           ZERO MI. 
      ELSE
         
         R=TABLE(2,J)-TABLE(1,J)
C
CD        WRITE(KFILDO,135)J,TABLE(1,J),TABLE(2,J),XDATA(K),R
CD135     FORMAT(' AT 135 IN VISMI--J,TABLE(1,J),TABLE(2,J),',
CD    1          'XDATA(K),R',I4,4F10.3)
C     
         XDATA(K)=(XDATA(K)-(J-.5))*R+TABLE(1,J)
C
CD        WRITE(KFILDO,136)J,XDATA(K),R
CD136     FORMAT(' AT 136 IN VISMI--J,XDATA(K),R',I4,2F10.3)
C
         XDATA(K)=XDATA(K)*FACTOR
      ENDIF
C
 160  CONTINUE
C
CCCD     WRITE(KFILDO,165)(XDATA(K),K=1,NVAL)
CCCD165  FORMAT(/,' IN VISMI AT 165--XDATA(K)',/,(15F8.2))
C
      RETURN
      END
