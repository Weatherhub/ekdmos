      SUBROUTINE ELVAR(KFILDO,CCALL,XP,YP,LTAG,LNDSEA,ELEV,XDATA,NSTA,K,
     1                 R,EVAR,LIO,IER)
C
C        DECEMBER  2007   GLAHN   MDL
C                                 ADAPTED FROM VARI; MADE CHECK ON 
C                                 XDATA( ).GT.999.5; EXCLUDED L=K;
C                                 EVAR SET = 0 WHEN LT 1 STATION 
C        DECEMBER  2007   GLAHN   SET VAR1=9999 WHEN LE 1 STATION
C                                 12/20/07 6:00 A.M.
C        DECEMBER  2007   GLAHN   MODIFIED TESTS FOR LIO ABOVE 
C                                 DISTSQ= (12/26/07)
C*************************NO LONGER USED
C
C        PURPOSE
C            TO COMPUTE A VARIABILITY VARIABLE WITHIN RADIUS R =
C            THE MEAN ABSOLUTE DIFFERENCE BETWEEN ALL STATION
C            ELEVATIONS WITHIN THE RADIUS R AND THE AVERAGE OF THOSE
C            SAME ELEVATIONS.  LIO CAN BE OF TYPE 0 OR 9.
C            FOR TYPE LIO = 0, USE STATION TYPES 0 AND 3;
C                         = 9, USE STATION TYPES 6 AND 9.
C            NOTE:  WHEN THERE IS ONLY ONE STATION OR NONE WITHIN
C                   THE RADIUS, THE RETURNED VALUE VAR1 = 9999.  THIS
C                   IS MODIFIED IN WTHOL2.
C
C        DATA SET USE
C            KFILDO   - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C          INPUT
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C            CCALL(K) = CALL LETTERS OF STATIONS (K=1,NSTA). USED
C                       ONLY FOR DIAGNOSTICS.  (CHARACTER*8)  (INPUT)
C               XP(J) = X-POSITION OF STATION J ON GRID (J=1,NSTA).
C               YP(J) = Y-POSITION OF STATION J ON GRID (J=1,NSTA).
C             LTAG(J) = DON'T USE THIS STATION IF LTAG( ) GT 0
C                       (J=1,NSTA).
C           LNDSEA(J) = LAND/SEA INFLUENCE FLAG FOR EACH STATION
C                       (J=1,ND1).
C                       0 = WILL BE USED FOR ONLY OCEAN WATER (=0)
C                           GRIDPOINTS.
C                       3 = WILL BE USED FOR ONLY INLAND WATER (=3)
C                           GRIDPOINTS.
C                       6 = WILL BE USED FOR BOTH INLAND WATER (=3)
C                           AND LAND (=9) GRIDPOINTS.
C                       9 = WILL BE USED FOR ONLY LAND (=9) GRIDPOINTS.
C             ELEV(K) = ELEVATION OF STATIONS (K=1,NSTA).  (INPUT)
C            XDATA(J) = THE DATA FOR WHICH TO FIND THE VARIABILITY
C                       (J=1,NSTA).
C                NSTA = NUMBER OF STATIONS IN LIST. 
C                   K = POSITION OF STATION IN LIST FOR WHICH THE
C                       VARIABILITY IS DESIRED.
C                   R = RADIUS OVER WHICH TO SEARCH.
C                 LIO = THE VARIABLE TO SPECIFY WHETHER THIS RUN IS
C                       FOR LAND (=9), INLAND WATER (=3), OR 
C                       OCEAN (=0) POINTS. 
C
C          OUTPUT
C                EVAR = THE VARIABILITY VARIABLE (SEE PURPOSE).
C                 IER = STATUS RETURN
C                         0 = GOOD.
C
C          INTERNAL
C              DISTSQ = DISTANCE (IN GRID UNITS) SQUARED BETWEEN
C                       TWO STATIONS.
C             IUSE(J) = SET TO 0 WHEN THE STATION IS NOT USED, AND TO
C                       1 WHEN IT IS (J=1,ND1).  THIS SAVES TESTS IN
C                       THE 2ND LOOP.  (AUTOMATIC)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      CHARACTER*8 CCALL(NSTA)
C
      DIMENSION XP(NSTA),YP(NSTA),LTAG(NSTA),LNDSEA(NSTA),ELEV(NSTA),
     1          XDATA(NSTA)
      DIMENSION IUSE(NSTA)
C        IUSE( ) IS AN AUTOMATIC ARRAY.
C
CD     CALL TIMPR(KFILDO,KFILDO,'START ELVAR          ')
C
      IER=0
      NCOUNT=0
      VAR=0.
      RSQ=R*R+.01
C        THE SMALL CONSTANT IS ADDED TO ASSURE A POINT IS NOT 
C        ELIMINATED BECAUSE OF ROUNDOFF.
      EVAR=0.
C
      DO 162 L=1,NSTA
      IUSE(L)=0
      IF(XDATA(L).GT.9998.5)GO TO 162
C        DO NOT USE STATION WITH MISSING DATA.
C
      IF(L.EQ.K)GO TO 162
C        DO NOT COUNT STATION ITSELF.
C
      IF(ABS(XP(K)-XP(L)).GT.R)GO TO 162
      IF(ABS(YP(K)-YP(L)).GT.R)GO TO 162
C        IN A LONG LIST OF STATIONS, THE ABOVE TWO TESTS SHOULD BE
C        MORE EFFICIENT THAN ALWAYS CALCULATING THE DISTANCE.
C        ALSO, THEY SHOULD RULE OUT MORE THAN THE TWO FOLLOWING
C        TESTS.
      IF(LTAG(L).GT.0)GO TO 162
C
      IF(LIO.EQ.9.AND.LNDSEA(K).LT.6)GO TO 162
      IF(LIO.EQ.0.AND.LNDSEA(K).GT.3)GO TO 162
C        THESE TESTS USE LAND STATIONS OR JOINT LAND/INLAND WATER
C        STATIONS FOR LIO=9 (TO BE USED FOR LAND GRIDPOINTS),
C        AND BOTH OCEAN AND INLAND WATER WHEN LIO = 0 (TO BE USED
C        FOR ALL WATER GRIDPOINTS).
C
C        NOTE THAT L = K IS NOT USED.
C
      DISTSQ=(XP(K)-XP(L))**2+(YP(K)-YP(L))**2
C
      IF(DISTSQ.LT.RSQ)THEN
         VAR=VAR+ELEV(L)
         NCOUNT=NCOUNT+1
         IUSE(L)=1
C           SAVING THIS MAKES TESTING BELOW MORE EFFICIENT.
C
CD        WRITE(KFILDO,150)K,CCALL(K),L,CCALL(L),
CD    1                    XP(K),XP(L),YP(K),YP(L),VAR,NCOUNT
CD150     FORMAT(/,'AT 150 IN ELVAR--K,CCALL(K),L,CCALL(L),',
CD    1            'XP(K),XP(L),YP(K),YP(L),VAR,NCOUNT',
CD    2             I6,2X,A8,I6,2X,A8,5F7.1,I4)
C
      ENDIF
      
 162  CONTINUE
C
      IF(NCOUNT.GT.1)THEN
C           WHEN NCOUNT = 1, THERE WILL BE ZERO VARIABILITY.
C           THIS DOES NOT GIVE A GOOD PICTURE, SO SET TO 9999.
         AVG=VAR/NCOUNT
      ELSE
         WRITE(KFILDO,170)R,CCALL(K)
 170     FORMAT(/,' ####CANNOT FIND TWO STATIONS TO AVERAGE IN ELVAR',
     1            ' WITHIN RADIUS R =',F6.1,' FOR STATION  ',A8,
     2            'SET EVAR=9999.  PROCEEDING.')
         EVAR=9999.
         GO TO 300    
      ENDIF
C
      DO 262 L=1,NSTA
C
      IF(IUSE(L).EQ.1)THEN
         EVAR=EVAR+ABS(ELEV(L)-AVG)
CD        WRITE(KFILDO,260)ELEV(L),AVG,EVAR
CD260     FORMAT(/' AT 260 IN ELVAR--XDATA(L),AVG,EVAR',3F8.1)
      ENDIF
C
 262  CONTINUE
C
      EVAR=EVAR/NCOUNT
C
CD     WRITE(KFILDO,265)EVAR,NCOUNT
CD265  FORMAT(/' AT 265 IN ELVAR--EVAR,NCOUNT',F8.2,I4)
CD     CALL TIMPR(KFILDO,KFILDO,'END   ELVAR          ')
C
 300  RETURN
      END
