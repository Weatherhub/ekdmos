      SUBROUTINE SKYAMT(KFILDO,XDATA,NVAL,TLO,SETLO,THI,SETHI,
     1                  CONST,NSCAL,EX1,EX2,IER)
C
C        MARCH     2008   GLAHN   MDL   MOS-2000
C
C        PURPOSE
C            TO POSTPROCESS A SKY COVER CATEGORY INTO A CONTINUOUS
C            VARIABLE PERCENT OF COVER.  ALL VALUES IN XDATA( ) 
C            LT TLO ARE SET TO SETLO AND ALL VALUES GT THI
C            TO SETHI.  EX1 AND EX2 ARE FOR POSSIBLE FUTURE USE.
C            THE VARIABLE IN XDATA( ) IS SCALED*CONST*10**NSCAL
C            BEFORE RETURNING.
C
C            THERE ARE 5 CUMULATIVE SKY CATEGORIES.  THESE MEAN:
C               CAT.    OCTAS     PERCENT   PLAIN LANGUEAGE
C               1          0            0      CLEAR
C               2       LE 2       LE  25      FEW
C               3       LE 4       LE  50      SCATTERED
C               4       LE 7       LT 100      BROKEN
C               5          8          100      OVERCAST
C           USE TLO = 1.5,  SETLO = 0
C               THI = 92.5, SETHI = 100
C           THSES VALUES CAN BE ADJUSTED FOR DESIRABLE DISTRIBUTION.
C           THE 92.5 VALUE CAME FROM SCLSKY USED FOR DISCRETE MOS
C           CATEGORIES ANALYZED WITH PROBABILITIES.
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            XDATA(K) = THE DATA TO SCALE (K=1,NVAL).  (INPUT-OUTPUT)
C                NVAL = THE NUMBER OF VALUES IN XDATA( ) BEING DEALT
C                       WITH.  (INPUT)
C                 TLO = LOW THRESHOLD.  WHEN A VALUE IN XDATA( ) IS
C                       LT TLOD, IT IS SET TO SETLOD.  (INPUT)
C               SETLO = SEE TLOD.  (INPUT)
C                 THI = HIGH THRESHOLD.  WHEN A VALUE IN XDATA( ) IS
C                       GT THID, IT IS SET TO SETHID.  (INPUT)
C               SETHI = SEE THID.  (INPUT)
C               CONST = CONSTANT TO FURNISH TO IN SCALING (SEE
C                       NSCAL BELOW).  (INPUT)
C               NSCAL = SCALING CONSTANT.  SCALING OF XDATA( ) IS
C                       XDATA( ) = XDATA( )*CONST*10**NSCAL.  (INPUT)
C                 EX1 = EXTRA PARAMETER NOT YET USED IN SKYAMT.
C                       (INPUT)
C                 EX2 = EXTRA PARAMETER NOT YET USED IN SKYAMT.
C                       (INPUT)
C                 IER = ERROR RETURN.
C                       0 = GOOD RETURN.
C                       (OUTPUT)
C          TABLE(J,M) = HOLDS THE LOWER AND UPPER CATEGORY BREAKPOINTS
C                       FOR THE NOCAT CATEGORIES OF SKY COVER
C                       (M=1,NOCAT), (J=1,2).  (INTERNAL)
C               NOCAT = THE NUMBER OF SKY COVER CATEGORIES FORECAST
C                       BY LAMP.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      PARAMETER(NOCAT=5)
C
      DIMENSION XDATA(NVAL)
      DIMENSION TABLE(2,NOCAT)
C
      DATA TABLE/   .0,   .0,
     2              .0,  2.5,
     3             2.5,  5.0,
     4             5.0,  9.25,
     5             9.25,10.0/
C        THE CATEGORY BREAKPOINTS ARE IN TERMS OF TENTHS OF COVER.
C        THEY WILL BE SCALED BY 10 WITH NSCAL.
C
CD     CALL TIMPR(KFILDO,KFILDO,'START SKYAMT        ')
      IER=0
C
CD     WRITE(KFILDO,102)TLO,SETLO,THI,SETHI,CONST,NSCAL,EX1,EX2
CD102  FORMAT(/' AT 102 IN SKYAMT--TLO,SETLO,THI,SETHI,CONST,NSCAL,',
CD    1        'EX1,EX2',5F10.4,I4,2F10.4)
C
CCCCD     WRITE(KFILDO,105)(XDATA(K),K=1,NVAL)
CCCCD105  FORMAT(/,' IN SKYAMT AT 105--XDATA(K)',/,(15F8.2))
C
      FACTOR=CONST*10.**NSCAL
C
      IF(TLO.LE.-99999.5.AND.
     1   THI.GE.+99998.5)GO TO 125
C
C         TRUNCATE AT HIGH AND/OR LOW ENDS.
C
      DO 120 K=1,NVAL
C
      IF(XDATA(K).LT.9998.5)THEN
C
         IF(XDATA(K).LT.TLO)THEN
            XDATA(K)=SETLO
         ELSEIF(XDATA(K).GT.THI)THEN
            XDATA(K)=SETHI
         ENDIF
C
      ENDIF
C
 120  CONTINUE
C
C        TRUNCATION HAS BEEN DONE, IF NEEDED.  NOW TURN
C        ANALYZED CATEGORICAL VALUES INTO ACTUAL HEIGHTS IN FEET.
C
 125  DO 160 K=1,NVAL
C
      IF(XDATA(K).GT.9998.5)GO TO 160
C        MISSINGS HAVE BEEN INSERTED WITH CLIPPING FOR THE
C        ARCHIVE.  FOR THE DISPOSABLE, THE BORDERS ARE FIRST
C        GUESS = 0.
C
      J=XDATA(K)+.499
C        THIS TRUNCATES TO THE CATEGORY NUMBER J.  A VALUE OF
C        2.5 TO 3.5 SHOULD GO INTO CATEGORY 3.
C        NOTE THAT J CAN GO OUTSIDE THE RANGE 1 TO NOCAT FOR
C        ANALYZED VALUES.
C
      IF(J.GE.NOCAT)THEN
         XDATA(K)=100.
C           THE LAST CATEGORY IS UPTO 100 PERCENT OF COVER.
C           NOTE THAT IT DOESN'T GET MULTIPLIED BY FACTOR BELOW.
      ELSEIF(J.LT.1)THEN
         XDATA(K)=0.
C           ANY ANALYZED CATEGORICAL VALUE LT 1, SET COVERAGE TO
C           ZERO PERCENT.
      ELSE
         
         R=TABLE(2,J)-TABLE(1,J)
C
CD        WRITE(KFILDO,135)J,TABLE(1,J),TABLE(2,J),XDATA(K),R
CD135     FORMAT(' AT 135 IN SKYAMT--J,TABLE(1,J),TABLE(2,J),',
CD    1          'XDATA(K),R',I4,4F10.3)
C     
         XDATA(K)=(XDATA(K)-(J-.5))*R+TABLE(1,J)
         IF(XDATA(K).GT.10.)XDATA(K)=10.
C           THE CAP IS AT 10 TENTHS.
C
CD        WRITE(KFILDO,136)J,XDATA(K),R
CD136     FORMAT(' AT 136 IN SKYAMT--J,XDATA(K),R',I4,2F10.3)
C
         XDATA(K)=XDATA(K)*FACTOR
      ENDIF
C
 160  CONTINUE
C
CCCCD     WRITE(KFILDO,165)(XDATA(K),K=1,NVAL)
CCCCD165  FORMAT(/,' IN SKYAMT AT 165--XDATA(K)',/,(15F8.2))
C
      RETURN
      END
