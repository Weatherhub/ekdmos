      SUBROUTINE SMOTHC(KFILDO,P,Q,FD2,NX,NY,BQ,NCOUNT,MESH)
C
C        MAY    1993   GLAHN, CHAMBERS   TDL   HP9000
C        MARCH  2001   GLAHN   ADDED NCOUNT( , )
C        MARCH  2001   GLAHN   ADDED NO SMOOTHING OF CENTERS
C        MARCH  2001   GLAHN   CHANGED INDICES L TO IX AND J TO JY;
C                              CHANGED ALGORITHM; ADDED MESH
C        APRIL  2001   GLAHN   ADDED FD2( , )
C        APRIL  2001   GLAHN   TWO GO TO 140'S REMOVED; TEST FOR
C                              FD2( , ) EQ 9999 BEFORE MODIFYING FOR
C                              Y GRADIENT BELOW 135
C        APRIL  2001   GLAHN   CHANGED ALGORITHM SO THAT NO SMOOTHING
C                              BECAUSE OF A POINT OR ITS NEIGHBORS
C                              NOT BEING CHANGED OVERRIDES A PARTIAL
C                              SMOOTHING OF ADJACENT GRADIENT POINTS
C        MAY    2001   GLAHN   CHANGES TO TESTS IN SMOOTHING ALGORITHM
C        MAY    2001   GLAHN   REARRANGED AND ADDED DIAGONAL
C                              COMPUTATIONS
C        MAY    2001   GLAHN   ADDED JFIRST
C        AUGUST 2001   GLAHN   MODIFIED BORDERS AND CORNERS CALCULATION
C                              TO MATCH SMOTHM; MODIFIED COMMENTS;
C                              ADDED BQ4; DELETED BP1; ADDED STOP ON
C                              UNEXPECTED ERROR AT 273
C        JUNE   2002   GLAHN   ADDED "SLP" TO PURPOSE
C        DEC.   2002   RUDACK  MODIFIED FORMAT STATEMENTS TO ADHERE
C                              TO THE F90 COMPILER STANDARDS FOUND ON 
C                              THE IBM SYSTEM
C        OCT.   2012   ENGLE   MODIFIED FORMAT STATEMENT
C                              101 FOR INTEL COMPILER
C
C        PURPOSE
C            TO SMOOTH SLP FIELD P( , ).  THIS VERSION DOES NOT MODIFY
C            A GRID POINT IF IT IS LOWER THAN THE POINTS ABOVE
C            AND BELOW WITHIN 240 KM OR IF IT IS LOWER THAN THE
C            SIDE POINTS WITHIN 240 KM.  THIS WILL GENERALLY WORK FOR
c            MESH LENGTHS UP TO 160 KM.  THE MINIMUM GRADIENT FROM THE 
C            POINT 240 KM AWAY AND THE CENTER POINT IS USED AND MUST
C            BE GE TGRAD MB/KM (CURRENTLY SET AT .012 MB/KM).  AN
C            ADDITIONAL CRITERION IS THAT THE CENTER POINT PRESSURE
C            BE LE 1013 MB.
C
C            NOTE:  THIS WAS WRITTEN FOR LAMP IN WHICH MESH IS THE
C                   "NOMINAL" MESH LENGTH, WHICH IS APPROXIMATELY
C                   RIGHT AT MID US LATITUDES AND IS IN INCREMENTS
C                   OF 80.  THE MESH LENGTH AT THE LATITUDE SPECIFIED
C                   IN TDLPACK MAY BE DIFFERENT, SO IF SMOTHC IS USED
C                   DIRECTLY OUTSIDE OF LAMP, SOME ADJUSTMENT MAY
C                   NEED TO BE MADE.  FOR INSTANCE, THE ROUTINE 
C                   FRONTV IN U201LIB HAS AN ADJUSTMENT.
C
C        DATA SET USE 
C            KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT) 
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C            P(IX,JY) = FIELD TO SMOOTH (IX=1,NX) (JY=1,NY).
C                       (INPUT-OUTPUT)
C            Q(IX,JY) = WORK ARRAY (IX=1,NX) (JY=1,NY).  (INTERNAL)
C          FD2(IX,JY) = THE ARRAY TO INDICATE SMOOTHING.  THIS 
C                       IS USED AS A SEPARATE WORK ARRAY TO Q( , )
C                       TO BRING BACK THE INDICATORS FOR WRITING
C                       TO A GRIDPOINT FILE.  THE VARIABLES NEEDED
C                       FOR WRITING ARE IN THE CALLING PROGRAM.
C              NX, NY = DIMENSIONS OF P( , ) AND Q( , ).  (INPUT)
C                  BQ = SMOOTHING PARAMETER.  SMOOTHED VALUE AT POINT P
C                       = ORIGINAL VALUE PLUS BQ/4 TIMES SUM OF
C                       SURROUNDING 4 POINTS, ALL DIVIDED BY BQ+1.
C                       A MODIFIED PROCEDURE IS USED ON THE BORDERS AND
C                       CORNERS THAT ASSUMES A BQ OF 4 INDICATES A MEAN
C                       OF ALL VALUES INVOLVED.  THESE CALCULATIONS ARE
C                       CONSISTENT WITH SMOTHM WHICH DEALS WITH MISSING
C                       VALUES.  THE EQUATIONS USED ARE THE SAME AS:
C                          Q(IX,JY)=(P(IX,JY)+(SUM/ISUM)*(ISUM/4)*BQ)/
C                             ((ISUM/4)*BQ+1.)
C                       WHERE SUM IS THE SUM OF THE ISUM VALUES 
C                       (2 FOR CORNERS AND 3 FOR BORDERS). 
C       NCOUNT(IX,JY) = COUNT OF CORRECTIONS MADE TO POINT IX,JY
C                       (IX=1,NX) (JY=1,NY).  SMOOTHING IS NOT DONE
C                       TO A POINT UNLESS ONE OF THE POINTS INVOLVED 
C                       IN THE SMOOTHING HAD A CORRECTION (NCOUNT
C                       NE 0).  (INPUT)
C                MESH = MESH LENGTH OF GRID IN P( , ).  (INPUT)
C                   F = FACTOR BY WHICH BQ IS REDUCED FOR POINTS 
C                       ADJACENT TO MINIMUM THAT IS NOT SMOOTHED.
C                       (INTERNAL)
C               TGRAD = THE GRADIENT IN MB THAT MUST BE MET FOR THE
C                       MINIMUM PRESERVING FEATURE TO BE USED.
C              MAXGPT = THE NUMBER OF GRID LENGTHS TO GO TO FIND THE
C                       GRADIENT = 240/MESH.  (INTERNAL)
C                 BQF = THE SMOOTHING PARAMETER FOR THE "ADJACENT"
C                       POINTS.
C              JFIRST = CONTROLS PRINTING OF F AND TGRAD ONLY ONCE PER
C                       RUN.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES CALLED
C            PRTGR
C
      CHARACTER*40 TITLE
C
      DIMENSION P(NX,NY),Q(NX,NY),FD2(NX,NY),NCOUNT(NX,NY)
      DIMENSION IOPT(8)
      
      DATA IP22/22/
      DATA TITLE/' '/
      DATA JFIRST/0/
C
      IOPT(1)=1
      IOPT(2)=1
      IOPT(3)=NX
      IOPT(4)=1
      IOPT(5)=NY
      IOPT(6)=1
      IOPT(7)=0
      IOPT(8)=0
C
      IF(BQ.EQ.0.)GO TO 280
C        BQ = 0 MEANS NO SMOOTHING.
      IF(MESH.GT.160)THEN
         WRITE(KFILDO,100)MESH
 100     FORMAT(/'****MESH =',I8,' IN SMOTHC TOO LARGE.',
     1           '  STOP IN SMOTHC AT 100.')
         STOP 100
      ENDIF
C
      F=.15
C        F = 0 MEANS THE ADJACENT POINTS WILL NOT BE SMOOTHED.
C        F = 1 MEANS THE ADJACENT POINTS WILL BE SMOOTHED NORMALLY.
C        INTERMEDIATE VALUES ARE POSSIBLE FOR A LIGHT SMOOTHING
C        ON ADJACENT POINTS.
      TGRAD=.012
C        TGRAD IS IN MB PER KM.
C
      IF(JFIRST.EQ.0)THEN
         WRITE(KFILDO,101)F,TGRAD
 101     FORMAT(/' FOR SMOOTHING, ADJACENT POINTS WILL BE SMOOTHED',
     1           F5.2,' OF THE FULL AMOUNT IN SMOTHC.'/
     2           ' THE THRESHOLD GRADIENT FOR CORRECTION IS',F6.3,
     3           ' MB PER KM.')
         JFIRST=1
      ENDIF
C
      BQF=BQ*F
C        BQF IS THE SMOOTHING PARAMETER FOR THE "ADJACENT" POINTS.
      MAXGPT=240/MESH
C        MAXGPT = THE NUMBER OF GRID LENGTHS TO GO TO FIND THE GRADIENT.
      BQ4=BQ/4.
      NXM1=NX-1
      NYM1=NY-1
C
C        INITIALIZE FD2( , ).
C
      DO 105 JY=1,NY
      DO 104 IX=1,NX
         FD2(IX,JY)=9999.
 104  CONTINUE
 105  CONTINUE
C
C        SMOOTH ALL EXCEPT OUTER ROWS AND COLUMNS.
C
      DO 200 JY=2,NYM1
      DO 199 IX=2,NXM1
      IF(P(IX,JY).GT.1013.)GO TO 199
C        NO SPECIAL SMOOTHING ARRANGEMENTS ARE MADE UNLESS THE
C        PRESSURE IS 1013 MB OR BELOW.
C
C***********************************************************************
C
C        VERTICAL ORIENTATION OF ISOBARS
C
C***********************************************************************
C
C        OBTAIN THE MIN OF THE TWO ENDPOINTS ALONG JY'TH ROW
C        240 KM FROM THE POINT BEING PROCESSED.  THIS
C        WILL ALWAYS GET A MIN UNLESS THE GRID IS UNREALISTICALLY
C        SMALL.  IF THE GRIDPOINT IS LT 240 KM FROM AN EDGE,
C        THE OPPOSITE DIRECTION IS USED FOR THE MIN.
C       
      ENDVAL=9999999.
C      
      IF(IX-MAXGPT.GE.1)THEN
         ENDVAL=P(IX-MAXGPT,JY)
      ENDIF
C
      IF(IX+MAXGPT.LE.NX)THEN
         ENDVAL=MIN(ENDVAL,P(IX+MAXGPT,JY))
      ENDIF    
C      
      IF(ENDVAL.EQ.9999999.)THEN
         WRITE(KFILDO,110)NX,NY,IX,JY
 110     FORMAT(/' ****GRID UNREALISTICALLY SMALL.',
     1           '  NX =',I5,'    NY =',I5,
     2           '  PROCESSING POINT IX,JY =',2I5/
     3           '     STOP AT 110 IN SMOTHC')
         STOP 110
      ENDIF
C
      RANGE=ENDVAL-P(IX,JY)
      GRAD=RANGE/240.
C
      IF(GRAD.GT.TGRAD)THEN
C           THE RANGE IS ALWAYS OVER 240 KM.
C
C           THIS LOOP USED WHEN THE ANALYSIS LINES ARE ORIENTED MORE
C           VERTICALLY THAN HORIZONTALLY.  THEREFORE, LOOK ACROSS
C           THE X AXIS FOR MINIMA TO NOT SMOOTH.
C
         DO 130 M=-MAXGPT,+MAXGPT
            IF(IX+M.LT.1.OR.IX+M.GT.NX)GO TO 130
            IF(P(IX+M,JY).LT.P(IX,JY))GO TO 132
C              POINT NOT A MINIMUM IN X DIRECTION, CHECK Y DIRECTION.
 130     CONTINUE
C    
C           DROP THROUGH HERE MEANS THE POINT WAS A MINIMUM IN THE 
C           X DIRECTION AND THE GRADIENT MET THE CRITERIA IN THAT
C           DIRECTION.
C 
CD        WRITE(KFILDO,131)IX,JY,GRAD,TGRAD,P(IX,JY)
CD131     FORMAT(/' NO SMOOTHING IN X DIRECTION--',
CD    1           'IX,JY,GRAD,TGRAD,P(IX,JY)',2I5,3F10.3)
         FD2(IX,JY)=0.
C           SETTING FD2( , ) = 0 MEANS THIS POINT WILL NOT BE SMOOTHED.
C
         IF(FD2(IX-1,JY).NE.0.)THEN
            IF(FD2(IX-1,JY).EQ.9999..OR.FD2(IX-1,JY).EQ.BQ)
     1         FD2(IX-1,JY)=BQF
         ENDIF
C
         IF(FD2(IX+1,JY).NE.0.)THEN
            IF(FD2(IX+1,JY).EQ.9999..OR.FD2(IX+1,JY).EQ.BQ)
     1         FD2(IX+1,JY)=BQF
         ENDIF
C
C           THIS WILL SMOOTH THE ADJOINING POINTS IN THE X DIRECTION
C           BY F TIMES THE NORMAL AMOUNT.  THESE POINTS COULD HAVE
c           ALREADY BEEN MODIFIED; HENCE THE TESTS.
C
C           TREAT THE OUTER COLUMNS THE SAME AS THE PENULTIMATE COLUMNS.
C
         IF(JY.EQ.2)THEN
            FD2(IX,1)=FD2(IX,2)
         ELSEIF(JY.EQ.NY-1)THEN
            FD2(IX,NY)=FD2(IX,NY-1)
         ENDIF
C
      ENDIF
C
C***********************************************************************
C
C        HORIZONTAL ORIENTATION OF ISOBARS
C
C***********************************************************************
C
C        OBTAIN THE MIN OF THE TWO ENDPOINTS ALONG IX'TH ROW
C        240 KM FROM THE POINT BEING PROCESSED.  THIS
C        WILL ALWAYS GET A MIN UNLESS THE GRID IS UNREALISTICALLY
C        SMALL.  IF THE GRIDPOINT IS LT 240 KM FROM AN EDGE,
C        THE OPPOSITE DIRECTION IS USED FOR THE MIN.
C       
 132  ENDVAL=9999999.
C
      IF(JY-MAXGPT.GE.1)THEN
         ENDVAL=P(IX,JY-MAXGPT)
      ENDIF
C
      IF(JY+MAXGPT.LE.NY)THEN
         ENDVAL=MIN(ENDVAL,P(IX,JY+MAXGPT))
      ENDIF    
C
      IF(ENDVAL.EQ.9999999.)THEN
         WRITE(KFILDO,133)NX,NY,IX,JY
 133     FORMAT(/' ****GRID UNREALISTICALLY SMALL.',
     1           '  NX =',I5,'    NY =',I5,
     2           '  PROCESSING POINT IX,JY =',2I5/
     3           '     STOP AT 133 IN SMOTHC')
         STOP 133
      ENDIF
C
      RANGE=ENDVAL-P(IX,JY)
      GRAD=RANGE/240.
C     
      IF(GRAD.GT.TGRAD)THEN
C           THE RANGE IS ALWAYS OVER 240 KM.
C
C           THIS LOOP USED WHEN THE ANALYSIS LINES ARE ORIENTED MORE
C           HORIZONTALLY THAN VERTICALLY.  THEREFORE, LOOK ALONG
C           THE Y AXIS FOR MINIMA TO NOT SMOOTH.
C
         DO 135 M=-MAXGPT,+MAXGPT
            IF(JY+M.LT.1.OR.JY+M.GT.NY)GO TO 135
            IF(P(IX,JY+M).LT.P(IX,JY))GO TO 142
C              POINT NOT A MINIMUM.
 135     CONTINUE
C    
C           DROP THROUGH HERE MEANS THE POINT WAS A MINIMUM IN THE 
C           Y DIRECTION AND THE GRADIENT MET THE CRITERIA IN THAT
C           DIRECTION.
C      
CD        WRITE(KFILDO,136)IX,JY,GRAD,TGRAD,P(IX,JY)
CD136     FORMAT(/' NO SMOOTHING IN Y DIRECTION--',
CD    1           'IX,JY,GRAD,TGRAD,P(IX,JY)',2I5,3F10.3)
         FD2(IX,JY)=0.
C           SETTING FD2( , ) = 0 MEANS THIS POINT WILL NOT BE SMOOTHED.
C
         IF(FD2(IX,JY-1).NE.0.)THEN
            IF(FD2(IX,JY-1).EQ.9999..OR.FD2(IX,JY-1).EQ.BQ)
     1         FD2(IX,JY-1)=BQF
         ENDIF
C
         IF(FD2(IX,JY+1).NE.0.)THEN
            IF(FD2(IX,JY+1).EQ.9999..OR.FD2(IX,JY+1).EQ.BQ)
     1         FD2(IX,JY+1)=BQF
         ENDIF
C
C           THIS WILL SMOOTH THE ADJOINING POINTS IN THE Y DIRECTION
C           BY F TIMES THE NORMAL AMOUNT.  THESE POINTS COULD HAVE
c           ALREADY BEEN MODIFIED; HENCE THE TESTS.
C
C           TREAT THE OUTER ROWS THE SAME AS THE PENULTIMATE ROW.
C
         IF(IX.EQ.2)THEN
            FD2(1,JY)=FD2(2,JY)
         ELSEIF(IX.EQ.NX-1)THEN
            FD2(NX,JY)=FD2(NX-1,JY)
         ENDIF
C
      ENDIF
C
C***********************************************************************
C
C        DIAGONAL ORIENTATION OF ISOBARS, UPPER LEFT TO LOWER RIGHT
C
C***********************************************************************
C
C        OBTAIN THE MIN OF THE TWO ENDPOINTS ALONG A DIAGONAL
C        240 KM FROM THE POINT BEING PROCESSED.  THE DIAGONAL IS
C        FROM THE LOWER LEFT TO THE UPPER RIGHT.  THIS
C        WILL ALWAYS GET A MIN UNLESS THE GRID IS UNREALISTICALLY
C        SMALL.  IF THE GRIDPOINT IS LT 240 KM FROM AN EDGE,
C        THE OPPOSITE DIRECTION IS USED FOR THE MIN.  FOR A DIAGONAL,
C        IN ORDER TO LOOK THE SAME DISTANCE AS ALONG A ROW OR COLUMN,
C        THE END POINT VALUES MUST BE INTERPOLATED FROM P(IX+LX,JY+LY),
C        WHERE LX = LY AND THE DISTANCE FOR EITHER INDIVIDUALLY IS MAXGPT 
C        DIVIDED BY THE SQUARE ROOT OF 2.
C    
 142  XLX=MAXGPT/1.444
      LX=XLX
C        LX IS THE TRUNCATED GRIDPOINT DISTANCE ALONG THE DIAGONAL.
C        THE INTERPOLATED VALUE (IN THE UPPER RIGHT QUADRANT) WILL BE 
C        BETWEEN P(IX+LX+1,JY+LX+1) AND P(IX+LX,JY+LX).      
      ENDVAL=9999999.
C      
      IF(IX-MAXGPT.GE.1.AND.
     1   JY-MAXGPT.GE.1)THEN
C        USE SAME DISTANCE MAXGPT FOR CHECKING WHETHER THE POINT IS
C        INSIDE THE GRID.
      
         ENDVAL=(P(IX-LX-1,JY-LX-1)-P(IX-LX,JY-LX))*(XLX-LX)+
     1                      P(IX-LX,JY-LX)
C           ENDVAL IS THE END POINT ALONG THE DIAGONAL AT A DISTANCE OF
C           MAXGPT.  BECAUSE THIS IS A DIAGONAL, INTERPOLATION IS 
C           SIMPLIFIED.  (MAXGPT/2)-LX IS THE FRACTIONAL DISTANCE THE
C           SECOND POINT (FARTHER OUT POINT) IS FROM THE FIRST POINT.
      ENDIF
C
      IF(IX+MAXGPT.LE.NX.AND.
     1   JY+MAXGPT.LE.NY)THEN
         ENDVAL=MIN(ENDVAL,(P(IX+LX+1,JY+LX+1)-P(IX+LX,JY+LX))*(XLX-LX)+
     1                      P(IX+LX,JY+LX))
      ENDIF    
C      
      IF(ENDVAL.EQ.9999999.)THEN
CD        WRITE(KFILDO,143)NX,NY,IX,JY
CD143     FORMAT(/' CANNOT PROCESS CORNER POINT AT 143.'
CD    1           '  NX =',I5,'    NY =',I5,
CD    2           '  PROCESSING POINT IX,JY =',2I5)
         GO TO 152
      ENDIF
C
      RANGE=ENDVAL-P(IX,JY)
      GRAD=RANGE/240.
C     
      IF(GRAD.GT.TGRAD)THEN
C           THE RANGE IS ALWAYS OVER 240 KM.
C
C           THIS LOOP USED WHEN THE ANALYSIS LINES ARE ORIENTED MORE
C           DIAGONALLY FROM UPPER LEFT TO LOWER RIGHT.
C
         DO 145 M=-LX,+LX
            IF(IX+M.LT.1.OR.IX+M.GT.NX)GO TO 145
            IF(JY+M.LT.1.OR.JY+M.GT.NY)GO TO 145
            IF(P(IX+M,JY+M).LT.P(IX,JY))GO TO 152
C              POINT NOT A MINIMUM.
C              NOTE THAT THIS CHECKING IS FOR SPECIFIC GRIDPOINTS
C              ON THE DIAGONAL, NOT INTERPOLATED VALUES.            
 145     CONTINUE
C    
C           DROP THROUGH HERE MEANS THE POINT WAS A MINIMUM ALONG 
C           DIAGONAL AND THE GRADIENT MET THE CRITERIA IN THAT
C           DIRECTION.
C      
CD        WRITE(KFILDO,146)IX,JY,GRAD,TGRAD,P(IX,JY)
CD146     FORMAT(/' NO SMOOTHING IN DIAGONAL LL TO UR DIRECTION--',
CD    1           'IX,JY,GRAD,TGRAD,P(IX,JY)',2I5,3F10.3)
         FD2(IX,JY)=0.
C           SETTING FD2( , ) = 0 MEANS THIS POINT WILL NOT BE SMOOTHED.
C
         IF(FD2(IX-1,JY-1).NE.0.)THEN
            IF(FD2(IX-1,JY-1).EQ.9999..OR.FD2(IX-1,JY-1).EQ.BQ)
     1                FD2(IX-1,JY-1)=BQF
         ENDIF
C
         IF(FD2(IX+1,JY+1).NE.0.)THEN
            IF(FD2(IX+1,JY+1).EQ.9999..OR.FD2(IX+1,JY+1).EQ.BQ)
     1                FD2(IX+1,JY+1)=BQF
         ENDIF
C
C           THIS WILL SMOOTH THE ADJOINING POINTS ALONG THE DIAGONAL
C           BY F TIMES THE NORMAL AMOUNT.  THESE POINTS COULD HAVE
C           ALREADY BEEN MODIFIED; HENCE THE TESTS.
C
C           TREAT THE OUTER ROWS AND COLUMNS THE SAME AS THE
C           PENULTIMATE ONES.
C
         IF(IX.EQ.2)THEN
            FD2(1,JY)=FD2(2,JY)
         ELSEIF(IX.EQ.NX-1)THEN
            FD2(NX,JY)=FD2(NX-1,JY)
         ENDIF
C
         IF(JY.EQ.2)THEN
            FD2(IX,1)=FD2(IX,2)
         ELSEIF(JY.EQ.NY-1)THEN
            FD2(IX,NY)=FD2(IX,NY-1)
         ENDIF
C    
      ENDIF
C
C***********************************************************************
C
C        DIAGONAL ORIENTATION OF ISOBARS, LOWER LEFT TO UPPER RIGHT
C
C***********************************************************************
C
C        OBTAIN THE MIN OF THE TWO ENDPOINTS ALONG A DIAGONAL
C        240 KM FROM THE POINT BEING PROCESSED.  THE DIAGONAL IS
C        FROM THE UPPER LEFT TO THE LOWER RIGHT.  THIS
C        WILL ALWAYS GET A MIN UNLESS THE GRID IS UNREALISTICALLY
C        SMALL.  IF THE GRIDPOINT IS LT 240 KM FROM AN EDGE,
C        THE OPPOSITE DIRECTION IS USED FOR THE MIN.  FOR A DIAGONAL,
C        IN ORDER TO LOOK THE SAME DISTANCE AS ALONG A ROW OR COLUMN,
C        THE END POINT VALUES MUST BE INTERPOLATED FROM P(IX+LX,JY+LY),
C        WHERE LX = -LY AND THE DISTANCE FOR EITHER INDIVIDUALLY IS MAXGPT 
C        DIVIDED BY THE SQUARE ROOT OF 2.
C    
 152  LX=MAXGPT/1.444
C        LX IS THE TRUNCATED GRIDPOINT DISTANCE ALONG THE DIAGONAL.
C        THE INTERPOLATED VALUE (IN THE UPPER LEFT QUADRANT) WILL BE 
C        BETWEEN P(IX-LX-1,JY+LX+1) AND P(IX-LX,JY+LX).      
      ENDVAL=9999999.
C      
      IF(IX-MAXGPT.GE.1.AND.
     1   JY+MAXGPT.LE.NY)THEN
C        USE SAME DISTANCE MAXGPT FOR CHECKING WHETHER THE POINT IS
C        INSIDE THE GRID.
      
         ENDVAL=(P(IX-LX-1,JY+LX+1)-P(IX-LX,JY+LX))*(XLX-LX)+
     1           P(IX-LX,JY+LX)
C           ENDVAL IS THE END POINT ALONG THE DIAGONAL AT A DISTANCE OF
C           MAXGPT.  BECAUSE THIS IS A DIAGONAL, INTERPOLATION IS 
C           SIMPLIFIED.  (MAXGPT/2)-LX IS THE FRACTIONAL DISTANCE THE
C           SECOND POINT (FARTHER OUT POINT) IS FROM THE FIRST POINT.
      ENDIF
C
      IF(IX+MAXGPT.LE.NX.AND.
     1   JY-MAXGPT.GE.1)THEN
         ENDVAL=MIN(ENDVAL,(P(IX+LX+1,JY-LX-1)-P(IX+LX,JY-LX))*
     1                        (XLX-LX)+P(IX+LX,JY-LX))
      ENDIF    
C      
      IF(ENDVAL.EQ.9999999.)THEN
CD        WRITE(KFILDO,153)NX,NY,IX,JY
CD153     FORMAT(/' CANNOT PROCESS CORNER POINT AT 153.'
CD    1           '  NX =',I5,'    NY =',I5,
CD    2           '  PROCESSING POINT IX,JY =',2I5)
         GO TO 199
      ENDIF
C
      RANGE=ENDVAL-P(IX,JY)
      GRAD=RANGE/240.
C     
      IF(GRAD.GT.TGRAD)THEN
C           THE RANGE IS ALWAYS OVER 240 KM.
C
C           THIS LOOP USED WHEN THE ANALYSIS LINES ARE ORIENTED MORE
C           DIAGONALLY FROM UPPER LEFT TO LOWER RIGHT.
C
         DO 155 M=-LX,+LX
            IF(IX+M.LT.1.OR.IX+M.GT.NX)GO TO 155
            IF(JY+M.LT.1.OR.JY+M.GT.NY)GO TO 155
            IF(P(IX-M,JY+M).LT.P(IX,JY))GO TO 199
C              POINT NOT A MINIMUM.
C              NOTE THAT THIS CHECKING IS FOR SPECIFIC GRIDPOINTS
C              ON THE DIAGONAL, NOT INTERPOLATED VALUES.            
 155     CONTINUE
C    
C           DROP THROUGH HERE MEANS THE POINT WAS A MINIMUM ALONG 
C           DIAGONAL AND THE GRADIENT MET THE CRITERIA IN THAT
C           DIRECTION.
C      
CD        WRITE(KFILDO,156)IX,JY,GRAD,TGRAD,P(IX,JY)
CD156     FORMAT(/' NO SMOOTHING IN DIAGONAL UL TO LR DIRECTION--',
CD    1           'IX,JY,GRAD,TGRAD,P(IX,JY)',2I5,3F10.3)
         FD2(IX,JY)=0.
C           SETTING FD2( , ) = 0 MEANS THIS POINT WILL NOT BE SMOOTHED.
C
         IF(FD2(IX-1,JY+1).NE.0.)THEN
            IF(FD2(IX-1,JY+1).EQ.9999..OR.FD2(IX-1,JY+1).EQ.BQ)
     1                FD2(IX-1,JY+1)=BQF
         ENDIF
C
         IF(FD2(IX+1,JY-1).NE.0.)THEN
            IF(FD2(IX+1,JY-1).EQ.9999..OR.FD2(IX+1,JY-1).EQ.BQ)
     1                FD2(IX+1,JY-1)=BQF
         ENDIF
C
C           THIS WILL SMOOTH THE ADJOINING POINTS ALONG THE DIAGONAL
C           BY F TIMES THE NORMAL AMOUNT.  NOTE THAT THE ADJACENT POINTS
C           CAN'T HAVE BEEN A MINIMUM.  THESE POINTS COULD HAVE ALREADY
C           BEEN MODIFIED; HENCE THE TESTS.
C
C           TREAT THE OUTER ROWS AND COLUMNS THE SAME AS THE
C           PENULTIMATE ONES.
C
         IF(IX.EQ.2)THEN
            FD2(1,JY)=FD2(2,JY)
         ELSEIF(IX.EQ.NX-1)THEN
            FD2(NX,JY)=FD2(NX-1,JY)
         ENDIF
C
         IF(JY.EQ.2)THEN
            FD2(IX,1)=FD2(IX,2)
         ELSEIF(JY.EQ.NY-1)THEN
            FD2(IX,NY)=FD2(IX,NY-1)
         ENDIF
C    
      ENDIF
C      
 199  CONTINUE
 200  CONTINUE
C
C        BEFORE SMOOTHING CHECK TO SEE IF THE POINT OR ITS IMMEDIATE
C        NEIGHBORS HAVE BEEN CHANGED ON THIS PASS.  NO SMOOTHING
C        DUE TO NO CHANGE OVERRIDES PARTIAL SMOOTHING DUE TO
C        POINTS BEING NEIGHBORS TO A MINIMUM POINT.
C
      DO 245 JY=2,NYM1
      DO 244 IX=2,NXM1
      IF(FD2(IX,JY).NE.0.)THEN
C        A POINT NOT BEING SMOOTHED BECAUSE OF GRADIENT IS LEFT
C        INTACT.
C
         IF(NCOUNT(IX,JY).NE.0.OR.
     1      NCOUNT(IX-1,JY).NE.0.OR.
     2      NCOUNT(IX+1,JY).NE.0.OR.
     3      NCOUNT(IX,JY-1).NE.0.OR.
     4      NCOUNT(IX,JY+1).NE.0)THEN
C                 ONE OR MORE OF THE POINTS INVOLVED IN THE SMOOTHING
C                 HAVE BEEN CHANGED.     
               IF(FD2(IX,JY).EQ.9999.)FD2(IX,JY)=BQ
C                 SMOOTH THIS POINT WHEN IT HAS NOT ALREADY BEEN SET.               
         ELSE
               IF(FD2(IX,JY).EQ.9999..OR.
     1            FD2(IX,JY).EQ.BQF)FD2(IX,JY)=-BQ
C                    DO NOT SMOOTH THIS POINT.  THIS IS SET NEGATIVE
C                    ONLY FOR THE PRTGR BELOW AND CONTOURING IN GEMPAK.
C                    NOTE THAT -BQ OVERRIDES BQF.
         ENDIF
C    
      ENDIF
C        
 244  CONTINUE
 245  CONTINUE
C
C        SMOOTH POINTS AS INDICATED BY THE BQ VALUE IN FD2(IX,JY)
C
      DO 255 JY=2,NYM1
      DO 250 IX=2,NXM1
C
      IF(FD2(IX,JY).LE.0.)THEN
         Q(IX,JY)=P(IX,JY)
      ELSE
         Q(IX,JY)=(P(IX,JY)+(FD2(IX,JY)/4.)*
     1            (P(IX-1,JY)+P(IX+1,JY)+P(IX,JY-1)+P(IX,JY+1)))/
     2            (FD2(IX,JY)+1.)
      ENDIF
C
 250  CONTINUE
 255  CONTINUE
C
C        SMOOTH BOTTOM AND TOP ROWS.
C
      DO 260 IX=2,NXM1
C
      IF(FD2(IX,1).EQ.9999.)THEN
         FD2(IX,1)=FD2(IX,2)
      ENDIF
C
      IF(FD2(IX,1).LE.0.)THEN
         Q(IX,1)=P(IX,1)
C           THIS PRESERVES THE NO SMOOTHING IF SO INDICATED.
         GO TO 257
      ENDIF
C
      IF(NCOUNT(IX,1).NE.0.OR.
     1   NCOUNT(IX-1,1).NE.0.OR.
     2   NCOUNT(IX+1,1).NE.0.OR.
     3   NCOUNT(IX,2).NE.0)THEN
           Q(IX,1)=(P(IX,1)+BQ4*(P(IX-1,1)+P(IX+1,1)+P(IX,2)))/
     1        (.75*BQ+1.)
      ELSE
           Q(IX,1)=P(IX,1)
      ENDIF
C
 257  IF(FD2(IX,NY).EQ.9999.)THEN
         FD2(IX,NY)=FD2(IX,NY-1)
      ENDIF
C
      IF(FD2(IX,NY).LE.0.)THEN
         Q(IX,NY)=P(IX,NY)
C           THIS PRESERVES THE NO SMOOTHING IF SO INDICATED.
         GO TO 260
      ENDIF
C
      IF(NCOUNT(IX,NY).NE.0.OR.
     1   NCOUNT(IX-1,NY).NE.0.OR.
     2   NCOUNT(IX+1,NY).NE.0.OR.
     3   NCOUNT(IX,NYM1).NE.0)THEN
           Q(IX,NY)=(P(IX,NY)+BQ4*
     1              (P(IX-1,NY)+P(IX+1,NY)+P(IX,NYM1)))/
     1        (.75*BQ+1.)
      ELSE
           Q(IX,NY)=P(IX,NY)
      ENDIF
C
 260  CONTINUE
C
C        SMOOTH 1ST AND LAST COLUMNS.
C
      DO 268 JY=2,NYM1
C
      IF(FD2(1,JY).EQ.9999.)THEN
         FD2(1,JY)=FD2(2,JY)
      ENDIF
C
      IF(FD2(1,JY).LE.0.)THEN
         Q(1,JY)=P(1,JY)
C           THIS PRESERVES THE NO SMOOTHING IF SO INDICATED.
         GO TO 267
      ENDIF
C
      IF(NCOUNT(1,JY).NE.0.OR.
     1   NCOUNT(1,JY-1).NE.0.OR.
     2   NCOUNT(1,JY+1).NE.0.OR.
     3   NCOUNT(2,JY).NE.0)THEN
           Q(1,JY)=(P(1,JY)+BQ4*(P(1,JY-1)+P(1,JY+1)+P(2,JY)))/
     1        (.75*BQ+1.)
      ELSE
           Q(1,JY)=P(1,JY)
      ENDIF
C
 267  IF(FD2(NX,JY).EQ.9999.)THEN
         FD2(NX,JY)=FD2(NX-1,JY)
      ENDIF
C
      IF(FD2(NX,JY).LE.0.)THEN
         Q(NX,JY)=P(NX,JY)
C           THIS PRESERVES THE NO SMOOTHING IF SO INDICATED.
         GO TO 268
      ENDIF
C
      IF(NCOUNT(NX,JY).NE.0.OR.
     1   NCOUNT(NX,JY-1).NE.0.OR.
     2   NCOUNT(NX,JY+1).NE.0.OR.
     3   NCOUNT(NXM1,JY).NE.0)THEN
           Q(NX,JY)=(P(NX,JY)+BQ4*
     1              (P(NX,JY-1)+P(NX,JY+1)+P(NXM1,JY)))/
     1        (.75*BQ+1.)
      ELSE
           Q(NX,JY)=P(NX,JY)
      ENDIF
C
 268  CONTINUE
C
C        MAKE SURE CORNERS OF FD2( , ) ARE NOT MISSING.
C
      IF(FD2(1,1).EQ.9999.)FD2(1,1)=FD2(1,2)
      IF(FD2(1,NY).EQ.9999.)FD2(1,NY)=FD2(2,NY)
      IF(FD2(NX,1).EQ.9999.)FD2(NX,1)=FD2(NX,2)
      IF(FD2(NX,NY).EQ.9999.)FD2(NX,NY)=FD2(NX,NY-1)
C
C        SMOOTH 4 CORNER POINTS.  THE MINIMUM PRESERVING FEATURE
C        IS NOT OPERATIVE HERE.
C
      IF(NCOUNT(1,1).NE.0.OR.
     1   NCOUNT(2,1).NE.0.OR.
     2   NCOUNT(1,2).NE.0)THEN
           Q(1,1)=(P(1,1)+BQ4*(P(2,1)+P(1,2)))/
     1        (.5*BQ+1.)
      ELSE
           Q(1,1)=P(1,1)
      ENDIF
C
      IF(NCOUNT(NX,1).NE.0.OR.
     1   NCOUNT(NXM1,1).NE.0.OR.
     2   NCOUNT(NX,2).NE.0)THEN
           Q(NX,1)=(P(NX,1)+BQ4*(P(NXM1,1)+P(NX,2)))/
     1        (.5*BQ+1.)
      ELSE
           Q(NX,1)=P(NX,1)
      ENDIF
C
      IF(NCOUNT(1,NY).NE.0.OR.
     1   NCOUNT(2,NY).NE.0.OR.
     2   NCOUNT(1,NYM1).NE.0)THEN
           Q(1,NY)=(P(1,NY)+BQ4*(P(2,NY)+P(1,NYM1)))/
     1        (.5*BQ+1.)
      ELSE
           Q(1,NY)=P(1,NY)
      ENDIF
C
      IF(NCOUNT(NX,NY).NE.0.OR.
     1   NCOUNT(NXM1,NY).NE.0.OR.
     2   NCOUNT(NX,NYM1).NE.0)THEN
           Q(NX,NY)=(P(NX,NY)+BQ4*(P(NXM1,NY)+P(NX,NYM1)))/
     1        (.5*BQ+1.)
      ELSE
           Q(NX,NY)=P(NX,NY)
      ENDIF
C
C        MAKE SURE A CORNER POINT IN Q( , ) IS NOT 9999.
C
      IF(Q(1,1).EQ.9999.)Q(1,1)=Q(1,2)
      IF(Q(1,NY).EQ.9999.)Q(1,NY)=Q(1,NY-1)
      IF(Q(NX,1).EQ.9999.)Q(NX,1)=Q(NX,2)
      IF(Q(NX,NY).EQ.9999.)Q(NX,NY)=Q(NX,NY-1)      
C
C        TRANSFER SMOOTHED FIELD IN Q( , ) BACK TO P( , ).
C
      DO 270 JY=1,NY
      DO 269 IX=1,NX
         P(IX,JY)=Q(IX,JY)
 269  CONTINUE
 270  CONTINUE
C
C        TRANSFORM FD2( , ) FOR GEMPAK CONTOURING.
C        
      DO 275 JY=1,NY
      DO 274 IX=1,NX
      IF(FD2(IX,JY).EQ.-BQ)GO TO 274
      IF(FD2(IX,JY).EQ.+BQ)GO TO 274
C
      IF(FD2(IX,JY).EQ.0.)THEN
         FD2(IX,JY)=-1.
      ELSEIF(FD2(IX,JY).EQ.BQF)THEN
         FD2(IX,JY)=+1.
      ELSE
         WRITE(KFILDO,273)FD2(IX,JY),IX,JY
 273     FORMAT(/' ****ERROR--FD2(IX,JY)=',F10.2,
     1           ' FOR NX,NY =',2I5,' IN SMOTHC.'/
     2           '     STOP AT 273.')
         STOP 273
      ENDIF
C
 274  CONTINUE
 275  CONTINUE
C
CD     TITLE(17:24)='Q-GRID  '
CD     CALL PRTGR(IP22,FD2,NX,NY,
CD    1           2.,0.,1.,0.,IOPT,TITLE,IER)
C
 280  RETURN
      END
