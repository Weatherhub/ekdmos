      SUBROUTINE CLOS(XP,YP,LTAG,NSTA,K,LS1,LS2)
C
C        MAY 1993   GLAHN   TDL   HP9000
C
C        PURPOSE
C           TO FIND 2 CLOSEST NEIGHBORS OF STATION K FOR
C            V400A AND V400B.  CALLED FROM ESP AND ESPW.
C
C        DATA SET USE
C           NONE.
C
C        VARIABLES
C          INPUT
C               XP(J) = X-POSITION OF STATION J ON GRID (J=1,NSTA).
C               YP(J) = Y-POSITION OF STATION J ON GRID (J=1,NSTA).
C             LTAG(J) = DON'T USE THIS STATION IF LTAG( ) GT 0
C                       (J=1,NSTA).
C                NSTA = NUMBER OF STATIONS IN LIST.
C                   K = POSITION OF STATION IN LIST FOR WHICH CLOSEST
C                       STATIONS ARE NEEDED.
C
C          OUTPUT
C                 LS1 = POSITION IN LIST OF CLOSEST STATION.
C                 LS2 = POSITION IN LIST OF 2ND CLOSEST STATION.
C
C          INTERNAL
C                SAV1 = DISTANCE TO CLOSEST STATION.
C                SAV2 = DISTANCE TO 2ND CLOSEST STATION.
C              DISTSQ = DISTANCE (IN GRID UNITS) SQUARED BETWEEN
C                       TWO STATIONS.
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION XP(NSTA),YP(NSTA),LTAG(NSTA)
C
      SAV1=999999.
      SAV2=999999.
C
      DO 162 L=1,NSTA
      IF(LTAG(L).GT.0)GO TO 162
      IF(K.EQ.L)GO TO 162
      DISTSQ=(XP(K)-XP(L))**2+(YP(K)-YP(L))**2
      IF(SAV2.LE.DISTSQ)GO TO 162
      IF(SAV1.GT.DISTSQ)GO TO 1611
      SAV2=DISTSQ
      LS2=L
      GO TO 162
C
 1611 SAV2=SAV1
      SAV1=DISTSQ
      LS2=LS1
      LS1=L
 162  CONTINUE
C
      RETURN
      END
