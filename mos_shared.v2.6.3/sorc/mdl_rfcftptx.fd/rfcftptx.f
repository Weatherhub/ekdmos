C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***                             
C                                                                       
C MAIN PROGRAM: RFCFTPTX     GENERATES THE RFCMXMN TX BULLETIN
C   PRGMMR: COSGROVE        ORG: OST22      DATE: 2002-04-30
C                       
C                                                                       
C ABSTRACT:  GENERATES THE RFCMXMN BULLETIN CONTAINING  
C   MRF MEDIUM RANGE MOS GUIDANCE FOR THE 0000 UTC CYCLE, AND
C   A COMBINATION OF AVN SHORT RANGE AND MRF MEDIUM RANGE MOS
C   GUIDANCE FOR THE 1200 UTC CYCLE FOR STATIONS IN MAIN U.S.  
C   THE FORECAST ELEMENTS IN THE MESSAGE ARE MAX/MIN TEMPS, 
C   GIVEN EVERY 12 HOURS FROM 24 TO 282.  NOTE THAT THE FORECASTS
C   FOR THESE BULLETINS MAY BE TAKEN FROM UP TO FOUR FILES: THE
C   MRF 1430 MOS FILES, AVN 1430 MOS FILES, THE MRF RFC MOS FILES,
C   AND THE AVN RFC MOS FILES 
C
C PROGRAM HISTORY LOG:                                                  
C   01-03-06  ALLEN    CONVERTED MOS1974 RFCMXMN CODE TO RUN ON THE
C                      NEW RFC MOS2000 GUIDANCE.
C   01-05-07  ALLEN    MODIFIED WRMSG_RFC TO UP THE MAXIMUM SIZE OF 
C                      THE BULLETIN TO 14080 (ROUGHLY 15000)
C   01-05-10  ALLEN    MODIFIED CODE TO ADD PROJECTIONS 216 TO 264.
C                      ALSO MODIFIED WRMSG_RFC TO WRITE OUT THE AWIPS
C                      HEADER FOR BULLETINS THAT WERE BROKEN INTO PARTS.
C                      I HAD TO ADD A VARIABLE TO THE CALL TO WRMSG_RFC.
C   02-01-22  RLC      MODIFIED CODE SO TO INCREASE THE NUMBER OF STATIONS
C                      THAT IT CAN READ FROM THE RANDOM ACCESS MOS FILES
C                      TO ACCOMMODATE THE 1430
C   02-04-09  COSGROVE MODIFIED CODE TO ADD PROJECTION 282 FOR 1081/1430 MOS
C                      SITES. 
C   03-04-16  COSGROVE INCREASED MAXSTA TO 3500 TO ACCOMMODATE NEW MARINE SITES
C                      AND THE SITES WE ARE ADDING IN THE FALL
C   03-05-29  COSGROVE INCREASED NOCHAR TO MAKE THE LENGTH OF THE LINE
C                      LONG ENOUGH TO HOLD 3 DIGIT MAX, 2 DIGIT MIN
C   03-07-15  COSGROVE AS PART OF THE GFS TRANSITION, CHANGED MODEL
C                      NUMBER 9'S TO 8'S
C   04-05-07  COSGROVE INCREASED THE NUMBER OF STATIONS BECAUSE IT NOW READS
C                      THE COOP/RFC DICTIONARY WITH 15000 STATIONS
C   04-08-11  COSGROVE CHANGED THE HEADERS FROM A MIX OF TAIFA- AND TAIFB- TO
C                      ALL SAY TAIFB- PER THE NWRFC'S REQUEST.
C   05-01-27  COSGROVE INCREASED NUMBER OF STATIONS TO 12,000.  CHANGED
C                      MAX/MIN IDS TO BE MERGED IDS.
C   05-08-18  COSGROVE WE ARE TRANSITIONING FROM SENDING THE PRODUCT 
C                      VIA THE STATFILE TO VIA NTC.  CHANGES NEED TO BE
C                      MADE.  A NEW VERSION OF PUTQ IS USED, WRMSG_RFC 
C                      WAS REDONE.  NO CHANGES WERE MADE TO THIS CODE THOUGH.
C   06-04-19  COSGROVE WE'RE ADDING A PRODUCT FOR THE ALASKA RFC.  THEY DO
C                      NOT NEED THE 4-DIGIT IDS STRIPPED TO 3-DIGIT.  CHANGED
C                      THE TEST AROUND 91 TO ONLY STRIP THE FIRST DIGIT IF
C                      IT'S A CONUS-4-DIGIT CALL LETTER
C   06-11-29  COSGROVE THE LINE LENGTH OF 85 IS NOT LONG ENOUGH TO HANDLE WHEN
C                      AK HAS -XX TEMPS FOR MANY OR ALL THE PERIODS.  INCREASED
C                      NOCHAR TO 95.
C   15-02-04  SCALLION INCREASED MAXSTA TO 48000.
C                                                                       
C USAGE:                                                                
C                                                                       
C        DATA SET USE             
C        INPUT FILES:
C             FORT.5  - STANDARD INPUT - JUNIT
C             FORT.6  - STANDARD OUTPUT - IOUT
C             FORT.10 - NMC STANDARD DATE FILE - IUNIT 
C             FORT.27 - MDL STATION DICTIONARY - IDUNIT
C             FORT.35 - AVN MOS 2000 FCST FILE - IAUNIT 
C             FORT.36 - MRF MOS 2000 FCST FILE - IMUNIT 
C             FORT.37 - RFC AVN MOS 2000 FCST FILE - IRAUNIT 
C             FORT.38 - RFC MRF MOS 2000 FCST FILE - IRMUNIT 
C
C        OUTPUT FILES:
C             FORT.60 - ALPHANUMERIC MESSAGE - NUNIT
C             FORT.65 - TRANSMISSION FILE - NOUT 
C
C        VARIABLES
C              AWIPPIL=REGIONAL BULLETIN HEADER (AWIPS PIL) THAT
C                      IS SENT IN TO WRMSG_RFC
C                BFILE=OUTPUT FILE NAME CONTAINING THE PRINTED BULLETIN
C                      CONTROL CHARACTERS FOR AFOS TRANSMISSION 
C               BULHDR=REGIONAL BULLETIN HEADER (EX._NMCFTPRSA)
C                      CHARACTER *10, READ IN FROM STATION CALL
C                      LETTER (INPUT)
C               CBULHD=BULLETIN HEADER  (CHAR*11)
C              CALLOUT=LIST OF CALL LETTERS AS THEY WILL BE PUT OUT
C                      IN THE MESSAGE.  WE MUST STRIP OFF THE LEADING
C                      K OR C ON THE PRIMARY STATIONS.
C                CCALL=LIST OF CALL LETTERS AFTER THE DICTIONARY
C                      LINKS HAVE BEEN CHECKED.
C                CHAR4=FOUR CHARACTER ARRAY USED TO HOLD 4 DIGIT INTEGER
C                CHAR6=SIX CHARACTER ARRAY USED TO HOLD 6 DIGIT INTEGER
C             COL_SAVE=AN ARRAY OF COLUMN COUNTERS FOR STATION CALL
C                      LETTERS ONLY (MAXSTA) (INITIALIZED TO COLUMN 7. 
C               DDHHMM=DAY, HOUR AND MINUTE (061200)
C              FCST( )=FORECASTS FROM MOS 2000 FORECAST FILES
C               IAUNIT=UNIT NUMBER FOR THE AVN MOS2000 FORECAST FILE
C                 ICYC=TIME CYCLE IS 00Z=1 12Z=2
C                ID(4)=4 WORD VARIABLE IDS FOR FORECASTS
C               IDUNIT=UNIT NUMBER FOR THE STATION DICTIONARY
C                 IERR=ERROR RETURN CODE.
C                   IK=STATION CALL LETTER COUNTER FOR EACH BULLETIN
C               IMUNIT=UNIT NUMBER FOR THE MRF MOS2000 FORECAST FILE
C                  INC=TIME INCREMENT
C                 IRFC=MAXIMUM NUMBER OF RFC BULLETINS (15) 
C              IRAUNIT=UNIT NUMBER FOR THE RFC AVN MOS2000 FORECAST FILE
C              IRMUNIT=UNIT NUMBER FOR THE RFC MRF MOS2000 FORECAST FILE
C                IUNIT=FILE NUMBER(10) USED FOR THE NMC DATE FILE
C                   KC=COUNTER FOR ARRAY KOUNT (UP TO NMOSTA)
C           KOUNT( , )=INITIALLY SET TO ZERO, INCREASED BY 1   
C                      WHERE A FORECAST IS ENTERED IN THAT LINE
C                      1ST DIMENSION = LINE      
C                      2ND DIMENSION = GREATER THAN OR EQUAL TO NMOSTA
C          KOUNT_WRM()=SPECIAL VERSION OF THE ARRAY "KOUNT" ARRAY USED 
C                      IN WRMSG_RFC. THIS ARRAY IS ACCOUNTS FOR
C                      THE EXTRA RECORD ".END" WHICH OCCURS AT THE  
C                      END OF EACH REGIONAL LIST OF STATIONS
C                KUNIT=HOLDER FOR THE FORECAST FILE UNIT NUMBER SENT
C                      INTO READ_MOSDA
C               LBULHD=NUMBER OF CHARACTERS IN BULLETIN HEADER
C                LDATE=CURRENT OPERATIONAL DATE IN TDL FORMAT (NGM)
C                 LINE=NUMBER OF LINES REQUIRED PER STATION
C                      OF BULLETIN (=1)
C                 LIST=LIST OF NMOSTA CALL LETTERS OF STATIONS,
C                      LEFT JUSTIFIED (8 CHARACTERS)
C               LLDATE=MRF OPERATIONAL DATE 
C                      NOTE: IF THE HOUR PORTION OF LDATE IS 12 
C                      THEN LLDATE IS EQUAL TO LDATE MINUS 12.
C               MAXSTA=MAXIMUM NUMBER OF STATIONS(ACTUALLY 2* THE NUMBER
C                      OF SITES IN THE DICTIONARY)
C                  MDG=CURRENT DAY, 2 DIGITS
C                  MHG=CURRENT HOUR, 2 DIGITS
C                 MM00=ARRAY USED TO HOLD THE 1430 IDENTIFIERS FOR THE   
C                      MAX/MIN TEMPERATURE FORECAST-FOR 00Z CYCLE.
C              MMRFC00=ARRAY USED TO HOLD THE RFC IDENTIFIERS FOR THE   
C                      MAX/MIN TEMPERATURE FORECAST-FOR 00Z CYCLE.
C                 MM12=SAME AS MM00, BUT FOR THE 12Z CYCLE
C              MMRFC12=SAME AS RFCMM00, BUT FOR THE 12Z CYCLE
C               MMDDHH=MONTH, DAY AND HOUR (120612)
C                 MMDD=MONTH AND DAY PLUS 24 HOURS
C                  MOG=CURRENT MONTH, 2 DIGITS
C             MSG( , )=AREA INTO WHICH TELETYPE MESSAGE IS BUILT  
C                      LAST DIMENSION=NOLINE SHOULD BE GE 
C                      LINE*NMOSTA+(NHEAD+NTAIL)*NHEAD
C                  MYG=CURRENT YEAR, 2 DIGITS
C                 MYGA=LAST TWO DIGITS OF CURRENT YEAR (FOR 
C                      DISPLAY PURPOSES) GOOD FOR YEAR 2000 AND
C                      BEYOND.
C                NBLAK=BLANK CHARACTER
C                NBSTA=BEGINNING STATION NUMBER (USED AFTER COMPLETION
C                 NCAT=CHARACTER*5 CATELOG NUMBER
C                  ND5=DIMENSION OF ARRAYS IN READ_MOSDA
C                 NDIG=NUMBER OF DIGITS (USED BY PUTMOS)
C                NESTA=ENDING STATION NUMBER (USED AFTER COMPLETION
C                      OF BULLETIN)
C                NHEAD=NUMBER OF WMO STATION CALL LEADER HEADERS
C               NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS PREPARED
C                      RETURNED FROM RDWMO
C               NOCHAR=1ST DIMENSION OF MSG( , )-MAX NUMBER OF CHARACTER
C               NOLINE=2ND DIMENSION OF MSG( , )-MAX NUMBER OF LINES 
C               NOHEAD=NUMBER OF LINES IN THE HEADER OF THE BULLETIN
C                      INCLUDING BLANK LINES. (NOHEAD=6)
C                 NOUT=FILE NUMBER(60) FOR PRINTED BULLETIN.
C                 NPOS=RECORDS TO SKIP TO START NEXT BULLETIN
C                NTAIL=NUMBER OF LINES IN THE TAIL OF THE BULLETIN, 
C                      INCLUDING BLANK LINES. (NTAIL=1)
C                NTEMP=NUMBER OF STATIONS RUN (USUALLY EQUAL TO NMOSTA)
C                NUNIT=FILE NUMBER(65) USED IN WRMSG_RFC FOR
C                      THE TRANSMISSION FILE OF THE BULLETINS.
C                 NWMO=NUMBER OF STATIONS IN WMO CALL LETTER ARRAY,
C                       WMO(I)
C                NWRMB=BEGINNING STATION NUMBER FOR KOUNT_WRM ARRAY
C                NWRME=ENDING STATION NUMBER FOR KOUNT_WRM ARRAY
C                  WMO=LIST OF WMO HEADERS(CHARACTER *8)
C                                                                       
C
C        SUBPROGRAMS CALLED:                                                 
C          UNIQUE: RDLNK,RDWMO(CALLS RDLSTA),PRMSGA,WRMSG_RFC
C
C          LIBRARY:                                                          
C             TDLLIB90: PUTMOS,PUTCHAR,RDLSTA
C             MDLLIB90: GET_NCEPDATE,UPDAT,READ_MOSDA
C                W3LIB: W3TAGB,W3TAGE
C
C        EXIT STATES: (STOP OCCURS IN MAIN UNLESS OTHERWISE STATED)
C              3 = EITHER INCORRECT UNIT NUMBER OR STATION RECORD
C                  COULD NOT BE READ IN READ_MOSDA
C             15 = UNABLE TO READ THE STANDARD NCEP DATE FILE   
C             30 = NUMBER OF STATIONS IN LIST ARE EXCEEDED (STOP
C                  OCCURS IN RDWMO)
C            115 = OUTPUT FILE IN WRMSG_RFC CANNOT BE OPENED (STOP 
C                  OCCURS IN SUBROUTINE WRMSG_RFC)
C                                                                       
C REMARKS:                                                              
C     24000 STATIONS MAX (MAXSTA IS # STAS * 2).  SUBROUTINES PRMSG AND WRMSG ARE 
C     CUSTOMIZED FOR RFCMXMN                                               
C                                                                    
C                                                                    
C ATTRIBUTES:                                                        
C   LANGUAGE: FORTRAN 90                                             
C   MACHINE:  IBM SP 
C$$$

      PROGRAM RFCMXMN

C
C        SET UNIT NUMBERS FOR INPUT/OUTPUT FILES
C
      PARAMETER(JUNIT=5,IOUT=6,IUNIT=10,IAUNIT=35,IMUNIT=36,
     *         IDUNIT=27,IRAUNIT=37,IRMUNIT=38,NUNIT=60,NOUT=65)

      PARAMETER(NOCHAR=95,MAXSTA=48000,LINE=1,IRFC=15)
      PARAMETER(NOHEAD=6,NTAIL=1,ND7=54)

      INTEGER NDATE,NSTA,KUNIT,KOUNT(LINE,MAXSTA),DDHHMM,
     * MMDDHH,MMDD,MM00(22),MM12(21),MMRFC00(22),MMRFC12(21),
     * ID(4),COL_SAVE(MAXSTA),INC,NWMO(MAXSTA),LLDATE,
     * NMOSTA,LDATE,IERR,MDG,MHG,MOG,MYG,NHEAD,
     * LBULHD,NESTA,NBSTA,ICYC,NPOS,NDIG,
     * KOUNT_WRM(LINE,MAXSTA),NWRME,NWRMB,INDEX(MAXSTA,15),
     * IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)

      REAL FCST(MAXSTA),DATAA(MAXSTA),DATAB(MAXSTA)

      CHARACTER*1 MSG(NOCHAR,MAXSTA*LINE+((NTAIL+NOHEAD)*IRFC)),
     *            NBLAK
      CHARACTER*4 CHAR4
      CHARACTER*5 NCAT
      CHARACTER*6 CHAR6
      CHARACTER*8 LIST(MAXSTA),CALLOUT(MAXSTA),CCALL(MAXSTA,6)
      CHARACTER*10 BULHDR(IRFC),AWIPPIL
      CHARACTER*11 CBULHD,WMO(MAXSTA)
      CHARACTER*80 BFILE

      DATA LBULHD/11/,NBLAK/' '/,NESTA/0/,NBSTA/0/,NPOS/0/
      DATA NCAT/'00000'/
      DATA CHAR4/'    '/,CHAR6/'      '/,BFILE/'       '/
C
C        HERE ARE THE PROJECTIONS (NOTE THAT IN MOS2000, THE MAX/MIN
C        PROJECTIONS ARE 6 HOURS AHEAD OF THEIR VALID TIME.  IE. 
C        THE 24-HR MAX HAS A TAU OF 30):
C          AS OF APR 2002 WE ADDED PROJ 282 FOR THE 1081/1430 MOS2000 SITES.
C
C        MM00 30,42,54,66,78,90,102,114,126,138,150,162,174,186,198,
C             210,222,234,246,258,270,282  MRF
C        MM12 30,42,54,66,78,(102,114,126,138,150,162,174,186,198,
C              210,222,234,246,258,270,282) AVN (MRF)
      DATA MM00/202140008,202240008,202140008,202240008,202140008,
     *          202240008,202140008,202240008,202140008,202240008,
     *          202140008,202240008,202140008,202240008,202140008,
     *          202240008,202140008,202240008,202140008,202240008,
     *          202140008,202240008/
      DATA MMRFC00/202150008,202250008,202150008,202250008,202150008,
     *             202250008,202150008,202250008,202150008,202250008,
     *             202150008,202250008,202150008,202250008,202150008,
     *             202250008,202150008,202250008,202150008,202250008,
     *             202150008,202250008/
      DATA MM12/202220008,202120008,202220008,202120008,202220008,
     *          202140008,202240008,202140008,202240008,
     *          202140008,202240008,202140008,202240008,202140008,
     *          202240008,202140008,202240008,202140008,202240008,
     *          202140008,202240008/
      DATA MMRFC12/202250008,202150008,202250008,202150008,202250008,
     *             202150008,202250008,202150008,202250008,
     *             202150008,202250008,202150008,202250008,202150008,
     *             202250008,202150008,202250008,202150008,202250008,
     *             202150008,202250008/

      DATA NMOSTA/0/,NDATE/0/,NDIG/0/
      DATA MDATE/0/,LDATE/0/,NHEAD/0/,LLDATE/0/

      CALL W3TAGB('RFCFTPTX ',2001,0261,0069,'OST22 ')
 
      ND5=MAXSTA

C        INITIALIZE ARRAYS
      DO 112 M=1,MAXSTA
	NWMO(M)=0
	COL_SAVE(M)=7
	FCST(M)=0.
      DO 112 K=1,LINE
        KOUNT(K,M)=0
	KOUNT_WRM(K,M)=0
  112 CONTINUE

C        READ BULLETIN HEADERS AND STATION CALL LETTERS                 

      CALL RDWMO(JUNIT,NMOSTA,LIST,WMO,NWMO,NHEAD,MAXSTA,
     *     BULHDR)
      WRITE(6,91)NMOSTA
 91   FORMAT(1X,'NMOSTA=',I10)
      NOLINE=(NMOSTA*LINE)+(NOHEAD+NTAIL)*NHEAD
C
C        DUE TO LIMITATIONS IN AWIPS, WE MUST REMOVE THE LEADING 
C        K AND C FOR ALL FIRST ORDER STATIONS WHEN WE WRITE OUT
C        THE CALL LETTERS IN THE BULLETIN.  THE ARRAY CALLOUT WILL
C        ONLY BE PRINTED OUT IN THE BULLETIN.  WE DON'T STRIP OFF
C        THE P FOR ALASKA SITES.
C         
       DO I=1,NMOSTA
         IF((LIST(I)(5:5).EQ." ").AND.(LIST(I)(4:4).NE." ").
     *      AND.(LIST(I)(1:1).NE."P"))THEN
            CALLOUT(I)=LIST(I)(2:8)
         ELSE
            CALLOUT(I)=LIST(I)
         ENDIF
       ENDDO
C
C        NOW USE RDLNK TO CHECK THE DICTIONARY FOR LINKS TO THE
C        CALL LETTERS IN "LIST".  PUT THE UPDATED LIST IN CCALL(,1)
C        AND USE THIS LIST TO RETRIEVE THE FORECASTS
C
       CALL RDLNK(IDUNIT,6,1,LIST,CCALL,NMOSTA,MAXSTA)
C
C        FILL THE MESSAGE ARRAY WITH BLANKS EXCEPT FOR THE 
C        1ST CHARACTER
      DO 110 M=1,NOLINE
      DO 110 K=1,NOCHAR
        MSG(K,M)=NBLAK
  110 CONTINUE

C        SET THE NUMBER OF STATIONS TO PROCESS;
C        USUALLY WILL BE NMOSTA
      NTEMP=NMOSTA
      WRITE(6,91)NTEMP
      WRITE(6,93)NHEAD
 93   FORMAT(1X,'NHEAD=',I5)
C
C        READ NCEP STANDARD DATE FILE AND PUT IN MDL FORMAT
C        
      CALL GET_NCEPDATE(IUNIT,MYG,MOG,MDG,MHG,LDATE,IERR)
      IF(IERR.NE.0) THEN
      CALL W3TAGE('RFCMXMN ')
        STOP 15 
      ENDIF

C        IF THE CYCLE (MHG OBTAINED FROM LDATE) IS 12
C        CALCULATE THE DATE THE MRF WAS RUN ,LLDATE, BY SIMPLY 
C        SUBTRACTING 12 HOURS FROM LDATE(THE TRUE OPERATIONAL DATE)
      IF(MHG.EQ.12)THEN
	LLDATE=LDATE-12
      ELSE
	LLDATE=LDATE
      ENDIF

C        FIND VALID DATES FOR HEADINGS.
      MDATE=LDATE
      DDHHMM=MDG*10000+MHG*100
      MMDDHH=MOG*10000+MDG*100+MHG
      CALL UPDAT(MDATE,24,MDATE)
      MMDD=MOD(MDATE,1000000)/100
C
C        FOR THE 00Z CYCLE, SET ICYC TO 1.  FOR 12Z, ICYC=2 
C
      ICYC=1
      IF(MHG.NE.0)ICYC=2
C
C        CONVERT INTEGER DATES TO CHARACTER STRINGS 
C        USING INTERNAL FORTRAN WRITES (RIGHT JUSTIFIED)
      WRITE(CHAR4,6000) MMDD
 6000 FORMAT(I4)
      IF(CHAR4(1:1).EQ.' ') CHAR4(1:1)='0'
      WRITE(CHAR6,6001) MMDDHH
 6001 FORMAT(I6)
      IF(CHAR6(1:1).EQ.' ') CHAR6(1:1)='0'

C*****
C*****   START CONSTRUCTION OF THE 
C*****   BULLETIN:
C*****

C        CREATE A SIX LINE HEADER
C        THE WMO HEADER WRITTEN IN SUBROUTINE WRMSG_RFC
C        IS THE FIRST RECORD OF THE HEADER
C        NOTE: HEADERS ARE ONLY GENERATED FOR EACH BULLETIN

      KC=0
      DO 10 I=1,NHEAD
	 NBSTA=NESTA+1
	 NESTA=NESTA+NWMO(I)
	 IF(I.EQ.1) NPOS=2
	 IF(I.GT.1) NPOS=NPOS+NWMO(I-1)+7
C        #2
	 CALL PUTCHAR(BULHDR(I),MSG(1,NPOS),10)
C        #3
         CALL PUTCHAR('.B NMC ',MSG(1,NPOS+1),7)
         CALL PUTCHAR(CHAR4,MSG(8,NPOS+1),4)
         CALL PUTCHAR(' DH12/DC',MSG(12,NPOS+1),8)
	 CALL PUTCHAR(CHAR6,MSG(20,NPOS+1),6)
C
C        PRIOR TO 9/14/04 WE USED TO HAVE A MIX OF FA AND FB HEADERS
C        TO DISTINGUISH BETWEEN AVN AND MRF.  WE CHANGED THIS AT THE
C        NWRFC'S REQUEST, BUT I LEFT THE LOGIC IN IN CASE WE EVER
C        NEEDED IT.  SO IF THE CODE LOOKS A BIT REDUNDANT, IT IS.
C
	 IF(ICYC.EQ.1)THEN
	   CALL PUTCHAR('/TAIFBX/TAIFBN/DRD+1/TAIFBX/TAIFBN',
     *                  MSG(26,NPOS+1),34)
	 ELSE
           CALL PUTCHAR('/TAIFBN/DRD+1/TAIFBX/TAIFBN',MSG(26,NPOS+1),27)
         ENDIF
C        #4
         CALL PUTCHAR('.B1 /DRD+2/',MSG(1,NPOS+2),11)
	 IF(ICYC.EQ.1) CALL PUTCHAR('TAIFBX/TAIFBN',MSG(12,NPOS+2),13)
	 IF(ICYC.EQ.2) CALL PUTCHAR('TAIFBX/TAIFBN',MSG(12,NPOS+2),13)
	 CALL PUTCHAR('/DRD+3/TAIFBX/TAIFBN/DRD+4/TAIFBX/TAIFBN',
     *                MSG(25,NPOS+2),40)
C        #5
	 CALL PUTCHAR('.B2 /DRD+5/TAIFBX/TAIFBN/DRD+6/TAIFBX/TAIFBN',
     *                 MSG(1,NPOS+3),44)
         CALL PUTCHAR('/DRD+7/TAIFBX/TAIFBN',MSG(45,NPOS+3),20)
C        #6
	 CALL PUTCHAR('.B3 /DRD+8/TAIFBX/TAIFBN/DRD+9/TAIFBX/TAIFBN',
     *                 MSG(1,NPOS+4),44)
         CALL PUTCHAR('/DRD+10/TAIFBX/TAIFBN',MSG(45,NPOS+4),21)
C        PLACES STATION IDENTIFIER
         IK=0
         DO 140 K=NBSTA,NESTA
	   IK=IK+1
	   KC=KC+1
           CALL PUTCHAR(CALLOUT(K),MSG(1,NPOS+IK+4),5)

C        EVERY STATION (CALL LETTER) WILL BE LISTED REGARDLESS
C        OF NUMBER OF EITHER AVN OR MRF FORECASTS
           KOUNT(1,KC)=1
 140     CONTINUE
 10   CONTINUE

C        READ IN THE MAX/MIN TEMPERATURE FORECASTS FROM 
C        THEIR RESPECTIVE DATA FILES

C        NOTE:
C        SINCE THE ARRAY "LIST" CONTAINS THE STATION CALL
C        LETTERS FOR ALL NINE BULLETINS, THE 10 (00Z) OR
C        9 (12Z) CALL TO READ_MOSDA ONLY NEEDS TO BE DONE ONCE 
C        
      ID(1)=0
      ID(2)=0
      ID(3)=18
      ID(4)=0
      INC=12
      IF(ICYC.EQ.1)KT=22
      IF(ICYC.EQ.2)KT=21

      DO 170 K=1,KT
C
C         SET THE DATA ARRAYS TO MISSING
C 
       DO 175 I=1,MAXSTA
         DATAA(I)=9999.
         DATAB(I)=9999.
         FCST(I)=9999.
 175   CONTINUE
C
C         FIRST, GO AND RETRIEVE THE DATA FROM THE APPROPRIATE
C         1430 STATION MOS FILE AND PUT IT IN DATAA
C
        IF(KT.EQ.22)ID(1)=MM00(K)
        IF(KT.EQ.21)ID(1)=MM12(K)
        ID(3)=ID(3)+INC
C
C         SET KUNIT TO THE APPROPRIATE RANDOM ACCESS FILE.
C         FOR THE ENTIRE 00Z CYCLE, YOU WANT THE MRF FILE.
C         FOR THE 12Z CYCLE, YOU WANT THE AVN FILE FOR TIME
C         PERIODS 1-5, AND THE MRF FILE FOR 6-9.  ALSO SET
C         THE NDATE VARIABLE ACCORDING TO WHETHER THE FORECAST
C         IS FROM THE AVN(LDATE) OR THE MRF(LLDATE)
C
        SELECT CASE(ICYC)
          CASE(1)
            KUNIT=IMUNIT 
            NDATE=LLDATE
          CASE(2)
            SELECT CASE(K)
              CASE(1:5)
                KUNIT=IAUNIT
                NDATE=LDATE
C
C          FOR THE 6TH PROJECTION OF THE 12Z, WHEN YOU SWITCH 
C          FROM USING AVN TO MRF, YOU HAVE TO 'JUMP' 12 HOURS
C          AHEAD IN ID(3) TO MATCH THE MRF VALID TIME TO THE
C          AVN VALID TIME
C
              CASE(6)
                KUNIT=IMUNIT
                NDATE=LLDATE
                ID(3)=ID(3)+INC
              CASE(7:21)
                KUNIT=IMUNIT
                NDATE=LLDATE
            END SELECT
        END SELECT
C
C        CALL READ_MOSDA TO RETRIEVE THE FORECAST
C
        CALL READ_MOSDA(IOUT,KUNIT,0,ID,NDATE,CCALL,NMOSTA,
     &                  DATAA,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,
     &                  INDEX,IER)
        IF(IER.EQ.3) THEN
          CALL W3TAGE('NEWRFCMXMN')
          STOP 3
        ENDIF
C
C        NOW GO AND RETRIEVE THE DATA FROM THE RFC STATION
C        MOS FILE AND PUT THAT IN DATAB
C
        IF(KT.EQ.22)ID(1)=MMRFC00(K)
        IF(KT.EQ.21)ID(1)=MMRFC12(K)
C
C         SET KUNIT TO THE APPROPRIATE RANDOM ACCESS FILE.
C         FOR THE ENTIRE 00Z CYCLE, YOU WANT THE MRF RFC FILE.
C         FOR THE 12Z CYCLE, YOU WANT THE AVN RFC FILE FOR TIME
C         PERIODS 1-5, AND THE MRF RFC FILE FOR 6-9.  ALSO SET
C         THE NDATE VARIABLE ACCORDING TO WHETHER THE FORECAST
C         IS FROM THE AVN(LDATE) OR THE MRF(LLDATE)
C
        SELECT CASE(ICYC)
          CASE(1)
            KUNIT=IRMUNIT 
            NDATE=LLDATE
          CASE(2)
            SELECT CASE(K)
              CASE(1:5)
                KUNIT=IRAUNIT
                NDATE=LDATE
C
C          FOR THE 6TH PROJECTION OF THE 12Z, YOU ALREADY  
C          'JUMPED' 12 HOURS AHEAD IN THE CASE STATEMENT ABOVE,
C          SO YOU DON'T HAVE TO DO IT AGAIN HERE
C
              CASE(6:21)
                KUNIT=IRMUNIT
                NDATE=LLDATE
            END SELECT
        END SELECT
C
C        CALL READ_MOSDA TO RETRIEVE THE FORECAST
C
        CALL READ_MOSDA(IOUT,KUNIT,0,ID,NDATE,CCALL,NMOSTA,
     &                  DATAB,MAXSTA,ND5,ND7,IS0,IS1,IS2,IS4,
     &                  INDEX,IER)
        IF(IER.EQ.3) THEN
          CALL W3TAGE('NEWRFCMXMN')
          STOP 3
        ENDIF
C
C        NEXT MERGE THE FORECASTS FROM BOTH FILES INTO
C        THE FCST ARRAY
C
        DO I=1,MAXSTA
         IF((NINT(DATAA(I)).NE.9999).AND.(NINT(DATAB(I)).
     &      EQ.9999))THEN
           FCST(I)=DATAA(I)
         ELSEIF((NINT(DATAB(I)).NE.9999).AND.(NINT(DATAA(I)).
     &      EQ.9999))THEN
           FCST(I)=DATAB(I)
         ELSEIF((NINT(DATAA(I)).NE.9999).AND.(NINT(DATAB(I)).
     &      NE.9999))THEN
           WRITE(6,178)LIST(I)
 178       FORMAT(/'THERE IS DATA FOR STATION ',A8,' AVAILABLE',
     &           ' IN BOTH FORECAST FILES.  SETTING FCST TO THE ',
     &           '1430 VALUE.')
           FCST(I)=DATAA(I)
         ELSE
           FCST(I)=9999.
         ENDIF
        ENDDO
C
C        PLACE MIN/MAX FCSTS
C
      NBSTA=0
      NESTA=0
      DO 160 I=1,NHEAD
	 NBSTA=NESTA+1
	 NESTA=NESTA+NWMO(I)
	 IF(I.EQ.1) NPOS=7
	 IF(I.GT.1) NPOS=NPOS+NWMO(I-1)+7
	IK=0
        DO 150 J=NBSTA,NESTA
	   IK=IK+1
	   NDIG=0
	   IF((FCST(J).GT.-0.50).AND.(FCST(J).LT.9.50))THEN
	     NDIG=1
             CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(COL_SAVE(J),
     *       NPOS-1+IK),NDIG,0,'X',KOUNT(1,J))
	   ENDIF
	   IF(((FCST(J).LE.-0.50).AND.(FCST(J).GT.-9.50)).OR.
     *        ((FCST(J).GE.9.50).AND.(FCST(J).LT.99.50)))THEN
	     NDIG=2
	     CALL PUTMOS(LIST(J),FCST(J),0.,1.,-9,99,MSG(COL_SAVE(J),
     *       NPOS-1+IK),NDIG,0,'XX',KOUNT(1,J))
           ENDIF
	   IF(((FCST(J).LE.-9.50).AND.(FCST(J).GT.-99.50)).OR.
     *        ((FCST(J).GE.99.50).AND.(FCST(J).LT.999.50)))THEN
	     NDIG=3
	     CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(COL_SAVE(J),
     *       NPOS-1+IK),NDIG,0,'XXX',KOUNT(1,J))
	   ENDIF

	   IF(NDIG.EQ.0)THEN
	     NDIG=1
	     CALL PUTCHAR('M',MSG(COL_SAVE(J),NPOS-1+IK),1)
           ENDIF

	   COL_SAVE(J)=COL_SAVE(J)+NDIG

	   IF(K.NE.KT)THEN
	     CALL PUTCHAR('/',MSG(COL_SAVE(J),NPOS-1+IK),1)
	     COL_SAVE(J)=COL_SAVE(J)+1
           ENDIF

 150    CONTINUE
 160  CONTINUE
 170  CONTINUE

CCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   TAIL TAIL TAIL
CCCCCCCCCCCCCCCCCCCCCCCCCCCC

C        CREATE A ONE LINE TAIL 
C        FOLLOWING THE END OF EACH BULLETIN
C        NOTE: TAILS ARE ONLY GENERATED FOR EACH BULLETIN

      NESTA=0
      NBSTA=0
      NWRMB=0
      NWRME=0

      DO 12 I=1,NHEAD

	 NBSTA=NESTA+1
	 NESTA=NESTA+NWMO(I)
	 IF(I.EQ.1) NPOS=NWMO(I)+7
	 IF(I.GT.1) NPOS=NPOS+NWMO(I)+7
         CALL PUTCHAR('.END',MSG(1,NPOS),4)

C        FILLIN THE ARRAY KOUNT_WRM WHICH WILL INCLUDE A
C        VALUE OF ONE FOR THE EXTRA RECORD CONTAINING THE TAIL
         NWRMB=NWRME+1
	 NWRME=NWRME+NWMO(I)+1
	   DO 401 K=NBSTA,NESTA
	     KOUNT_WRM(1,K+I-1)=KOUNT(1,K)
             IF(K.EQ.NESTA) KOUNT_WRM(1,NWRME)=1
 401       CONTINUE

 12   CONTINUE


C**********************************************************
C        END OF BULLETIN
C**********************************************************
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        START OUTPUT OF BULLETINS

C        FOR EACH WMO HEADER, WRITE THE MESSAGE TO THE TRAN FILE
	NWRMB=0
	NWRME=0
	NBSTA=0
	NESTA=0
      DO 400 N=1,NHEAD
        NBSTA=NESTA+1
        NESTA=NESTA+NWMO(N)
	NWRMB=NWRME+1
	NWRME=NWRME+NWMO(N)+1

        CALL PRMSGA(MSG,NOCHAR,NOLINE,KOUNT,LINE,NMOSTA,NUNIT,
     *               NOHEAD,NTAIL,NBSTA,NESTA)

        CBULHD=WMO(N)
        AWIPPIL=BULHDR(N)
        CALL WRMSG_RFC(MSG,NOCHAR,NOLINE,NWRMB,NWRME,LINE,NCAT,
     *       KOUNT_WRM,LINE,MAXSTA,CBULHD,LBULHD,MDG,MHG,NOHEAD,NOUT,
     *       BFILE,NHEAD,AWIPPIL)
 400  CONTINUE

C
      CALL W3TAGE('RFCMXMN ')

      STOP
      END
