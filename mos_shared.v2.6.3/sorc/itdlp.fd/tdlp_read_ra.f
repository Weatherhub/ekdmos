      SUBROUTINE TDLP_READ_RA(KFILDO,KFILIN,CFILP,ND1,ND5,IVECT,
     1                        L3264B,NBYWD,NREC,IPACK,CCALL,NSTA,
     2                        IRECTYPE,IOCTET,IER)
C
C        HISTORY
C
C        MAY       2013   ENGLE       CREATED.
C
C        PURPOSE
C
C            TO READ TDLPACK DATA RECORDS FROM A RANDOM ACCESS FILE.
C            THE ROUTINE CAN ALSO READ STATION CALL LETTERS.o
C
C        VARIABLES (INPUT/OUTPUT)
C
C              KFILDO = UNIT NUMBER OF DEFAULT PRINT FOR MOS-2000
C                       ROUTINES. HERE KFILDO IS A SCRATCH FILE.
C              KFILIN = UNIT NUMBER OF THE INPUT TDLPACK FILE.
C               CFILP = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE TDLPACK FILE (SEQ OR RA).
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE READ.
C                 ND5 = MAXIMUM SIZE OF RECORD. USED TO DIMENSION IPACK( ).
C               IVECT = FLAG FOR WHETHER THE '-invect' COMMAND LINE OPTION IS GIVEN.
C              L3264B = DEFAULT BIT SIZE OF INTEGERS.
C               NBYWD = WORD SIZE OF INTEGERS.
C                NREC = NUMBER OF RECORDS READ FROM INPUT TDLPACK FILE.
C            IPACK(I) = INTEGER ARRAY (I=1,ND5) TO HOLD TDLPACK DATA IN PACKED FORMAT.
C            CCALL( ) = CHARACTER*8 VARIABLE ARRAY OF SIZE ND1 TO HOLD
C                       STATION CALL LETTERS.
C                NSTA = NUMBER OF STATIONS READ FROM INPUT TDLPACK FILE.
C            IRECTYPE = VALUE FOR EACH TYPE OF RECORD FOUND IN A TDLPACK FILE.
C                       1 = STATION CALL LETTERS
C                       2 = TDLPACK DATA
C                       3 = TRAILER RECORD
C              IOCTET = NUMBER OF BYTES A RECORD CONTAINS. THIS VALUE COMES FROM THE
C                       TDLPACK FILE.
C                 IER = ERROR RETURN VALUE FROM SUBROUTINES.
C
C        VARIABLES (INTERNAL)
C
C                 IOS = HOLDS THE IOSTAT VALUE FROM FORTRAN I/O STATEMENTS.
C        IRECTPE_PREV = SAVED INTEGER THAT HOLDS IRECTYPE FROM THE PREVIOUS
C                       ENTRY INTO TDLP_READ_RA.
C               JD(I) = ID TO SEARCH FOR. (I=1,4)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).
C
C        MOS-2000 SUBROUTINES CALLED
C           RDTDLMC, CLFILM
C
      IMPLICIT NONE
C
      INTEGER, INTENT(IN) :: KFILDO,KFILIN,ND1,ND5,L3264B
      INTEGER, INTENT(IN) :: IVECT,NBYWD,NREC
      INTEGER, INTENT(INOUT) :: IPACK(ND5),IER,NSTA,IRECTYPE,IOCTET
      CHARACTER*8, INTENT(INOUT) :: CCALL(ND1)
      CHARACTER*60, INTENT(IN) :: CFILP
C
      INTEGER :: II
      INTEGER :: IOS,JD(4),L3264W
      INTEGER, SAVE :: IRECTYPE_PREV
C
      IER=0
      JD(:)=0
      L3264W=64/L3264B
C
C        WITH THE NATURE OF TDLPACK RANDOM ACCESS FILES, ITS
C        BEST TO GET THE STATION CALL LETTERS RIGHT AWAY 
C        (IF ONE EXISTS) AND BE DONE WITH IT.
C
      IF(NREC.EQ.1)THEN
C           SET JD(1) TO SEARCH FOR STATION CALL LETTER ID.
         JD(1)=400001000
         CALL RDTDLMC(KFILDO,KFILIN,CFILP,JD,CCALL,ND1*L3264W,
     1                IOCTET,L3264B,IER)
C
C           IF IER=0, THEN STATION CALL LETTERS WERE FOUND. NOW
C           DETERMINE NSTA. STOP IF NSTA GT ND1.
C
         IF(IER.EQ.0)THEN
            NSTA=IOCTET/L3264W
            IRECTYPE=1
            IF(NSTA.GT.ND1)THEN
               WRITE(6,90)
 90            FORMAT(/' error: maximum number of stations exceeded.'/)
               CALL ITDLP_STOP
            ENDIF
            RETURN
         ENDIF
C
C           IER=155 MEAN NO STATION CALL LETTER RECORD WAS FOUND AND STOP
C           IF IVECT=1 ('-invect' OPTION IS GIVEN).
C
         IF(IER.EQ.155.AND.IVECT.EQ.1)THEN
            WRITE(6,100)TRIM(CFILP)
 100        FORMAT(/' error: random access file ',A,' does ',
     1              'not contain station call letters'/)
            CALL ITDLP_STOP
         ENDIF
C
C           IER=155 AND '-invect' IS NOT GIVEN.
C
         IF(IER.EQ.155.AND.IVECT.EQ.0)THEN
C              CODE COMES HERE IF NO STATION CALL LETTER RECORD EXISTS
C              ON THE RA FILE. BECAUSE THE RA FILE MIGHT BE GRIDDED,
C              YOU NEED TO CLOSE THE FILE FORMALLY VIA CLFILM.
            CALL CLFILM(KFILDO,KFILIN,IER)
C              SET JD(1)=9999 TO INVENTORY ALL TDLPACK RECORDS.
            JD(1)=9999
            CALL RDTDLM(KFILDO,KFILIN,CFILP,JD,IPACK,ND5,
     1                  IOCTET,L3264B,IER)
            IRECTYPE=2
         ENDIF
C
C        WHEN NREC GT 1, JUST INVENTORY ALL TDLPACK DATA RECORDS.
C
      ELSEIF(NREC.GT.1)THEN
         JD(1)=9999
         CALL RDTDLM(KFILDO,KFILIN,CFILP,JD,IPACK,ND5,
     1               IOCTET,L3264B,IER)
         IRECTYPE=2
C
C           IER THAT IS NOT 153 IS FATAL.
c
         IF(IER.NE.0.AND.IER.NE.153)THEN
            WRITE(6,110)TRIM(CFILP)
 110        FORMAT(/' error: trouble reading random access file ',A)
            CALL ITDLP_STOP
         ELSEIF(IER.EQ.153)THEN
C              IER=153 MEANS THE LAST RECORD WAS READ FROM THE RA FILE.
            CALL CLFILM(KFILDO,KFILIN,IER)
            IER=910
         ENDIF
C
      ENDIF 
C
C        THE VALUE OF IOCTET IN THIS ROUTINE WHICH IS OBTAINED FROM
C        CALLING RDTDLMC AND RDTDLM IS THE NUMBER OF ELEMENTS USED
C        IN IPACK( ) (I.E. NUMBER OF 4-BYTE WORDS). MULTIPLY BY
C        FOUR IN ORDER TO OBTAIN THE TDLPACK RECORD SIZE IN BYTES.
C
      IF(NBYWD.EQ.4) IOCTET=IOCTET*4
      IF(NBYWD.EQ.8) IOCTET=IOCTET*8
C
      RETURN
      END SUBROUTINE TDLP_READ_RA
