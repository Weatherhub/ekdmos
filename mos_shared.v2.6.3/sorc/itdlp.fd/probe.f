      SUBROUTINE PROBE(KFILDO,ND1,ND7,IS2,GDATA,CPRBTYP,CCOORDS)
C
C       JULY     2013   ENGLE   MDL   CREATED
C
C        PURPOSE
C
C            TO PROBE A GRID AND PRINT EITHER A GRIDPOINT VALUE
C            OR AN INTERPOLATED VALUE TO A LATITUDE/LONGITUDE
C            POINT.
C
C        VARIABLES (INPUT/OUTPUT)
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE READ.
C                 ND7 = DIMENSION IF IS0( ), IS1( ), IS2( ), AND IS4 ( ).
C              IS2(I) = HOLDS THE OCTETS DECODED FROM SECTION 2
C                       (I=1,12).  SECTION 2 IS NOT PRESENT WHEN
C                       DATA ARE NOT GRIDPOINT.
C            GDATA(I) = ARRAY HOLDING THE DATA VALUES FOR A RECORD, (I=1,ND5)
C             CPRBTYP = CHARACTER VARIABLE THAT HOLDS THE PROBE TYPE.
C                       'ij' = PROBE A GRID FOR GRIDPONT VALUES.
C                       'll' = PROBE A GRID FOR AN INTERPOLATED VALUE AT A GIVEN
C                              LATITUDE/LONGITUDE (INTERPOLATION IS BILINEAR).
C             CCOORDS = CHARACTER VARIABLE THAT HOLDS THE COORDINATES OF THE PROBE
C                       POINT. THIS STRING WILL EITHER CONTAIN GRIDPOINT I,J
C                       COORDINATES OR LAT/LON COORDS.
C
C        VARIABLES (INTERNAL)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA).
C                 IDX = FIRST POSITION OF A SEARCH CAHRACTER IN A STRING,
C                       RETURNED FROM INDEX FUNCTION.
C                  IX = INTEGER GRIDPOINT IN THE IX DIRECTION.
C                  JY = INTEGER GRIDPOINT IN THE JY DIRECTION.
C                 LAT = LATITUDE OF THE PROBE POINT.
C                 LON = LONGITUDE OF THE PROBE POINT.
C               MPROJ = MAP PROJECTION.
C                       3 = LAMBERT CONFORMAL
C                       5 = POLAR STEREOGAPHIC (NORTHERN)
C                       7 = MERCATOR
C                  NX = NUMBER OF GRIDPOINTS IN THE IX DIRECTION.
C                  NY = NUMBER OF GRIDPOINTS IN THE jy DIRECTION.
C              ORIENT = ORIENTATION LONGITUDE OF THE GRID.
C               PDATA = THE INTERPOLATED VALUE AT GRIDPOINT XI,YJ.
C                  XI = THE GRID COORDINATE IN THE IX DIRECTION OF LAT.
C                XLAT = STANDARD LATITUDE OF THE GRID (I.E. THE LATITUDE
C                       IN WHICH MESHL APPLIES).
C              XLATLL = LATITUDE OF LOWER LEFT (1,1) CORNER POINT OF
C                       THE GRID.
C              XLONLL = LONGITUDE OF LOWER LEFT (1,1) CORNER POINT OF
C                       THE GRID.  DON'T USE NEGATIVE.
C              XMESHL = MESH LENGTH IN METERS AT XLAT DEGREES N LATITUDE.
C                  YJ = THE GRID COORDINATE IN THE JY DIRECTION OF LON.
C
C        MODULES
C            ISO_C_BINDING, ITDLP_MOD
C
C        C/C++ FUNCTIONS CALLED
C            ATOF
C
C        MOS-2000 SUBROUTINES CALLED
C            LMLLIJ, PSLLIJ, MCLLIJ, INTRPB
C
      USE, INTRINSIC :: ISO_C_BINDING
      USE ITDLP_MOD
      IMPLICIT NONE
C        INPUT/OUTPUT VARIABLES
      INTEGER, INTENT(IN) :: KFILDO,ND1,ND7
      INTEGER, INTENT(IN) :: IS2(ND7)
      REAL, INTENT(IN) :: GDATA(IS2(3),IS2(4))
      CHARACTER(LEN=*) :: CPRBTYP,CCOORDS
C        INTERNAL VARIABLES
      INTEGER :: I,J,IDX,IX,JY
      INTEGER :: MPROJ,NX,NY
      REAL :: LAT,LON,XLATLL,XLONLL,ORIENT,XMESHL,XLAT
      REAL :: XI,YJ,DIR(ND1,2),PDATA
C        INITIALIZE VARIABLES
      IDX=0
      IX=0
      JY=0
      MPROJ=IS2(2)
      NX=IS2(3)
      NY=IS2(4)
      XLATLL=REAL(IS2(5))/10000.
      XLONLL=REAL(IS2(6))/10000.
      ORIENT=REAL(IS2(7))/10000.
      XMESHL=REAL(IS2(8))/1000.
      XLAT=REAL(IS2(9))/10000.
C
C        PERFORM ACTIONS BASED ON PROBE TYPE.
C
      IF(CPRBTYP(1:2).EQ.'ij')THEN
C           PROBE TYPE IS 'ij'. SCAN FOR '.' AND '-' 
C           CHARACTERS. FOR AN IJ PROBE TYPE, THESE 
C           CHARACTERS SHOULD NOT BE PRESENT.
         IDX=INDEX(CCOORDS,'.')
         IDX=INDEX(CCOORDS,'-')
C           IF THE SCANNING RETURNS A NON-ZERO POSITION, THEN
C           WE NEED TO STOP THE PROGRAM.
         IF(IDX.NE.0)THEN
            WRITE(6,100)
 100        FORMAT(/' error: Invalid coordinates for probe ',
     1              'type ij'/)
            CALL ITDLP_STOP
         ENDIF
C           PERFORM INTERNAL READ FROM CCOORDS TO IX,JY.
         READ(CCOORDS,'(I,I)')IX,JY
C           CHECK IF PROBE POINT IS OUTSIDE THE GRID.
         IF((IX.LE.0.OR.IX.GT.NX).OR.
     1     (JY.LE.0.OR.JY.GT.NY))THEN
            WRITE(6,105)IX,JY
 105        FORMAT(/' error: Gridpoint ',I,I,' is outside the grid'/)
            CALL ITDLP_STOP
         ELSE
C              PRINT TO SCREEN.
            WRITE(6,110)IX,JY,GDATA(IX,JY)
 110        FORMAT(I4,',',I4,',',F12.4)
         ENDIF
C
      ELSEIF(CPRBTYP(1:2).EQ.'ll')THEN
C           PROBE TYPE IS 'll'. SCAN CCOORDS FOR ',' AND STORE
C           THAT POSITION IN IDX.
         IDX=INDEX(CCOORDS,',')
         IF(IDX.EQ.0)THEN
            WRITE(6,115)
 115        FORMAT(/' error: Invalid coordinates for probe type ll'/)
            CALL ITDLP_STOP
         ENDIF
C           USE THE C++ FUNCTION ATOF TO CONVERT FROM CHARACTER 
C           STRING TO REAL. A FORTRAN INTERFACE FOR ATOF HAS BEEN
C           PROVIDED IN ITDLP_MOD.
         LAT=ATOF(TRIM(CCOORDS(1:IDX-1))//C_NULL_CHAR)
         LON=ATOF(TRIM(CCOORDS(IDX+1:))//C_NULL_CHAR)
C           CONVERT FROM LAT/LON COORDINATES TO GRIDPOINT 
C           COORDINATES.
         IF(MPROJ.EQ.3)THEN
C              GRID IS LAMBERT CONFORMAL
            CALL LMLLIJ(KFILDO,LAT,-LON,XMESHL,ORIENT,XLAT,
     1                  XLATLL,XLONLL,XI,YJ)
         ELSEIF(MPROJ.EQ.5)THEN
C              GRID IS POLAR STEREOGRAPHIC
            CALL PSLLIJ(KFILDO,LAT,-LON,XMESHL,ORIENT,XLAT,
     1                  XLATLL,XLONLL,XI,YJ)
         ELSEIF(MPROJ.EQ.7)THEN
C              GRID IS MERCATOR
            CALL MCLLIJ(KFILDO,LAT,-LON,XMESHL,ORIENT,XLAT,
     1                  XLATLL,XLONLL,XI,YJ)
         ENDIF
C           CHECK IF LAT/LON POINT IS OUTSIDE THE GRID.
         IF((NINT(XI).LE.0.OR.NINT(XI).GT.NX).OR.
     1     (NINT(YJ).LE.0.OR.NINT(YJ).GT.NY))THEN
            WRITE(6,120)LAT,LON
 120        FORMAT(/' error: Lat/Lon ',F8.4,F9.4,
     1              ' is outside the grid'/)
            CALL ITDLP_STOP
         ENDIF
C           STORE THE CALCULATED GRIDPOINTS IN DIR( , ).
         DIR(1,1)=XI
         DIR(1,2)=YJ
C           CALL INTRPB TO PERFORM THE INTERPOLATION.
         CALL INTRPB(KFILDO,GDATA,NX,NY,DIR,ND1,1,PDATA)
C           PRINT TO SCREEN.
         WRITE(6,150)LAT,LON,PDATA
 150     FORMAT(F8.4,',',F9.4,',',F12.4)
C
      ENDIF
C
      RETURN
      END SUBROUTINE PROBE
