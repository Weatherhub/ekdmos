      PROGRAM ITDLP
C
C        HISTORY
C
C        JANUARY   2012   ENGLE       CREATED.
C        APRIL     2014   ENGLE       VERSION 1.0.0
C        APRIL     2014   ENGLE       VERSION 1.0.1
C        JULY      2014   ENGLE       VERSION 1.1.0
C        OCTOBER   2014   ENGLE       VERSION 1.2.0
C        MARCH     2015   ENGLE       VERSION 1.3.0
C
C        PURPOSE
C
C            THE PROGRAM ITDLP IS DESIGNED TO INVENTORY A TDLPACK
C            SEQENTIAL OR RANDOM ACCESS FILE. THE WILL OUTPUT TO THE SCREEN
C            (UNIT=6), INFORMATION ABOUT EACH TDLPACK RECORD. THE IDEA IS TO
C            PROVIDE A WGRIB2-LIKE OUTPUT AND FUNCTIONALITY.
C
C        VARIABLES
C            CARGS( ) = CHARACTER*256 VARIABLE ARRAY OF SIZE (I=1,64) 
C                       THAT WILL HOLD THE COMMAND LINE ARGUMENTS.
C            CCALL( ) = CHARACTER*8 VARIABLE ARRAY OF SIZE ND1 TO HOLD
C                       STATION CALL LETTERS.
C             CCOORDS = CHARACTER VARIABLE THAT HOLDS THE COORDINATES OF THE PROBE
C                       POINT. THIS STRING WILL EITHER CONTAIN GRIDPOINT I,J
C                       COORDINATES OR LAT/LON COORDS.
C               CFILP = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE TDLPACK FILE (SEQ OR RA).
C             CFILGIS = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE ASCII GIS OUTPUT FILE.
C             CFILBIN = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE BINARY OUTPUT FILE.
C             CFILTXT = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE TEXT OUTPUT FILE.
C             CPRBTYP = CHARACTER VARIABLE THAT HOLDS THE PROBE TYPE.
C                       'ij' = PROBE A GRID FOR GRIDPONT VALUES.
C                       'll' = PROBE A GRID FOR AN INTERPOLATED VALUE AT A GIVEN
C                              LATITUDE/LONGITUDE (INTERPOLATION IS BILINEAR).
C                       THE BINARY DATA OUTPUT FILE.
C          CNEWDDFILE = CHARACTER*256 VARIABLE THAT CONTAINS THE NAME OF
C                       THE OUTPUT TDLPACK FILE WITH A NEW DD.
C            CTDLPOUT = CHARACTER*256 VARIABLE THAT CONTAINS THE FILENAME OF
C                       OUTPUT TDLPACKFILE USING THE '-tdlp' OPTION.
C              CRAOUT = CHARACTER*256 THAT HOLDS THE OUTPUT TDLPACK RA FILENAME.
C               FSIZE = INPUT TDLPACK FILE SIZE.
C            GDATA(I) = ARRAY HOLDING THE DATA VALUES FOR A RECORD, (I=1,ND5)
C                GMAX = MAX VALUE (IGNORING MISSING) OF GDATA ( ).
C               GMEAN = MEAN VALUE (IGNORING MISSING) OF GDATA ( ).
C                GMIN = MIN VALUE (IGNORING MISSING) OF GDATA ( ).
C                GSUM = SUM OF VALUES (IGNORING MISSING) OF GDATA ( ).
C             IAPPEND = FLAG FOR WHETHER THE '-append' COMMAND OPTION HAS BEEN INVOKED
C                IBIN = FLAG FOR WHETHER THE '-bin' COMMAND LINE OPTION IS GIVEN.
C               ICHDD = FLAG FOR WHETHER THE COMMAND LINE OPTION '-change-dd' IS GIVEN
C              ICOUNT = COUNT OF GOOD (I.E. NON-MISSING) DATA VALUES.
C               ID(I) = MOS-2000 ID GIVEN WITH '-id', (I=1,4)
C              ID2(I) = MOS-2000 ID READ FROM INPUT TDLPACK FILE, (I=1,4)
C              IDEBUG = FLAG FOR WHETHER THE '-debug' OPTION HAS BEEN INVOKED.
C             IDMATCH = LOGICAL, TRUE IF ID( ) WAS FOUND IN TDLPACK FILE.
C               IDATE = DATE/TIME OF A RECORD IN YYYYMMDDHH FORMAT.
C             IENDIAN = HOLDS EITHER THE ENDIANNESS OF THE TDLPACK FILE. THIS VALUE IS
C                       SET IN THE ROUTINE TDLP_OPEN.
C                 IER = ERROR RETURN VALUE FROM SUBROUTINES.
C                IFHR = FORECAST PROJECTION IN HOURS OF THE RECORD.
C              IFTYPE = FLAG FOR WHETHER THE '-ftype' COMMAND LINE OPTION IS GIVEN.
C                IGIS = FLAG FOR WHETHER THE '-gis' COMMAND LINE OPTION IS GIVEN.
C               IGIVE = INDICATES HOW MUCH OUTPUT IS WANTED.
C                       1 = ID'S ONLY--FILLS IS0( ), IS1( ), IS2( ),
C                           AND MOST OF IS4( ).
C                       OTHERWISE, BOTH ID'S AND DATA (SEE ABOVE).
C                IGRD = FLAG FOR WHETHER THE '-grid' COMMAND LINE OPTION IS GIVEN.
C                IMID = FLAG FOR WHETHER THE '-id' COMMAND LINE OPTION IS GIVEN.
C              IOCTET = NUMBER OF BYTES A RECORD CONTAINS. THIS VALUE COMES FROM THE
C                       TDLPACK FILE.
C                 IOS = HOLDS THE IOSTAT VALUE FROM FORTRAN I/O STATEMENTS.
C            IPACK(I) = INTEGER ARRAY (I=1,ND5) TO HOLD TDLPACK DATA IN PACKED FORMAT.
C              IPRINT = FLAG FOR THE LEVEL VERBOSITY OF PRINTING TO SCREEN.
C                       -1 = DEFAULT VALUE (SIMPLE OUTPUT)
C                       +1 = MORE VERBOSE OUTPUT, GIVEN BY '-v' OPTION.
C              IPROBE = FLAG FOR WHETHER THE '-probe' COMMAND LINE OPTION IS GIVEN.
C              IRAOUT = FLAG FOR IF THE '-tdlpra' COMMAND LINE OPTION HAS BEEN INVOKED.
C                IREC = RECORD NUMBER GIVEN BY '-rec <number>' COMMAND LINE OPTION.
C            IRECTYPE = VALUE FOR EACH TYPE OF RECORD FOUND IN A TDLPACK FILE.
C                       1 = STATION CALL LETTERS
C                       2 = TDLPACK DATA
C                       3 = TRAILER RECORD
C       IRECTYPE_PREV = PREVIOUS VALUE OF IRECTYPE.
C              IS0(I) = HOLDS THE BYTES DECODED FROM SECTION 0 (I=1,3).
C                       (OUTPUT)
C              IS1(I) = HOLDS THE BYTES DECODED FROM SECTION 1
C                       (I=1,MAX OF ND7).  (OUTPUT)
C              IS2(I) = HOLDS THE OCTETS DECODED FROM SECTION 2
C                       (I=1,12).  SECTION 2 IS NOT PRESENT WHEN
C                       DATA ARE NOT GRIDPOINT.  (OUTPUT)
C              IS4(I) = HOLDS THE BYTES DECODED FROM SECTION 4 (I=1,4),
C                       EXCEPT FOR THE DATA THEMSELVES.  (OUTPUT)
C               ISECT = VARIABLE TO HOLD THE WHICH TDLPACK SECTIONS ARE PRINTED. THIS
C                       VALUE IS DETERMINED BY THE COMMAND LINE OPTIONS '-is0', '-is1',
C                       '-is2', AND '-is4'. THE NUMBER BIT THAT REPRESENTS A TDLPACK IS
C                       SECTION WILL BE TURNED ON.
C                ISRA = FLAG FOR IF INPUT TDLPACK FILE IS RANDOM ACCESS.
C                ISSQ = FLAG FOR IF INPUT TDLPACK FILE IS SEQUENTIAL.
C            ITDLPOUT = FLAG FOR WHEHTER THE '-tdlp' OPTION HAS BEEN INVOKED.
C                ITXT = FLAG FOR WHETHER THE '-text' COMMAND LINE OPTION IS GIVEN.
C              IUDATE = FLAG FOR WHETHER THE '-date' COMMAND LINE OPTION IS GIVEN.
C               IVECT = FLAG FOR WHETHER THE '-invect' COMMAND LINE OPTION IS GIVEN.
C                 IVT = FLAG FOR WHETHER THE '-vt' COMMAND LINE OPTION IS GIVEN.
C            IWORK(I) = INTEGER WORK ARRAY (I=1,ND5).
C              KFILDO = UNIT NUMBER OF DEFAULT PRINT FOR MOS-2000
C                       ROUTINES. HERE KFILDO IS A SCRATCH FILE.
C              KFILIN = UNIT NUMBER OF THE INPUT TDLPACK FILE.
C              L3264B = BIT SIZE OF SCALAR INTEGERS.
C              L3264W = NUMBER OF SCALAR INTEGER WORDS TO HOLD 64-BITS.
C           MASTER(I) = INTEGER ARRAY (I=1,6) THAT HOLDS THE RANDOM ACCESS
C                       MASTER KEY RECORD.
C               NARGS = NUMBER OF COMMAND LINE ARGUMENTS GIVEN.
C              NBYTES = NUMBER OF BYTES TO WRITE TO CFILBIN.
C               NBYWD = WORD SIZE OF INTEGERS.
C               NCALL = COUNTER FOR THE NUMBER OF STATION CALL LETTER RECORDS READ
C                       FROM INPUT.
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE READ.
C                 ND5 = MAXIMUM SIZE OF RECORD. USED TO DIMENSION IPACK( ).
C                 ND7 = DIMENSION IF IS0( ), IS1( ), IS2( ), AND IS4 ( ).
C               NEWDD = THE NEW DD VALUE GIVEN IN THE COMMAND LINE OPTION '-change-dd'.
C              NPMISS = NUMBER OF PRIMARY MISSING VALUES PACKED.
C                NREC = NUMBER OF RECORDS READ FROM INPUT TDLPACK FILE.
C              NSMISS = NUMBER OF SECONDARY MISSING VALUES PACKED.
C                NSTA = NUMBER OF STATIONS READ FROM INPUT TDLPACK FILE.
C               PMISS = PRIMARY MISSING VALUE.
C               SMISS = SECONDARY MISSING VALUE.
C
C        MOS-2000 SUBROUTINES CALLED
C           UNPACK, UPDAT
C
C        ITDLP SOURCE SUBROUTINES CALLED
C           CSIGNAL, PRINV, RDCLARGS, TDLP_READ_SQ, TDLP_READ_RA,
C           PRGRID, WRGIS, WRBIN, WRTDLP, ITDLP_STOP
C
      IMPLICIT NONE
      EXTERNAL ITDLP_STOP
C
      INTEGER, PARAMETER :: ND1=3000000
      INTEGER, PARAMETER :: ND2=2945
      INTEGER, PARAMETER :: ND3=2049
      INTEGER, PARAMETER :: ND2X3=ND2*ND3
      INTEGER, PARAMETER :: ND5=MAX(ND1,ND2X3)
      INTEGER, PARAMETER :: ND7=71
C
      CHARACTER*2 :: CPRBTYP
      CHARACTER*7 :: STATUX
      CHARACTER*8 :: CCALL(ND1)
      CHARACTER*20 :: CCOORDS
      CHARACTER*256 :: CFILP,CFILGIS,CFILBIN,CFILTXT
      CHARACTER*256 :: CNEWDDFILE,CTDLPOUT,CRAOUT
      CHARACTER*256, DIMENSION(64) :: CARGS
C
      INTEGER :: I,K,II,N
      INTEGER :: KFILIO,L3264W,NTOTBY,NTOTRC
      INTEGER :: KFILIN,ICOUNT,IDATE,IFHR,IOS,IGIVE,IOCTET,NBYWD
      INTEGER :: L3264B,IGRD,IREC,ISRA,ISSQ,NREC,IER,KFILDO,IGIS
      INTEGER :: IBIN,NBYTES,IPRINT,ICHDD,NEWDD,FSIZE,IVT,IVECT
      INTEGER :: NARGS,NCOLS,NSTA,IRECTYPE,ISECT,IMID,IPROBE,IFTYPE
      INTEGER :: IENDIAN,IUDATE,DATE,IDEBUG,IRECTYPE_PREV,IRECSIZE
      INTEGER :: ITDLPOUT,ISTDIN,ITXT,IRECS,NCALL,NPMISS,NSMISS
      INTEGER :: IRAOUT,IAPPEND,KFILRA,IRASIZE
      INTEGER, DIMENSION(4) :: ID,ID2
      INTEGER, DIMENSION(6) :: MASTER
      INTEGER, DIMENSION(ND5) :: IPACK,IWORK
      INTEGER, DIMENSION(ND7) :: IS0,IS1,IS2,IS4
C
      REAL :: GMAX,GMIN,GSUM,GMEAN
      REAL :: PMISS,SMISS
      REAL, DIMENSION(ND5) :: GDATA
C
      LOGICAL :: IDMATCH,DATEMATCH
C
C        INTIALIIZE VARIABLES
C
      DATE=0
      DATEMATCH=.FALSE.
      IAPPEND=0
      ID=0
      ID2=0
      IDEBUG=0
      IDMATCH=.FALSE.
      IENDIAN=0
      IFTYPE=0
      IGIVE=2
      IPROBE=0
      IRASIZE=1
      IRECS=0
      IRECSIZE=0
      IRECTYPE=99
      ISECT=0
      ISTDIN=0
      ITDLPOUT=0
      IUDATE=0
      GDATA(:)=0.
      KFILDO=12
      KFILIN=20
      KFILIO=21
      KFILRA=49
      L3264B=32
      L3264W=64/L3264B
      MASTER(:)=0
      NBYWD=L3264B/8
      NCALL=0
      NREC=0
      NSTA=0
      NTOTBY=0
      NTOTRC=0
      PMISS=0.
      SMISS=0.
      STATUX='SCRATCH'
C
C        TRAP FOR CTRL+C (SIGINT) SIGNAL.
C
      CALL CSIGNAL(2,ITDLP_STOP)
C
C        READ AND PARSE COMMAND LINE OPTIONS.
C
      CALL RDCLARGS(KFILDO,KFILIN,NARGS,CARGS,IGRD,IREC,ISRA,
     1              ISSQ,CFILP,IGIS,CFILGIS,IPRINT,IBIN,CFILBIN,
     2              ICHDD,NEWDD,FSIZE,CNEWDDFILE,IVT,IVECT,NCOLS,
     3              ISECT,IMID,ID,IPROBE,CPRBTYP,CCOORDS,IFTYPE,
     4              IENDIAN,IUDATE,DATE,IDEBUG,IRECSIZE,ITDLPOUT,
     5              CTDLPOUT,ISTDIN,ITXT,CFILTXT,MASTER,IRAOUT,
     6              CRAOUT,IAPPEND,IRASIZE)
      IF(NARGS.EQ.0) CALL USAGE(ND1,ND2,ND3,ND2X3,ND5)
C
C        LOOP THROUGH READING THE RECORDS OF THE TDLPACK FILE,
C        PERFORMING OPERATIONS SPECIFICED FROM COMMAND LINE
C        OPTIONS.
C
      II=1
      N=0
      DO
C
      IRECTYPE_PREV=IRECTYPE
C
C        IF '-id' HAS BEEN GIVEN AND WE HAVE ALREADY MATCHED
C        TO AN ID ON FILE AND THE FILE IS RANDOM ACCESS,
C        THEN FINISH THE PROGRAM.
C
      IF(ISRA.EQ.1.AND.IMID.EQ.1.AND.IDMATCH) GO TO 9999
C
C        CALL APPROPRIATE READ SUBROUTINE DEPENDING ON VALUE
C        OF ISRA AND ISSQ.
C
      NREC=NREC+1
      IDATE=0
      IF(ISSQ.EQ.1)THEN
         CALL TDLP_READ_SQ(KFILDO,KFILIN,CFILP,ND1,ND5,
     1                     IVECT,L3264B,NBYWD,NREC,IPACK,
     2                     CCALL,NSTA,IRECTYPE,IOCTET,IER)
         IF(IER.EQ.910) GO TO 9999
      ELSEIF(ISRA.EQ.1)THEN
         CALL TDLP_READ_RA(KFILDO,KFILIN,CFILP,ND1,ND5,
     1                     IVECT,L3264B,NBYWD,NREC,IPACK,
     2                     CCALL,NSTA,IRECTYPE,IOCTET,IER)
         IF(IER.EQ.910) GO TO 9999
         IF(IOCTET.EQ.0)THEN
            NREC=NREC+1
            CYCLE
         ENDIF
      ENDIF
C
C        COUNT THE NUMBER OF STATION LETTER RECORDS.
C
      IF(IRECTYPE.EQ.1)THEN
         N=N+1
         NCALL=N
      ENDIF
C
C        IF '-i' HAS BEEN INVOKED, CALL RDSTDIN TO READ
C        FROM STANDARD INPUT.
C
      IF(ISTDIN.EQ.1)THEN
         IF(NREC.EQ.1)CALL RDSTDIN(KFILIN,IRECS)
         IF(IRECS.NE.NREC)THEN
            CYCLE
         ELSEIF(IRECS.EQ.NREC)THEN
            CALL RDSTDIN(KFILIN,IRECS)
         ENDIF
      ENDIF
C
C        CALL FILE_INFO. IFTYPE IS SET TO 1 WHEN THE USER INVOKES
C        THE -ftype OPTION. THIS OPTION GIVES THE USER INFORMATION
C        ABOUT THE FILE.  THE FILE IS NOT INVENTORIED.
C
      IF(IFTYPE.EQ.1) THEN
         CALL FILE_INFO(KFILDO,CFILP,ISSQ,ISRA,IRECTYPE,IENDIAN,
     1                  MASTER)
         GO TO 9999
      ENDIF
C
C        SET IGIVE=1 FOR DEFAULT (SIMPLE) OUTPUT. IGIVE=1 WILL NOT
C        UNPACK THE DATA. SET IGIVE=2 IF ThE USER WANTS MORE INFORMATION
C        VIA THE '-v' OPTION.
C
      IF(IPRINT.EQ.-1) IGIVE=1
      IF(ICHDD.EQ.1)   IGIVE=1
      IF(IPRINT.EQ.1)  IGIVE=2
      IF(IGIS.EQ.1)    IGIVE=2
      IF(IBIN.EQ.1)    IGIVE=2
      IF(ITXT.EQ.1)    IGIVE=2
      IF(IVECT.EQ.1)   IGIVE=2
      IF(IPROBE.EQ.1)  IGIVE=2
      IF(BTEST(ISECT,4)) IGIVE=2
C
      IF(IRECTYPE.EQ.2)THEN
C           BEFORE WE CALL UNPACK, WE CAN CHECK THE DATE FROM
C           IPACK(5).  THE DATE (YYYYMMDDHH) IS CONTAINED WITHIN.
         DATEMATCH=.FALSE.
         IDATE=IPACK(5)
         IF(IUDATE.EQ.1)THEN
            IF(IDATE.LT.DATE) CYCLE
            IF(IDATE.EQ.DATE) DATEMATCH=.TRUE.
            IF(IDATE.GT.DATE) GO TO 9999
         ENDIF
C           ONLY NEED TO CALL UNPACK WHEN IRECTYPE=2.
         CALL UNPACK(KFILDO,IPACK,IWORK,GDATA,ND5,IS0,IS1,IS2,IS4,ND7,
     1               PMISS,SMISS,IGIVE,L3264B,IER)
C
C           CHECK FOR PRIMARY AND SECONDARY MISSING VALUES
C
         IF(BTEST(IS4(2),1))THEN
            PMISS=REAL(IS4(4))
         ENDIF
         IF(BTEST(IS4(2),0))THEN
            SMISS=REAL(IS4(5))
         ENDIF
C
C           EXTRACT THE DATE, MOS-2000 ID, AND FORECAST
C           PROJECTION FROM IS1( ) ARRAY.
C
         ID2(1)=IS1(9)
         ID2(2)=IS1(10)
         ID2(3)=IS1(11)
         ID2(4)=IS1(12)
         IFHR=IS1(13)
C 
C           GET MAX,MIN,MEAN VALUES OF THE GRID DATA.
C           NOTE: ICOUNT, NPMISS, AND NSMISS NEED TO BE
C           RESET TO ZERO FOR EACH TDLPACK RECORD.
C
         ICOUNT=0
         NPMISS=0
         NSMISS=0
         IF(IPRINT.EQ.+1)THEN
            GSUM=0
            DO I=1,IS4(3)
               IF(NINT(GDATA(I)).EQ.NINT(PMISS).AND.
     1            BTEST(IS4(2),1))THEN
                  NPMISS=NPMISS+1
               ELSEIF(NINT(GDATA(I)).EQ.NINT(SMISS).AND.
     1            BTEST(IS4(2),0))THEN
                  NSMISS=NSMISS+1
               ELSE
                  ICOUNT=ICOUNT+1
                  GSUM=GSUM+GDATA(I)
               ENDIF
            END DO
            IF(ICOUNT.EQ.0)THEN
               GMAX=9999.
               GMIN=9999.
               GMEAN=9999.
            ELSE
               GMAX=MAXVAL(GDATA(1:IS4(3)),
     &              MASK=(GDATA.NE.PMISS.AND.GDATA.NE.SMISS))
               GMIN=MINVAL(GDATA(1:IS4(3)),
     &              MASK=(GDATA.NE.PMISS.AND.GDATA.NE.SMISS))
               GMEAN=GSUM/REAL(ICOUNT)
            ENDIF
         ENDIF
C
C           CHECK TO SEE IF IDS MATCH
C
         IDMATCH=.FALSE.
         IF(IMID.EQ.1)THEN
            IF((ID(1).EQ.ID2(1)).AND.(ID(2).EQ.ID2(2)).AND.
     1         (ID(3).EQ.ID2(3)).AND.(ID(4).EQ.ID2(4)))THEN
               IDMATCH=.TRUE.
            ENDIF
         ENDIF
C
      ENDIF ! ON IRECTYPE.EQ.2
C
C        WRITE INFORMATION TO THE SCREEN ACCORDING TO
C        COMMAND LINE OPTIONS. IREC=-1 HERE MEANS TO 
C        INVENTORY THE ENTIRE FILE.
C
      IF(IREC.EQ.-1.OR.IREC.EQ.-2.OR.IREC.EQ.NREC)THEN
C
C           CHECK FOR '-probe' OPTION BEING USED ON A
C           VECTOR (STATION) FILE.
C
         IF(IPROBE.EQ.1.AND.IRECTYPE.EQ.1)THEN
            WRITE(6,85)
 85         FORMAT(/' error: TDLPACK record is not gridded.'/)
            CALL ITDLP_STOP
         ENDIF
C
C           CHECK IF '-date' AND/OR '-id' OPTIONS HAVE BEEN
C           INVOKED AND ALSO CHECK IF THE DATE AND ID REQUESTED
C           BY THE USER HAS BEEN FOUND
C
         IF(IMID.EQ.1.AND.IUDATE.EQ.1)THEN
            IF(.NOT.IDMATCH.AND..NOT.DATEMATCH) CYCLE
         ELSE
            IF(IMID.EQ.1.AND..NOT.IDMATCH) CYCLE
            IF(IUDATE.EQ.1.AND..NOT.DATEMATCH) CYCLE
         ENDIF
C
C           CALL ITDLP_PRINT FOR TDLPACK RECORD INFORMATION
C           PRINTING TO SCREEN.
C
         CALL PRINV(ND7,IPRINT,IVT,IRECTYPE,NSTA,NREC,IDATE,IS1,
     1              IS4,IFHR,GMAX,GMIN,GMEAN,IUDATE,
     2              IRECTYPE_PREV,IOCTET,IRECSIZE,NPMISS,NSMISS)
         IF(IRECTYPE.EQ.3.AND.DATEMATCH) GO TO 9999
C
C           CALL PRGRID TO GET GRID SPECS FOR SUPERIMAGEGEN
C
         IF(IGRD.EQ.1) CALL PRGRID(KFILDO,IS2,ND7,IER)
C
C           CALL ROUTINES TO WRITE OUTPUT FILES. THIS
C           CAN ONLY HAPPEN IF THE USER HAS GIVEN A SPECIFIC
C           TDLPACK RECORD NUMER VIA '-rec <number>' ALONG
C           WITH THE APPROPRIATE OPTION FOR THE OUTPUT FILE.
C
         IF(IGIS.EQ.1.AND.IREC.NE.-1) THEN
C              OPTION '-gis' IS GIVEN
            CALL WRGIS(KFILDO,ND5,ND7,IS1,IS2,GDATA,CFILGIS)
         ELSEIF(IBIN.EQ.1.AND.IREC.NE.-1)THEN
C              OPTION '-bin' IS GIVEN
            NBYTES=IS4(3)*NBYWD
            CALL WRBIN(KFILDO,CFILBIN,NBYTES,GDATA,IER)
         ELSEIF(ITXT.EQ.1.AND.IREC.NE.-1)THEN
C              OPTION '-text' IS GIVEN
            CALL WRTEXT(KFILDO,CFILTXT,IS4(3),GDATA,IER)
         ENDIF
C
C           WHEN ICHDD=1, CALL WRTDLP TO WRITE THE CONTENTS
C           THE INPUT TDLPACK FILE (SEQ ONLY) TO A NEW FILE
C           WITH A NEW DD. 
C
         IF(ICHDD.EQ.1.OR.ITDLPOUT.EQ.1)THEN
            CALL WRTDLP(KFILDO,KFILIO,FSIZE,L3264B,L3264W,NBYWD,ND1,ND5,
     1                  NSTA,NCALL,CCALL,IPACK,IOCTET,NEWDD,
     2                  CNEWDDFILE,IRECTYPE,ICHDD,ITDLPOUT,CTDLPOUT,
     3                  NTOTBY,NTOTRC,IER)
         ENDIF
C
C           WHEN IRAOUT=1 ('-tdlpra' OPTION HAS BEEN INVOKED),
C           WRITE TDLPACK RECORDS TO RANDOM ACCESS FILE.
C
         IF(IRAOUT.EQ.1)THEN
            CALL WRTDLPRA(KFILDO,KFILRA,L3264B,L3264W,NBYWD,ND1,ND5,
     1                    CRAOUT,IAPPEND,IPACK,IOCTET,IRASIZE,MASTER,
     2                    ISSQ,ISRA,IRECTYPE,NSTA,NCALL,CCALL,IUDATE,
     3                    IER)
            IF(IER.EQ.900)GO TO 9999
         ENDIF
C
C           WHEN IVECT=1 ('-invect' OPTION HAS BEEN INVOKED), 
C           PRINT STATION CALL LETTERS AND DATA VALUES
C
         IF(IVECT.EQ.1.AND.IRECTYPE.EQ.2)THEN
            IF(NCOLS.EQ.1) WRITE(6,106)(CCALL(K),GDATA(K),K=1,NSTA)
            IF(NCOLS.GT.1) WRITE(6,107)(CCALL(K),GDATA(K),K=1,NSTA)
 106        FORMAT((<NCOLS>(A8,F10.4)))
 107        FORMAT((<NCOLS-1>(A8,F10.4,1X,'|',1X),A8,F10.4))
         ENDIF
C
C
C
         IF(IPROBE.EQ.1)THEN
            CALL PROBE(KFILDO,ND1,ND7,IS2,GDATA,CPRBTYP,CCOORDS)
         ENDIF
C
C           PRINT INFORMATION ABOUT IDENTIFICATION SECTION(S)
C           CONTENT.
C
         IF(ISECT.NE.0.AND.IRECTYPE.EQ.2)THEN
            CALL PRISX(KFILDO,ND7,ISECT,IS0,IS1,IS2,IS4,IER)
         ENDIF
C
C           REACH THE LAST RECORD. TIME TO CLOSE.
C
         IF(IREC.EQ.NREC)GO TO 9999
C
      ELSEIF(IREC.LT.NREC) THEN
C           RECORD NUMBER GIVEN WITH '-rec' IS NOT FOUND.
         WRITE(6,110)IREC
 110     FORMAT(/' error: TDLPACK record ',I0.0,' not found'/)
         STOP 
C
      ENDIF
C        
      END DO
C
C        ERROR HANDLING.
C
 900  WRITE(6,*)'ERROR READING TDLPACK FILE'
      WRITE(6,*)'IOSTAT = ',IOS
      STOP 900
C
 902  WRITE(6,*)'PLEASE SPECIFY INPUT TDLPACK FILE'
      STOP 902
C
      CALL ITDLP_STOP(KFILIN)
 9999 IF((ITDLPOUT.EQ.1.OR.ICHDD.EQ.1).AND.NCALL.GT.0)THEN
         CALL TRAIL(KFILDO,KFILIO,L3264B,L3264W,NTOTBY,NTOTRC,IER)
         CLOSE(KFILIO)
      ENDIF
      IF(IRAOUT.EQ.1)THEN
         CALL CLFILM(KFILDO,KFILRA,IER)
      ENDIF
      CALL ITDLP_STOP(KFILIN)
      END PROGRAM ITDLP
