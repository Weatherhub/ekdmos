      SUBROUTINE WRBIN(KFILDO,CFILBIN,NBYTES,DATA,IER)
C
C        HISTORY
C
C        APRIL     2013   ENGLE       CREATED.
C
C        PURPOSE
C
C           TO WRITE OUT A GRIDDED TDLPACK RECORD TO AN UNFORMATTED
C           BINARY FILE.  THE OUTPUT FILE IS OPENED WITH ACCESS=
C           "STREAM" SO THERE ARE NOT FORTRAN RECORDS.
C
C        VARIABLES (INPUT/OUTPUT)
C
C              KFILDO = UNIT NUMBER OF DEFAULT PRINT FOR MOS-2000
C                       ROUTINES. HERE KFILDO IS A SCRATCH FILE.
C             CFILBIN = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE BINARY OUTPUT FILE.
C              NBYTES = NUMBER OF BYTES TO WRITE TO CFILBIN.
C             DATA(I) = ARRAY HOLDING THE DATA VALUES FOR A RECORD, (I=1,NBYTES)
C                 IER = ERROR RETURN VALUE FROM SUBROUTINES.
C
C        VARIABLES (INTERNAL)
C
C                 IOS = HOLDS THE IOSTAT VALUE FROM FORTRAN I/O STATEMENTS.
C
      IMPLICIT NONE
C        INPUT/OUTPUT VARIABLES
      CHARACTER*1, INTENT(IN) :: DATA(NBYTES)
      CHARACTER*60, INTENT(IN) :: CFILBIN
      INTEGER, INTENT(IN) :: KFILDO,NBYTES
      INTEGER, INTENT(INOUT) :: IER
C        INTERNAL VARIABLES
      INTEGER :: I,IOS
C
C        OPEN THE FILE WITH STREAM ACCESS FOR WRITING.
C
      OPEN(UNIT=40,FILE=CFILBIN,FORM="BINARY",ACCESS="STREAM",
     1     IOSTAT=IOS,ERR=100,STATUS="REPLACE")
C
C        LOOP THROUGH ALL DATA VALUES AND WRITE TO BINARY
C        FILE.
C
      DO I=1,NBYTES
         WRITE(40,IOSTAT=IOS,ERR=110)DATA(I)
      END DO
C
C        STOP PROGRAM IF THERE IS TROUBLE OPENING BINARY
C        OUTPUT FILE
C
 100  IF(IOS.NE.0)THEN
         WRITE(6,105)TRIM(CFILBIN),IOS
 105     FORMAT(/' error: Trouble opening binary output file ',
     1           A,'.  iostat = ',I3/)
         CALL ITDLP_STOP
      ENDIF
C
C        STOP PROGRAM IF THERE IS TROUBLE WRITING TO BINARY
C        OUTPUT FILE
C
 110  IF(IOS.NE.0)THEN
         WRITE(6,115)TRIM(CFILBIN),IOS
 115     FORMAT(/' error: Trouble writing to binary output file ',
     1           A,'.  iostat = ',I3/)
         CALL ITDLP_STOP
      ENDIF
C
      CLOSE(40)
C
      RETURN
      END SUBROUTINE WRBIN
