      SUBROUTINE PRINV(ND7,IPRINT,IVT,IRECTYPE,NSTA,NREC,
     1                 IDATE,IS1,IS4,IFHR,GMAX,GMIN,GMEAN,
     2                 IUDATE,IRECTYPE_PREV,IOCTET,IRECSIZE,
     3                 NPMISS,NSMISS)
C
C        HISTORY
C
C        FEBRUARY  2014   ENGLE       CREATED.
C
C        PURPOSE
C
C            TO PRINT TO SCREEN, INFORMATION ABOUT TDLPACK RECORD(S)
C            CONTAINED IN THE INPUT FILE.
C
C        DATA SET USE
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT)
C
C        VARIABLES (INPUT/OUTPUT)
C               IDATE = DATE/TIME OF A RECORD IN YYYYMMDDHH FORMAT.
C                IFHR = FORECAST PROJECTION IN HOURS OF THE RECORD.
C              IPRINT = FLAG FOR THE LEVEL VERBOSITY OF PRINTING TO SCREEN.
C                       -1 = DEFAULT VALUE (SIMPLE OUTPUT)
C                       +1 = MORE VERBOSE OUTPUT, GIVEN BY '-v' OPTION.
C            IRECTYPE = VALUE FOR EACH TYPE OF RECORD FOUND IN A TDLPACK FILE.
C                       1 = STATION CALL LETTERS
C                       2 = TDLPACK DATA
C                       3 = TRAILER RECORD
C       IRECTYPE_PREV = PREVIOUS VALUE OF IRECTYPE.
C              IS1(I) = HOLDS THE BYTES DECODED FROM SECTION 1
C                       (I=1,MAX OF ND7).
C              IS4(I) = HOLDS THE BYTES DECODED FROM SECTION 4 (I=1,4),
C                       EXCEPT FOR THE DATA THEMSELVES.
C              IUDATE = FLAG FOR WHETHER THE '-date' COMMAND LINE OPTION IS GIVEN.
C                 IVT = FLAG FOR WHETHER THE '-vt' COMMAND LINE OPTION IS GIVEN.
C                GMAX = MAX VALUE (IGNORING MISSING) OF GDATA ( ).
C               GMEAN = MEAN VALUE (IGNORING MISSING) OF GDATA ( ).
C                GMIN = MIN VALUE (IGNORING MISSING) OF GDATA ( ).
C                 ND7 = DIMENSION IF IS0( ), IS1( ), IS2( ), AND IS4 ( ).
C              NPMISS = NUMBER OF PRIMARY MISSING VALUES PACKED.
C                NREC = NUMBER OF RECORDS READ FROM INPUT TDLPACK FILE.
C              NSMISS = NUMBER OF SECONDARY MISSING VALUES PACKED.
C                NSTA = NUMBER OF STATIONS READ FROM INPUT TDLPACK FILE.
C
C        VARIABLES (INTERNAL)
C              IVDATE = VALID DATE OF WHEN '-vt' IS INVOKED.
C               PLAIN = CHARACTER*32 TO HOLD THE PLAIN LANGUAGE OF THE TDLPACK RECORD.
C
C        SUBROUTINES CALLED
C           MOSLIB: UPDAT
C
      IMPLICIT NONE
C        INPUT/OUTPUT VARIABLES
      INTEGER, INTENT(IN) :: ND7,IPRINT,IVT,IRECTYPE,NSTA,NREC,IDATE
      INTEGER, INTENT(IN) :: IFHR,IUDATE,IRECTYPE_PREV,IOCTET,IRECSIZE
      INTEGER, INTENT(IN) :: NPMISS,NSMISS
      INTEGER, INTENT(IN), DIMENSION(ND7) :: IS1,IS4
C
      REAL, INTENT(IN) :: GMAX,GMIN,GMEAN
C        INTERNAL VARIABLES
      INTEGER :: I
      INTEGER :: IVDATE
      CHARACTER*3 :: CADVANCE
      CHARACTER*32 :: PLAIN
C
C           UNPACK PLAIN LANGUAGE.
C
         CALL UNPKPLAIN(ND7,IS1,PLAIN)
C
C           WRITE STATEMENT FOR BASIC TDLPACK RECORD INFO.
C
         IF(IRECTYPE.EQ.1)THEN
C              STATION CALL LETTER RECORD.
            WRITE(6,90)NREC,IDATE,NSTA
 90         FORMAT(I0.1,':','d=',I10.10,':','STATION CALL ',
     1             'LETTER RECORD',':',I0.0)
C
         ELSEIF(IRECTYPE_PREV.EQ.1.AND.IUDATE.EQ.1)THEN
C              SPECIAL PRINT FOR STATION CALL LETTER RECORD.
C              PROGRAM COMES HERE IF '-date' OPTION HAS BEEN
C              INVOKED AND ALLOWS THE CALL LETTER RECORD TO
C              TO BE PRINTED FOR THE PARTICULAR DATE.
            WRITE(6,90)NREC-1,0,NSTA
         ENDIF
C
         IF(IRECTYPE.EQ.2)THEN
C              TDLPACK DATA RECORD.
            IF(IPRINT.NE.0) THEN
C
               IF(IRECSIZE.EQ.0) CADVANCE='YES'
               IF(IRECSIZE.EQ.1) CADVANCE='NO'
C
               IF(IVT.EQ.1) THEN
                  CALL UPDAT(IDATE,IFHR,IVDATE)
                  WRITE(6,101,ADVANCE=CADVANCE)NREC,IVDATE,
     1            (IS1(I),I=9,12),IFHR,PLAIN
               ELSE
                  WRITE(6,100,ADVANCE=CADVANCE)NREC,IDATE,
     1            (IS1(I),I=9,12),IFHR,PLAIN
               ENDIF
 100           FORMAT(I0.1,':','d=',I10.10,':',3(I9.9,1X),
     1                I10.10,':',I3,'-HR FCST:',A)
 101           FORMAT(I0.1,':','vt=',I10.10,':',3(I9.9,1X),
     1                I10.10,':',I3,'-HR FCST:',A)
C
               IF(IRECSIZE.EQ.1) WRITE(6,1011)IOCTET
 1011                            FORMAT(':',I0.1)
C
            ENDIF
C
         ELSEIF(IRECTYPE.EQ.3) THEN
C              TRAILER RECORD.
            WRITE(6,102)NREC,IDATE
 102        FORMAT(I0.1,':','d=',I10.10,':','TRAILER RECORD' )
C
         ENDIF
C
C           FOLLOW UP THE WRITE ABOVE WITH ADDITIONAL SIMPLE
C           STATISTICS IF '-v' OPTION IS GIVEN WHICH WOULD
C           SET IPRINT=+1.
C
         IF(IRECTYPE.EQ.2.AND.IPRINT.EQ.+1) THEN
            WRITE(6,105)GMAX,GMIN,GMEAN
 105        FORMAT(4X,'MAX = ',F0.3,':MIN = ',F0.3,
     1             ':MEAN = ',F0.3)
            WRITE(6,110)IS4(3),NPMISS,NSMISS
 110        FORMAT(4X,'NDATA = ',I0.1,':NPMISS = ',I0.1,
     1             ':SMISS = ',I0.1)
         ENDIF
C
      RETURN
      END SUBROUTINE PRINV
