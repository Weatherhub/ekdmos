      SUBROUTINE WRTDLPRA(KFILDO,KFILRA,L3264B,L3264W,NBYWD,ND1,ND5,
     1                    CRAOUT,IAPPEND,IPACK,IOCTET,IRASIZE,MASTER,
     2                    ISSQ,ISRA,IRECTYPE,NSTA,NCALL,CCALL,IUDATE,
     3                    IER)
C
C        HISTORY
C
C        DECEMBER  2014   ENGLE       CREATED. ADAPTED FROM U350.F
C
C        PURPOSE
C
C            TO CREATE, AND WRITE TDLPACK DATA TO A NEW RANDOM ACCESS
C            FILE.
C
C        VARIABLES (INPUT/OUTPUT)
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C              KFILRA = UNIT NUMBER OF THE OUTPUT RANDOM ACCESS FILE.
C              L3264B = BIT SIZE OF SCALAR INTEGERS.
C              L3264W = NUMBER OF SCALAR INTEGER WORDS TO HOLD 64-BITS.
C               NBYWD = WORD SIZE OF INTEGERS.
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE READ.
C                 ND5 = MAXIMUM SIZE OF RECORD. USED TO DIMENSION IPACK( ).
C              CRAOUT = CHARACTER*256 THAT HOLDS THE OUTPUT TDLPACK RA FILENAME.
C             IAPPEND = FLAG FOR WHETHER THE '-append' COMMAND OPTION HAS BEEN INVOKED
C            IPACK(I) = INTEGER ARRAY (I=1,ND5) TO HOLD TDLPACK DATA IN PACKED FORMAT.
C              IOCTET = NUMBER OF BYTES A RECORD CONTAINS. THIS VALUE COMES FROM THE
C                       TDLPACK FILE.
C             IRASIZE = FLAG FOR IF THE '-rasize' COMMAND LINE OPTION HAS BEEN INVOKED
C                       1 = SMALL TEMPLATE (DEFAULT)
C                       2 = LARGE TEMPLATE
C           MASTER(I) = INTEGER ARRAY (I=1,6) THAT HOLDS THE RANDOM ACCESS
C                       MASTER KEY RECORD.
C                ISSQ = FLAG FOR IF INPUT TDLPACK FILE IS SEQUENTIAL.
C                ISRA = FLAG FOR IF INPUT TDLPACK FILE IS RANDOM ACCESS.
C            IRECTYPE = VALUE FOR EACH TYPE OF RECORD FOUND IN A TDLPACK FILE.
C                       1 = STATION CALL LETTERS
C                       2 = TDLPACK DATA
C                       3 = TRAILER RECORD
C                NSTA = NUMBER OF STATIONS READ FROM INPUT TDLPACK FILE.
C               NCALL = COUNTER FOR THE NUMBER OF STATION CALL LETTER RECORDS READ
C                       FROM INPUT.
C            CCALL( ) = CHARACTER*8 VARIABLE ARRAY OF SIZE ND1 TO HOLD
C                       STATION CALL LETTERS.
C              IUDATE = FLAG FOR WHETHER THE '-date' COMMAND LINE OPTION IS GIVEN.
C                 IER = ERROR RETURN VALUE FROM SUBROUTINES.
C
C        VARIABLES (INTERNAL)
C               ID(I) = MOS-2000 ID GIVEN WITH (I=1,4).
C              MAXENT = MAXIMUM NUMBER OF ENTRIES IN EACH KEY RECORD 
C                       OF THE FILE TO BE ESTABLISHED.
C               MINPK = VALUES ARE PACKED IN GROUPS OF MINIMUM SIZE
C                       MINPK.  ONLY WHEN THE NUMBER OF BITS TO HANDLE
C                       A GROUP CHANGES WILL A NEW GROUP BE FORMED.
C              NBYTES = SIZE IN BYTES OF PHYSICAL KEY AND DATA RECORDS.
C                       THIS IS MODIFIED, IF NECESSARY, TO MAKE IT 
C                       EVENLY DIVISIBLE BY 8.
C              NREPLA = RECORD REPLACEMENT FLAG. 
C                       0 = NOT REPLACING RECORD. 
C                       1 = REPLACING, ERROR IF RECORD NOT FOUND TO 
C                           REPLACE. 
C                       2 = REPLACING, WRITE NEW RECORD IF RECORD NOT 
C                           FOUND TO REPLACE.
C              NCHECK = IDENTIFICATION CHECKING FLAG. 
C                       0 = DON'T CHECK FOR DUPLICATES.
C                       1 = CHECK FOR DUPLICATES, ERROR IF FOUND.
C          NCALL_PREV = HOLDS THE STATION CALL LETTER RECORD NUMBER
C                       FROM THE PREVIOUS ENTRY INTO WRTDLPRA.
C          NDATE_PREV = HOLDS THE DATE OF THE TDLPACK DATA FROM THE PREVIOUS
C                       ENTRY INTO WRTDLPRA.
C
C        MOS-2000 SUBROUTINES CALLED
C           PRSID1,WRTDLM, WRTDLMC
C
C        ITDLP SOURCE SUBROUTINES CALLED
C           CREATERA
C
      IMPLICIT NONE
C        INTPUT/OUTPUT VARIABLES
      INTEGER, INTENT(IN) :: KFILDO,KFILRA,L3264B,L3264W,NBYWD,ND1,ND5
      INTEGER, INTENT(IN) :: IAPPEND,IOCTET,IRASIZE,ISSQ,ISRA,IRECTYPE
      INTEGER, INTENT(IN) :: NSTA,NCALL,IUDATE
      INTEGER, INTENT(INOUT) :: IER
      INTEGER, INTENT(IN), DIMENSION(6) :: MASTER
      INTEGER, DIMENSION(ND5) :: IPACK
      CHARACTER(LEN=*), INTENT(IN) :: CRAOUT
      CHARACTER(LEN=8), INTENT(IN), DIMENSION(NSTA) :: CCALL
C        INTERNAL VARIABLES
      INTEGER :: IFIRST,MAXENT,MINPK,NBYTES,NSIZE
      INTEGER :: NREPLA,NCHECK,NVALUE
      INTEGER, DIMENSION(4) :: ID 
      INTEGER, DIMENSION(15) :: IDPARS
      INTEGER, SAVE :: NCALL_PREV,NDATE_PREV
C
      DATA IFIRST/0/,NCALL_PREV/0/,NDATE_PREV/0/
C        INITIALIZE
      ID(:)=0
      IDPARS(:)=0
      IER=0
      MINPK=14
      NREPLA=0
      NCHECK=1
C
C        SET MAXENT AND NBYTES. IF THE INPUT FILE IS RA, THEN
C        GET VALUES FROM MASTER( ).  IF INPUT IS SQ, THEN
C        USE IRASIZE (NOTE: IRASIZE = 1 IS THE DEFAULT).
C
      IF(ISSQ.EQ.1)THEN
         IF(IRASIZE.EQ.1)THEN
C              SMALL RA FILE
            MAXENT=300
            NBYTES=2000
         ELSEIF(IRASIZE.EQ.2)THEN
C              LARGE RA FILE
            MAXENT=832
            NBYTES=20000
         ENDIF
      ELSEIF(ISRA.EQ.1)THEN 
         MAXENT=MASTER(5)
         NBYTES=MASTER(3)*(L3264B/8)
      ENDIF
C
C        UPON FIRST ENTRY INTO CREATERA, CREATE THE OUTPUT
C        RANDOM ACCESS FILE.
C
      IF(IFIRST.EQ.0.AND.IAPPEND.EQ.0)THEN
         CALL CREATERA(KFILDO,L3264B,KFILRA,CRAOUT,MAXENT,NBYTES)
      ENDIF
C
C        PACK STATION CALL LETTER RECORD ONLY WHEN NCALL_PREV=1.
C
      IF((NCALL.EQ.1.AND.IRECTYPE.EQ.1).OR.
     1   (NSTA.GT.0.AND.IRECTYPE.EQ.2.AND.IFIRST.EQ.0))THEN
          ID(1)=400001000
          ID(2)=0
          ID(3)=0
          ID(4)=0
          CALL WRTDLMC(KFILDO,KFILRA,CRAOUT,ID,CCALL,NSTA*L3264W,
     1                 NREPLA,NCHECK,L3264B,IER)
          NCALL_PREV=NCALL
      ENDIF
      IF(NCALL.GT.NCALL_PREV)THEN
         IER=900
      ENDIF
C
C        IRECTYPE=2 THEREFORE A TDLPACK DATA RECORD.
C
      IF(IRECTYPE.EQ.2)THEN
         ID(1)=IPACK(6)
         ID(2)=IPACK(7)
         ID(3)=IPACK(8)
         ID(4)=IPACK(9)
         CALL PRSID1(KFILDO,ID,IDPARS)
         IF(ISSQ.EQ.1.AND.IUDATE.EQ.0)THEN
            IF(IDPARS(1)/100.NE.4.AND.IDPARS(1)/100.NE.8)THEN
               WRITE(6,105)
 105           FORMAT(/' error: Must use -date when writing model',
     1                 ', MOS, or observation TDLPACK records from ',
     2                 'sequential to random access.'/)
               IER=900
               RETURN
            ENDIF
         ENDIF
         NDATE_PREV=IPACK(5)
         NSIZE=IOCTET/(L3264B/8)
C
C           WRITE THE PACKED TDLPACK RECORD (FROM IPACK) TO THE
C           OUTPUT RANDOM ACCESS FILE. RECORDS ARE WRITTEN WITHOUT
C           REPLACEMENT (NREPLA=0) AND DUPLICATES ARE NOT CHECKED
C           (NCHECK=0).
C
         CALL WRTDLM(KFILDO,KFILRA,CRAOUT,ID,IPACK,NSIZE,
     1               NREPLA,NCHECK,L3264B,IER)
         IF(IER.EQ.157)THEN
            WRITE(6,110)ID
 110        FORMAT(/' error: Cannot write duplicate ID ',3(I9.9,1X),
     1             I10.10,' to random access file. itdlp will stop.'/)
            IER=900
            RETURN
         ENDIF
      ENDIF
C
C        IRECTYPE=3 THEREFORE A TRAILER RECORD. THESE ARE NOT WRITTEN
C        TO RANDOM ACCESS FILES
C
      IF(IRECTYPE.EQ.3) IER=900
C
C        INCREMENT IFIRST AND RETURN.
C
      IFIRST=IFIRST+1
      RETURN
      END SUBROUTINE WRTDLPRA
