      SUBROUTINE WRGIS(KFILDO,ND5,ND7,IS1,IS2,GDATA,CFILGIS)
C
C        HISTORY
C
C        APRIL     2013   ENGLE       CREATED.
C
C        PURPOSE
C            TO WRITE TO A FORMATTED OUTPUT FILE, A GIS ASCII GRID
C            FILE.
C
C        VARIABLES (INPUT/OUTPUT)
C
C              KFILDO = UNIT NUMBER OF DEFAULT PRINT FOR MOS-2000
C                       ROUTINES. HERE KFILDO IS A SCRATCH FILE.
C                 ND5 = MAXIMUM SIZE OF RECORD. USED TO DIMENSION IPACK( ).
C                 ND7 = DIMENSION IF IS0( ), IS1( ), IS2( ), AND IS4 ( ).
C              IS1(I) = HOLDS THE BYTES DECODED FROM SECTION 1
C                       (I=1,MAX OF ND7).
C              IS2(I) = HOLDS THE OCTETS DECODED FROM SECTION 2
C                       (I=1,12).  SECTION 2 IS NOT PRESENT WHEN
C                       DATA ARE NOT GRIDPOINT.
C            GDATA(I) = ARRAY HOLDING THE DATA VALUES FOR A RECORD, (I=1,ND5)
C             CFILGIS = CHARACTER*60 VARIABLE THAT CONTAINS THE NAME OF
C                       THE ASCII GIS OUTPUT FILE.
C
C        VARIABLES (INTERNAL)
C               EQLAT = LATITUDE OF EQUATOR.
C           GRID(I,J) = ALLOCATABLE ARRAY (I=1,NX) (J=1,NY) THAT HOLDS THE
C                       2-DIMENSIONAL GRID
C                  IJ = COUNTER USED WHEN CONVERING 1-D GDATA TO 2-D GRID.
C                 IOS = HOLDS THE IOSTAT VALUE FROM FORTRAN I/O STATEMENTS.
C               MPROJ = MAP PROJECTION.
C                       3 = LAMBERT CONFORMAL
C                       5 = POLAR STEREOGAPHIC (NORTHERN)
C                       7 = MERCATOR
C               NPLAT = LATITUDE OF NORTH POLE.
C                  NX = NUMBER OF GRIDPOINTS IN THE IX DIRECTION.
C                  NY = NUMBER OF GRIDPOINTS IN THE JY DIRECTION.
C              ORIENT = ORIENTATION LONGITUDE OF THE GRID.
C                XIMO = GRID COORDINATE IN XI DIRECTION OF LOWER LEFT GRIDPOINT.
C                XLAT = STANDARD LATITUDE OF THE GRID (I.E. THE LATITUDE
C                       IN WHICH MESHL APPLIES).
C              XLATLL = LATITUDE OF LOWER LEFT (1,1) CORNER POINT OF
C                       THE GRID.
C           XLLCENTER = DISTANCE IN METERS IN IX DIRECTION OF THE LOWER
C                       LEFT GRIDPOINT FROM THE MAP PROJECTION ORIGIN.
C              XLONLL = LONGITUDE OF LOWER LEFT (1,1) CORNER POINT OF
C                       THE GRID.  DON'T USE NEGATIVE.
C              XMESHL = MESH LENGTH IN METERS AT XLAT DEGREES N LATITUDE.
C                YJMO = GRID COORDINATE IN YJ DIRECTION OF LOWER LEFT GRIDPOINT.
C           YLLCENTER = DISTANCE IN METERS IN JY DIRECTION OF THE LOWER
C                       LEFT GRIDPOINT FROM THE MAP PROJECTION ORIGIN.
C
C        MOS-2000 ROUTINES CALLED
C            LMLLIJ, MCLLIJ, PSLLIJ
C
      IMPLICIT NONE
C        INPUT/OUTPUT VARIABLES
      INTEGER, INTENT(IN) :: KFILDO,ND5,ND7
      INTEGER, INTENT(IN), DIMENSION(ND7) :: IS1,IS2
      REAL, INTENT(IN), DIMENSION(ND5) :: GDATA
      CHARACTER(LEN=60),INTENT(IN) :: CFILGIS
C        INTERNAL VARIABLES
      INTEGER :: I,J,IJ,IOS
      INTEGER :: MPROJ,NX,NY
      REAL :: XLATLL,XLONLL,XLAT,ORIENT,XMESHL
      REAL :: XILL,YJLL,XLLCENTER,YLLCENTER
      REAL :: EQLAT,NPLAT
      REAL :: XIMO,YJMO,DI,DJ
      REAL, ALLOCATABLE, DIMENSION(:,:) :: GRID
C        INITIALIZE VARIABLES
      EQLAT=0.
      IOS=0
      NPLAT=90.
C
C        DETERMINE IF TDLPACK RECORD IS GRIDDED
C
      IF(IS1(2).EQ.0)THEN
         WRITE(6,100)
 100     FORMAT(/' error: TDLPACK record is not gridded. ',
     1           ' GIS output file not created'/)
         CALL ITDLP_STOP
      ENDIF
C
C        OPEN THE OUTPUT GIS ASCII FILE.
C
      OPEN(UNIT=30,FILE=CFILGIS,FORM="FORMATTED",
     1     IOSTAT=IOS,ERR=900,STATUS="REPLACE")
C
C        EXTRACT GRID INFORMATION FROM IS2( ) ARRAY.
C
      MPROJ=IS2(2)
      NX=IS2(3)
      NY=IS2(4)
      XLATLL=IS2(5)/10000.
      XLONLL=IS2(6)/10000.
      ORIENT=IS2(7)/10000.
      XMESHL=IS2(8)/1000.
      XLAT=IS2(9)/10000.
C
C        CONVERT THE LOWER-LEFT LAT/LON GRIDPOINT OF THE GRID
C        TO PROJECTED GRID COORDINATES. NORMALLY, THIS IS CALCULATED
C        TO BE 1,1, HOWEVER WE PASS WE SPECIFY THE GRID'S STANDARD
C        LATITUDE AND ORIENTATION LONGITUDE (CENTRAL MERIDIAN) AS
C        THE "LOWER-LEFT", BUT IN REALITY IT IS THE MAP PROJECTION
C        ORIGIN. THE TRUE LOWER-LEFT GRIDPOINT THEN RECEIVES A GRID
C        COORDINATE THAT IS ACCEPTABLE FOR ARCGIS.
C
C        IN SHORT, WE ARE COMPUTING THE DISTANCE (IN GRID UNITS)
C        FROM THE MAP ORIGIN TO THE LOWER-LEFT GRIDPOINT.
C       
C        THE GRID UNITS ARE THEN MULTIPLIED BY THE MESH LENGTH.
C
      IF(MPROJ.EQ.3)THEN
C           THE MAP PROJECTION IS LAMBER CONFORMAL. CALL LMLLIJ TO
C           COMPUTE THE GRID COORDINATES OF THE LOWER-LEFT GRIDPOINT.
         CALL LMLLIJ(KFILDO,XLAT,ORIENT,XMESHL,ORIENT,XLAT,XLATLL,
     1               XLONLL,XIMO,YJMO)
C
      ELSEIF(MPROJ.EQ.5)THEN
C           THE MAP PROJECTION IS NORTHERN POLAR STEREOGRAPHIC. CALL
C           PSLLIJ TO COMPUTE THE GRID COORDINATES OF THE LOWER-LEFT
C           GRIDPOINT.
C
C           NOTE: IN A POLAR STEREOGRPAHIC PROJECTION, THE MAP ORIGIN
C                 IS THE POLE POINT, NOT THE INTERSECTION OF THE 
C                 STANDARD LATITUDE AND ORIENTATION LONGITUDE. THE 
C                 LATITUDE PASSED IN IS THE NPLAT. TECHNICALLY, ANY
C                 LONGITUDE VALUE IS VALID, BUT WE PASS IN THE
C                 LOWER-LEFT GRIDPOINT LONGITUDE.
        CALL PSLLIJ(KFILDO,NPLAT,ORIENT,XMESHL,ORIENT,XLAT,XLATLL,
     1              XLONLL,XIMO,YJMO)
C
      ELSEIF(MPROJ.EQ.7)THEN
C           THE MAP PROJECTION IS MERCATOR. CALL MCLLIJ TO
C           COMPUTE THE GRID COORDINATES OF THE LOWER-LEFT GRIDPOINT.
        CALL MCLLIJ(KFILDO,EQLAT,ORIENT,XMESHL,XLAT,XLATLL,XLONLL,
     1              XIMO,YJMO)
C
      ENDIF
C
C        GET THE COORDINATES OF THE LOWER LEFT GRIDPOINT IN
C        MAP PROJECTION COORDINATES (METERS). XLLCENTER AND
C        YLLCENTER REPRESENT THE DISTANCE FROM THE INTESECTION
C        OF THE STANDARD LATITUDE AND ORIENTATION LONGITUDE
C        OF THE MAP PROJECTION.
C
      DI=XIMO-1
      DJ=YJMO-1
      XLLCENTER=-DI*XMESHL
      YLLCENTER=-DJ*XMESHL
C
C        WRITE THE HEADER TO THE OUTPUT GIS ASCII FILE
C
      WRITE(30,200,IOSTAT=IOS,ERR=910)NX,NY,XLLCENTER,YLLCENTER,
     1                                XMESHL,9999.0000
 200  FORMAT('NCOLS ',I,/
     1       'NROWS ',I,/
     2       'XLLCENTER ',F16.6,/
     3       'YLLCENTER ',F16.6,/
     4       'CELLSIZE ',F16.6,/
     5       'NODATA_VALUE ',F9.4)
C
C        ALLOCATE GRID( , ) AND CONVERT GRIDPOINT VALUES FROM
C        1D GDATA TO 2D GRID.
C
      ALLOCATE(GRID(NX,NY))
      DO J=1,NY
         DO I=1,NX
            IJ=(NX*(J-1))+I
            GRID(I,J)=GDATA(IJ)
         END DO
      END DO
C
C        WRITE THE GRIDPOINT VALUES TO OUTPUT GIS ASCII
C        FILE, 1 GRIDPOINT PER LINE.  THE J DIMENSION
C        (NORTH/SOUTH) IS WRITTEN "FLIPPED" SO NO FURTHER
C        PROCESSING IS NEEDED IN ARCGIS.
C
      DO J=NY,1,-1
         DO I=1,NX
            WRITE(30,*,IOSTAT=IOS,ERR=910)GRID(I,J)
         END DO
      END DO
      DEALLOCATE(GRID)
C
C        STOP PROGRAM IF THERE IS TROUBLE OPENING
C        GIS OUTPUT FILE
C
 900  IF(IOS.NE.0)THEN
         WRITE(6,905)TRIM(CFILGIS),IOS
 905     FORMAT(/' error: Trouble opening gis output file ',
     1           A,'.  iostat = ',I3/)
         CALL ITDLP_STOP
      ENDIF
C
C        STOP PROGRAM IF THERE IS TROUBLE WRITING TO GIS
C        OUTPUT FILE
C
 910  IF(IOS.NE.0)THEN
         WRITE(6,915)TRIM(CFILGIS),IOS
 915     FORMAT(/' error: Trouble writing to gis output file ',
     1           A,'.  iostat = ',I3/)
         CALL ITDLP_STOP
      ENDIF
C 
      CLOSE(30)
C
      RETURN
      END SUBROUTINE WRGIS
