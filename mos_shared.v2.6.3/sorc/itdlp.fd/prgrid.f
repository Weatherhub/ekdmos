      SUBROUTINE PRGRID(KFILDO,IS2,ND7,IER)
C
C        HISTORY
C
C        JANUARY   2012   HUNTEMANN   CREATED FOR ITDLP
C        MARCH     2014   ENGLE       CHANGED FORMAT FOR LATITUDES FROM F7.4
C                                     TO F8.4.
C
C        PURPOSE
C
C            TO PRINT TO SCREEN, THE GRID INFORMATION OF A
C            GRIDDED TDLPACK RECORD FOR SUPERIMAGEGEN. THIS ROUTINE
C            IS ENTERED WHEN THE '-grid' OPTION IS GIVEN TO ITDLP.
C
C        VARIABLES (INPUT/OUTPUT)
C
C              KFILDO = UNIT NUMBER OF DEFAULT PRINT FOR MOS-2000
C                       ROUTINES. HERE KFILDO IS A SCRATCH FILE.
C              IS2(I) = HOLDS THE OCTETS DECODED FROM SECTION 2
C                       (I=1,12).  SECTION 2 IS NOT PRESENT WHEN
C                       DATA ARE NOT GRIDPOINT.  (OUTPUT)
C                 ND7 = DIMENSION IF IS0( ), IS1( ), IS2( ), AND IS4 ( ).
C                 IER = ERROR RETURN VALUE FROM SUBROUTINES.
C
C        VARIABLES (INTERNAL)
C              CMPROJ = CHARACTER*64 TO HOLD THE TYPE OF MAP PROJECTION.
C               LATLL = LATITUDE OF THE LOWER LEFT GRIDPOINT.
C               LATUR = LATITUDE OF THE UPPER RIGHT GRIDPOINT.
C               LONLL = LONGITUDE OF THE LOWER LEFT GRIDPOINT.
C               LONUR = LONGITUDE OF THE UPPER RIGHT GRIDPOINT.
C               MESHL = MESH LENGTH OF THE GRID IN METERS.
C                  NX = NUMBER OF GRIDPOINTS IN THE IX DIRECTION.
C                  NY = NUMBER OF GRIDPOINTS IN THE JY DIRECTION.
C              ORIENT = ORIENTATION LONGITUDE OF THE GRID.
C              STDLAT = STANDARD LATITUDE OF THE GRID (I.E. THE LATITUDE
C                       IN WHICH MESHL APPLIES).
C
C        MOS-2000 SUBROUTINE CALLED
C           LMIJLL, PSIJLL, MCIJLL
C
      IMPLICIT NONE
C        INPUT/OUTPUT/VARIABLES
      INTEGER, INTENT(IN) :: KFILDO,ND7
      INTEGER, INTENT(IN), DIMENSION(ND7) :: IS2
      INTEGER, INTENT(INOUT) :: IER
C        INTERNAL VARIABLES
      CHARACTER(LEN=64) :: CMPROJ
      INTEGER :: NX,NY
      REAL :: LATLL,LONLL,LATUR,LONUR,ORIENT,STDLAT,MESHL
C
C        GET GRID SPECS FROM IS2 ARRAY.
C
      NX=IS2(3)
      NY=IS2(4)
      LATLL=REAL(IS2(5))/10000.
      LONLL=REAL(IS2(6))/10000.
      ORIENT=REAL(IS2(7))/10000.
      MESHL=REAL(IS2(8))/1000.
      STDLAT=REAL(IS2(9))/10000.
C
C        CALL THE APPROPRIATE SUBROUTINE TO CONVERT FROM
C        GRID (IJ) COORDINATES TO LAT/LON FOR THE UPPER-RIGHT
C        GRIDPOINT.
C
      IF(IS2(2).EQ.3)THEN
C           MAP PROJECTION IS LAMBERT CONFORMAL
         CALL LMIJLL(KFILDO,REAL(NX),REAL(NY),MESHL,ORIENT,STDLAT,
     1               LATLL,LONLL,LATUR,LONUR,IER)
         CMPROJ='LAMBERT CONFORMAL'
      ELSEIF(IS2(2).EQ.5)THEN
C           MAP PROJECTION IS POLAR STEREOGRAPHIC
         CALL PSIJLL(KFILDO,REAL(NX),REAL(NY),MESHL,ORIENT,STDLAT,
     1               LATLL,LONLL,LATUR,LONUR)
         CMPROJ='POLAR STEREOGRAPHIC'
      ELSEIF(IS2(2).EQ.7)THEN
C           MAP PROJECTION IS MERCATOR
         CALL MCIJLL(KFILDO,REAL(NX),REAL(NY),MESHL,STDLAT,
     1               LATLL,LONLL,LATUR,LONUR)
         CMPROJ='MERCATOR'
      ENDIF
C
C        PRINT THE GRID INFORMATION
C
      WRITE(6,100)CMPROJ,NX,NY,LATLL,LATUR,-LONLL,
     1            -LONUR,-ORIENT,STDLAT,MESHL
 100  FORMAT(4X,'MAP PROJECTION: ',A,/,
     1       4X,'            NX: ',I5,/,
     2       4X,'            NY: ',I5,/,
     3       4X,'LOWER LEFT LAT: ',F8.4,': UPPER RIGHT LAT: ',F8.4,/,
     4       4X,'LOWER LEFT LON: ',F9.4,': UPPER RIGHT LON: ',F9.4,/,
     5       4X,'    ORIENT LON: ',F9.4,/,
     6       4X,'  STANDARD LAT: ',F7.4,/,
     7       4X,'   MESH LENGTH: ',F13.6,' (M)')
C
      RETURN
      END SUBROUTINE PRGRID
