      SUBROUTINE RDELMLST(KFILDO,KFILIE,NFIELDS,ID68,
     1                    IDMDL,JPLAIN,MODELID,IMAP,
     2                    IPWR10,IPWR2,IREPEAT,ND10,
     3                    JCONVRT,ISTOP) 
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDELMLST
C   PRGMMR: RUDACK          ORG: W/OSD21          DATE: 2003-06-01
C
C ABSTRACT: TO READ THE ELEMENTS FOUND IN THE ELEMENT LIST FILE
C           AND STORE THESE VALUES.  THESE ELEMENTS ARE ALSO
C           CHECKED FOR DUPLICATES.  IF DUPLICATE ELEMENTS ARE
C           FOUND, THE DUPLICATE IS FLAGED AND IGNORED DURING
C           PROCESSING.
C
C PROGRAM HISTORY LOG:
C
C        JUNE       2003   RUDACK   MDL   MOS-2000
C        SEPTEMBER  2005   RUDACK         MODIFIED CODE TO MEET
C                                         OPERATIONAL REQUIREMENTS.
C
C        MAY      2006     RUDACK   MDL   CHANGED THE READING FORMAT
C                                         OF THE ELEMENT LIST FILE FROM
C                                         "25I5" TO "25I6".  THIS WAS 
C                                         NECESSARY TO ACCOMMODATE FIELDS
C                                         STORED AS 6 DIGITS (E.G., THE
C                                         1000 MB GEOPOTENTIAL HEIGHT 
C                                         EXPRESSED IN TERMS OF PASCALS.)
C        OCTOBER  2007     RUDACK   MDL   MODIFIED CODE TO READ IN THE 
C                                         ENTIRE FOURTH WORD OF THE 
C                                         MOS-2000 ID.  THIS WAS NECESSARY
C                                         IN ORDER TO BE ABLE TO PROCESS
C                                         AN ELEMENT THAT CONTAINED A 
C                                         THRESHOLD VALUE (E.G., PROBABILITY
C                                         OF VISIBILITY < 405 METERS).
C                                         INCREASED DIMENSION OF ID68( , ) 
C                                         BY FIVE IN ORDER TO READ IN GRIB 
C                                         IDENTIFIERS FOR SECTION 4.
C        NOVEMBER 2007    JRW      MDL    MERGED 10/07 AND 9/05 VERSIONS.  
C                                         COMPLETED OPERATIONAL DOCUMENTATION
C        OCTOBER  2012   ENGLE     MDL    JPLAIN IS NOW DEFINED AS CHARACTER*1
C
C USAGE: CALLED BY RDGRIB1 AND RDGRIB2
C
C DATA SET USE:
C        INPUT FILE:
C         FORT.KFILIE - UNIT NUMBER OF FILE CONTAINING THE ELEMENT LIST.  (INPUT)
C        OUTPUT FILE:
C         FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C 
C VARIABLES:
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C              KFILIE = UNIT NUMBER OF ELEMENT LIST FILE.  (INPUT)
C             NFIELDS = NUMBER OF FIELDS TO PROCESS.  (OUTPUT)
C           ID68(J,K) = ARRAY CONTAINING PRODUCT DEFINITION INFORMATION.
C                       ONLY THE FOLLOWING ELEMENTS ARE SET - OTHERS = -1 
C                       (FOR GRIB1) OR -9999 (FOR GRIB2).
C                       (J=1,30) (K=1,NFIELDS).
C                       (3)-ORIGINATING CENTER OF THE DATA 
C                       (4)-GENERATION PROCESS ID NUMBER 
C                       (5)-GRID IDENTIFICATION NUMBER (FOR GRIB1 ONLY)
C                       (8)-INDICATOR OF PARAMETER AND UNITS 
C                       (9)-INDICATOR OF TYPE OF LEVEL OR LAYER 
C                       (10)-VALUE 1 OF LEVEL BEING PROCESSED 
C                       (11)-VALUE 2 OF LEVEL BEING PROCESSED 
C                       (18)-P1 FORECAST PROJECTION OR THE FORECAST PROJECTION 
C                            CORRESPONDING TO THE BEGINNING OF THE FORECAST TIME 
C                            PERIOD (IF AN AVERAGE OR AN ACCUMULATION VALUE
C                            IS DESIRED, FOR EXAMPLE).
C                       (19)-P2 (FORECAST PROJECTION CORRESPONDING TO THE END 
C                            OF THE FORECAST TIME PERIOD.
C                       (20)-TIME RANGE INDICATOR (E.G. AVERAGE) 
C                       (21)-INTEGER PRODUCT DEFINITION TEMPLATE (FOR GRIB2 ONLY)
C                       (22)-PARAMETER CATEGORY (FOR GRIB2 ONLY)
C          IDMDL(J,K) = ARRAY CONTAINING THE MOS-2000 ID TO BE 
C                       TDLPACKED (J=1,4) (K=1,NFIELDS).  (OUTPUT) 
C         JPLAIN(J,K) = PLAIN LANGUAGE IDENTIFICATION OF FIELDS READ 
C                       FROM THE ELEMENT LIST FILE (J=1,30)
C                       (K=1,NFIELDS) (CHARACTER*1).  (OUTPUT)     
C          MODELID(K) = MODEL TYPE BEING PROCESSED (E.G. RUC, ETA)  
C                       (CHARACTER*4) (K=1,NFIELDS).  (OUTPUT)
C             IMAP(K) = MAP PROJECTION OF OUTPUT GRID (K=1,NFIELDS). (OUTPUT)
C                       3 = N.H. LAMBERT 
C                       5 = N.H. POLAR STEREOGRAPHIC
C                       7 = MERCATOR        
C           IPWR10(K) = POWER OF 10 SCALING TO USE FOR TDLPACKING. 
C                       READ FROM THE PDS FILE (K=1,NFIELDS).  
C                       (OUTPUT)
C            IPWR2(K) = POWER OF 2 SCALING TO USE (TDLP VERSION) FOR 
C                       TDLPACKING. READ FROM THE GDS FILE 
C                       (K=1,NFIELDS).  (OUTPUT)
C          IREPEAT(K) = FLAG (HAVING A VALUE OF ONE) INDICATES THAT A 
C                       PARTICULAR ELEMENT IS A DUPLICATE.  (OUTPUT)
C                ND10 = MAXIMUM NUMBER OF ELEMENTS THAT CAN BE PROCESSED  
C                       IN THE ELEMENT LIST FILE.  SET BY PARAMETER IN 
C                       DRU130.  (INPUT)  
C             JCONVRT = FLAG INDICATING IF GRIB1 (=1) OR GRIB2 (=2) DATA
C                       IS TO BE TDLPACKED.  (INPUT)
C            ISTOP(1) = ISTOP( ) IS INCREMENTED BY 1 EACH TIME
C                       AN ERROR OCCURS THAT IS NOT FATAL.  (OUTPUT)
C               JTERM = FLAG HAVING A VALUE OF ONE IF THE TERMINATOR
C                       HAS BEEN REACHED, OTHERWISE ZERO.  (INTERNAL)
C
C SUBROUTINES CALLED: W3TAGE
C
C EXIT STATES:   0 - SUCCESSFUL RUN
C              117 - ELEMENT LIST FILE GT ANTICIPATED MAX.
C              925 - PROBLEM READIN GRIB ELEMENTS IN ELEMENT LIST.
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C$$$
C
      CHARACTER*4  MODELID(ND10)
CINTEL
C      CHARACTER*32 JPLAIN(32,ND10)
      CHARACTER*1 JPLAIN(32,ND10)
CINTEL
C
      DIMENSION IMAP(ND10),IPWR10(ND10),IPWR2(ND10),IREPEAT(ND10)
      DIMENSION ID68(30,ND10),IDMDL(4,ND10)
      DIMENSION ISTOP(2)
C
C        FOR GRIB1, ARRAY 'ID68( )' IS INITIALIZED TO -1. 
C        A VALUE OF -1 MEANS TO ALLOW ANY VALUE OF THIS 
C        PARAMETER TO BE FOUND.  FOR GRIB2, ARRAY 'ID68( )' 
C        IS INITIALIZED TO -9999.
C    
      IF(JCONVRT.EQ.1) THEN
         DO 101 J=1,30
            DO 100 JJ=1,ND10
               ID68(J,JJ)=-1
 100        CONTINUE
 101     CONTINUE
      ELSE
         DO 112 J=1,30
            DO 110 JJ=1,ND10
C               ID68(J,JJ)=-9999
 110        CONTINUE
 112     CONTINUE 
      ENDIF
C
      JTERM=0
C
C        READ ALL THE ELEMENTS IN THE GRID LIST FILE AND PRINT
C        THESE ELEMENTS TO KFILDO.
C      
      K=1
 113  READ(KFILIE,114,ERR=900,END=120) (ID68(J,K),J=1,30)
 114  FORMAT(25I6,I6,2(I6,I10))
C
C        CHECK IF TERMINATOR HAS BEEN REACHED. 
C 
      IF(ID68(1,K).EQ.99999) THEN
         JTERM=1
         GOTO 116
      ENDIF
C
      READ(KFILIE,115,ERR=900,END=120) (IDMDL(J,K),J=1,4),
     1     MODELID(K),IMAP(K),IPWR10(K),IPWR2(K),(JPLAIN(MM,K),MM=1,32)
 115  FORMAT(3I10.9,I10.10,A4,1X,3(I2,1X),1X,32A1)
C 
      K=K+1
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        ELEMENTS IN THE ELEMENT LIST FILE DOES NOT EXCEED THE 
C        MAXIMUM NUMBER OF ANTICIPATED ELEMENTS (ND10) TO BE PACKED 
C        FROM THE ELEMENT LIST FILE.
C 
 116  IF(K.GT.ND10) THEN
         WRITE(KFILDO,117) K,ND10
 117     FORMAT(/,' ****THE NUMBER OF ELEMENTS READ IN THE',
     1            ' ELEMENT LIST FILE (',I4,')',' IS',
     2            ' GREATER',/,5X,'THAN THE ANTICIPATED MAXIMUM',
     3            ' NUMBER OF ELEMENTS TO BE PACKED (ND10 = ',
     4            I4,').',/,5X,'STOP 117 IN RDELMLST.')
         CALL W3TAGE('RDELMLST')
         STOP 117
      ENDIF
C
C        READ THE NEXT ELEMENT IN THE LIST.
C
      IF(JTERM.EQ.0) GOTO 113
C
 120  NFIELDS=K-1
C
      WRITE(KFILDO,145)
 145  FORMAT(/,' THE FOLLOWING VARIABLES ARE FOUND IN',
     1         ' THE ELEMENT LIST FILE AND',
     2         ' ARE TO BE TDLPACKED.',/)
C
      DO 160 K=1,NFIELDS 
         WRITE(KFILDO,114) (ID68(J,K),J=1,30)
         WRITE(KFILDO,147) (IDMDL(J,K),J=1,4),
     1         MODELID(K),IMAP(K),IPWR10(K),IPWR2(K),
     2         (JPLAIN(MM,K),MM=1,32)
 147     FORMAT(3I10.9,1X,I10.10,A4,1X,3(I2,1X),1X,32A1)
 160  CONTINUE
C
C        TEST FOR DUPLICATE ELEMENTS IN THE ELEMENT LIST. 
C        CHECK 'ID68( )' SINCE THIS IS WHERE THE GRIB PRODUCT
C        DEFINITION IS SPECIFIED.
C
C        INITIALIZE 'IREPEAT( )' TO ZERO.
C
      DO 170 K=1,ND10
         IREPEAT(K)=0
 170  CONTINUE
C
      DO 220 K=1,NFIELDS-1
         DO 210 J=K+1,NFIELDS
            DO 200 L=1,30
               IF(ID68(L,K).NE.ID68(L,J)) GOTO 210
 200        CONTINUE
            IREPEAT(J)=1
            ISTOP(1)=ISTOP(1)+1
 210     CONTINUE
 220  CONTINUE           
C
C        WRITE OUT ANY DUPLICATE ELEMENTS TO KFILDO.
C
      DO 400 K=1,NFIELDS
         IF(IREPEAT(K).EQ.1) THEN
            WRITE(KFILDO,300)  
 300        FORMAT(/,' ****THE FOLLOWING ELEMENT IS A',
     1               ' DUPLICATE IN THE ELEMENT',
     2               ' LIST FILE AND WILL BE IGNORED',
     3               ' DURING PROCESSING.')    
            WRITE(KFILDO,114) (ID68(J,K),J=1,30)
            WRITE(KFILDO,147) (IDMDL(J,K),J=1,4),
     1            MODELID(K),IMAP(K),IPWR10(K),IPWR2(K),
     2            (JPLAIN(MM,K),MM=1,32)
         ENDIF
 400  CONTINUE
C
      GOTO 950
C
 900  WRITE(KFILDO,925) KFILIE 
 925  FORMAT(/,' ****PROBLEM READING THE GRIB ELEMENTS IN',
     1         ' THE ELEMENT LIST FILE ON UNIT NUMBER ',
     2         I2,'.',/,5X,'STOP 925 IN RDELMLST.')
      CALL W3TAGE('RDELMLST')
      STOP 925
C
 950  RETURN
      END
