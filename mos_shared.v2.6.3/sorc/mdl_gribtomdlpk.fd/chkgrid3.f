         SUBROUTINE CHKGRID3(KFILDO,IS2,ND7,JMAP,NNXS,NNYS,
     1                       AALAT,AALON,OORIENT,XXLAT,DDX)
C     
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: CHKGID2
C   PRGMMR: RUDACK          ORG: W/OSD21          DATE: 2003-07-01
C
C ABSTRACT: CHECK THAT THE GRID CHARACTERISTICS IN THE GRIDDED
C           RANDOM ACCESS FILE ARE EQUAL TO THOSE FOUND IN
C           THE GRID LIST FILE.  THE LATITUDE AND LONGITUDE
C           COMPARISON BETWEEN GRIDS ARE PERFORMED TO THE NEAREST
C           MILLIDEGREE.  THE TRUE MESH COMPARISON IS PERFORMED
C           TO THE NEAREST MILLIMETER.
C
C USAGE: CALLED BY RDGRIB2
C
C PROGRAM HISTORY LOG:
C    JUNE      2003   RUDACK   MDL   MOS-2000          
C    OCTOBER   2004   RUDACK   CHANGED 'INT( )' FUNCTIONS
C                              TO 'NINT( )'.
C    SEPTEMBER 2005   RUDACK   MODIFIED CODE TO MEET
C                              OPERATIONAL REQUIREMENTS.
C
C DATA SET USE:
C   OUTPUT FILE:
C      FORT.KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C VARIABLES:
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  PARAMETER SET IN DRU130.  
C                       (INPUT)
C                JMAP = MAP PROJECTION OF OUTPUT GRID.  (INPUT)
C                       3 = N.H. LAMBERT 
C                       5 = N.H. POLAR STEREOGRAPHIC
C                       7 = MERCATOR
C                NNXS = X DIMENSION OF OUTPUT GRID.  (INPUT)                       
C                NNYS = Y DIMENSION OF OUTPUT GRID.  (INPUT) 
C               AALAT = LATITUDE OF ORIGIN (1,1) OF OUTPUT GRID (DEGREES N). 
C                       READ FROM THE GLF.  (INPUT)    
C               AALON = LONGITUDE OF ORIGIN (1,1) OF OUTPUT GRID (POSITIVE 
C                       DEGREES WEST).  READ FROM THE GLF. (INPUT)
C             OORIENT = ORIENTATION OF OUTPUT GRID.  MERIDIAN PARALLEL TO Y    
C                       AXIS (POSITIVE DEGREES WEST).  (INPUT)  
C               XXLAT = FOR LAMBERT CONICAL CONFORMAL, THE LATITUDE WHERE
C                       CONE IS TANGENT FOR THE OUTPUT GRID.  FOR NORTH POLAR 
C                       STEREOGRAPHIC, LATITUDE WHERE GRID LENGTH APPLIES.  
C                       (INPUT)
C                 DDX = X INCREMENT (IN KM) AT XLAT OF OUTPUT GRID.  (INPUT) 
C                LTAG = FLAG REPRESENTING THE TYPE OF MAP PROJECTION
C                       FOR WHICH AN ERROR OCCURRED IN COMPARING GRID
C                       CHARACTERISTICS.  (INTERNAL) 
C
C EXIT STATES:
C   COND =   0 - SUCCESSFUL RUN
C          970 - GRID CHARACTERISTICS DO NO MATCH. MAP PROJ, LOWER
C                LEFT LON, GRID ORIENT, TRUE MESH, AND LAT AT TRUE
C                MESH: LTAG =1
C          980 - GRID CHARACTERISTICS DO NO MATCH. MAP PROJ, LOWER
C                LEFT LON, GRID ORIENT, TRUE MESH, AND LAT AT TRUE
C                MESH: LTAG =2
C REMARKS:  NONE
C
C  SUBROUTINES CALLED:  W3TAGE 
C  ATTRIBUTES:
C
C  LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C  MACHINE:  IBM SP
C$$$
C
      DIMENSION IS2(ND7)
C
      LTAG=0
C
C        PLACE THE GRID CHARACTERISTICS FOUND IN THE GRID LIST
C        FILE INTO A FORM THAT WILL ALLOW A COMPARISON WITH VALUES 
C        FOUND IN THE GRIDDED RANDOM ACCESS FILE. 
C
      JALAT=NINT(AALAT*1000)
      JALON=NINT(AALON*1000)
      JORIENT=NINT(OORIENT*1000)
      JXLAT=NINT(XXLAT*1000)
      JDX=NINT(DDX*1000)
C
C        CONVERT THE IS2( ) VALUES TO THE NEAREST MILLIDEGREE AND 
C        MILLIMETER FOR COMPARISON PURPOSES.
C
      IS2_5=INT(IS2(5)/10)
      IS2_6=INT(IS2(6)/10)
      IS2_7=INT(IS2(7)/10)
      IS2_8=INT(IS2(8)/1000)
      IS2_9=INT(IS2(9)/10)
C
C        SINCE THE CHECK THAT ENSURES THAT THE LONGITUDE VALUES 
C        (READ IN FROM THE GRID LIST FILE) ARE POSITIVE HAS
C        ALREADY BEEN PERFORMED IN 'CHKGRID2', THERE IS NO NEED
C        TO RECHECK.
C  
C        CHECK THE GRID CHARACTERISTICS.
C 
      IF((IS2(2).NE.JMAP).OR.
     1   (IS2(3).NE.NNXS).OR.
     2   (IS2(4).NE.NNYS).OR.
     3   (IS2_5.NE.JALAT).OR.
     4   (IS2_6.NE.JALON).OR.
     5   (IS2_7.NE.JORIENT).OR.
     6   (IS2_8.NE.JDX).OR.
     7   (IS2_9.NE.JXLAT)) THEN 
C
         IF((JMAP.EQ.3).OR.(JMAP.EQ.5)) THEN
            LTAG=1
         ELSEIF(JMAP.EQ.7) THEN
            LTAG=2
         ENDIF
C        
C           GOTO ERROR DIAGNOSTIC PRINT.
C
         GOTO 950
C
      ENDIF
C
C        IF ALL THE GRID METADATA IS CONSISTENT EXIT THE PROGRAM. 
      GOTO 985
C
 950  WRITE(KFILDO,960)
 960  FORMAT(/,' ****THE GRID CHARACTERISTICS FOUND IN THE RANDOM',
     1         ' ACCESS FILE DO NOT MATCH THE',/,5X,'GRID',
     2         ' CHARACTERISTICS FOUND IN THE GRID LIST FILE. ',
     3         ' STOP IN CHKGRID3.',/,5X,'NOTE: THE LATITUDE AND',
     4         ' LONGITUDE VALUES PRINTED IN THE TABLE BELOW', 
     5         ' ARE',/,5X,'ROUNDED TO THE NEAREST MILLIDEGREE. ',
     6         ' THE TRUE MESH IS ROUNDED TO THE',/,5X,'NEAREST',
     7         ' MILLIMETER.')    
C
      IF(LTAG.EQ.1) THEN
         WRITE(KFILDO,970) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,
     2                     IS2_7,JORIENT,IS2_8,JDX,
     3                     IS2_9,JXLAT 
 970     FORMAT(/,26X,'VALUES IN RANDOM ACCESS FILE',5X,
     1           'VALUES IN GRID LIST FILE',//,5X,'MAP PROJECTION',21X,
     2           I2,29X,I2,/,5X,'NX',28X,I7,24X,I7,/,5X,'NY',28X,I7,24X,
     3           I7,/,5X,'LOWER LEFT LATITUDE',11X,I7,24X,I7,/,5X,
     4           'LOWER LEFT LONGITUDE',10X,I7,24X,I7,/,5X,
     5           'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     6           'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     7           'LATITUDE AT TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID3')
         STOP 970 
C
      ELSEIF(LTAG.EQ.2) THEN
C
         WRITE(KFILDO,980) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,IS2_7,JORIENT,
     2                     IS2_8,JDX,IS2_9,JXLAT 
 980     FORMAT(/,26X,'VALUES IN RANDOM ACCESS FILE',5X,
     1           'VALUES IN GRID LIST FILE',//,5X,'MAP PROJECTION',
     2           21X,I2,29X,I2,/,5X,'NX',28X,I7,24X,I7,/,5X,'NY',28X,I7,
     3           24X,I7,/,5X,'LOWER LEFT LATITUDE',11X,I7,24X,I7,/,5X,
     4           'LOWER LEFT LONGITUDE',10X,I7,24X,I7,/,5X,
     5           'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     6           'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     7           'LATITUDINAL TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID3')
         STOP 980 
C 
      ENDIF
C
 985  RETURN
      END
