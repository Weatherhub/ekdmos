      SUBROUTINE GETGRID(CMODL,IGRID,IGRIB,II,JJ,IX,IY,IOFFX,IOFFY,
     *          POLEX,POLEY,ORLAT,ORLONG,ORIENT,DX,DY,MAPPROJ,
     *          TANLAT,JUNIT,JERR)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C        MAY   2001    ALLEN    MDL    IBM SP
C                              -MODIFIED SUBROUTINE GTGRID BECAUSE
C  				THE GRID FILE NOW CONTAINS THE MAP
C 				PROJECTION TYPE, AND THE LATITUDE
C				WHERE THE CONE IS TANGENT FOR
C 				LAMBERT CONFORMAL GRIDS
C        SEP   2001    ALLEN    MODIFIED FORMAT STATEMENTS 110 AND 120
C                               TO GO WITH THE GREATER PRECISION WE PUT
C                               IN THE GRIDS FILE
C        JUNE  2002    MALONEY  EDITED COMMENTS FOR TANLAT, DX, AND
C                               MAPPROJ, B/C I ADDED STANDARD LATITUDE OF
C                               60N TO GRID FILE (THIS VALUE WAS
C                               HARDCODED INTO GRB2TDLP BEFORE, WHICH
C                               CAUSED PROBLEMS WITH ANY NON-N.P. 
C                               STEREOGRAPHIC GRIDS.)
C        MARCH 2005    DAGOSTARO INCREASED THE NUMBER OF GRIDS THE CODE CAN
C                                HANDLE FROM 30 TO 50.
C
C        PURPOSE
C            GETS MDL GRID DESCRIPTION CONSTANTS FROM A FILE.
C            THE CONSTANTS ARE READ FROM A FILE THAT CONTAINS THE
C            DESCRIPTION CONSTANTS FOR EVERY GRID MDL USES.
C            SOME OF THESE GRIDS ARE SUBGRIDS OF NMC GRIDS AND
C            THE PROPER OFFSETS ARE ALSO GIVEN.
C            A MAXIMUM OF 50 MODEL GRIDS IS CURRENTLY SUPPORTED.
C
C        DATA SET USE
C     fort.xx     - GRID IDENTIFIERS: 
C                   xx IS THE VALUE IN JUNIT.
C
C        VARIABLES
C      JUNIT      = UNIT NUMBER FOR THE GRID CONSTANT FILE.(INPUT)
C      CMODL      = IDENTIFIER OF GRID REQUESTED (CHAR*4) (INPUT)
C      CMODLS()   = IDENTIFIER OF GRIDS (CHAR*4)
C      IGRID      = NMC ASSIGNED ON-84 VALUE OF GRID. (OUTPUT)
C      IGRIDS()   = NMC ASSIGNED ON-84 VALUE OF GRIDS
C      IGRIB      = NMC ASSIGNED GRIB VALUE OF GRID.(OUTPUT)
C      IGRIBS()   = NMC ASSIGNED GRIB VALUE OF GRIDS
C      II         = X DIMENSION OF GRID. (OUTPUT)
C      IIS()      = X DIMENSION OF GRIDS
C      JJ         = Y DIMENSION OF GRID. (OUTPUT)
C      JJS()      = Y DIMENSION OF GRIDS
C      IX         = X DIMENSION OF NMC'S GRID. (OUTPUT)
C      IXS()      = X DIMENSION OF NMC'S GRIDS
C      IY         = Y DIMENSION OF NMC'S GRID. (OUTPUT)
C      IYS()      = Y DIMENSION OF NMC'S GRIDS
C      IOFFX      = OFFSET OF TDL GRID TO NMC GRID (X DIRECTION) 
C                   (OUTPUT)
C      IOFFXS()   = OFFSETS OF TDL GRID TO NMC GRID (X DIRECTION) 
C      IOFFY      = OFFSET OF TDL GRID TO NMC GRID (Y DIRECTION) 
C                   (OUTPUT)
C      IOFFYS()   = OFFSETS OF TDL GRID TO NMC GRID (Y DIRECTION) 
C      MAPPROJ    = MAP PROJECTION TYPE: (OUTPUT)
C                   NPS = NORTHERN POLAR STEREOGRAPHIC
C                   LAM = LAMBERT CONFORMAL
C                   MER = MERCATOR
C      MAPPROJS() = MAP PROJECTIONS 
C      POLEX      = X COORDINATE OF NORTH POLE  (OUTPUT)
C      POLEXS()   = X COORDINATES OF NORTH POLE
C      POLEY      = Y COORDINATE OF NORTH POLE  (OUTPUT)
C      POLEYS()   = Y COORDINATES OF NORTH POLE  
C      ORLAT      = LATITUDE OF ORIGIN (1,1) OF GRID. (DEGREES N)
C                   (OUTPUT)
C      ORLATS()   = LATITUDES OF ORIGIN (1,1) OF GRID. (DEGREES N)
C      ORLONG     = LONGITUDE OF ORIGIN (1,1) OF GRID. (DEGREES W)
C                   (OUTPUT)
C      ORLONGS()  = LONGITUDES OF ORIGIN (1,1) OF GRID. (DEGREES W)
C      ORIENT     = ORIENTATION OF GRID.  MERIDIAN PARALLEL TO Y
C                   AXIS. (DEGREES WEST)  (OUTPUT)
C      ORIENTS()  = ORIENTATIONS OF GRID.  MERIDIANS PARALLEL TO Y
C                   AXIS. (DEGREES WEST)  
C      DX         = X INCREMENT (IN KM). (OUTPUT)
C      DXS()      = X INCREMENTS (IN KM).
C      DY         = Y INCREMENT (IN KM).  (OUTPUT)
C      DYS()      = Y INCREMENTS (IN KM).  (OUTPUT)
C      NUM        = NUMBER OF GRIDS READ FROM FILE.
C      TANLAT     = FOR LAMBERT CONFORMAL GRIDS, THE LATITUDE WHERE 
C                   CONE IS TANGENT.  FOR NORTH POLAR STEREOGRAPHIC
C                   GRIDS, THE STANDARD LATITUDE WHERE DX IS VALID.
C                   FOR MERCATOR......(OUTPUT)
C      TANLATS    = LATITUDES WHERE CONES ARE TANGENT/STANDARD LAT.
C      JERR       = ERROR RETURN CODE.  (OUTPUT)
C                   0 = NO ERRORS
C                   1 = MAXIMUM # OF GRIDS WERE READ.  MAY NEED TO
C                       INCREASE MAXMOD IN PARAMETER STATEMENT.
C                   2 = ERROR READING GRID FILE.
C                   3 = REQUESTED GRID NOT FOUND
C      MAXMOD     = MAXIMUM NUMBER OF MODELS TO READ FROM GRID FILE.
C
C REMARKS:
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  IBM SP
C
C$$$
      PARAMETER (MAXMOD=50)
      CHARACTER*4 CMODL,CMODLS(MAXMOD)
      CHARACTER*3 MAPPROJ,MAPPROJS(MAXMOD)
      INTEGER IGRIDS(MAXMOD),IGRIBS(MAXMOD),IIS(MAXMOD),JJS(MAXMOD),
     *        IXS(MAXMOD),IYS(MAXMOD),IOFFXS(MAXMOD),IOFFYS(MAXMOD)
      REAL POLEXS(MAXMOD),POLEYS(MAXMOD),ORLATS(MAXMOD),
     *     ORLONGS(MAXMOD),TANLATS(MAXMOD),
     *     ORIENTS(MAXMOD),DXS(MAXMOD),DYS(MAXMOD)
      DATA ISTART/0/
      SAVE ISTART,CMODLS,IGRIDS,IGRIBS,IIS,JJS,IXS,IYS,IOFFXS,
     *     IOFFYS,POLEXS,POLEYS,ORLATS,ORLONGS,ORIENTS,DXS,DYS,
     *     MAPPROJS,TANLATS,NUM
      JERR=0
      IF (ISTART.EQ.0) THEN
	ISTART=1
        NUM=1
C        READ GRID CONSTANTS FOR THE NEXT MODEL
 100    READ(JUNIT,110,END=600,ERR=888) CMODLS(NUM),IGRIDS(NUM),
     *     IGRIBS(NUM),IIS(NUM),JJS(NUM),IXS(NUM),IYS(NUM),
     *     IOFFXS(NUM),IOFFYS(NUM),POLEXS(NUM),POLEYS(NUM),
     *     ORLATS(NUM),ORLONGS(NUM)
 110    FORMAT(A4,8I4,4F10.4)
        READ(JUNIT,120,ERR=888) ORIENTS(NUM),DXS(NUM),DYS(NUM),
     *     MAPPROJS(NUM),TANLATS(NUM)  
 120    FORMAT(4X,F9.4,2F11.6,2X,A3,3X,F8.4)
        IF (NUM.EQ.MAXMOD) THEN
          WRITE(6,130) MAXMOD
 130      FORMAT(' A MAXIMUM OF ',I4,' RECORDS WERE READ FROM THE ',
     *    'GRID FILE.'/' INCREASE MAXMOD IN GTGRID IF MORE MODELS ',
     *    'ARE IN THE GRID FILE.')
          JERR=1
	  NUM=NUM+1
	  GOTO 600
        ENDIF
        NUM=NUM+1
        GOTO 100                                                         
 600    NUM=NUM-1                                                        
      ENDIF
      DO 700 J=1,NUM
	IF (CMODL.EQ.CMODLS(J)) THEN
	  IGRID=IGRIDS(J)
	  IGRIB=IGRIBS(J)
	  II=IIS(J)
	  JJ=JJS(J)
	  IX=IXS(J)
	  IY=IYS(J)
	  IOFFX=IOFFXS(J)
	  IOFFY=IOFFYS(J)
	  POLEX=POLEXS(J)
	  POLEY=POLEYS(J)
	  ORLAT=ORLATS(J)
	  ORLONG=ORLONGS(J)
	  ORIENT=ORIENTS(J)
	  DX=DXS(J)
	  DY=DYS(J)
          MAPPROJ=MAPPROJS(J)
          TANLAT=TANLATS(J)
	  RETURN
	ENDIF
 700  CONTINUE
C        IF FALL THROUGH LOOP, THE REQUESTED GRID (CMODL)
C        WAS NOT FOUND.  RETURN WITH JERR=3
      IGRID=0
      IGRIB=0
      II=0
      JJ=0
      IX=0
      IY=0
      IOFFX=0
      IOFFY=0
      POLEX=0.
      POLEY=0.
      ORLAT=0.
      ORLONG=0.
      ORIENT=0.
      DX=0.
      DY=0.
      MAPPROJ='XXX'
      TANLAT=0.
      JERR=3
      WRITE(6,720) CMODL
 720  FORMAT(' GRID DESCRIPTIONS NOT FOUND FOR: ',A4)
      RETURN
 888  WRITE(6,889)                                                     
 889  FORMAT(' UNEXPECTED ERROR READING GRID FILE.')
      JERR=2
      RETURN                                                           
      END
