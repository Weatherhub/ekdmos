      SUBROUTINE RDSECT1(KFILDO,KFILSC1,LISTSEC0,LISTSEC1,IP12)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDSECT1
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: TO READ THE VALUES FOUND IN THE INDICATOR AND 
C   IDENTIFICATION FILE AND STORE THESE VALUES. 
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES:
C         FORT.KFILSC1 - UNIT NUMBER OF FILE CONTAINING VALUES FOR
C                        SECTIONS 0 AND 1.  (INPUT)
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C            KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                     THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                     IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                     THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                     THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                     FILE MIGHT HAVE THE SAME NAME AND BE
C                     OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                     ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                     BE DIFFERENT FOR EACH RUN.  (INPUT)
C           KFILSC1 = UNIT NUMBER OF FILE CONTAINING VALUES FOR
C                     SECTIONS 0 AND 1.  (INPUT)
C       LISTSEC0(1) = MESSAGE DISCIPLINE ( SEE CODE TABLE 0.0 )
C                       (OUTPUT)
C       LISTSEC0(2) = GRIB EDITION NUMBER (CURRENTLY 2).  (OUTPUT)
C       LISTSEC1(1) = IDENTIFICATION OF ORIGINATING CENTRE
C                     ( SEE COMMON CODE TABLE C-1 ) (OUTPUT)
C                        7 - US NATIONAL WEATHER SERVICE 
C       LISTSEC1(2) = IDENTIFICATION OF ORIGINATING SUB-CENTRE.  (OUTPUT)
C       LISTSEC1(3) = GRIB MASTER TABLES VERSION NUMBER.  (OUTPUT)
C                       ( SEE CODE TABLE 1.0 )
C                        0 - EXPERIMENTAL
C                        1 - INITIAL OPERATIONAL VERSION NUMBER
C       LISTSEC1(4) = GRIB LOCAL TABLES VERSION NUMBER  (OUTPUT)
C                       ( SEE CODE TABLE 1.1 )
C                        0     - LOCAL TABLES NOT USED
C                        1-254 - NUMBER OF LOCAL TABLES VERSION USED
C       LISTSEC1(5) = SIGNIFICANCE OF REFERENCE TIME (CODE TABLE 1.2)  (OUTPUT)
C                        0 - ANALYSIS
C                        1 - START OF FORECAST
C                        2 - VERIFYING TIME OF FORECAST
C                        3 - OBSERVATION TIME
C       LISTSEC1(6) = YEAR ( 4 DIGITS ) (VALUE IS ASSIGNED IN 'RDTLPK'.) 
C                     (OUTPUT)
C       LISTSEC1(7) = MONTH (VALUE IS ASSIGNED IN 'RDTLPK'.)  (OUTPUT)
C       LISTSEC1(8) = DAY (VALUE IS ASSIGNED IN 'RDTLPK'.)  (OUTPUT)
C       LISTSEC1(9) = HOUR (VALUE IS ASSIGNED IN 'RDTLPK'.) (OUTPUT)
C      LISTSEC1(10) = MINUTE (CANNOT BE DETERMINED FROM 'NDATE')  (OUTPUT)
C      LISTSEC1(11) = SECOND (CANNOT BE DETERMINED FROM 'NDATE')  (OUTPUT)
C      LISTSEC1(12) = PRODUCTION STATUS OF PROCESSED DATA.  (OUTPUT)
C                       ( SEE CODE TABLE 1.3 )
C                        0 - OPERATIONAL PRODUCTS
C                        1 - OPERATIONAL TEST PRODUCTS
C                        2 - RESEARCH PRODUCTS
C                        3 - RE-ANALYSIS PRODUCTS
C      LISTSEC1(13) = TYPE OF PROCESSED DATA ( SEE CODE TABLE 1.4 ) (OUTPUT)
C                        0  - ANALYSIS PRODUCTS
C                        1  - FORECAST PRODUCTS
C                        2  - ANALYSIS AND FORECAST PRODUCTS
C                        3  - CONTROL FORECAST PRODUCTS
C                        4  - PERTURBED FORECAST PRODUCTS
C                        5  - CONTROL AND PERTURBED FORECAST PRODUCTS
C                        6  - PROCESSED SATELLITE OBSERVATIONS
C                        7  - PROCESSED RADAR OBSERVATIONS
C               IP12 = UNIT NUMBER TO WHERE THE LIST OF VALUES PERTAINING
C                      TO SECTIONS 0 AND 1 IN THE GRIB2 MESSAGE ARE PRINTED.  
C                      THESE VALUES ARE SET IN THE GRIB2 INDICATOR AND
C                      IDENTIFICATION INPUT FILE.  (INPUT)
C              JTERM = TERMINATOR VALUE FOUND IN THE INPUT FOR SECTION 1.
C                      (INTERNAL)
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  125 - ERROR READING TERMINATOR
C                  225 - ERROR READING KFILSC1
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      DIMENSION LISTSEC0(2),LISTSEC1(13)
C
      DATA JTERM/99999999/
C
C        READ ALL THE ENTRIES IN THE INDICATOR AND IDENTIFICATION 
C        LIST FILE (SECTIONS 0 AND 1).
C      
      READ(KFILSC1,114,ERR=220,END=120) LISTSEC0(1),LISTSEC0(2),
     1                                  (LISTSEC1(N),N=1,5),
     2                                  LISTSEC1(12),
     3                                  LISTSEC1(13),ITERM
 114  FORMAT(9(I10)/I10)
C
C        CHECK IF TERMINATOR HAS BEEN REACHED. 
C 
 120  IF(ITERM.NE.99999999) THEN
         WRITE(KFILDO,125) KFILSC1
 125     FORMAT(/,' ****PROBLEM READING THE TERMINATOR SUPPLIED TO',
     1            ' THE INDICATOR AND IDENTIFICATION LIST FILE',/,4X,
     2            ' ON UNIT NUMBER ',I2,'.  STOP 125 IN RDSECT1.')
         CALL W3TAGE('RDSECT1')
         STOP 125
      ENDIF
C
      IF(IP12.GT.0) THEN
         WRITE(IP12,145)
 145     FORMAT(/,' THE FOLLOWING ENTRIES FOUND IN',
     1            ' THE INDICATOR AND IDENTIFICATION SECTION',/,
     2            ' ARE TO BE PLACED INTO GRIB2:',/)
         WRITE(IP12,114) LISTSEC0(1),LISTSEC0(2),
     1                   (LISTSEC1(N),N=1,5),LISTSEC1(12),
     2                   LISTSEC1(13),ITERM
      ENDIF
C
      GOTO 950 
C
 220  WRITE(KFILDO,225) KFILSC1
 225  FORMAT(/,' ****PROBLEM READING THE VALUES SUPPLIED TO',
     1            ' THE INDICATOR AND IDENTIFICATION LIST FILE',
     2            ' ON UNIT NUMBER',I2,'.',/,5X,'STOP 225 IN',
     3            ' RDSECT1.')
      CALL W3TAGE('RDSECT1')
      STOP 225
C
 950  RETURN
      END
