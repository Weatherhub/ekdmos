      SUBROUTINE RDSECT2(KFILDO,KFILSC2,ISCT2LEN,FSCT2,NFSCT2,
     1                   ND10,ND12,IP13,JBIRG2,NGROUPS2,JGROUPS2)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDSECT2
C   PRGMMR: HUNTEMANN      ORG: W/OST24          DATE: 2013-11-15
C
C ABSTRACT: TO READ THE VALUES FOUND IN THE LOCAL USE FILE 
C   AND STORE THESE VALUES. 
C
C PROGRAM HISTORY LOG:
C   13-11-15  HUNTEMANN   ADDED TO OPERATIONAL REPOSITORY 
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES:
C        FORT.KFILSC2 - UNIT NUMBER OF FILE CONTAINING THE LOCAL 
C                       USE SECTION LIST. (INPUT)
C        OUTPUT FILES:
C         FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C                        
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C             KFILSC2 = UNIT NUMBER OF FILE CONTAINING THE LOCAL 
C                       USE SECTION.  (INPUT)
C        IDMDL(4,K,J) = FOUR WORD MOS-2000 ID TO BE PROCESSED. 
C                       (K=1,ND10) (J=1,ND12) (INTERNAL)
C         NLINES(K,J) = NUMBER OF WEATHER KEY STRINGS TO BE PROCESSED IN
C                       THIS FIELD OF THE CURRENT GROUP.
C                       (K=1,ND10) (J=1,ND12) (INTERNAL)
C        LINE(10,K,J) = WEATHER KEY STRING TO BE PROCESSED.
C                       (K=1,ND10) (J=1,ND12) (INTERNAL)
C            ISCT2LEN = MAXIMUM DIMENSION OF FSCT2( ).  LOCAL USE
C                       SECTION OF THE GRIB2 MESSAGE (SECTION 2). (INPUT)
C        FSCT2(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED LOCAL
C                       USE FILE. EACH INTEGER IN THE ARRAY IS A DECIMAL
C                       REPRESENTATION THAT CORRESPONDS TO AN ASCII
C                       CHARACTER. 0 IN THE ARRAY MARKS THE ENDING OF A
C                       UNIQUE WEATHER KEY. (N=1,NFSCT2) (K=1,ND10) 
C                       (J=1,ND12) (OUTPUT)
C         NFSCT2(K,J) = DIMENSION OF FSCT2(N,K,J).  LOCAL USE SECTION 
C                       OF THE GRIB2 MESSAGE (SECTION 2). 
C                       (K=1,ND10) (J=1,ND12) (OUTPUT)
C                ND10 = MAXIMUM NUMBER OF FIELDS ALLOWED IN ANY ONE
C                       SPECIFIC GROUP.  (INPUT)
C                ND12 = MAXIMUM NUMBER OF GROUPS (OR MESSAGES) THAT CAN
C                       BE PROCESSED IN ONE RUN OF U135.  (INPUT)
C                IP13 = UNIT NUMBER TO WHERE THE LIST OF VALUES PERTAINING
C                       TO SECTION 2 IN THE GRIB2 MESSAGE ARE PRINTED.
C                       THESE VALUES ARE SET IN THE GRIB2 LOCAL USE SECTION
C                       INPUT FILE.  (INPUT)
C         JBIRG2(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                       FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                       CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                       BEING PROCESSED (PERTAINING TO SECTION 2)
C                       (K=1,ND10) (J=1,ND12).  (OUTPUT)
C               ITERM = VALUE OF '99999999' INDICATING THAT THE FINAL 
C                       GROUP HAS BEEN REACHED AND THAT ALL WEATHER
C                       KEYS ARE READY TO BE PROCESSED. (INTERNAL)
C            NGROUPS2 = TOTAL NUMBER OF GROUPS (OR MESSAGES) THAT WILL BE
C                       PROCESSED IN ONE RUN OF U135 PERTAINING TO SECTION 2.
C                       (OUTPUT)
C         JGROUPS2(J) = NUMBER OF ENTRIES (GRIDS) IN EACH GROUP THAT WILL BE
C                       PLACED INTO GRIB2 PERTAINING TO SECTION 2 (J=1,ND12).  
C                       (OUTPUT)
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  221 - NUMBER OF RECORDS READ EXCEEDS ND10
C                  230 - NUMBER OF GROUPS READ EXCEEDS ND12
C                  925 - ERROR READING KFILSC2
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$

      DIMENSION IDMDL(4,ND10,ND12),NLINES(ND10,ND12),
     1          FSCT2(ISCT2LEN,ND10,ND12),NFSCT2(ND10,ND12),
     2          JBIRG2(ND10,ND12),JGROUPS2(ND12)
C
      CHARACTER*80 LINE(10,ND10,ND12)
C
      JTERM=0
C
C     READ THE ENTRIES IN THE LOCAL USE FILE.
C
      K=1
      J=1
C
 110  READ(KFILSC2,111,ERR=900,END=240) (IDMDL(N,K,J),N=1,4),NLINES(K,J)
 111  FORMAT(3I10.9,I4.3,I3)
C    
      NFSCT2(K,J)=0 
C
      DO 116 NN=1,NLINES(K,J)
         READ(KFILSC2,115,ERR=900,END=240) LINE(NN,K,J)
 115     FORMAT(A80)
C 
         IF(LINE(NN,K,J)(1:6).EQ."999999")THEN
            GOTO 900
         ENDIF
C
C        ASSIGN CHARACTERS TO AN DECIMAL ARRAY.
C
         I = 1
         DO WHILE((I.LE.LEN(LINE(NN,K,J))).AND.
     1          (LINE(NN,K,J)(I:I).NE.' '))
            NFSCT2(K,J)=NFSCT2(K,J)+1
            FSCT2(NFSCT2(K,J),K,J)=ICHAR(LINE(NN,K,J)(I:I))
            I=I+1
         END DO
C
         NFSCT2(K,J)=NFSCT2(K,J)+1
         FSCT2(NFSCT2(K,J),K,J) = 0
 116  CONTINUE
C
      READ(KFILSC2,117,ERR=900,END=240) ITERM,JBIRG2(K,J),NFLAG
 117  FORMAT(3I10)
C
C        TALLY THE NUMBER OF ENTRIES IN EACH GROUP.
C
      JGROUPS2(J)=JGROUPS2(J)+1
C
C        IF THE '777777' TERMINATOR HAS BEEN REACHED, INCREMENT TO
C        THE NEXT GROUP.
C
      IF(JBIRG2(K,J).EQ.777777) THEN
         J=J+1
         INEW=1
      ENDIF
C 
C        CHECK IF TERMINATOR HAS BEEN REACHED. 
C
      IF(NFLAG.EQ.99999999) THEN
         JTERM=1
         GOTO 220
      ENDIF
C
       IF(INEW.EQ.1) THEN
          INEW=0
          K=1
       ELSE 
          K=K+1
       ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        ENTRIES IN THE LOCAL USE FILE DOES NOT EXCEED THE 
C        MAXIMUM NUMBER OF ANTICIPATED ENTRIES (ND10)
C        TO BE PROCESSED.
C 
 220  IF(K.GT.ND10) THEN
         WRITE(KFILDO,221) K,ND10
 221     FORMAT(/,' ****THE NUMBER OF RECORDS READ IN THE',
     1            ' LOCAL USE FILE (',I4,')',' IS',
     2            ' GREATER',/,5X,'THAN THE ANTICIPATED MAXIMUM',
     3            ' NUMBER OF RECORDS TO BE PACKED (ND10 = ',
     4            I4,').  STOP 221 IN RDSECT2.')
         CALL W3TAGE('RDSECT2')
         STOP 221
      ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        GROUPS IN THE LOCAL USE SECTION FILE DOES NOT EXCEED
C        THE MAXIMUM NUMBER OF ANTICIPATED GROUPS (ND12) TO
C        BE PROCESSED.
C
      IF(JGROUPS2(J).GT.(ND12-1)) THEN
         WRITE(KFILDO,230) JGROUPS2(J),ND12
 230     FORMAT(/,' ****THE NUMBER OF GROUPS READ IN THE',
     1            ' LOCAL USE FILE (',I4,') IS GREATER',/,5X,
     2            ' THAN THE ANTICIPATED MAXIMUM NUMBER OF',
     3            ' GROUPS TO BE PACKED (ND12 = ',I4,'). ',
     4            ' STOP 230 IN RDSECT2.')
         CALL W3TAGE('RDSECT2')
         STOP 230
      ENDIF
C
C        READ THE NEXT ENTRY IN THE LOCAL USE FILE.
C
      IF(JTERM.EQ.0) GOTO 110
C
 240  NGROUPS2=J-1
C
      IF(IP13.GT.0) THEN
         WRITE(IP13,245)
 245     FORMAT(/,' THE FOLLOWING ENTRIES ARE FOUND IN',
     1            ' THE LOCAL USE FILE AND',
     2            ' ARE TO BE PLACED INTO GRIB2:',/)
         DO 265 J=1,NGROUPS2  
            DO 260 K=1,JGROUPS2(J)
               WRITE(IP13,111) (IDMDL(N,K,J),N=1,4),NLINES(K,J)
               WRITE(IP13,115) (LINE(I,K,J),I=1,NLINES(K,J))
               WRITE(IP13,117) ITERM,JBIRG2(K,J)
 260        CONTINUE
 265     CONTINUE
      ENDIF 
C
      GOTO 950
C     
 900  WRITE(KFILDO,925) KFILSC2 
 925  FORMAT(/,' ****PROBLEM READING THE GRIB ENTRIES IN',
     1         ' THE LOCAL USE FILE ON UNIT NUMBER ',
     2         I2,'.',/,5X,'STOP 925 IN RDSECT2.')
      CALL W3TAGE('RDSECT2')
      STOP 925
C
 950  RETURN
      END
