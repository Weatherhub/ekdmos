      SUBROUTINE RDSECT4(KFILDO,KFILSC4,IDMDL,JPLAIN,IPDSNUM,
     1                   IPDSTMPL,IPDSTMPLEN,ND10,ND12,IP15,
     2                   JBIRG4,NGROUPS4,JGROUPS4,ISTOP) 
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDSECT4
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: TO READ THE VALUE FOUND IN THE PRODUCT DEFINITION FILE 
C   AND STORE THESE VALUES.
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C   06-02-14  RLC     MODIFIED THE READ STATEMENT TO ALLOW FOR MORE
C                     ENTRIES IN THE SECT4 FILE FOR PROPER USE OF
C                     PDT 4.9.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES:
C         FORT.KFILSC4 - UNIT NUMBER OF FILE CONTAINING THE PRODUCT
C                        DEFINITION DESCRIPTOR LIST.  (INPUT)
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C             KFILSC4 = UNIT NUMBER OF FILE CONTAINING THE PRODUCT 
C                       DEFINITION DESCRIPTOR LIST.  (INPUT)
C        IDMDL(4,K,J) = FOUR WORD MOS-2000 ID TO BE PROCESSED (K=1,ND10),
C                       (J=1,ND12).  (OUTPUT)
C      JPLAIN(32,K,J) = THE PLAIN LANGUAGE DESCRIPTION OF THE VARIABLES
C                       READ FROM THE PRODUCT DEFINITION FILE (K=1,ND10),
C                       (J=1,ND12).  (CHARACTER*32)  (OUTPUT)
C        IPDSNUM(K,J) = PRODUCT DEFINITION TEMPLATE NUMBER (SEE CODE TABLE 4.0)
C                       (K=1,ND10),(J=1,ND12).  (OUTPUT)
C     IPDSTMPL(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED PRODUCT 
C                       DEFINITION TEMPLATE (N=IPDSNUM).  EACH ELEMENT OF 
C                       THIS INTEGER ARRAY CONTAINS AN ENTRY (IN THE ORDER 
C                       SPECIFIED) OF THE PRODUCT DEFINITION TEMPLATE 4.N. 
C                       (K=1,ND10),(J=1,ND12).  (OUTPUT)
C          IPDSTMPLEN = MAXIMUM DIMENSION OF IPDSTMPL(N,K,J).  PRODUCT
C                       DEFINITION SECTION OF THE GRIB2 MESSAGE (SECTION 4).
C                       (INPUT)
C                ND10 = MAXIMUM NUMBER OF FIELDS ALLOWED IN ANY ONE
C                       SPECIFIC GROUP.  (INPUT)
C                ND12 = MAXIMUM NUMBER OF GROUPS (OR MESSAGES) THAT CAN
C                       BE PROCESSED IN ONE RUN OF U135.  (INPUT)
C                IP15 = UNIT NUMBER TO WHERE THE LIST OF VALUES PERTAINING
C                       TO SECTION 4 IN THE GRIB2 MESSAGE ARE PRINTED.
C                       THESE VALUES ARE SET IN THE GRIB2 PRODUCT DEFINITION
C                       INPUT FILE.  (INPUT)
C         JBIRG4(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                       FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                       CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                       BEING PROCESSED (PERTAINING TO SECTION 4)
C                       (K=1,ND10),(J=1,ND12).  (OUTPUT)
C            NGROUPS4 = TOTAL NUMBER OF GROUPS (OR MESSAGES) THAT WILL BE
C                       PROCESSED IN ONE RUN OF U135 PERTAINING TO SECTION 4.
C                       (OUTPUT)
C         JGROUPS4(J) = NUMBER OF ENTRIES (GRIDS) IN EACH GROUP THAT WILL BE
C                       PLACED INTO GRIB2 PERTAINING TO SECTION 4 (J=1,ND12).  
C                       (OUTPUT)
C            ISTOP(J) = FOR J=1, ISTOP( ) IS INCREMENTED BY 1 EACH TIME
C                       FOR J=2, ISTOP( ) IS INCREMENTED BY 1 WHENEVER
C                       AN INPUT DATA RECORD IS NOT FOUND.  (INPUT/OUTPUT)
C               JTERM = FLAG HAVING A VALUE OF ONE IF THE TERMINATOR
C                       HAS BEEN REACHED, OTHERWISE ZERO.  (INTERNAL)
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  117 - TOO MANY RECORDS IN PRODUCT DEFN. FILE
C                  119 - TOO MANY GROUPS IN INDICATOR & ID LIST FILE
C                  925 - ERROR READING PRODUCT DEFINITION FILE
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      CHARACTER*32 JPLAIN(32,ND10,ND12)
C
      DIMENSION IPDSNUM(ND10,ND12),IPDSTMPL(IPDSTMPLEN,ND10,ND12),
     1          IDMDL(4,ND10,ND12),JBIRG4(ND10,ND12),
     2          JGROUPS4(ND12),ISTOP(2)
C
      JTERM=0
C
C        READ ALL THE ENTRIES IN THE PRODUCT DEFINITION FILE.
C      
      K=1
      J=1
C
 113  READ(KFILSC4,114,ERR=900,END=120) (IDMDL(N,K,J),N=1,4),
     1                                  (JPLAIN(MM,K,J),MM=1,32)
 114  FORMAT(3I10.9,1X,I10.10,1X,32A1)
C
      READ(KFILSC4,115,ERR=900,END=120) IPDSNUM(K,J),
     1                          (IPDSTMPL(N,K,J),N=1,IPDSTMPLEN),ITERM,
     2                           JBIRG4(K,J)
 115  FORMAT(3(10I10/),7I10,/,2I10)
C
C        TALLY THE NUMBER OF ENTRIES IN EACH GROUP.
C
      JGROUPS4(J)=JGROUPS4(J)+1
C
C        IF THE '777777' TERMINATOR HAS BEEN REACHED, INCREMENT TO
C        THE NEXT GROUP.
C
      IF(JBIRG4(K,J).EQ.777777) THEN
         J=J+1
         INEW=1
      ENDIF
C 
C        CHECK IF TERMINATOR HAS BEEN REACHED. 
C
      IF(IDMDL(1,1,J).EQ.99999999) THEN
         JTERM=1
         GOTO 116
      ENDIF
C 
       IF(INEW.EQ.1) THEN
          INEW=0
          K=1
       ELSE 
          K=K+1
       ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        ENTRIES IN THE PRODUCT DEFINITION LIST FILE DOES NOT
C        EXCEED THE MAXIMUM NUMBER OF ANTICIPATED ENTRIES (ND10)
C        TO BE PROCESSED.
C 
 116  IF(K.GT.ND10) THEN
         WRITE(KFILDO,117) K,ND10
 117     FORMAT(/,' ****THE NUMBER OF RECORDS READ IN THE',
     1            ' PRODUCT DEFINITION FILE (',I4,')',' IS',
     2            ' GREATER',/,5X,'THAN THE ANTICIPATED MAXIMUM',
     3            ' NUMBER OF RECORDS TO BE PACKED (ND10 = ',
     4            I4,').  STOP 117 IN RDSECT4.')
         CALL W3TAGE('RDSECT4')
         STOP 117
      ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        GROUPS IN THE INDICATOR AND IDENTIFICATION LIST FILE
C        DOES NOT EXCEED THE MAXIMUM NUMBER OF ANTICIPATED
C        GROUPS (ND12) TO BE PROCESSED.
C
      IF(JGROUPS4(J).GT.(ND12-1)) THEN
         WRITE(KFILDO,119) JGROUPS4(J),ND12
 119     FORMAT(/,' ****THE NUMBER OF GROUPS READ IN THE',
     1            ' INDICATOR AND IDENTIFICATION DEFINITION',
     2            ' FILE (',I4,') IS GREATER',/,5X,
     3            ' THAN THE ANTICIPATED MAXIMUM NUMBER OF',
     4            ' GROUPS TO BE PACKED (ND12 = ',I4,'). ',
     5            ' STOP 119 IN RDSECT4.')
         CALL W3TAGE('RDSECT4')
         STOP 119
      ENDIF
C
C        READ THE NEXT ENTRY IN THE PRODUCT DEFINITION LIST.
C
      IF(JTERM.EQ.0) GOTO 113
C
 120  NGROUPS4=J-1
C
      IF(IP15.GT.0) THEN
         WRITE(IP15,145)
 145     FORMAT(/,' THE FOLLOWING ENTRIES ARE FOUND IN',
     1            ' THE PRODUCT DEFINITION FILE AND',
     2            ' ARE TO BE PLACED INTO GRIB2:',/)
         DO 165 J=1,NGROUPS4  
            DO 160 K=1,JGROUPS4(J)
               WRITE(IP15,114) (IDMDL(N,K,J),N=1,4),
     1                         (JPLAIN(MM,K,J),MM=1,32)
               WRITE(IP15,115) IPDSNUM(K,J),
     1                         (IPDSTMPL(N,K,J),N=1,IPDSTMPLEN),
     2                         ITERM,JBIRG4(K,J)
 160        CONTINUE
 165     CONTINUE
      ENDIF 
C
      GOTO 950
C
 900  WRITE(KFILDO,925) KFILSC4 
 925  FORMAT(/,' ****PROBLEM READING THE GRIB ENTRIES IN',
     1         ' THE PRODUCT DEFINITION FILE ON UNIT NUMBER ',
     2         I2,'.',/,5X,'STOP 925 IN RDSECT4.')
      CALL W3TAGE('RDSECT4')
      STOP 925
C
 950  RETURN
      END
