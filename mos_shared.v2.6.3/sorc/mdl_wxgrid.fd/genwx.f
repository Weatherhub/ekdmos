      SUBROUTINE GENWX(KFILDO,KFIL10,ID,IDPARS,JD,NDATE,
     1                 KFILRA,RACESS,NUMRA,CCALL,ICALLD,
     2                 CCALLD,NAME,STALAT,STALON,
     3                 ISDATA,SDATA,DIR,ND1,NSTA,
     4                 NGRIDC,NGRID,ND11,NSLAB,IPACK,IWORK,DATA,ND5,
     5                 LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     6                 NBLOCK,LASTD,NSTORE,NFETCH,
     7                 IS0,IS1,IS2,IS4,ND7,
     8                 KFILAC,IP20,IP21,ND2X3,
     9                 IP12,ISTAV,L3264B,L3264W,
     A                 THRPHS,THRQPF,THRTRW,THRSVR,
     B                 IPHSBC,PHSTMP,NSVRLOC,NX,NY,IER)
C 
C        MARCH     2010   HUNTEMANN   MDL   MOS-2000 
C        MARCH     2013   HUNTEMANN   MDL   UPDATED DOCUMENTATION,
C                                           ADDED IMPLICIT NONE,
C                                           ADDED JSTA TO CALLS,
C                                           REORDERED CALLING SEQUENCES
C
C        PURPOSE
C
C           SUBROUTINE GENWX GENERATES THE MOS WEATHER GRID.  GRIDS OF
C           WEATHER ARE WRITTEN AS TWO DISTINCT ENTITIES.  THE FIRST
C           ENTITY IS A GRID OF SMALL INTEGER VALUES WHICH ARE WRITTEN TO 
C           TDLPACK.  THE SECOND ENTITY IS A SERIES OF NULL-TERMINATED 
C           STRINGS OF ASCII CHARACTERS CALLED KEYS (WEATHER KEYS) 
C           WHICH ARE WRITTEN TO AN ASCII OUTPUT FILE.  THE INTEGERS ON 
C           THE WEATHER GRID SERVE AS INDICES, EACH POINTING TO ONE OF 
C           THE UNIQUE KEYS IN THE SERIES. 
C
C           COORDINATED MOS DATA AT EACH GRIDPOINT AND PROJECTION IS
C           USED TO DETERMINE PRECIPITATION POTENTIAL INDEX,
C           PRECIPITATION PHASE POTENTIALS, THUNDERSTORM AND SEVERE
C           THUNDERSTORM POTENTIALS, AND CATEGORICAL PRECIPITATION
C           INTENSITY AND CHARACTER INITIALIZATIONS FOR EACH GRIDPOINT.
C           PRECIPITATION POTENTIALS ARE THEN CATEGORIZED.  PRECIPITATION
C           CATEGORY, PHASE, CHARACTER, AND INTENSITY ARE COMBINED TO 
C           CREATE UP TO A MAXIMUM OF THREE EXPLICIT WEATHER ELEMENTS.
C           THUNDERSTORM CATEGORY AND SEVERE THUNDERSTORM CATEGORY
C           CREATE UP TO TWO ADDITIONAL WEATHER ELEMENTS. THESE (UP TO)
C           FIVE WEATHER ELEMENTS ARE COMBINED TO GENERATE THE WEATHER
C           KEYS WHICH ARE THEN INDEXED. SEE CALLED ROUTINES FOR 
C           ADDITIONAL DETAILS.
C
C           IF NO POP IS AVAILABLE, THE WEATHER GRID CANNOT BE COMPUTED.
C           OBSTRUCTION TO VISION/VISIBILITY CODES ARE NOT CURRENTLY 
C           IMPLEMENTED.
C
C           THIS ROUTINE IS FOR GRIDPOINT DATA.
C
C            COMPUTES         FROM  
C               228 500       222 030   GRIDDED MOS TEMPERATURE       AND
C                             223 030   GRIDDED MOS DEWPOINT          AND
C                             228 630   GRIDDED MOS POPO3             AND
C                             223 290   GRIDDED HR MOS POP6           AND
C                             223 390   GRIDDED HR MOS POP12          AND
C                             223 250   GRIDDED MOS POP6              AND
C                             223 350   GRIDDED MOS POP12             AND
C                             228 545   GRIDDED MOS COND P-TYPE       AND
C                             828 545   GRIDDED MOS P-TYPE THRESHOLDS AND
C                             223 293   GRIDDED HR MOS 6-H QPF        AND
C                             223 270   GRIDDED MOS 6-H QPF           AND
C                             227 120   GRIDDED MOS 3-H TSTM          AND
C                             227 220   GRIDDED MOS 6-H TSTM          AND
C                             227 230   GRIDDED MOS 6-H TSTM          AND
C                             227 330   GRIDDED MOS 12-H TSTM         AND
C                             227 170   GRIDDED MOS 3-H UNCSVR TSTM   AND
C                             227 380   GRIDDED MOS 12-H UNCSVR TSTM
C
C           GRIDPOINT DATA ARE RETURNED IN DATA( ).
C               
C        DATA SET USE 
C
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C              IP12 - INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                     STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                     THE FILE WHOSE UNIT NUMBER IS IP12.  (OUTPUT)
C         KFILRA(J) - THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                     ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C            KFILAC - UNIT NUMBER FOR ASCII OUTPUT OF WEATHER KEY 
C                     LIST (OUTPUT)
C              IP20 - UNIT NUMBER FOR ASCII OUTPUT OF WEATHER KEY
C                     STATION VALUES OF WEATHER KEYS FOR STATIONS
C                     IN STATION LIST. (INPUT)
C              IP21 - UNIT NUMBER FOR ASCII  OUTPIT OF WEATHER KEY LIST
C                     WITH INDICES FOR TROUBLESHOOTING. (OUTPUT)
C 
C        VARIABLES 
C
C           MOS2K SYSTEM VARIABLES (INPUT)
C
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) 
C                       FILE. (INPUT)
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT-OUTPUT) 
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTORID CORRESPONDING TO ID( ) 
C                       (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C             JD(J,N) = THE BASIC INTEGER VARIABLE ID (J=1,4) (N=1,NPRED).
C                       THIS IS THE SAME AS ID(J,N), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3, ),
C                       T = IDPARS(8, ),
C                       I = IDPARS(13, ),
C                       S = IDPARS(14, ),
C                       G = IDPARS(15, ), AND
C                       THRESH( ).
C                       JD( , ) IS USED TO FOR INPUT TO CONSTG, BECAUSE
C                       INTERPOLATION INTO THE GRID MAY BE REQUIRED,
C                       BUT IS NOT PART OF THE BASIC ID.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C           KFILRA(J) = THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                       ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES ASSOCIATED WITH KFILRA(J)
C                       (J=1,NUMRA).(CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF VALUES IN KFILRA( ) AND RACESS( ).
C                       (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ).  EQUIVALENCED TO ICALL( , , ). 
C                       (CHARACTER*8)  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).  THIS ARRAY IS USED 
C                       TO READ THE STATION DIRECTORY FROM A MOS-2000
C                       EXTERNAL FILE.  EQUIVALENCED TO CCALLD( ). 
C                       (CHARACTER*8)  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS ARRAY IS USED 
C                       IN CONST TO READ THE STATION DIRECTORY.  EQUIVALENCED 
C                       TO ICALLD( , ).  (CHARACTER*8)  (INTERNAL)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT/OUTPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT/OUTPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C            SDATA(K) = DATA RETURNED WHEN DATA ARE VECTOR (K=1,NSTA).
C                       (OUTPUT)
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN NGRIDC( ,M).
C                       (INPUT/OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                       (INPUT/OUTPUT)
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN DIR( , , ),
C                       MAXIMUM OF ND11.  (INPUT/OUTPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ) AND DIR( , , ).  (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , , ) AND
C                       IN GRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID.  SEE LSTORE(10, ).  FOR THE
C                       COMPUTATION ROUTINES RETURNING A GRID, THIS
C                       VALUE MUST BE OUTPUT BY GFETCH.
C                       NSLAB = 0 FOR VECTOR DATA (AND FOR NO DATA
C                       RETURNED AND NWORDS = 0), AS STORED BY GSTORE;
C                       OTHERWISE, GRIDDED DATA ARE INDICATED.  (OUTPUT) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = ARRAY TO HOLD RETURNED DATA WHEN THE DATA ARE
C                       AT GRIDPOINTS. (J=1,ND2X3).  (OUTPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), DATA( ) AND
C                       CCALLD( ); SECOND DIMENSION OF ICALLD( , ).
C                       THESE ARE GENERAL PURPOSE ARRAYS, SOMETIMES USED
C                       FOR GRIDS.  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C               LASTL = THE LAST LOCATION IN CORE( ) USED FOR MOS-2000 INTERNAL
C                       STORAGE.  INITIALIZED TO 0 ON FIRST ENTRY TO GSTORE.
C                       ALSO INITIALIZED IN U201 IN CASE GSTORE IS NOT ENTERED.
C                       MUST BE CARRIED WHENEVER GSTORE IS TO BE CALLED.
C                       (INPUT/OUTPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR MOS-2000
C                       INTERNAL STORAGE.  MUST BE CARRIED WHENEVER GSTORE
C                       IS TO BE CALLED.  (INPUT)
C              NSTORE = THE NUMBER OF TIMES GSTORE HAS BEEN ENTERED.  GSTORE
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.  GFETCH
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C              KFILAC = UNIT NUMBER OF ASCII WEATHER KEY LIST FILE.
C                       (INPUT)
C                IP20 = INDICATES WHETHER (>0) OR NOT (=0) A DIAGNOSTIC
C                       WEATHER KEY LIST PRINTED WITH CORRESPONDING
C                       INDICES FROM THE GRID WILL BE WRITTEN TO IP20.
C                       SAME OUTPUT AS KFILAC, EXCEPT WITH INDICES FOR 
C                       CHECKOUT. (INPUT)
C                IP21 = STATION VALUES OF WEATHER KEYS FOR STATIONS
C                       IN STATION LIST. (INPUT)
C               ND2X3 = DIMENSION OF GRIDDED VARIABLES. (INPUT)
C                IP12 = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                       THE FILE WHOSE UNIT NUMBER IS IP12.
C               ISTAV = 1 WHEN THE DATA RETURNED ARE VECTOR (STATION)
C                         DATA.  
C                       0 WHEN THE DATA RETURNED ARE GRID DATA.
C                         THIS ESSENTIALLY TELLS U720 (PRED25/PRED26)
C                         HOW TO TREAT THE DATA, EVEN IF MISSING (9999)
C                        (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.
C                       (INPUT)
C                 IER = STATUS RETURN.
C                         0 - GOOD RETURN.
C                        47 - DATA NOT RETURNED FROM GTVEGR.
C                       103 - IDPARS(1) AND IDPARS(2) NOT ACCOMMODATED IN
C                             THIS ROUTINE.
C                       180 - VECTOR DATA WAS RETURNED, NOT GRIDDED.
C                       187 - PROJECTION NOT ACCOMMODATED IN THIS ROUTINE.
C                       200 - CANNOT MAKE WEATHER FORECAST
C                       300 - WEATHER FORECAST RETURNED IS INCOMPLETE
C                       SEE CALLED ROUTINES FOR OTHER VALUES.
C                       (INTERNAL-OUTPUT)
C
C           WEATHER GRID VARIABLES (INPUT)
C
C                  NX = NUMBER OF GRIDPOINTS IN X DIRECTION. (INPUT)
C                  NY = NUMBER OF GRIDPOINTS IN Y DIRECTION. (INPUT)
C              IPHSBC = FLAG FOR USING CONDITIONAL PRECIPITATION TYPE
C                       BEST CATEGORY THRESHOLDS:
C                          1 - USE THRESHOLDS
C                       ELSE - DON'T USE THRESHOLDS
C                       SEE SUBROUTINE WXPPHS FOR MORE INFORMATION.
C                       (INPUT)
C              PHSTMP = PRECIPITATION PHASE CUTOVER TEMPERATURE
C                       THRESHOLD TO LIQUID.  IF THE MOS FORECAST
C                       TEMPERATURE AT A GRIDPOINT IS AT OR ABOVE 
C                       PHSTMP, THE PRECIPITATION PHASE IS INITIALIZED 
C                       TO LIQUID. SEE SUBROUTINE WXPPHS FOR MORE
C                       INFORMATION. (INPUT)
C           THRPHS(I) = PRECIPITATION PHASE POTENTIAL THRESHOLDS, 
C                       EXPRESSED IN PERCENT, OF THE FOUR PERMITTED 
C                       CATEGORICAL PROBABILITY FORECASTS. ELEMENTS 
C                       WITHIN THRPHS MUST BE BETWEEN 0 AND 100 PERCENT, 
C                       INCLUSIVE, AND BE MONOTONICALLY INCREASING.
C                       (I=1,4) (INPUT)
C           THRQPF(I) = QPF6 THRESHOLDS, EXPRESSED IN PERCENT, 
C                       OF THE CATEGORICAL INTENSITY FORECASTS. 
C                       ELEMENTS WITHIN THRQPF MUST BE BETWEEN 
C                       0 AND 100 PERCENT, INCLUSIVE, AND BE 
C                       MONOTONICALLY INCREASING. (I=1,2) (INPUT) 
C           THRTRW(I) = THUNDER POTENTIAL THRESHOLDS, EXPRESSED 
C                       IN PERCENT, OF THE CATEGORICAL THUNDERSTORM
C                       PROBABILITY FORECASTS. ELEMENTS WITHIN THRTRW 
C                       MUST BE BETWEEN 0 AND 100 PERCENT, INCLUSIVE, 
C                       AND BE MONOTONICALLY INCREASING. 
C                       (I=1,4) (INPUT)
C             NSVRLOC = FLAG FOR ORDER OF ASCII WEATHER SUBKEYS.
C                          1 - SEVERE ALWAYS FIRST
C                          2 - SCT,NUM,DEF SEVERE ALWAYS FIRST
C                          3 - SHOW LESS SEVERE AT LATER PROJECTIONS
C                       (INPUT) 
C           THRSVR(I) = SEVERE POTENTIAL THRESHOLDS, EXPRESSED IN PERCENT, 
C                       OF THE CATEGORICAL SEVERE THUNDERSTORM FORECASTS.
C                       ELEMENTS WITHIN THRSVR MUST BE BETWEEN 
C                       0 AND 100 PERCENT, INCLUSIVE, AND BE 
C                       MONOTONICALLY INCREASING. (I=1,4) (INPUT)
C
C           WEATHER GRID VARIABLES (INTERNAL)
C
C           ITABLE(I) = CCCFFF OF THE WEATHER GRID FORECASTS. 
C                       (I=1) (INTERNAL)
C             JSTA(K) = THE GRIDPOINT LOCATIONS OF THE STATIONS 
C                       IN THE STATION LIST. (K=1,NSTA) (INTERNAL)
C             GPPI(J) = PRECIPITATION POTENTIAL INDEX GRIDPOINT 
C                       FORECASTS. (J=1,ND2X3) (INTERNAL)
C          NPCAT(I,J) = PRECIPITATION CATEGORY GRIDPOINT FORECASTS 
C                       FOR EACH SUBKEY. SEE SUBROUTINE WXPPHS
C                       FOR DETAILS. (I=1,3) (J=1,ND2X3) 
C                       (INTERNAL)
C         PPHASE(I,J) = POTENTIAL INDEX GRIDPOINT FORECASTS FOR EACH 
C                       PRECIPITATION PHASE.
C                       PPHASE(1,J) = FREEZING POTENTIAL INDEX
C                       PPHASE(2,J) = FROZEN POTENTIAL INDEX
C                       PPHASE(3,J) = LIQUID POTENTIAL INDEX
C                       (I=1,3) (J=1,ND2X3) (INTERNAL)
C          NPPHS(I,J) = PRECIPITATION TYPE (ZR, IP, S, R) FOR EACH 
C                       COMPONENT OF THE WEATHER KEY AT GRIDPOINTS. 
C                       SEE ROUTINE WXPPHS FOR DETAILS. (I=1,3) 
C                       (J=1,ND2X3) (INTERNAL)
C            NXPIN(J) = PRECIPITATION INTENSITY CATEGORY GRIDPOINT
C                       FORECASTS. SEE SUBROUTINE WXPINT FOR MORE
C                       INFORMATION. (J=1,ND2X3) (INTERNAL)
C            NXPCH(J) = PRECIPITATION CHARACTER CATEGORY GRIDPOINT
C                       FORECASTS. SEE SUBROUTINE WXPCHR FOR MORE
C                       INFORMATION. (J=1,ND2X3) (INTERNAL)
C            NXWX1(J) = EXPLICIT WEATHER CODE REPRESENTATION
C                       AT GRIDPOINTS FOR THE FIRST SUBKEY. SEE
C                       SUBROUTINE WXEXPL FOR MORE INFORMATION.
C                       (J=1,ND2X3) (INTERNAL)
C            NXWX2(J) = EXPLICIT WEATHER CODE REPRESENTATION
C                       AT GRIDPOINTS FOR THE SECOND SUBKEY. SEE
C                       SUBROUTINE WXEXPL FOR MORE INFORMATION.
C                       (J=1,ND2X3) (INTERNAL)
C            NXWX3(J) = EXPLICIT WEATHER CODE REPRESENTATION
C                       AT GRIDPOINTS FOR THE THIRD SUBKEY. SEE
C                       SUBROUTINE WXEXPL FOR MORE INFORMATION.
C                       (J=1,ND2X3) (INTERNAL)
C             GTPI(J) = THUNDER POTENTIAL INDEX GRIDPOINT FORECASTS.
C                       SEE SUBROUTINE WXTSTM FOR DETAILS.
C                       (J=1,ND2X3) (INTERNAL)
C             GSPI(J) = SEVERE POTENTIAL INDEX GRIDPOINT FORECASTS. 
C                       SEE SUBROUTINE WXTSVR FOR DETAILS.
C                       (J=1,ND2X3) (INTERNAL)
C            NXTSW(J) = EXPLICIT THUNDER CODE REPRESENTATION
C                       AT GRIDPOINTS FOR THUNDERSTORMS AND SEVERE
C                       THUNDERSTORMS. SEE SUBROUTINES WXTSTM AND WXTSVR
C                       FOR DETAILS. (J=1,ND2X3) (INTERNAL)
C               RRMIS = MISSING DATA FLAG. (INTERNAL)
C              RRMIS2 = MISSING DATA FLAG. (INTERNAL)
C              MISSEL = INTEGER VALUE DENOTING AN ELEMENT NOT FETCHED.
C                       (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            WXPCAT, WXPPHS, WXPCHR, WXPINT, WXTSTM, WXTSVR,
C            WXEXPL, WXKEYS
C
C***********************************************************************
C
      IMPLICIT NONE
C
C        DECLARE MOS2K SYSTEM VARIABLES:
C
      CHARACTER*8 CCALL(ND1,6)
      CHARACTER*8 CCALLD(ND5)
      CHARACTER*20 NAME(ND1)
      CHARACTER*60 RACESS(5)
C
      INTEGER KFILDO,KFIL10,ID(4),IDPARS(15),JD(4),NDATE,
     1        KFILRA(5),NUMRA,ICALLD(L3264W,ND5),
     2        ISDATA(ND1),ND1,NSTA,
     3        NGRIDC(6,ND11),NGRID,ND11,NSLAB,IPACK(ND5),IWORK(ND5),ND5,
     4        LSTORE(12,ND9),ND9,LITEMS,ND10,LASTL,
     5        NBLOCK,LASTD,NSTORE,NFETCH,
     6        IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7),ND7,
     7        KFILAC,IP20,IP21,ND2X3,
     8        IP12,ISTAV,L3264B,L3264W,
     9        IER
C
      REAL    STALAT(ND1),STALON(ND1),SDATA(ND1),DIR(ND1,2,ND11),
     1        DATA(ND5),CORE(ND10)
C
C        DECLARE WEATHER GRID VARIABLES (INPUT):
C
      INTEGER IPHSBC,NSVRLOC,NX,NY
C
      REAL    THRPHS(4),THRQPF(2),THRTRW(4),THRSVR(4),PHSTMP
C
C        DECLARE WEATHER GRID VARIABLES (INTERNAL):
C
      INTEGER JSTA(NSTA),NPCAT(3,ND2X3),NPPHS(3,ND2X3),NXPIN(ND2X3),
     1        NXPCH(ND2X3),NXWX1(ND2X3),NXWX2(ND2X3),NXWX3(ND2X3),
     2        NXTSW(ND2X3),MISSEL,J,K,L
C
      REAL    GPPI(ND2X3),PPHASE(3,ND2X3),GTPI(ND2X3),GSPI(ND2X3),
     1        RRMIS,RRMIS2
C
      INTEGER ITABLE(1) / 228500 /
C
C***D WRITE(KFILDO,100)(ID(J),J=1,4)
 100  FORMAT(' *********** IN GENWX *************'/' ',4I10)
C
C***********************************************************************
C
C        INITIALIZE CONSTANTS.
C
      RRMIS=9999.
      RRMIS2=-1.
      MISSEL=9
C
C        INITIALIZE VARIABLES.
C
      IER=0
C
C        VERIFY THE PROCESSING INDICATOR, IDPARS(1) AND IDPARS(2).
C
      IF(ITABLE(1).EQ.IDPARS(1)*1000+IDPARS(2).AND.
     1  (IDPARS(7).EQ.0.OR.IDPARS(7).EQ.1.OR.
     2   IDPARS(7).EQ.2.OR.IDPARS(7).EQ.3.OR.
     3   IDPARS(7).EQ.4.OR.IDPARS(7).EQ.5.OR.
     4   IDPARS(7).EQ.6.OR.IDPARS(7).EQ.7))GO TO 108
C
      WRITE(KFILDO,107)(ID(L),L=1,4)
 107  FORMAT(/,' ****GENWX ENTERED FOR VARIABLE',
     1        2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,
     2        ' NOT ACCOMMODATED.')
      IER=103
      GO TO 340
C
 108  CONTINUE
C
C******************* PRECIPITATION POTENTIAL INDEX *********************
C
C        ASSIGN PRECIPITATION POTENTIAL INDEX DERIVED FROM
C        GRIDDED MOS POP12, POP6, AND POPO3. THIS ROUTINE WILL ALSO 
C        RETURN JSTA, WHICH CONTAINS THE GRIDPOINT LOCATIONS OF
C        THE STATIONS FOR DIAGNOSTIC PRINTING IN SUBSEQUENT ROUTINES.
C
      CALL WXPCAT(KFILDO,KFIL10,IP12,KFILRA,RACESS,
     1            NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,
     5            ND5,NGRIDC,NGRID,ND11,NSLAB,
     6            LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,
     9            L3264B,L3264W,ISTAV,
     A            NX,NY,JSTA,GPPI,RRMIS,RRMIS2,MISSEL,
     B            IER)
C
      IF(IER.NE.0) GO TO 340
C
C     IF ONLY PPI IS WANTED, NO NEED TO GO PAST THIS POINT. RETURN.
C
      IF(IDPARS(1)*1000+IDPARS(2).EQ.ITABLE(1).AND.IDPARS(7).EQ.1)THEN
         DATA=GPPI
         GO TO 250
      ENDIF
C
C******************** PHASE AND CATEGORIES *****************************
C
C        ASSIGN PRECIPITATION PHASE AND CATEGORIES BASED ON THE GRIDDED
C        MOS CONDITIONAL PROBABILITIES OF FREEZING, FROZEN, AND LIQUID 
C        PRECIPITATION, (OPTIONAL: THE GRIDDED MOS THRESHOLDS FOR 
C        CONDITIONAL BEST PRECIPITATION TYPE CATEGORY), AND THE LIQUID 
C        PHASE THRESHOLD TEMPERATURE:
C
      CALL WXPPHS(KFILDO,KFIL10,IP12,KFILRA,
     1            RACESS,NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,ND5,
     5            NGRIDC,NGRID,ND11,NSLAB,
     6            LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,L3264B,L3264W,
     9            ISTAV,IPHSBC,PHSTMP,THRPHS,NX,NY,JSTA,
     A            GPPI,NPCAT,NPPHS,PPHASE,
     B            RRMIS,MISSEL,IER)
C
      IF(IER.NE.0) GO TO 340
C
C     IF ONLY PHASE POTENTIAL IS WANTED, NO NEED TO GO PAST THIS POINT. 
C     RETURN.
C
      IF(IDPARS(1)*1000+IDPARS(2).EQ.ITABLE(1))THEN
         IF(IDPARS(7).EQ.2)THEN
            DATA=PPHASE(1,1:ND2X3)
            GO TO 250
         ELSEIF(IDPARS(7).EQ.3)THEN
            DATA=PPHASE(2,1:ND2X3)
            GO TO 250
         ELSEIF(IDPARS(7).EQ.4)THEN
            DATA=PPHASE(3,1:ND2X3)
            GO TO 250
         ENDIF
      ENDIF
C
C**************************** CHARACTER ********************************
C
C        ASSIGN A PRECIPITATION CHARACTER BASED ON THE MOS
C        PROBABILITIES OF RAIN, SHOWERS, AND DRIZZLE, THE SEASON,
C        AND USER-DEFINED THRESHOLDS FOR THE CHARACTER PROBABILITIES:
C 
CD***NOTE 7/26/2011: NO POP-C GRID AVAILABLE FOR A CHARACTER SUBROUTINE.
CD***NOTE 3/14/2012: THIS IS A PLACEHOLDER ROUTINE.
C
      CALL WXPCHR(KFILDO,KFIL10,IP12,KFILRA,RACESS,
     1            NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,
     5            ND5,NGRIDC,NGRID,ND11,
     6            NSLAB,LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,
     9            L3264B,L3264W,ISTAV,
     A            JSTA,NPCAT(1,1:ND2X3),NXPCH,
     B            RRMIS,MISSEL,IER)
C
      IF(IER.NE.0) GO TO 340
C
C        NOTE THAT THUNDER/SEVERE ROUTINES AFTER THIS MAY CHANGE 
C        VALUE OF NXPCH TO 2 (CONVECTIVE). DRIZZLE IS NOT IMPLEMENTED 
C        IN THIS DEVELOPMENT.
C
C************************** INTENSITY **********************************
C
C        ASSIGN A PRECIPITATION INTENSITY BASED ON THE GRIDDED MOS
C        QPF GUIDANCE AND USER-DEFINED QPF CATEGORICAL THRESHOLDS:
C
      CALL WXPINT(KFILDO,KFIL10,IP12,KFILRA,RACESS,
     1            NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,
     5            ND5,NGRIDC,NGRID,ND11,
     6            NSLAB,LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,
     9            L3264B,L3264W,ISTAV,
     A            THRQPF,NX,NY,JSTA,NPCAT(1,1:ND2X3),NXPIN,
     B            RRMIS,MISSEL,IER)
C
      IF(IER.NE.0) GO TO 340
C
C     IF ONLY CATEGORICAL INTENSITY IS WANTED, NO NEED TO GO PAST THIS 
C     POINT. RETURN.
C
      IF(IDPARS(1)*1000+IDPARS(2).EQ.ITABLE(1).AND.IDPARS(7).EQ.7)THEN
         DATA=NXPIN
         GO TO 250
      ENDIF
C
C*************************** THUNDER ***********************************
C
C        ASSIGN THUNDERSTORM CATEGORIES BASED ON
C        THE THUNDER POTENTIAL INDEX DERIVED FROM
C        GRIDDED MOS THUNDERSTORM PROBABILITIES AND USER-DEFINED
C        PROBABILITY-TO-CATEGORY THRESHOLDS:
C
      CALL WXTSTM(KFILDO,KFIL10,IP12,KFILRA,RACESS,
     1            NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,
     5            ND5,NGRIDC,NGRID,ND11,
     6            NSLAB,LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,
     9            L3264B,L3264W,ISTAV,
     A            THRTRW,NX,NY,JSTA,
     B            NPCAT,NXPCH,NXTSW,GPPI,GTPI,
     C            RRMIS,RRMIS2,MISSEL,IER)
C
      IF(IER.NE.0) GO TO 340
C
C     IF ONLY THUNDER POTENTIAL IS WANTED, NO NEED TO GO PAST THIS 
C     POINT. RETURN.
C
      IF(IDPARS(1)*1000+IDPARS(2).EQ.ITABLE(1).AND.IDPARS(7).EQ.5)THEN
         DATA=GTPI
         GO TO 250
      ENDIF
C
C*************************** SEVERE ************************************
C
C        ASSIGN SEVERE THUNDERSTORM CATEGORIES BASED ON THE 
C        SEVERE THUNDER POTENTIAL INDEX DERIVED FROM GRIDDED MOS 
C        UNCONDITIONAL SEVERE THUNDERSTORM PROBABILITIES AND 
C        USER-DEFINED PROBABILITY-TO-CATEGORY THRESHOLDS:
C
      CALL WXTSVR(KFILDO,KFIL10,IP12,KFILRA,RACESS,
     1            NUMRA,ID,IDPARS,JD,NDATE,
     2            CCALL,NAME,STALAT,STALON,
     3            ISDATA,SDATA,DIR,ND1,NSTA,
     4            ND2X3,ICALLD,CCALLD,IPACK,IWORK,
     5            ND5,NGRIDC,NGRID,ND11,
     6            NSLAB,LSTORE,ND9,LITEMS,CORE,ND10,
     7            NBLOCK,NFETCH,LASTL,LASTD,NSTORE,
     8            IS0,IS1,IS2,IS4,ND7,
     9            L3264B,L3264W,ISTAV,
     A            THRSVR,NX,NY,JSTA,
     B            NPCAT,NXPCH,NXTSW,GPPI,GTPI,
     C            GSPI,RRMIS,RRMIS2,MISSEL,IER)
C
      IF(IER.NE.0) GO TO 340
C
C     IF ONLY SEVERE POTENTIAL IS WANTED, NO NEED TO GO PAST THIS 
C     POINT. RETURN.
C
      IF(IDPARS(1)*1000+IDPARS(2).EQ.ITABLE(1).AND.IDPARS(7).EQ.6)THEN
         DATA=GSPI
         GO TO 250
      ENDIF
C
C********************** EXPLICIT WEATHER CODES *************************
C
C        COMBINE PRECIPITATION CATEGORY, PHASE, CHARACTER, AND
C        INTENSITY INTO EXPLICIT WEATHER CODES:
C
      CALL  WXEXPL(KFILDO,ID,IDPARS,CCALL,
     1             ND1,NSTA,ND2X3,ND5,JSTA,
     2             NPCAT,NPPHS,NXPCH,NXPIN,
     3             NXWX1,NXWX2,NXWX3,
     4             RRMIS,MISSEL,IER)
C
C*************************** WEATHER KEYS ******************************
C
C        CONVERT THE EXPLICIT WEATHER CODES AND THUNDER CODES INTO
C        WEATHER SUBKEYS AND KEYS. INDEX THE WEATHER KEYS AND
C        WRITE THE WEATHER KEY LIST:
C
      CALL WXKEYS(KFILDO,KFILAC,ND2X3,IP20,IP21,ND1,
     1            ID,IDPARS,NDATE,NSTA,CCALL,
     2            NSVRLOC,NX,NY,JSTA,NPCAT,PPHASE,NXPIN,NXTSW,
     3            GPPI,GTPI,GSPI,NXWX1,NXWX2,NXWX3,DATA,
     4            RRMIS,IER)
C
      IF (IER.NE.0)THEN
  127    FORMAT(/' ****PROBLEM ASSIGNING WEATHER STRING', 
     1           ' IN WXKEYS. CHECK VALUES OF',
     2           ' WEATHER KEY LIST FOR VARIABLE = ',
     3           3I10.9,I11.3,'.')
         WRITE(KFILDO,127)ID
      ENDIF
C
C        CHECK IF THE GRID SPECIFICATIONS TO PACK MATCH
C        THE GRID SPECIFICATIONS FROM THE WEATHER GRID CONTROL FILE.
C
C        BECAUSE MOS-2000 USES THE SAME IDS FOR ALL RESOLUTIONS,
C        IT IS POSSIBLE THAT AN UNPACKED GRID WILL CHANGE THE VALUES
C        OF TDLPACK SECTION 2 (IS2, GRID DEFINITION SECTION) AND 
C        GIVE UNEXPECTED RESULTS.  THIS CHECK IS ALSO DONE IN THE
C        SUBROUTINES THAT CALL GTVEGR TO UNPACK DATA, AND THIS FINAL 
C        CHECK MAY BE REDUNDANT.
C      
  250 IF(IS2(3).NE.NX.OR.IS2(4).NE.NY)THEN
         WRITE(KFILDO,270)ID,IS2(3),NX,IS2(4),NY
  270    FORMAT(/,' ****GRID CHARACTERISTICS FOR VARIABLE ',4I11,
     1            ' DO NOT MATCH IN GENWX.',/,
     2            ' IS2(3) =',I8,'    NX   = ',I10,/,
     3            ' IS2(4) =',I8,'    NY   = ',I10,/,
     4            ' CHECK INPUT DATA.')
      ELSE
         GO TO 350                                
      ENDIF
C
C        MUST SET RETURNABLE ARRAY TO MISSING FOR SAFETY
C        WHEN DATA CANNOT BE RETURNED.
C
 340  DO 341 J=1,ND2X3
         DATA(J)=RRMIS
 341  CONTINUE
C
      IER=200
C         
 350  CONTINUE
C
      RETURN
      END
