      SUBROUTINE CKMISS(KFILDO,FD1,ID,ND2X3,IMSSGD,MISSEL,RRMIS,IER)
C 
C        JUNE      2012   HUNTEMANN  MDL   MOS-2000 
C        MARCH     2013   HUNTEMANN  MDL   ADDED IMPLICIT NONE.
C
C        PURPOSE 
C            TO CHECK AN UNPACKED GRID FOR NON-MISSING DATA.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              FD1(J) = ARRAY CONTAINING GRIDPOITN FORECASTS TO CHECK FOR
C                       NON-MISSING VALUES. (J=1,ND2X3) (INPUT)
C               ID(J) = THE PREDICTOR ID FOR FD1 (J=1,4).  (INPUT)
C               ND2X3 = DIMENSION OF GRIDDED VARIABLES. (INPUT)
C              MISSEL = INTEGER VALUE DENOTING AN ELEMENT NOT FETCHED 
C                       (INPUT).
C               RRMIS = MISSING DATA FLAG (INPUT).
C              IMSSGD = FLAG INDICATING FD1 CONTAINS MISSING VALUES.
C                            1 = FD1 CONTAINS NON-MISSING VALUES
C                       MISSEL = FD1 CONTAINS ONLY MISSING VALUES
C                       (OUTPUT)
C              IMIDPT = GRIDPOINT LOCATION OF "MIDDLE" OF GRID.
C                       (INTERNAL)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0 = GOOD RETURN.
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      IMPLICIT NONE
C
      REAL FD1(ND2X3),RRMIS
      INTEGER ID(4),MISSEL,IMSSGD,IMIDPT,ND2X3,J,IER,KFILDO
C
C        INTIALIZE IMSSGD FLAG TO MISSING
C
      IMSSGD=MISSEL
C
C        FIRST, CHECK AN ARBITRARY GRIDPOINT FROM THE
C        CENTER OF THE DOMAIN. IF IT'S NON-MISSING, 
C        THERE IS NO NEED TO LOOP THROUGH ALL GRIDPOINTS.
C
      IMIDPT=INT(0.5*ND2X3)
C
      IF(FD1(IMIDPT).NE.RRMIS)THEN
         IMSSGD=1
      ENDIF
C
C        IF THE CENTER GRIDPOINT IS MISSING,
C        LOOP THROUGH ALL GRIDPOINTS IN PARALLEL
C        TO CHECK FOR NON-MISSING VALUES. BECAUSE
C        THIS LOOP USES OMP, GOTO AND DO WHILE
C        COULD NOT BE USED.
C
      IF(IMSSGD.NE.1)THEN
!$OMP PARALLEL DO 
!$OMP& SHARED(FD1,IMSSGD)
!$OMP& PRIVATE(J)
         DO 100 J=1,ND2X3
            IF(FD1(J).NE.RRMIS)THEN
              IMSSGD=1
            ENDIF
 100     CONTINUE
!$OMP END PARALLEL DO
      ENDIF
C
C     WRITE A DIAGNOSTIC IF ALL GRIDPOINTS ARE MISSING.
C
      IF(IMSSGD.NE.1) WRITE(KFILDO,110)(ID(J),J=1,4)
  110 FORMAT(/' ****VARIABLE = ',4I10.9,' CONTAINS ONLY',
     1        ' MISSING VALUES.')
C
  200 RETURN
      END 
