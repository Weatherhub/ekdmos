      SUBROUTINE EOVWND(KFILDO,VWIND,FDU,FDV,NX,NY,XNP,YNP,MAPP)
C
C        NOVEMBER 1994   GLAHN   TDL   MOS-2000 
C        AUGUST   2002   GLAHN   ADDED LAMBERT AND MERCATOR CAPABILITY.
C
C        PURPOSE 
C            TO COMPUTE THE EARTH-ORIENTED V-WIND IN M/SEC FROM 
C            GRID-ORIENTED WINDS FOR EACH GRIDPOINT.  ACTUALLY, THE
C            RETURNED WINDS WILL BE IN THE SAME UNITS AS THE INPUT
C            GRIDS.  THE MAP PROJECTION HAS BEEN GUARANTEED IN THE
C            CALLING ROUTINE EOWND TO BE 3, 5, OR 7.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C        VWIND(IX,JY) = COMPUTED EARTH-ORIENTED V-WIND IN M/SEC 
C                       (IX=1,NX) (JY=1,NY).  (OUTPUT)
C          FDU(IX,JY) = U-WINDS IN M/SEC (IX=1,NX) (JY=1,NY).  (INPUT)
C          FDV(IX,JY) = V-WINDS IN M/SEC (IX=1,NX) (JY=1,NY).  (INPUT)
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C                 XNP = NORTH POLE POSITION IN IX DIRECTION WITH RESPECT
C                       TO LL CORNER AS (1,1).  (INPUT)
C                 YNP = NORTH POLE POSITION IN JY DIRECTION WITH RESPECT
C                       TO LL CORNER AS (1,1).  (INPUT)
C                MAPP = MAP PROJECTION
C                       3 = LAMBERT,
C                       5 = POLAR STEREOGRAPHIC
C                       7 = MERCATOR
C                       (INPUT)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      DIMENSION VWIND(NX,NY),FDU(NX,NY),FDV(NX,NY)
C
D     WRITE(KFILDO,100)
D100  FORMAT(' TURNING V')
C
C        THIS IS FOR POLAR STEREOGRAPHIC AND LAMBERT MAP PROJECTIONS.
C        THE POLE POINT MUST BE FOR THE PROJECTION MAPP.
C
      IF(MAPP.EQ.5.OR.MAPP.EQ.3)THEN
C
         DO 150 JY=1,NY
         Y=YNP-JY
C
         DO 149 IX=1,NX
         X=XNP-IX
         R=SQRT(X*X+Y*Y)
         VWIND(IX,JY)=FDU(IX,JY)*X/R+FDV(IX,JY)*Y/R
 149     CONTINUE
 150     CONTINUE
C
C        THIS IS FOR MERCATOR MAP PROJECTION.  NO TURNING IS
C        REQUIRED.
C
      ELSEIF(MAPP.EQ.7)THEN
C
         DO 350 JY=1,NY
         DO 349 IX=1,NX
         VWIND(IX,JY)=FDV(IX,JY)
 349     CONTINUE
 350     CONTINUE
C
      ENDIF
C
      RETURN
      END
