      SUBROUTINE DIRCMP(KFILDO,IP12,CCALL,NAME,STALAT,STALON,DIR,
     1                  NGRIDC,ND1,NSTA,IER)
C
C        FEBRUARY 1995   GLAHN   TDL   MOS-2000
C        JULY     1996   GLAHN   ADDED ACCURACY TO LAT/LON AND GRIDLENGTH
C        JANUARY  2002   GLAHN   FORMAT 153 CHANGE F6.2 TO F7.2
C        JUNE     2002   GLAHN   ADDED MERCATOR CAPABILITY
C        APRIL    2003   GLAHN   SET DIR( , ) = 9999 WHEN NOT COMPUTED
C        MARCH    2005   GLAHN   CHANGED FORMAT 153 TO ACCOMMODATE 
C                                GRIDS OF SIZE GT 999
C
C        PURPOSE
C            TO COMPUTE XI AND YJ POSITIONS OF STATIONS IN TERMS OF
C            THE GRID SPECIFIED IN NGRIDC.  ONLY POLAR STEREOGRAPHIC,
C            LAMBERT, AND MERCATOR MAP PROJECTIONS ARE ALLOWED FOR.
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C                IP12 = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS AND THEIR I,J POSITIONS ON THE
C                       GRID WILL BE PRINTED TO THE FILE WHOSE UNIT
C                       NUMBER IS IP12.  (INPUT)
C            CCALL(K) = 8 STATION CALL LETTERS (K=1,NSTA).  USED FOR
C                       PRINTOUT ONLY.  (CHARACTER*8)  (INPUT)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA) OF THE GRID CHARACTERISTICS
C                       IN NGRIDC( ).  (OUTPUT)
C           NGRIDC(L) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR THE GRID.
C                       (INPUT)
C                       L=1--MAP PROJECTION NUMBER,
C                            3 = LAMBERT,
C                            5 = POLAR STEREOGRAPHIC,
C                            7 = MERCATOR,
C                            THE ONLY MAP PROJECTIONS SUPPORTED IN MOS-2000. 
C                            (THIS NUMBER CORRESPONDS TO WMO/NMC USAGE,
C                            BUT NOT GRIB 2).
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000 (NOT USED
C                            FOR MERCATOR),
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000, AND
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       (INPUT)
C                NSTA = THE NUMBER OF STATIONS IN CCALL( ).  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       60 = RETURN FROM DIRCMP WHEN THE MAP PROJECTION NUMBER
C                            IS NOT WHAT IS EXPECTED (I.E., 3, 5, OR 7).
C                       
C        NONSYSTEM SUBROUTINES USED 
C            PSLLIJ, LMLLIJ
C
      CHARACTER*8 CCALL(ND1)
      CHARACTER*20 NAME(ND1)
C
      DIMENSION STALAT(ND1),STALON(ND1)
      DIMENSION DIR(ND1,2),NGRIDC(6)
C
      IER=0
C      
      MAP=NGRIDC(1)
      XMESHL=NGRIDC(2)/1000.
      XLAT=NGRIDC(3)/10000.
      ORIENT=NGRIDC(4)/10000.
      XLATLL=NGRIDC(5)/10000.
      XLONLL=NGRIDC(6)/10000.
C
      DO 140 K=1,NSTA
C
C***      WRITE(KFILDO,120)MAP,K,XMESHL,XLAT,ORIENT,XLATLL,XLONLL,
C***     1                 CCALL(K),NAME(K),STALAT(K),STALON(K)
C*** 120  FORMAT(' IN DIRCMP--MAP,K,XMESHL,XLAT,ORIENT,XLATLL,XLONLL,',
C***     1       'CCALL(K),NAME(K),STALAT(K),STALON(K)',/,
C***     2       '          ',2I5,5F8.1,1X,A8,1X,A20,2F10.4)
C
      IF(MAP.EQ.3)THEN
         CALL LMLLIJ(KFILDO,STALAT(K),STALON(K),XMESHL,ORIENT,XLAT,
     1               XLATLL,XLONLL,DIR(K,1),DIR(K,2))
      ELSEIF(MAP.EQ.5)THEN 
         CALL PSLLIJ(KFILDO,STALAT(K),STALON(K),XMESHL,ORIENT,XLAT,
     1               XLATLL,XLONLL,DIR(K,1),DIR(K,2))
      ELSEIF(MAP.EQ.7)THEN 
         CALL MCLLIJ(KFILDO,STALAT(K),STALON(K),XMESHL,XLAT,
     1               XLATLL,XLONLL,DIR(K,1),DIR(K,2))
      ELSE
         WRITE(KFILDO,130)MAP
 130     FORMAT(/' ****MAP PROJECTION NUMBER =',I3,' NOT EXPECTED.'
     1           ,'  FATAL ERROR IN DIRCMP.')
         IER=60
C         
C           SET THE LATITUDES AND LONGITUDES IN DIR(K, ) = 9999.
C           WHEN THEY CAN'T BE COMPUTED.  THIS WILL HAPPEN ON THE
C           FIRST TIME THROUGH THE LOOP FOR K
C 
         DO 135 L=1,NSTA
         DIR(L,1)=9999.
         DIR(L,2)=9999.
 135     CONTINUE
C          
         GO TO 200
      ENDIF
C      
 140  CONTINUE
C
      IF(IP12.EQ.0)GO TO 200
C        IP12 IS PROVIDED AS ZERO FROM RDSTR1, BECAUSE THE STATION
C        LOCATIONS FOR ALL GRIDS FOUND FOR DAY 1 WILL BE PRINTED ELSEWHERE.
      WRITE(IP12,152)
 152  FORMAT(/' IX, JY GRID LOCATIONS FOR EACH STATION FOR THIS GRID')
C
      DO 155 K=1,NSTA
      WRITE(IP12,153)CCALL(K),NAME(K),DIR(K,1),DIR(K,2)
 153  FORMAT(' ',A8,1X,A20,3X,2F9.2)
 155  CONTINUE
C
 200  RETURN
      END 
 
