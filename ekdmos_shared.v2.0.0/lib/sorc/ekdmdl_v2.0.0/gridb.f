      SUBROUTINE GRIDB(KFILDO,ID,IDPARS,THRESH,P,FD,NX,NY,IER)
C
C        MAY   1994   GLAHN   TDL   MOS-2000
C        APRIL 2003   GLAHN   SPELL CHECK 
C 
C        PURPOSE 
C            TO MAKE A GRID BINARY VARIABLE.  WHEN IDPARS = 5,
C            THE BINARY TAKES THE VALUE OF 1 WHEN THE ORIGINAL
C            VARIABLE IS GE THRESH AND 0 OTHERWISE.  IDPARS
C            IS IDPARS(3) IN THE CALLING PROGRAM.
C 
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C               ID(J) = 4-WORD VARIABLE ID (J=1,4).  USED ONLY FOR
C                       DIAGNOSTIC PRINT.  (INPUT)
C              IDPARS = SINGLE VALUE INDICATING THE GRID BINARY PROCESS.
C                       IT SHOULD BE 5 WHEN GRIDB IS ENTERED.  (INPUT)
C              THRESH = THE THRESHOLD VALUE.  P(IX,JY) BECOMES 1 WHEN
C                       THE INPUT P(IX,JY) GE THRESH, AND 0 OTHERWISE.
C                       (INPUT)
C            P(IX,JY) = FIELD TO BE BINARIED (IX=1,NX) (JY=1,NY).
C                       (INPUT-OUTPUT)
C           FD(IX,JY) = WORK ARRAY (IX=1,NX) (JY=1,NY).  THIS MAY NOT
C                       ACTUALLY BE USED, BUT IS PROVIDED IN CASE IT
C                       IS NEEDED.  (INTERNAL)
C                  NX = SIZE OF GRID IN IX DIRECTION.  (INPUT)
C                  NY = SIZE OF GRID IN JY DIRECTION.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0  = GOOD RETURN.
C                       64 = THRESHOLD IS MISSING VALUE.
C                       65 = IDPARS VALUE NOT FOUND IN THIS ROUTINE.
C                       66 = RESERVED.
C                       67 = RESERVED. 
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION ID(4)
      DIMENSION P(NX,NY),FD(NX,NY)
C
      IER=0
C
      IF(THRESH.NE.9999.)GO TO 110
C
C        THE THRESHOLD IS INDICATED AS MISSING.
C        SET THE VALUES TO MISSING.
C
      WRITE(KFILDO,101)THRESH,IDPARS,(ID(J),J=1,4)
 101  FORMAT(/' ****THRESHOLD =',F7.0,' FOR BINARY PARAMETER IDPARS =',
     1        I3,' IN PREDICTOR',1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,/,
     2        '     IS INDICATED AS MISSING IN SUBROUTINE GRDIB.',
     3        '  ALL VALUES SET TO MISSING.')
      IER=64
      GO TO 805
C
 110  IF(IDPARS.NE.5)GO TO 800
C
C        MAKE THE BINARIES.  THE BINARY IS SET 1 WHEN THE ORIGINAL
C        VALUE IS LE THRESH.
C
      DO 160 JY=1,NY
      DO 159 IX=1,NX
C
      IF(P(IX,JY).GE.THRESH)THEN
         P(IX,JY)=1.
      ELSE
         P(IX,JY)=0.
      ENDIF   
C
 159  CONTINUE
 160  CONTINUE
C
C*********ADD OTHER BINARY TRANSFORMATIONS HERE.
C
      GO TO 900
C
C        AT THIS POINT, THE BINARY PARAMETER HAS NOT BEEN
C        FOUND.  PRINT DIAGNOSTIC, AND SET ALL VALUES TO MISSING.
C
 800  WRITE(KFILDO,801)IDPARS,(ID(J),J=1,4)
 801  FORMAT(/' ****THE BINARY PARAMETER IDPARS =',I3,
     1        ' IN PREDICTOR',1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3/
     2        '     WAS NOT FOUND IN SUBROUTINE GRIDB.',
     3        '  ALL VALUES SET TO MISSING.')
      IER=65
C
 805  DO 810 JY=1,NY
      DO 809 IX=1,NX
      P(IX,JY)=9999.
 809  CONTINUE
 810  CONTINUE
C
 900  RETURN
      END 
