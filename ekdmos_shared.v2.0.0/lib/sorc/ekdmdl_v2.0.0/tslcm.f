      SUBROUTINE TSLCM(KFILDO,SDATA,DIR,SINL,XMAPF,ND1,NSTA,
     1                 DATA,NX,NY,MAPP,XMESHL,ORIENT,XLAT,
     2                 ALATLL,ALONLL,IWS,IER)
C
C        JULY     2002   GLAHN   TDL   MOS-2000
C        AUGUST   2002   GLAHN   ADDED LAMBERT AND MERCATOR CAPABILITY
C
C        PURPOSE 
C            TO COMPUTE THE WEST (POSITIVE) OR SOUTH (POSITIVE)
C            TERRAIN UPSLOPE.  COMPUTATION ROUTINE FOR TSLOP.
C            BOTH GRID COMPONENTS HAVE TO BE COMPUTED TO TURN TO
C            EARTH ORIENTED.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT)
C            SDATA(K) = RETURNED TERRAIN UPSLOPE IN M/M FOR EARTH
C                       ORIENTED WEST (IWS=1) OR SOUTH (IWS=2) SLOPE
C                       FOR NSTA STATIONS (K=1,NSTA).  (OUTPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE
C                       GRID FOR THE COMBINATION OF GRID
C                       CHARACTERISTICS AND STATION K (K=1,NSTA).
C                       (INPUT)
C             SINL(K) = SINE OF THE LATITUDE AT EACH STATION 
C                       (K=1,NSTA).  (INTERNAL/OUTPUT)
C            XMAPF(K) = MAP SCALE FACTOR AT EACH STATION (K=1,NSTA).
C                       (INTERNAL/OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT
C                       WITH.  FIRST DIMENSION OF DIR( , ).  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT
C                       WITH.  (INPUT)
C         DATA(IX,JY) = THE TERRAIN FIELD (IX=1,NX)  (JY=1,NY).  (INPUT)
C                  NX = DIMENSION OF DATA( , ) IN IX DIRECTION.  (INPUT)
C                  NY = DIMENSION OF DATA( , ) IN JY DIRECTION.  (INPUT)
C                MAPP = MAP PROJECTION.  (INPUT)
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  USES 
C                       MAP FACTOR IN XMAPF( ) TO GET ACTUAL GRID
C                       LENGTH AT STATION K.   (INPUT)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES
C                       WEST.  DO NOT USE NEGATIVE.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS
C                       CORRECT.  DO NOT USE NEGATIVE.  (INPUT)
C              ALATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES NORTH.  (INPUT)
C              ALONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES WEST.  DO NOT USE NEGATIVE.
C                       (INPUT)
C                 IWS = SWITCH FOR WEST OR SOUTH COMPONENT.
C                       1 = WEST,
C                       2 = SOUTH.
C                 IER = ERROR RETURN.
C                         0 = GOOD RETURN.
C                        60 = INCORRECT MAP PROJECTION.
C                       183 = TURNING OF WIND TO EARTH ORIENTED NOT
C                             PROVIDED FOR FOR LAMBERT MAP.
C                       (OUTPUT)
C                  TU = UPSLOPE IN U DIRECTION AT STATION K.  (INTERNAL)
C                  TV = UPSLOPE IN V DIRECTION AT STATION K.  (INTERNAL)
C                DIST = GRID LENGTH CORRECTED FOR MAP FACTOR.
C                       (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      DIMENSION SINL(NSTA),XMAPF(NSTA),SDATA(NSTA)
      DIMENSION DIR(ND1,2)
      DIMENSION DATA(NX,NY)
C
      IER=0
C
C        CALCULATE MAP FACTOR FOR THE PROJECTION MAPP FOR EACH
C        STATION.  ALSO, CALCULATE XNP AND YNP,THE NORTH POLE
C        GRIDPOINT POSITIONS CORRESPONDING TO THE POLAR 
C        STEREOGRAPHIC OR LAMBERT GRID BEING USED.  THERE IS
C        NO POLE POSITION FOR THE MERCATOR.
C
      IF(MAPP.EQ.5)THEN
         CALL PSLLIJ(KFILDO,90.,0.,XMESHL,ORIENT,XLAT,
     1               ALATLL,ALONLL,XNP,YNP)
         CALL PSMAPS(KFILDO,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1               DIR(1,1),DIR(1,2),SINL,XMAPF,NSTA,IER)
      ELSEIF(MAPP.EQ.3)THEN
         CALL LMLLIJ(KFILDO,90.,0.,XMESHL,ORIENT,XLAT,
     1               ALATLL,ALONLL,XNP,YNP,IER)
C           IER SHOULD BE OK HERE FOR FINDING THE POLE POSITION.
         CALL LMMAPS(KFILDO,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1               DIR(1,1),DIR(1,2),SINL,XMAPF,NSTA,IER)
      ELSEIF(MAPP.EQ.7)THEN
         CALL MCMAPS(KFILDO,XMESHL,XLAT,ALATLL,ALONLL,
     1               DIR(1,1),DIR(1,2),SINL,XMAPF,NSTA,IER)
      ELSE
         WRITE(KFILDO,110)MAPP
 110     FORMAT(/' ****INCORRECT MAP PROJECTION =',I4,
     1           ' IN TSLCM.')
         IER=60
         GO TO 300
      ENDIF
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,115)IER
 115     FORMAT(/' ****ERROR IN TSLCM, IER =',I4,
     1           ' CALCULATING MAP FACTOR.')
         GO TO 300
      ENDIF
C
      DO 200 K=1,NSTA
      DIST=XMESHL/XMAPF(K)
C        XMAPF(K) IS THE MAP FACTOR AT THE POINT
C        DIR(K,1), DIR(K,2).  THE DISTANCE BETWEEN GRIDPOINTS 
C        IS XMESHL/XMAPF(K).
      IX=DIR(K,1)
      JY=DIR(K,2)
C        TRUNCATION OF POSITION ALLOWS COMPUTATION OVER 1 GRID LENGTH.
C
C        COMPUTE THE U (WEST) UPSLOPE AT THE IX OF THE STATION.
C
      DX1=DATA(IX+1,JY)-DATA(IX,JY)
      DX2=DATA(IX+1,JY+1)-DATA(IX,JY+1)
      DX=DX1+(DX2-DX1)*(DIR(K,2)-JY)
C        DX IS THE GRID ORIENTED WEST SLOPE COMPUTED OVER ONE 
C        GRIDLENGTH INTERPOLATED TO THE Y STATION POSITION AT DIR(K, ),
C
C        COMPUTE THE V (SOUTH) UPSLOPE AT THE JY OF THE STATION.
C
      DY1=DATA(IX,JY+1)-DATA(IX,JY)
      DY2=DATA(IX+1,JY+1)-DATA(IX+1,JY)
      DY=DY1+(DY2-DY1)*(DIR(K,1)-IX)
C        DX IS THE GRID ORIENTED SOUTH SLOPE COMPUTED OVER ONE
C        GRIDLENGTH INTERPOLATED TO THE X STATION POSITION AT DIR(K, ),
C
C        TURN TO EARTH ORIENTED FROM THE DX AND DY GRID ORIENTED
C        COMPONENTS ON POLAR STEREOGRAPHIC OR LAMBERT GRID.
C        MERCATOR REQUIRES NO TURNING.  THE UPSLOPE IS IN TERMS
C        OF M PER M.
C
      IF(MAPP.EQ.3.OR.MAPP.EQ.5)THEN
         Y=YNP-JY
         X=XNP-IX
         R=SQRT(X*X+Y*Y)
C
         IF(IWS.EQ.1)THEN
            SDATA(K)=((DX*Y-DY*X)/R)/DIST
C              THIS IS THE SAME AS SDATA(K)=DX*Y/R-DY*X/R IN EOUWND.
C              THIS IS THE WEST COMPONENT, POSITIVE WITH A WEST SLOPE.
         ELSE
            SDATA(K)=((DX*X+DY*Y)/R)/DIST
C              THIS IS THE SAME AS SDATA(K)=DX*X/R+DY*Y/R IN EOVWND.
C              THIS IS THE SOUTH COMPONENT, POSITIVE WITH A SOUTH SLOPE.
         ENDIF
C
      ELSEIF(MAPP.EQ.7)THEN
C
         IF(IWS.EQ.1)THEN
            SDATA(K)=DX/DIST
         ELSE
            SDATA(K)=DY/DIST
         ENDIF
C
      ENDIF
C      
 200  CONTINUE
C
 300  RETURN
      END      
