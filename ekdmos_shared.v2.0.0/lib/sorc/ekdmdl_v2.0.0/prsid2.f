      SUBROUTINE PRSID2(KFILDO,ID,IDPARS,THRESH)
C 
C        MARCH    1996   GLAHN   TDL   MOS-2000 
C        MAY      2000   GLAHN   COMMAS INSERTED IN /D FORMAT STATEMENT
C        JANUARY  2004   GLAHN   CORRECTED ERROR.  NEGATIVE THRESHOLD
C                                WAS NOT HANDLED CORRECTLY.  FOR 
C                                INSTANCE A -.5 WOULD HAVE COME OUT 1.5;
C                                COMMENTS LIMITED TO 72 CHARACTERS
C
C        PURPOSE 
C            TO PARSE A 4-WORD ID INTO ITS 15 INTEGER PARTS AND THE
C            THRESHOLD.  IT IS ASSUMED THAT "G" IN WORD 4 = 0.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C               ID(J) = THE PREDICTOR ID'S (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       PREDICTOR
C                       ID'S CORRESPONDING TO ID(J) (J=1,15).
C                       (OUTPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C              THRESH = THE THRESHOLD.  (OUTPUT)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      DIMENSION ID(4),IDPARS(15)
C
C        PARSE THIS PREDICTOR ID.
C
      IDPARS(1)=ID(1)/1000000
      IDPARS(2)=ID(1)/1000-IDPARS(1)*1000
      IDPARS(3)=ID(1)/100-IDPARS(1)*10000-IDPARS(2)*10
      IDPARS(4)=ID(1)-(ID(1)/100)*100
C
      IDPARS(5)=ID(2)/100000000
      IDPARS(6)=ID(2)/10000-IDPARS(5)*10000
      IDPARS(7)=ID(2)-(ID(2)/10000)*10000
C
      IDPARS(9)=ID(3)/1000000
      IDPARS(10)=ID(3)/100000-IDPARS(9)*10
      IDPARS(11)=ID(3)/1000-IDPARS(9)*1000-IDPARS(10)*100
      IDPARS(12)=ID(3)-(ID(3)/1000)*1000
      IDPARS(8)=IDPARS(9)/100
      IDPARS(9)=IDPARS(9)-IDPARS(8)*100
C
      IWXXXX=ID(4)/100000
C        IWXXXX IS THE FRACTIONAL PART OF THE THRESHOLD, WITH SIGN.
      IYY=ID(4)/1000-IWXXXX*100
C        IYY IS THE EXPONENT OF THE FRACTIONAL PART.  NOTE THAT 
C        WHEN NEGATIVE, IT HAS HAD 50 ADDED TO IT IN ID(4).
      IDPARS(13)=ID(4)/100-IWXXXX*1000-IYY*10
C        GET RID OF THE 50, IF IT EXISTS.  IT NEEDS TO BE THERE 
C        IN THE COMPUTATION OF IDPARS(13)
      IF(IYY.GT.25)IYY=IYY-50
C        IT IS ASSUMED IN THE STATEMENT ABOVE THAT THE 10'S
C        EXPONENT WILL NOT EXCEED 25 IN MAGNITUDE.  IYY CAN NOW
C        BE POSITIVE OR NEGATIVE.
      IW=IWXXXX/10000
C        IW IS THE SIGN PORTION OF THE THRESHOLD.
      IXXXX=IWXXXX-IW*10000
C        IXXXX IS THE FRACTIONAL PART OF THE THRESHOLD SANS SIGN.
      THRESH=FLOAT(IXXXX)*10.**(IYY-4)
C        THE MINUS 4 ABOVE IS BECAUSE THE DECIMAL POINT IS ASSUMED
C        TO BE AT THE LEFT OF THE XXXX.  THAT IS, XXXX IS A FRACTION
C        AND MUST BE MULTIPLIED BY 10**-4 TO BE CORRECT.
      IF(IW.GT.0)THRESH=-THRESH
C        THE THRESHOLD IS NEGATIVE WHEN IW GT 0.
      IDPARS(14)=ID(4)/10-(ID(4)/100)*10
      IDPARS(15)=0
C        IDPARS(15) = "G" HAS NO MEANING IN U700.
C        1         2         3         4         5         6         7 X
D     WRITE(KFILDO,150)(ID(J),J=1,4),(IDPARS(J),J=1,15),THRESH
D150  FORMAT('  IN PRSID2--(ID(J),J=1,4),(IDPARS(J),J=1,15),THRESH'/
D    1       '     ',4I10,4X,15I4,F13.5)
      RETURN
      END
