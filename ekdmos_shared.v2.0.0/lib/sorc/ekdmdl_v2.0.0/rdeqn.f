      SUBROUTINE RDEQN(KFILDO,KFILEQ,EQNNAM,NDATE,
     1                 IP4,IP13,IP14,IP15,IP19,IP20,IP21,
     2                 CCALL,IFOUND,NSTA,IALL,INITF,CCALLD,   
     3                 KGP,NGP,LGP,
     4                 MTRMS,MTANDS,
     5                 IDEQN,IDTAND,
     6                 LOCSTA,CONST,
     7                 AVG,CORR,
     8                 COEF,
     9                 ND1,ND2,ND3,ND5,ND13,IER)
C
C        FEBRUARY  1996   GLAHN   TDL   MOS-2000
C        JANUARY   1997   GLAHN   CHANGED EQUATION READING FORMAT TO E13.7
C        JANUARY   1997   GLAHN   ADDED IFOUND( ) TO CALL
C        FEBRUARY  1997   GLAHN   ERROR NOS CHANGED; OTHER MINOR CHANGES
C        SEPTEMBER 1998   GLAHN   IP(18-20) CHANGED TO IP(19-21)
C        OCTOBER   1998   GLAHN   DIAGNOSTIC MODIFIED, EQN FILES REWOUND
C        NOVEMBER  1998   GLAHN   CHANGED PRINT AT 111
C        DECEMBER  1998   GLAHN   CHANGED CCALL( ) TO CCALL( , )
C        JANUARY   1999   GLAHN   ELIMINATED NSETS, READING ONE SET;
C                                 RETURN WHEN FILE NAMES DON'T MATCH;
C                                 RETAINS IER1 FROM RDC
C        FEBRUARY  1999   GLAHN   COMMENT CHANGE
C        MAY       1999   GLAHN   DOUBLE SPACE REMOVED AT LOOP 195
C        MAY       1999   GLAHN   REMOVED ICAT
C        JULY      1999   GLAHN   ADDED IP13, 1P14, NDATE TO CALL;
C                                 CHECK FOR DATE FOR U900
C        OCTOBER   1999   GLAHN   INTERCHANGED IP13 AND IP14 PRINT
C        APRIL     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                 CONFORM TO FORTRAN 90 STANDARDS
C                                 ON THE IBM SP
C        APRIL     2000   DALLAVALLE   FIXED IF TEST AND MESSAGE
C                                 WHEN OPERATIONAL EQUATIONS
C                                 DO NOT MATCH THE INPUT DATE
C        MAY       2000   GLAHN   CHANGED FORMAT 1115 FROM 6X TO 5X
C        MAY       2000   MCE     CHANGED FORMAT IN CALL TO RDC
C                                 TO ADD COMMA.
C        JUNE      2000   ALLEN   CHANGED CODE SO THAT IT WILL CHECK
C                                 ALL SIX LINKS IN THE DICTIONARY, NOT
C                                 JUST THE FIRST TWO.
C        SEP       2000   MCE     MODIFIED LOOP FOR MATCHING UP STATIONS
C                                 ON EQNS WITH STATIONS IN LIST.  WE 
C                                 NEEDED TO MAKE THIS MORE EFFICIENT
C                                 TO CUT DOWN THE TIME TO PROCESS TSVR
C                                 EQNS
C        NOVEMBER  2000   GLAHN   CHANGED I3 TO I6 IN FORMAT 1220;
C                                 CHANGED I4 TO I6 IN FORMAT 133 & 210;
C                                 CHANGED 30I4 TO 20I6 IN FORMAT 223;
C                                 INITIALIZED LOCSTA( ); COMMENTS
C        NOVEMBER  2000   GLAHN   MODIFIED TO USE IN U700 BUT NOT U900;
C                                 RETAINED MM=1,6 LOOP, BUT ALL TESTS
C                                 ARE DONE TO RETAIN ALL DIAGNOSTICS;
C                                 MODIFIED FORMATS 123 AND 1232;
C                                 REWORKED AROUND 1232 SLIGHTLY
C                                 NOTE:  REDQN9 IS USED IN U900
C        APRIL     2001   JPD     COMMENTED OUT CALLS TO TIMPR
C        JANUARY   2002   JCM     COMMENTED OUT FILENAME CHECK 
C                                 NEAR 111
C        JANUARY   2003   RLC     MERGED ALL AVAILABLE VERSIONS OF
C                                 RDEQN FOR LIBRARY WEEK. RETAINED THE
C                                 9/02 MCE CHANGE WHERE YOU JUMP OUT OF
C                                 THE 125 STATION LOOP AS SOON AS YOU 
C                                 FIND THE FIRST MATCH.  BG WAS CHECKING
C                                 ALL OF THE LINKS, BUT THIS TAKES A LOT OF 
C                                 TIME.
C
C        PURPOSE
C            TO READ ONE SET OF EQUATIONS FROM A FILE FOR U700 OR
C            U900.   A SET IS DEFINED AS A GROUP OF EQUATIONS ALL
C            HAVING THE SAME PREDICTANDS.  TEMPERATURE EQUATIONS
C            DERIVED SIMULTANEOUSLY FOR SEVERAL 3-HOURLY PROJECTIONS
C            AND A MAXIMUM WOULD BE ONE SET, WHETHER SINGLE STATION OR
C            REGIONALIZED, BECAUSE THE PREDICTANDS ARE THE SAME. 
C            ANOTHER GROUP OF TEMPERATURE EQUATIONS FOR OTHER 
C            PROJECTIONS WOULD BE ANOTHER SET, AS WOULD BE A SET OF
C            DEW POINT EQUATIONS NOT DERIVED SIMULTANEOUSLY WITH THE 
C            TEMPERATURE EQUATIONS.  EACH SET IS READ FROM A PARTICULAR
C            LOGICAL FILE, WHERE A LOGICAL FILE HAS A UNIT NUMBER IN 
C            KFILEQ AND A NAME IN EQNNAM.  HOWEVER, A UNIT NUMBER AND 
C            ASSOCIATED NAME COULD BE THE SAME AS IN A PREVIOUS CALL 
C            IF THE EQUATIONS ARE ON THE SAME PHYSICAL FILE.  THAT IS,
C            ALL SETS OF, SAY, TEMPERATURE EQUATIONS COULD BE ON THE 
C            SAME PHYSICAL FILE, EVEN THOUGH THE PROJECTIONS MIGHT BE
C            DIFFERENT, AND AS SUCH DIVIDED INTO SETS.  THE LIST
C            OF STATIONS CONSISTING OF ALL STATIONS WITH THE
C            THE EQUATIONS IS PROVIDED BACK IN CCALL( ,1) WHEN 
C            IALL = 1; OTHERWISE, WHEN IALL = 0, CCALL( ,1) AND
C            CCALL( ,2) WILL CONTAIN THE PRIMARY AND BACKUP 
C            STATIONS, AND THE EQUATION STATION LIST IS KEYED
C            TO EITHER.  WHEN CALLED FROM U700, EQNNAM IS THE FILE
C            NAME AND MUST MATCH THE INTERNAL FILE NAME FOR 
C            SAFETY.  WHEN CALLED FROM U900, THE INTERNAL FILE
C            NAME IS NOT PRESENT, BUT IS REPLACED WITH A DATE/TIME,
C            WHICH MUST MATCH NDATE.
C   
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILEQ    - UNIT NUMBER FOR READING EQUATION FILE.
C                        (INPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILEQ = UNIT NUMBER FOR READING EQUATION FILE.  (INPUT)
C              EQNNAM = FOR U700, NAME OF FILE CORRESPONDING TO KFILEQ.
C                       FOR U900, THIS IS NOT USED (NDATE = 9999). 
C                       (CHARACTER*60)  (INPUT)
C               NDATE = FOR U900, NDATE IS THE DATE/TIME BEING PROCESSED.
C                       THIS MUST BE WITHIN THE BEGINNING AND ENDING
C                       DATES (MONTH/DAY) AND FOR THE HOUR (CYCLE)
C                       SPECIFIED IN THE EQUATION FILE.
C                       FOR U700, THIS MUST BE 9999.
C                 IP4 = INDICATES WHETHER (>0) OR NOT (=0) THE LIST
C                       OF STATIONS TO MAKE FORECASTS FOR IS TO BE
C                       WRITTEN TO UNIT IP4.  THIS WILL HAPPEN ONLY
C                       IF IALL = 1.  (INPUT)
C                IP13 = INDICATES WHETHER (>0) OR NOT (=0) A DIAGNOSTIC
C                       WILL BE PROVIDED FOR A STATION IN THE LIST
C                       CCALL( , ) THAT DOES NOT HAVE AN EQUATION IN 
C                       THE SET BEING PROCESSED.  THIS WOULD BE
C                       USEFUL ONLY WHEN IALL = 0.
C                IP14 = INDICATES WHETHER (>0) OR NOT (=0) A DIAGNOSTIC
C                       WILL BE WRITTEN WHEN AN EQUATION SET IS FOUND
C                       THAT DOES NOT MATCH NDATE.  (THIS IS NOT USED
C                       FOR U700; NDATE = 9999.)
C                IP15 = INDICATES WHETHER (>0) OR NOT (=0) A DIAGNOSTIC
C                       WILL BE WRITTEN TO UNIT IP15 WHEN A STATION
C                       WITH THE EQUATIONS IS NOT IN THE MASTER LIST
C                       IN CCALL( ).  WHEN IALL = 1, THIS WOULD NOT
C                       OCCUR, BECAUSE IN THAT CASE CCALL( ) CONSISTS
C                       OF ALL THE STATIONS WITH THE EQUATIONS.
C                IP19 = INDICATES WHETHER (>0) OR NOT (=0) THE
C                       PREDICTAND MULTIPLE CORRELATIONS WILL BE
C                       WRITTEN TO UNIT IP19 FOR VIEWING.  (INPUT)
C                IP20 = INDICATES WHETHER (>0) OR NOT (=0) THE
C                       PREDICTAND MEANS WILL BE WRITTEN TO UNIT IP20
C                       FOR VIEWING.  (INPUT)
C                IP21 = INDICATES WHETHER (>0) OR NOT (=0) THE
C                       EQUATIONS WILL BE WRITTEN TO UNIT IP21 FOR 
C                       VIEWING.  (INPUT)
C          CCALL(K,J) = THE LIST OF STATIONS TO MAKE FORECASTS FOR
C                       (J=1) (K=1,NSTA).  WHEN IALL = 0, THIS WILL
C                       BE INPUT AND THE STATIONS WITH THE
C                       EQUATIONS ARE KEYED TO (J=1,6),
C                       WHERE, NORMALLY, CCALL( ,1) WILL CONTAIN THE
C                       ICAO STATION IDENTIFIERS AND CCALL( ,2) WILL
C                       CONTAIN THE OLD STATION CALL LETTERS.
C                       WHEN IALL NE 0, CCALL( ,1) WILL BE OUTPUT
C                       AND WILL, BE THE STATIONS WITH THE EQUATIONS.
C                       (CHARACTER*8) (INPUT OR OUTPUT)
C           IFOUND(K) = INITIALLY SET TO ZERO (K=1,ND1).  SET TO 1
C                       WHEN AN EQUATION FOR A STATION IN THE
C                       CCALL( , ) LIST IS FOUND IN A SET SO THAT
C                       DUPLICATES CAN BE DETECTED.  THIS IS ISDATA( )
C                       IN U700 AND U900.  (INTERNAL)
C                NSTA = THE NUMBER OF STATIONS TO MAKE FORECASTS FOR.
C                       WHEN IALL = 0, THIS WILL BE INPUT.  WHEN
C                       IALL NE 0, THIS WILL BE OUTPUT.
C                       (INPUT OR OUTPUT)
C                IALL = 0 WHEN THE MASTER LIST OF STATIONS IS INPUT IN
C                         CCALL( ,1) AND IS TO BE USED.
C                     = 1 WHEN THE MASTER LIST OF STATIONS IS GENERATED 
C                         IN RDEQN AND IS COMPOSED OF ALL STATIONS IN 
C                         THE EQUATIONS.  THIS LIST WILL BE IN THE ORDER 
C                         THE STATIONS ARE ENCOUNTERED IN THE EQUATIONS.
C                       (INPUT OR OUTPUT)
C               INITF = 0 WHEN EVERY STATION IN THE LIST IN CCALL( ,1) 
C                         HAS AN EQUATION AND INITIALIZATION OF 
C                         FCST( , , ) DOES NOT HAVE TO BE DONE IN
C                         FCST71 AND FCST72.
C                     = 1 OTHERWISE.
C                       (OUTPUT)
C           CCALLD(K) = SCRATCH ARRAY (K=1,ND1)  (CHARACTER*8)  
C                       (INTERNAL)
C                 KGP = THE NUMBER OF EQUATIONS FOR THIS SET.  (OUTPUT)
C              NGP(L) = FOR EACH EQUATION (L=1,KGP) IN THIS SET, THE 
C                       NUMBER OF STATIONS IN EACH GROUP.  THESE ARE
C                       THE MATCHES FOUND BETWEEN THE INPUT LIST
C                       AND THE EQUATION LIST(S).  (OUTPUT)
C              LGP(L) = FOR EACH EQUATION (L=1,KGP) IN THIS SET, THE 
C                       LOCATION IN LOCSTA ( ,I) OF WHERE THE FIRST 
C                       STATION IN THE SET IS.  (OUTPUT)
C            MTRMS(L) = THE NUMBER OF TERMS IN EACH EQUATION 
C                       (L=1,KGP) FOR THIS SET.  (OUTPUT)
C              MTANDS = THE NUMBER OF PREDICTANDS FOR EACH EQUATION. 
C                       (OUTPUT)
C        IDEQN(J,L,M) = THE 4-WORD ID (J=1,4) FOR EACH PREDICTOR
C                       (M=1,NTRMS) IN EACH EQUATION (L=1,KGP),
C                       FOR THIS EQUATION SET.  (THE VALUES
C                       (IDEQN(5, , ), IDEQN(6, , ), AND 
C                       IDEQN(7, , ) ARE USED LATER.  (OUTPUT)
C        IDTAND(J,NN) = THE PREDICTAND ID'S (J=1,4) AND PREDICTAND NN 
C                       (NN=1,MTANDS) FOR THIS EQUATION SET.  (OUTPUT)
C           LOCSTA(K) = THE LOCATION IN FCST ( ,N) OF WHERE TO PUT THE 
C                       FORECAST, WHERE K IS IN ORDER OF THE EQUATIONS
C                       AS READ IN (K=1,NSTA).  (OUTPUT)
C         CONST(L,NN) = THE EQUATION CONSTANTS FOR GROUP L (L=1,KGP) 
C                       AND PREDICTAND NN (NN=1,MTANDS) FOR THIS
C                       EQUATION SET.  (OUTPUT)
C           AVG(L,NN) = THE PREDICTAND MEANS FOR GROUP L (L=1,KGP) AND 
C                       PREDICTAND NN (NN=1,MTANDS) FOR THIS EQUATION 
C                       SET.  (OUPTUT)
C          CORR(L,NN) = THE MULTIPLE CORRELATIONS FOR GROUP L 
C                       (L=1,KGP) AND PREDICTAND NN (NN=1,MTANDS) FOR
C                       THIS EQUATION SET.  (OUTPUT)
C        COEF(L,M,NN) = THE COEFFICIENTS FOR GROUP L (L=1,KGP),
C                       TERM M (M=1,MTRMS), AND PREDICTAND NN 
C                       (NN=1,MTANDS) FOR THIS EQUATION SET.  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT
C                       WITH.  USED AS DIMENSION OF SEVERAL VARIABLES.
C                       (INPUT)
C                 ND2 = MAXIMUM NUMBER OF TERMS IN ANY EQUATION.
C                       USED AS DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                 ND3 = MAXIMUM NUMBER OF PREDICTANDS IN ANY EQUATION.
C                       USED AS DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                 ND5 = THE MAXIMUM NUMBER OF CALL LETTERS FOR A SET
C                       OF EQUATIONS.  THIS COULD BE GT ND1.  DIMENSION
C                       CCALLD( ).
C                ND13 = MAXIMUM NUMBER OF DIFFERENT EQUATIONS PER SET.
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       165 = EXTERNAL AND INTERNAL FILE NAMES DON'T
C                             MATCH WHEN NDATE = 9999.
C                       166 = NUMBER OF PREDICTANDS = ZERO OR GT ND3.
C                       167 = NUMBER OF TERMS IN EQUATION = 0 OR GT ND2.
C                       168 = SYSTEM ERROR READING.
C                       169 = NUMBER OF EQUATIONS GT ND1 OR GT ND13.
C                       171 = NO EQUATION FOR ONE OR MORE STATIONS
C                             IN THE STATION LIST.
C                        20 = ERROR OR END OF FILE ON UNIT KFILEQ OR
C                             DATES ON EQUATION FILE DO NOT MATCH NDATE
C                             (FROM RDC OR RDEQN).
C                        21 = LIST TOO LONG FOR DIMENSION ND ON UNIT
C                             KFIL (FROM RDC).
C               NTAND = THE NUMBER OF PREDICTANDS FOR THIS PARTICULAR 
C                       SET OF EQUATIONS.  (INTERNAL)
C              NCOUNT = COUNTS TOTAL NUMBER OF STATIONS TO MAKE 
C                       FORECASTS FOR IN ALL SETS.  (INTERNAL)
C              JCOUNT = COUNTS NUMBER OF STATIONS TO MAKE FORECASTS FOR
C                       FOR EACH SET.  (INTERNAL)
C                KEEP = KEEPS TRACK OF WHETHER A STATION IS IN THE 
C                       INPUT LIST MORE THAN ONCE, WHICH WILL CAUSE A
C                       DIAGNOSTIC.  (INTERNAL)
C              ICOUNT = REGULATES PRINT SPACING IN DO 195 LOOP.
C                       (INTERNAL)
C
C        NONSYSTEM SUBROUTINES USED 
C            IERX, RDC
C     
      CHARACTER*4 STATE
      CHARACTER*8 CCALL(ND1,6),CTEMP(14)
      CHARACTER*8 CCALLD(ND5)
      CHARACTER*60 EQNNAM,EQNCHK
C
      DIMENSION IFOUND(ND1),LOCSTA(ND1)
      DIMENSION NGP(ND13),LGP(ND13),MTRMS(ND13)
      DIMENSION IDEQN(7,ND13,ND2)
      DIMENSION CONST(ND13,ND3),
     1          AVG(ND13,ND3),
     2          CORR(ND13,ND3)
      DIMENSION COEF(ND13,ND2,ND3)
      DIMENSION IDTAND(4,ND3)
C
C     CALL TIMPR(KFILDO,KFILDO,'START RDEQN         ')
C
      IER=0
      INITF=0
      NCOUNT=1
      KGP=0
      IF(IALL.EQ.1)NSTA=0 
C        WHEN IALL = 0, THE NUMBER OF STATIONS WILL BE DETERMINED
C        IN RDEQN.
C
C        INITIALIZE IFOUND( ).
C
      DO 105 K=1,ND1
      IFOUND(K)=0
      LOCSTA(K)=0
 105  CONTINUE
C
      IF(NDATE.EQ.9999)THEN
C
C           READ AND CHECK THE FILE NAME.  THIS IS FOR U700.
C
         STATE=' 110'
         READ(KFILEQ,110,IOSTAT=IOS,ERR=900,END=900)EQNCHK
 110     FORMAT(' ',A60)
C
C     JAN 02:
C     COMMENTED OUT THIS BLOCK TO DISABLE THE INTERNAL NAME CHECK.
C
C        IF(EQNCHK.NE.EQNNAM)THEN
C           WRITE(KFILDO,111)EQNNAM,EQNCHK
C111        FORMAT(/,' ****INPUT EQUATION FILE NAME     ',A60,/,
C    1               '     DOES NOT MATCH INTERNAL NAME ',A60,
C    2               ' AT 111 IN RDEQN.',/,
C    3               '     READING OF EQUATIONS ABORTED.')
C           IER=165
C           GO TO 950
C        ENDIF
C
      ELSE
C
C           READ AND CHECK THE DATE.  THIS IS FOR U900.
C
         STATE=' 111'
         READ(KFILEQ,1115,IOSTAT=IOS,ERR=900,END=900)
     1        IHOUR,MONDAS,MONDAE
 1115    FORMAT(1X,I4,5X,I4,1X,I4)
         CALL DOY(NDATE,JY,JM,JD,JH,MDAY)
C
         IF(IHOUR.EQ.JH*100)THEN
C
            IF(MONDAS.LE.MONDAE)THEN
               IF(JM*100+JD.GE.MONDAS.AND.
     1            JM*100+JD.LE.MONDAE)GO TO 1119
            ELSE
               IF(JM*100+JD.LE.MONDAE.OR.
     1            JM*100+JD.GE.MONDAS)GO TO 1119
            ENDIF
C 
         ENDIF
C                           
         IF(IP14.NE.0)WRITE(IP14,1117)IHOUR,MONDAS,MONDAE,JY,JM,JD,JH,
     1                                EQNNAM
 1117    FORMAT(/,' ****EQUATION SET FOR HOUR',I4.2,
     1            '  STARTING AND ENDING DATES',2I6.4,
     2            '  DOES NOT MATCH NDATE',I6,I3.2,I2.2,I3.2,/,
     3            '     ON FILE ',A60,/,
     4            '     PROCEEDING TO ANOTHER EQUATION SET.')
         IER=20
         GO TO 950
      ENDIF
C    
C        READ NUMBER OF PREDICTANDS.
C
 1119 STATE=' 112'
      READ(KFILEQ,112,IOSTAT=IOS,ERR=900,END=900)NTAND
 112  FORMAT(1X,I4)
C
      IF(NTAND.EQ.0)THEN
         WRITE(KFILDO,113)EQNNAM
 113     FORMAT(/,' ****NUMBER OF PREDICTANDS EQUALS ZERO AT 113',
     1            ' IN RDEQN FOR FILE ',A60,/,
     2            '     READING OF EQUATIONS ABORTED.')
         IER=166
         GO TO 950
C
      ENDIF
C
      IF(NTAND.GT.ND3)THEN
         WRITE(KFILDO,1130)NTAND,ND3,EQNNAM
 1130    FORMAT(/,' ****NUMBER OF PREDICTANDS =',I4,' GT ND3 =',I4,
     1            ' AT 1130 IN RDEQN FOR FILE ',A60,/,
     2            '     READING OF EQUATIONS ABORTED.')
         IER=166
         GO TO 950
C
      ENDIF
C
C        READ PREDICTAND DEFINITIONS.
C
      WRITE(KFILDO,1135)EQNNAM
 1135 FORMAT(/,' PREDICTANDS FOR EQUATION SET ON FILE ',A60)
      IF(IP21.NE.0.AND.IP21.NE.KFILDO)WRITE(IP21,1135)EQNNAM
C
      STATE=' 114'
C
      DO 115 N=1,NTAND
      READ(KFILEQ,114,IOSTAT=IOS,ERR=900,END=900)
     1                (IDTAND(J,N),J=1,4)
 114  FORMAT(1X,I9,2I10,I11)
 115  CONTINUE
C
      DO 120 N=1,NTAND
      WRITE(KFILDO,117)(IDTAND(J,N),J=1,4)
 117  FORMAT(3(1X,I9.9),1X,I10.3)
      IF(IP21.NE.0.AND.IP21.NE.KFILDO)
     1     WRITE(IP21,117)(IDTAND(J,N),J=1,4)
C
 120  CONTINUE
C
C        READ CALL LETTERS FOR THE EQUATIONS TO FOLLOW.
C        THIS SECTION APPLIES WHEN ONLY THE STATIONS INPUT
C        FROM THE CALLING PROGRAM ARE TO BE USED, NOT ALL
C        THOSE WITH THE EQUATIONS.
C
 121  IF(IALL.EQ.0)THEN
C
C           ONLY THE STATIONS IN CCALL( , ) ARE TO BE USED.
C
         CALL RDC(KFILDO,KFILDO,KFILEQ,CCALLD,ND5,CTEMP,14,
     1            '(14(1X,A8))',NSTA1,'99999999',IER1)
         IF(IER1.NE.0)THEN
            WRITE(KFILDO,122)EQNNAM
 122        FORMAT(/,' ****READING OF EQUATIONS ABORTED AT 122',
     1              ' IN RDEQN ON FILE ',A60)
            GO TO 800
         ENDIF
C
         IF(NSTA1.EQ.0)GO TO 190
C           AN EMPTY STATION LIST TERMINATES READING FOR THIS
C           SET.  THERE SHOULD BE A TERMINATOR ON THE INPUT
C           OR A DIAGNOSTIC WILL OCCUR IN RDC.
C 
         IF(IP21.NE.0)THEN
            WRITE(IP21,1220)NSTA1,(CCALLD(J),J=1,NSTA1)
 1220       FORMAT(/,' ****************************************',/,/,
     1               ' ',I6,' STATIONS IN LIST STORED WITH',
     2               ' THESE EQUATIONS',/,('   ',10(' ',A8)))
         ENDIF
C
         KGP=KGP+1
C
         IF(KGP.GT.ND1)THEN
            WRITE(KFILDO,1225)KGP,ND1,EQNNAM
 1225       FORMAT(/,' ****NUMBER OF STATIONS TO MAKE FORECASTS',
     1               ' FOR =',I6,' EXCEEDS ND1 =',I6,
     2               ' AT 1225 IN RDEQN.',/,'     ON FILE ',A60,/,
     3               '     READING OF EQUATIONS ABORTED.')
            KGP=KGP-1
            IER=169
            GO TO 950
C
         ENDIF
C
         IF(KGP.GT.ND13)THEN
            WRITE(KFILDO,1226)ND13,EQNNAM
 1226       FORMAT(/,' ****NUMBER OF GROUPS TO MAKE FORECASTS',
     1               ' FOR EXCEEDS ND13 =',I6,' AT 1226 IN RDEQN.',/,
     2               '     ON FILE ',A60,/,
     3               '     READING OF EQUATIONS ABORTED.')
            KGP=KGP-1
            IER=169
            GO TO 950
C
         ENDIF
C
         JCOUNT=0
         LGP(KGP)=NCOUNT
C
         DO 125 K=1,NSTA1
         KEEP=0
C
C           IF THE STATION READ IS NOT TO BE USED, DON'T 
C           COUNT IT.  AS OF 9/2000, CHANGED 6-WAY IF TEST
C           TO LOOP THROUGH ALL THE LINKS OF CCALL( , ).
C           NOTE THAT WE ARE MOST LIKELY TO FIND A MATCH IN
C           THE FIRST LINK, SO WE CHECK ALL STATIONS IN
C           THIS LINK BEFORE PROCEEDING TO THE SECOND, ETC.
C           ALL TESTS ARE MADE SO AS TO RETAIN ALL DIAGNOSTICS.
C           THAT IS, FOR EACH NSTA1 STATIONS IN CCALLD(K),
C           FOR EACH LINK MM=1,6 IN CCALL( ,MM), AND
C           FOR EACH NSTA STATIONS IN CCALL(L,MM), A CHECK IS
C           MADE FOR CCALLD(K) VS CCALL(L,MM).  TO CIRCUMVENT
C           THESE CHECKS, FORMAT 123 DIAGNOSTIC WILL BE OMITTED.
C           NOTE THAT THE NSTA LOOP NEEDS TO BE INSIDE THE NSTA1
C           LOOP SO THAT THE FORECASTS WILL BE IN THE ORDER OF
C           THE STATION LIST IN CCALL( , ).
C
C           NOTE: IN THE 1/2003 DR. GLAHN VERSION, HE WAS NOT JUMPING
C           OUT OF THE 125 LOOP WHEN HE FOUND A MATCH BECAUSE HE WAS
C           CHECKING ALL OF THE LINKS FOR DUPLICATES.  WE DISABLED
C           THIS TO CUT DOWN ON TIME, AND WE WILL CHECK FOR DUPLICATE
C           LINKS IN THE STATION DICTIONARY AS PART OF DICTIONARY 
C           MAINTENANCE.
C
         DO 1244 MM=1,6
C
         DO 124 L=1,NSTA
C***         WRITE(KFILDO,1227)K,L,CCALLD(K),CCALL(L,1),IFOUND(L)
C*** 1227    FORMAT(' IN RDEQN AT 1227,K,L,CCALLD(K),CCALL(L,1),',
C***     1          'IFOUND(L),',2I6,2X2A8,I4)
C
         IF(CCALLD(K).EQ.CCALL(L,MM))THEN
C
            IF(NCOUNT.GT.ND1)THEN
               WRITE(KFILDO,1228)ND1,EQNNAM
 1228          FORMAT(/,' ****NUMBER OF STATIONS TO MAKE FORECASTS',
     1                  ' FOR EXCEEDS ND1 =',I6,' AT 1228 IN RDEQN.',/,
     2                  '     ON FILE ',A60,/,
     3                  '     READING OF EQUATIONS ABORTED.')
               IER=169
               GO TO 950
            ENDIF
C
            IF(KEEP.GT.0)WRITE(KFILDO,123)CCALL(L,1)
 123        FORMAT(' ****STATION ',A8,' IS IN THE INPUT LIST',
     1             ' MORE THAN ONCE AND HAS AN EQUATION.',
     2             '  NOT NECESSARILY AN ERROR.')
            KEEP=1
C
C              ALLOW LOOP TO CONTINUE IN CASE A STATION IS IN
C              CCALL( , ) MORE THAN ONCE.
C
            IF(IFOUND(L).EQ.1)THEN
               WRITE(KFILDO,1232)CCALLD(K),EQNNAM
 1232          FORMAT(' ****STATION ',A8,' IS IN AN EQUATION STATION',
     1                ' LIST MORE THAN ONCE ON FILE '/
     2                '      ',A60,'   THE FIRST EQUATION IS USED.')
C
C         THE FOLLOWING GO TO STATEMENT JUMPS OUT OF THE LOOP WHEN THE
C         STATION IS FOUND.  THIS SAVES LOTS OF TIME
C
             GO TO 125
            ELSE
               IFOUND(L)=1
               LOCSTA(NCOUNT)=L
C                 STATIONS NOT IN CCALL( , ) ARE NOT KEPT OR COUNTED.
               NCOUNT=NCOUNT+1
               JCOUNT=JCOUNT+1
C                 JCOUNT COUNTS THE STATIONS IN THIS EQUATION SET.
C
C         THE FOLLOWING GO TO STATEMENT JUMPS OUT OF THE LOOP WHEN THE
C         STATION IS FOUND.  THIS SAVES LOTS OF TIME
C
             GO TO 125
            ENDIF
C
         ENDIF
C
 124     CONTINUE
C 
 1244    CONTINUE
C
         IF(IP15.NE.0.AND.KEEP.EQ.0)WRITE(IP15,1245)CCALLD(K)
 1245    FORMAT('     STATION ',A8,' IS IN THE EQUATION FILE, BUT',
     1          ' IS NOT IN THE LIST FOR WHICH FORECASTS ARE DESIRED.')
 125     CONTINUE
C
         NGP(KGP)=JCOUNT
      ELSE
C
C           ALL STATIONS WITH THE EQUATIONS ARE TO BE USED.  EVEN
C           DUPLICATES ARE KEPT.  THE ORDER WILL BE AS READ WITH
C           THE EQUATIONS.  THIS MEANS THAT MORE THAN ONE FORECAST 
C           COULD BE MADE FOR A STATION, EITHER FROM THE SAME
C           EQUATION OR FROM DIFFERENT ONES.
C
         CALL RDC(KFILDO,KFILDO,KFILEQ,CCALL(NCOUNT,1),
     1            ND1-NCOUNT+1,CTEMP,14,'(14(1X,A8))',NSTA1,
     2            '99999999',IER1)
C
         IF(IER1.NE.0)THEN
            WRITE(KFILDO,126)EQNNAM
 126        FORMAT(/,' ****READING ABORTED AT 126 IN RDEQN,',
     1              ' FOR FILE ',A60)
            GO TO 800
         ENDIF
C
         IF(NSTA1.EQ.0)GO TO 190
C 
         IF(IP21.NE.0)WRITE(IP21,127)
 127     FORMAT(/,' ****************************************',/)
C
C           AN EMPTY STATION LIST TERMINATES READING FOR THIS
C           SET.  THERE SHOULD BE A TERMINATOR ON THE INPUT
C           OR A DIAGNOSTIC WILL OCCUR IN RDC.
C
         IF(KGP.GT.ND1)THEN
            WRITE(KFILDO,130)ND1,EQNNAM
 130        FORMAT(/,' ****NUMBER OF STATIONS TO MAKE FORECASTS',
     1               ' FOR EXCEEDS ND1 =',I6,' AT 130 IN RDEQN.',/,
     2               '     FOR FILE ',A60,/,
     3               '     READING OF EQUATIONS ABORTED.')
            IER=169
            GO TO 950
C
         ELSE
            KGP=KGP+1
            NGP(KGP)=NSTA1
            LGP(KGP)=NCOUNT
C
            DO 131 K=1,NSTA1
            LOCSTA(NCOUNT)=NCOUNT
C***D           WRITE(KFILDO,1305)KGP,NGP(KGP),
C***D    1                        LGP(KGP),
C***D    2                        NSTA1,NCOUNT,
C***D    3                        LOCSTA(NCOUNT)
C***D1305       FORMAT(' KGP,NGP(KGP),',
C***D    1              'LGP(KGP),',
C***D    2              'NSTA1,NCOUNT,',
C***D    3              'LOCSTA(NCOUNT) IN RDEQN',
C***D    4              ' AT 1305'/10I6) 
            NCOUNT=NCOUNT+1
 131        CONTINUE
C
            NSTA=NCOUNT-1
         ENDIF
C
      ENDIF
C
      IF(IP21.EQ.0)GO TO 1335
      IF(NGP(KGP).EQ.0)THEN
         WRITE(IP21,132)
 132     FORMAT(' EQUATIONS DO NOT APPLY TO ANY STATIONS',
     1          ' IN THE STATION LIST')
      ELSE
         WRITE(IP21,133)NGP(KGP),(CCALL(LOCSTA(K),1),
     2                  K=LGP(KGP),LGP(KGP)+NGP(KGP)-1)
 133     FORMAT(' FOLLOWING EQUATION(S) APPLY TO THE FOLLOWING',I6,
     2          ' STATIONS',/,('   ',10(' ',A8)))
      ENDIF
C
C        READ THE NUMBER OF TERMS IN THE EQUATIONS.
C
 1335 STATE=' 134'
      READ(KFILEQ,134,IOSTAT=IOS,ERR=900,END=900)NTRMS
 134  FORMAT(' ',I4)
C
      IF(NTRMS.EQ.0)THEN
         WRITE(KFILDO,135)EQNNAM
 135     FORMAT(/,' ****ZERO TERMS INDICATED FOR NEXT EQUATION',
     1            ' AT 135 IN RDEQN ON FILE ',A60,/,
     2            '     READING OF EQUATIONS ABORTED.')
         IER=167
         GO TO 950
      ENDIF
C
      IF(NTRMS.GT.ND2)THEN
         WRITE(KFILDO,1350)NTRMS,ND2,EQNNAM
 1350    FORMAT(/,' ****NUMBER OF TERMS INDICATED FOR NEXT',
     1            ' EQUATION =',I4,' IS GT ND2 =',I4,/,
     2            '     AT 1350 IN RDEQN ON FILE ',A60,/,
     3            '     READING OF EQUATIONS ABORTED.')
         IER=167
         GO TO 950
      ENDIF
C
C        READ THE PREDICTOR ID'S.
C
      STATE=' 136'
      READ(KFILEQ,136,IOSTAT=IOS,ERR=900,END=900)
     1            ((IDEQN(J,KGP,M),J=1,4),M=1,NTRMS)
 136  FORMAT(' ',I9.9,2I10,I11)
C
C        READ THE PREDICTAND AVERAGES, CORRELATIONS, AND CONSTANTS.
C        SINCE THE DECIMAL POINT IS WITH THE EQUATIONS, IT DOESN'T
C        MATTER WHETHER E12.5, E12.7, OR EVEN E12.0 IS USED FOR
C        READING.
C 
      STATE=' 137'
      READ(KFILEQ,138,IOSTAT=IOS,ERR=900,END=900)
     1                   (AVG(KGP,N),N=1,NTAND)
      STATE=' 138'
      READ(KFILEQ,138,IOSTAT=IOS,ERR=900,END=900)
     1                   (CORR(KGP,N),N=1,NTAND)
      STATE=' 139'
      READ(KFILEQ,138,IOSTAT=IOS,ERR=900,END=900)
     1                   (CONST(KGP,N),N=1,NTAND)
 138  FORMAT(' ',10(E13.7))
C     
C        READ THE COEFFICIENTS.
C
      STATE=' 140'
C
      DO 140 M=1,NTRMS
      READ(KFILEQ,138,IOSTAT=IOS,ERR=900,END=900)
     1                 (COEF(KGP,M,N),N=1,NTAND)
 140  CONTINUE
C
      MTRMS(KGP)=NTRMS
      IF(MTRMS(KGP).EQ.0)KGP=KGP-1
C        THE ABOVE STATEMENT CAUSES THE PREVIOUSLY READ
C        EQUATION TO NOT BE KEPT BECAUSE THERE ARE NO
C        STATIONS WITH IT FOR WHICH FORECASTS ARE TO BE MADE.
      MTANDS=NTAND
C
C        WRITE PREDICTAND MEANS IF DESIRED.
C
      IF(IP20.NE.0)THEN
         WRITE(IP20,145)KGP,(AVG(KGP,N),N=1,MIN(6,NTAND))
 145     FORMAT(' AVERAGE FOR EQUATION NO.',I4,22X,6F12.5)
         IF(NTAND.GT.6)WRITE(IP20,146)(AVG(KGP,N),N=7,NTAND)
 146     FORMAT(51X,6F12.5)
      ENDIF   
C
C        WRITE MULTIPLE CORRELATIONS IF DESIRED.
C
      IF(IP19.NE.0)THEN
         WRITE(IP19,150)(CORR(KGP,N),N=1,MIN(6,NTAND))
 150     FORMAT(' CORRELATION COEFFICIENTS',26X,6F12.5)
         IF(NTAND.GT.6)WRITE(IP19,151)(CORR(KGP,N),N=7,NTAND)
 151     FORMAT(51X,6F12.5)
      ENDIF   
C
C        WRITE EQUATIONS IF DESIRED.
C
      IF(IP21.NE.0)THEN
         WRITE(IP21,160)(CONST(KGP,N),N=1,MIN(6,NTAND))
 160     FORMAT(' REGRESSION EQUATION(S)',/,'    CONSTANT',39X,6E12.5)
         IF(NTAND.GT.6)WRITE(IP21,161)(CONST(KGP,N),N=7,NTAND)
 161     FORMAT(51X,6E12.5)
C
         DO 180 M=1,NTRMS
         WRITE(IP21,170)
     1        (IDEQN(J,KGP,M),J=1,4),
     2        (COEF(KGP,M,N),N=1,MIN(6,NTAND))
 170     FORMAT(3X,3(1X,I9.9),1X,I10.3,7X,6E12.5)
         IF(NTAND.GT.6)WRITE(IP21,171)(COEF(KGP,M,N),N=7,NTAND)
 171     FORMAT(51X,6E12.5)
C
 180     CONTINUE
C
C           WRITE CALL LETTERS FOR THIS SET.
C
C***D        WRITE(KFILDO,182)KGP,LGP(KGP),NGP(KGP)
C***D182     FORMAT(' IN RDEQN AT 182',4I10)
      ENDIF
C
      GO TO 121
C
 190  IF(IP21.NE.0)WRITE(IP21,191)
 191  FORMAT(/,' ****************************************')
      IF(IALL.NE.0)GO TO 200
C
C        WHEN A STATION LIST IS FURNISHED, CHECK TO SEE 
C        WHETHER AN EQUATION EXISTS FOR ALL STATIONS FOR
C        THIS SET.
C
      ICOUNT=0
C        ICOUNT USED TO DOUBLE SPACE ONLY FOR THE FIRST MISSING
C        STATION.
C
      DO 195 K=1,NSTA
      IF(IFOUND(K).NE.0)GO TO 195
      ICOUNT=ICOUNT+1
      IF(IP13.EQ.0)GO TO 194
C        THE BELOW MAY GIVE VOLUMINOUS PRINT IN OPERATIONS
C        BECAUSE THERE MAY BE NO EQUATIONS FOR LOTS OF STATIONS
C        FOR SOME PREDICTANDS.
C
      IF(ICOUNT.EQ.1)THEN
         WRITE(IP13,192)CCALL(K,1),EQNNAM
 192     FORMAT(/,' ****',A8,' NO EQUATION FOUND FOR THIS STATION',
     1            ' ON FILE ',A60)
      ELSE
         WRITE(IP13,193)CCALL(K,1),EQNNAM
 193     FORMAT(' ****',A8,' NO EQUATION FOUND FOR THIS STATION',
     1          ' ON FILE ',A60)
      ENDIF
C
 194  IER=171
      INITF=1
C        FCST( , , ) NEEDS TO BE INITIALIZED TO 9999 WHEN
C        THERE IS NOT AN EQUATION FOR ALL STATIONS FOR ALL
C        SETS.
 195  CONTINUE
C 
 200  CONTINUE
C
C        IF ALL STATIONS WITH THE EQUATIONS ARE TO BE USED,
C        PRINT THE LIST IF IP4 NE 0.  IF IALL = 0, THE LIST
C        WILL HAVE ALREADY BEEN PRINTED.
C
      IF(IALL.NE.0.AND.IP4.NE.0)THEN
         WRITE(IP4,210)NSTA,(CCALL(K,1),K=1,NSTA)
 210     FORMAT(/,' ',I6,' STATIONS TO MAKE FORECASTS FOR,',
     1           ' ALL THOSE WITH THE EQUATIONS',/,
     2          ('    ',A8,9(' ',A8)))
      ENDIF
C
D     WRITE(KFILDO,218)KGP
D218  FORMAT(/' KGP'/(' 'I4))
D     WRITE(KFILDO,219)MTANDS
D219  FORMAT(/' MTANDS'/(' 'I4))
D     WRITE(KFILDO,220)(NGP(M),M=1,KGP)
D220  FORMAT(/' NGP'/(' '30I4))
D     WRITE(KFILDO,221)(LGP(M),M=1,KGP)
D221  FORMAT(/' LGP'/(' '30I4))
D     WRITE(KFILDO,222)(MTRMS(M),M=1,KGP)
D222  FORMAT(/' MTRMS'/(' '30I4))
D     WRITE(KFILDO,223)(LOCSTA(K),K=1,NSTA)
D223  FORMAT(/' LOCSTA'/(' '20I6))
C
 800  IF(IER.EQ.0)IER=IER1
C        IER1 COMES FROM RDC.
      GO TO 950
C
 900  CALL IERX(KFILDO,KFILDO,IOS,'RDEQN ',STATE)
      IER=168
C
 950  CONTINUE      
C
C     CALL TIMPR(KFILDO,KFILDO,'END RDEQN           ')
C
      RETURN
      END       

          
