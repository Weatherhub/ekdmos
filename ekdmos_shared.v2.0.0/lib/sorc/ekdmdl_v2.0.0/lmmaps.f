      SUBROUTINE LMMAPS(KFILDO,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1                  XI,YJ,SINL,XMAPF,NSTA,IER)
C
C        JULY     2002   GLAHN   TDL   MOS-2000 
C                                ADAPTED FROM LMMAPF
C
C        PURPOSE 
C            TO RETURN THE SIN OF THE LATITUDE AND THE MAP FACTOR FOR
C            A SET OF NSTA POINTS WHOSE X, Y COODINATES ARE IN 
C            XI( ) AND YI( ) FOR A LAMBERT CONFORMAL MAP.  
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INPUT)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES
C                       WEST.  DO NOT USE NEGATIVE.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS
C                       CORRECT.  DO NOT USE NEGATIVE.  (INPUT)
C              ALATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES NORTH.  (INPUT)
C              ALONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES WEST.  DO NOT USE NEGATIVE.
C                       (INPUT)
C               XI(K) = THE X LOCATION OF THE POINTS (K=1,NSTA).  (INPUT)
C               YJ(K) = THE Y LOCATION OF THE POINTS (K=1,NSTA).  (INPUT)
C             SINL(K) = SINE OF LATITUDE AT THE POINT XI(K),YJ(K)
C                       (K=1,NSTA).  (OUTPUT)
C            XMAPF(K) = THE MAP FACTOR AT THE POINT XI(K),YJ(K)
C                       (K=1,NSTA).  (OUTPUT)
C                NSTA = NUMBER OF VALUES BEING DEALT WITH.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                       105 = FROM LMIJLL.
C              COXLAT = COLATITUDE OF XLAT IN RADIANS.  (INTERNAL)
C              COALAT = COLATITUDE OF ALAT IN RADIANS.  (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            LMIJLL
C
      PARAMETER (PI=3.14159,
     1           RADPDG=PI/180.)
c
      DIMENSION XI(NSTA),YJ(NSTA),SINL(NSTA),XMAPF(NSTA)
C
      IER=0
C
      IF(XMESHL.LE.0..OR.ORIENT.LT.0..OR.XLAT.LT.0..OR.ALONLL.LT.0.)THEN
         WRITE(KFILDO,100)XMESHL,ORIENT,XLAT,ALONLL
 100     FORMAT(/' ****PROBLEM IN LMMAPS WITH EITHER'/
     1           '     XMESHL =',F12.4,',',
     2           '     ORIENT =',F12.4,',',
     3           '     XLAT   =',F12.4,', OR',
     4           '     ALONLL =',F12.4,'.')
         IER=184
         GO TO 200
      ENDIF
C
      COXLAT=RADPDG*(90.-XLAT)
      F=SIN(COXLAT)/(TAN(COXLAT/2.)**COS(COXLAT))
C
C        CALCULATE THE LATITUDE AND LONGITUDE OF EACH GRIDPOINT, THEN
C        THE SINE OF THE LATITUDE AND THE MAP FACTOR.
C
      DO 150 K=1,NSTA
      CALL LMIJLL(KFILDO,XI(K),YJ(K),XMESHL,ORIENT,XLAT,
     1            ALATLL,ALONLL,ALAT,ALON,IER)
C
      IF(ALAT.EQ.9999.)THEN
C           IF A PROBLEM OCCURS IN LMIJLL, A DIAGNOSTIC WILL HAVE
C           BEEN PRINTED THERE.  IER MAY NE 0 AND ALAT BE OK.
C           IER NE 0 WILL BE RETURNED TO CALLING PROGRAM.
         SINL(K)=9999.
         XMAPF(K)=9999.
      ELSE
         COALAT=RADPDG*(90.-ALAT)
         SINL(K)=SIN(RADPDG*ALAT)
         XMAPF(K)=F*((TAN(COALAT/2.)**COS(COXLAT))/SIN(COALAT))
      ENDIF
C
 150  CONTINUE
C
 200  RETURN
      END      
      
      
