      SUBROUTINE UPSLCM(KFILDO,SDATA,DIR,FDUS,FDVS,TM,TDIR,
     1           XMAPFT,ND1,NSTA,XMESHT,DATA,NX,NY,NDATE,IFLAG)
C
C        JULY     2002   GLAHN   TDL   MOS-2000
C        NOVEMBER 2002   WEISS   CHANGED IF STATMENTS COMPARING
C                                MISSING OR ZERO TERRAIN AND WIND
C                                TO INTEGER TO INTEGER COMPARISONS
C        FEBRUARY 2003   WEISS   MODIFIED FORMAT STATEMENTS
C                                TO CONFORM TO FORTRAN 90
C                                STANDARDS ON THE IBM-SP
C        JUNE     2003   GLAHN   CHANGED TESTS ON X FOR ZERO 
C                                INSTALLED BY WEISS FROM
C                                IF(NINT(ABS(X)+0.5).EQ.0)
C                                TO (IF(X.LT..00001)
C        AUGUST   2003   GLAHN   ADDED ABS TO TESTS ON TV, TU,
C                                FDUS( ) AND FDVS( ); CHANGED
C                                .00001 TO .000001 IN 5 TESTS
C
C        PURPOSE 
C            TO COMPUTE THE UPSLOPE WIND FROM U AND V WINDS.
C            INTERPOLATION OF SLOPE AT THE STATION, WHERE THE WINDS
C            ARE, IS BILINEAR WITHIN THE BOX WHERE THE STATION IS.
C            COMPUTATION ROUTINE FOR UPSLOP.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT)
C            SDATA(K) = RETURNED UPSLOPE WIND IN M/SEC.  (OUTPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE
C                       GRID FOR THE COMBINATION OF GRID
C                       CHARACTERISTICS AND STATION K (K=1,NSTA).
C                       (INPUT)
C             FDUS(K) = U WINDS AT STATIONS (K=1,NSTA).  (INPUT)
C             FDVS(K) = V WINDS AT STATIONS (K=1,NSTA).  (INPUT)
C               TM(K) = TERRAIN SLOPE MAGNITUDE (K=1,NSTA).
C                       (INTERNAL)
C             TDIR(K) = TERRAIN SLOPE DIRECTION (K=1,NSTA).
C                       (INTERNAL)
C           XMAPFT(K) = MAP FACTOR AT STATIONS FOR TERRAIN GRID
C                       (K=1,NSTA).  (INPUT)
C                 ND1 = FIRST DIMENSION OF DIR( , ).  (INPUT)
C                NSTA = NUMBER OF STATIONS BEING DEALT WITH.  USED AS
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C              XMESHT = GRID LENGTH IN M AT THE LATITUDE XLAT FOR
C                       TERRAIN GRID.   (INPUT)
C         DATA(IX,JY) = THE TERRAIN FIELD (IX=1,NX)  (JY=1,NY).  (INPUT)
C                  NX = DIMENSION OF DATA( , ) IN IX DIRECTION.  (INPUT)
C                  NY = DIMENSION OF DATA( , ) IN JY DIRECTION.  (INPUT)
C               IFLAG = FLAG TO DETERMINE WHETHER SLOPE COMPUTATIONS
C                       NEED BE MADE:
C                       0 = YES,
C                       1 = NO.
C                  TU = UPSLOPE IN U DIRECTION AT STATION K.  (INTERNAL)
C                  TV = UPSLOPE IN V DIRECTION AT STATION K.  (INTERNAL)
C                WDIR = WIND DIRECTION IN DEGREES.  (INTERNAL)
C              DEGPRA = DEGREES PER RADIAN.  (INTERNAL)
C              RAPDEG = RADIANS PER DEGREE.  (INTERNAL)
C                  WM = MAGNITUDE OF WIND.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      PARAMETER (PI=3.14159,
     1           DEGPRA=180./PI,
     2           RAPDEG=PI/180.)
C
      DIMENSION FDUS(NSTA),FDVS(NSTA),XMAPFT(NSTA),SDATA(NSTA),TM(NSTA),
     1          TDIR(NSTA)
      DIMENSION DIR(ND1,2)
      DIMENSION DATA(NX,NY)
C
      Z90=90.0
C
D     WRITE(KFILDO,100)
D100  FORMAT(/' IN UPSLCM')
C
      IF(IFLAG.EQ.1)THEN
C           TM( ) AND TDIR( ) ALREADY HAVE TERRAIN VALUES, AND THEY
C           DON'T HAVE TO BE COMPUTED.
         GO TO 150
      ENDIF
C
      DO 140 K=1,NSTA
      IX=DIR(K,1)
      JY=DIR(K,2)
C
      IF(IX.EQ.9999.OR.IX-1.LT.1.OR.IX+1.GT.NX.OR.
     1                 JY-1.LT.1.OR.JY+1.GT.NY)THEN
C           ABOVE GUARDS AGAINST MISSING STATION POSITION
C           AND LOCATION BEING TOO CLOSE TO THE GRID EDGE
C           TO COMPUTE.
         TM(K)=9999.
         TDIR(K)=9999.
         GO TO 140
      ELSE
C
C        COMPUTE THE U UPSLOPE AT THE IX OF THE STATION.
C
C           COMPUTE THE WEST UPSLOPE AT BOTH JY AND JY+1 OVER A
C           SINGLE GRID BOX AND INTERPOLATE TO STATION BETWEEN
C           JY AND JY+1.  THE DIFFERENCE IS DIVIDED BY THE MESH
C           LENGTH, ADJUSTED FOR MAP FACTOR.  IT IS ASSUMED THERE
C           ARE NO MISSING VALUES IN THE TERRAIN FIELD.
C
         D1=DATA(IX+1,JY)-DATA(IX,JY)
         D2=DATA(IX+1,JY+1)-DATA(IX,JY+1)
         TUU=(D1+(D2-D1)*(DIR(K,2)-JY))
         TU=TUU*XMAPFT(K)/XMESHT
C
C           COMPUTE THE SOUTH UPSLOPE AT BOTH IX AND IX+1 OVER A
C           SINGLE GRID BOX AND INTERPOLATE TO STATION BETWEEN
C           IX AND IX+1.  THE DIFFERENCE IS DIVIDED BY THE MESH
C           LENGTH, ADJUSTED FOR MAP FACTOR.
C
         D3=DATA(IX,JY+1)-DATA(IX,JY)
         D4=DATA(IX+1,JY+1)-DATA(IX+1,JY)
         TVV=(D3+(D4-D3)*(DIR(K,1)-IX))
         TV=TVV*XMAPFT(K)/XMESHT
C
C           COMPUTE THE UPSLOPE DIRECTION.
C
         IF(ABS(TV).LT..000001)THEN
C
            IF(ABS(TU).LT..000001)THEN
               TM(K)=0.
               TDIR(K)=9999.
               GO TO 140
             ELSE
                TDIR(K)=SIGN(Z90,TU)
             ENDIF
C
         ELSE
            TDIR(K)=DEGPRA*ATAN2(TU,TV)
         ENDIF
C
C           COMPUTE MAGNITUDE OF TERRAIN UPSLOPE AND WIND.
C
         TM(K)=SQRT(TU**2+TV**2)
      ENDIF
C
 140  CONTINUE
C
 150  DO 170 K=1,NSTA 
C
C        COMPUTE THE DIRECTION THE WIND IS BLOWING TOWARD
C        NOT FROM.  BUT FIRST CHECK FOR MISSING AS SET
C        ABOVE.
C
      IF(NINT(TM(K)).EQ.9999)THEN
         SDATA(K)=9999.
         GO TO 170
      ELSEIF(TM(K).LT..000001)THEN
C           TM( ) IS ALWAYS POSITIVE.
         SDATA(K)=0.
         GO TO 170
      ENDIF
C
      IF(ABS(FDVS(K)).LT..000001)THEN
C
         IF(ABS(FDUS(K)).LT..000001)THEN
            SDATA(K)=0.
            GO TO 170
          ELSE
             WDIR=SIGN(Z90,FDUS(K))
          ENDIF
C
      ELSE
         WDIR=DEGPRA*ATAN2(FDUS(K),FDVS(K))
      ENDIF
C
C        COMPUTE MAGNITUDE OF WIND.
C
      TW=SQRT(FDUS(K)**2+FDVS(K)**2)
C
C        COMPUTE UPSLOPE WIND.  IT IS THE PRODUCT OF THE
C        SLOPE AND WIND MAGNITUDES TIMES THE COSINE OF THE
C        ANGLE BETWEEN THEM.  THE SLOPE ANGLE IS TOWARD HIGH
C        HEIGHT; THE WIND DIRECTION IS IN THAT SAME DIRECTION.
C
      SDATA(K)=TM(K)*TW*COS(RAPDEG*(TDIR(K)-WDIR))
C        THE UPSLOPE HAS UNITS M/M.  THE WIND IS IN M/SEC.
C        THIS GIVES THE UPSLOPE WIND IN M/SEC.
C
 170  CONTINUE
C
      RETURN
      END      
