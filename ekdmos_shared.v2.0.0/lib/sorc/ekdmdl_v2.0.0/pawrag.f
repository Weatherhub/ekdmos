      SUBROUTINE PAWRAG(KFILDO,KFILX,CFILX,ID,
     1                  RECORD,IA,IC,NX,NY,IPACK,ND5,MINPK,
     2                  IS0,IS1,IS2,IS4,ND7,
     3                  IPLAIN,PLAIN,NCHAR,
     4                  XMISSP,XMISSS,LX,IOCTET,
     5                  L3264B,L3264W,IER)
C
C        SEPTEMBER 2000   GLAHN   TDL   MOS-2000
C        JUNE      2012   ENGLE   MODIFIED CALL TO INCLUDE PLAIN.
C                                 PLAIN LANGUAGE IS NOW PACKED BY
C                                 USING IACHAR FUNCTION.
C 
C        PURPOSE 
C            TO PREPARE FOR PACKING, PACK, AND WRITE A RECORD TO
C            A MOS-2000 EXTERNAL FILE.  CREATED FOR U361, BUT SHOULD
C            BE USEFUL FOR OTHER SIMILAR ROUTINES.  PAWRA IS FOR
C            USE BY U351 FOR VECTOR DATA; PAWRAG IS FOR U361 FOR
C            GRIDPOINT DATA.
C
C        DATA SET USE 
C            KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT) 
C            KFILX     - UNIT NUMBER FOR THE OUTPUT RANDOM ACCESS FILE.
C                        (INPUT/OUTPUT)
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C               KFILX = UNIT NUMBER OF THE OUTPUT RANDOM ACCESS FILE.
C                       (INPUT)
C               CFILX = THE NAME OF THE OUTPUT RANDOM ACCESS FILE.
C                       (CHARACTER*60)  (INPUT)
C               ID(J) = MOS ID OF VARIABLE TO WRITE (J=1,4).  (INPUT)
C           RECORD(J) = HOLDS CONSTANT DATA FOR WRITING (J=1,ND5).
C                       (INPUT)
C               IA(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C               IC(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                  NX = X EXTENT OF THE GRID.  (INPUT)
C                  NY = Y EXTENT OF THE GRID.  (INPUT)
C            IPACK(J) = SCRATCH ARRAY USED IN PACKING THE DATA
C                       (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF RECORD( ), IA( ), IC( ), AND
C                       IPACK( ).  ND5 = ND2X3 IN CALLING PROGRAM.
C               MINPK = VALUES ARE PACKED IN GROUPS OF MINIMUM SIZE
C                       MINPK.  ONLY WHEN THE NUMBER OF BITS TO HANDLE
C                       A GROUP CHANGES WILL A NEW GROUP BE FORMED.  
C                       (INPUT)
C              IS0(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 0 (L=1,ND7).  (INTERNAL)
C              IS1(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 1 (L=1,ND7).  (INPUT)
C              IS2(L) = HOLDS THE VALUES FOR GRIB SECTION 2 (L=1,ND7).
C                       NOT ACTUALLY USED.  (INTERNAL)
C              IS4(L) = HOLDS THE VALUES FOR GRIB SECTION 4.  NONE OF 
C                       THE VALUES NEED BE FURNISHED BY THE USER.
C                       IS4(2) IS SET TO INDICATE NON-GRIDPOINT DATA,
C                       COMPLEX PACKING, ORIGINAL SCALED VALUES TO BE
C                       PACKED (NOT SECOND ORDER SPATIAL DIFFERENCES),
C                       AND MISSING VALUES OR NOT DEPENDING ON WHETHER 
C                       OR NOT XMISS NE OR EQ ZERO, RESPECTIVELY.
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ),AND IS4( ).
C                       (INPUT)
C               PLAIN = THE PLAIN LANGUAGE DESCRIPTION OF THE PREDICTORS
C                       EQUIVALENCED TO IPLAIN( , ). (CHARACTER*32)
C         IPLAIN( , ) = NAME OF VARIABLE TO PACK.  THIS IS CHARACTER
C                       DATA IN AN INTEGER ARRAY.  (INPUT)
C               NCHAR = THE NUMBER OF CHARACTERS OF PLAIN LANGUAGE 
C                       IN PLAIN TO PACK WITH THE DATA.  (INPUT)
C              XMISSP = PRIMARY MISSING VALUE INDICATOR.  (INPUT)
C              XMISSS = SECONDARY MISSING VALUE INDICATOR.  SET TO 0 TO 
C                       IN CALLING PROGRAM TO INDICATE THERE WILL BE NO 
C                       SECONDARY MISSING VALUE INDICATOR (SEE XMISSP).
C                       (INPUT)
C                  LX = THE NUMBER OF GROUPS (THE NUMBER OF 2ND ORDER 
C                       MINIMA).  WHILE NEEDED ONLY IN SUBROUTINE PACK,
C                       IT IS OUTPUT IN THE ARGUMENT LIST OF PAWRAG IN
C                       CASE THE USER WANTS TO KNOW IT.  (OUTPUT)
C              IOCTET = THE TOTAL MESSAGE SIZE IN OCTETS.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2). 
C                       (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD VALUE.
C                       OTHER VALUES COME FROM CALLED SUBROUTINES.
C 
C        NON SYSTEM SUBROUTINES CALLED 
C           PACK2D, UNPKBG, WRTDLM
C
      CHARACTER*32 PLAIN
      DIMENSION RECORD(ND5)
      DIMENSION IA(ND5),IC(ND5),IPACK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION IPLAIN(L3264W,4)
      DIMENSION ID(4)
C
      IER=0
C
C        ZERO ARRAYS, BUT NOT IS1( ) AND IS2( ).  IS2( ) IS THE GRID
C        DEFINITION SECTION AND IS1(2) THROUGH IS1(9) HAVE BEEN 
C        FILLED BY THE CALLING PROGRAM.  IS1(17) IS INPUT AS 
C        THE SCALE FACTOR.
C
      DO 110 J=1,ND7
      IS0(J)=0
      IS4(J)=0
 110  CONTINUE
C
C        SET IDS INTO IS1( ).  IS1(2) HAS BEEN SET TO 1 IN CALLING
C        PROGRAM TO INDICATE NO BIT MAP AND GRIDPOINT DATA.
C
      IS1(9)=ID(1)
      IS1(10)=ID(2)
      IS1(11)=ID(3)
      IS1(12)=ID(4)
C
C        PUT PLAIN LANGUAGE INTO IS1( ) FOR PACKING.
C
      LOC=1
C        LOC = WORD POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
      IPOS=1
C        IPOS = BIT POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
C
      IS1(22)=NCHAR
C
CINTEL
C
C        USE IACHAR FUNCTION TO PUT ONE CHARACTER (BYTE) FROM
C        PLAIN INTO ONE IS1( ) WORD.
C
      DO J=1, NCHAR
         IS1(J+22)=IACHAR(PLAIN(J:J))
      END DO
C      DO 120 J=1,NCHAR
C      CALL UNPKBG(KFILDO,IPLAIN(1,1),4*L3264W,LOC,IPOS,
C     1            IS1(J+22),8,L3264B,IER,*900)
CC        NOTE THAT THIS PUTS ONE BYTE PER IS1( ) WORD.
C 120  CONTINUE
CINTEL
C
C        PACK FROM RECORD( ) TO IPACK( ).
C
      CALL PACK2D(KFILDO,RECORD,IA,IC,NX,NY,IS0,IS1,IS2,IS4,
     1            ND7,XMISSP,XMISSS,IPACK,ND5,
     2            MINPK,LX,IOCTET,L3264B,IER)
      IF(IER.NE.0)GO TO 900
D     WRITE(KFILDO,125)(IS2(J),J=1,ND7)
D125  FORMAT(/' IN PAWRAG AT 125--IS2( )'/(10I10))
C
C        WRITE THE DATA.
C      
      CALL WRTDLM(KFILDO,KFILX,CFILX,ID,IPACK,(IOCTET*8)/L3264B,
     1            2,0,L3264B,IER)
 900  RETURN
      END
      
