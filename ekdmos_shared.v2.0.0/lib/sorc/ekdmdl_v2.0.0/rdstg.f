      SUBROUTINE RDSTG(KFILDO,IP4,IP5,KFILD,NEW,NALPH,NCALER,
     1                 CCALL,CCALLD,NAME,NELEV,IWBAN,
     2                 STALAT,STALON,ITIMEZ,ISDATA,
     3                 NGP,IPACK,ND1,KGP,NSTA,ND5,IER)
C 
C        SEPTEMBER 2003   GLAHN   TDL   MOS-2000 
C        OCTOBER   2003   GLAHN   ADDED IPACK( ), ITIMEZ( ), ISDATA( ),
C                                 AND CCALLD( )  TO DIMENSION STATEMENTS
C
C        PURPOSE 
C            SWITCHES AMONG READING ROUTINES (E.G., RDSTGN,
C            RDSTGA, RDSTGF, AND RDSTGD).  EACH READS A LIST
C            OF "STATIONS" AND MATCH THE ENTRIES WITH A DIRECTORY.
C            THE LIST AND DIRECTORY ENTRIES ARE IN ASCII.
C            MOST READERS ARE ARE FOR RELATIVELY SHORT LISTS OF 
C            STATIONS FOR WHICH CALL LETTERS ARE APPROPRIATE;
C            RDSTGF AND RDSTGD ARE FOR LARGE NUMBER OF GRIDPOINTS,
C            WHICH ARE TREATED AS STATIONS.
C            
C            RDSTGA ALPHABATIZES STATIONS WINTHIN GROUPS IN THE
C                   LIST READ.
C            RDSTGN RETURNS THE GROUPS AS READ.
C            RDSTGF IS FOR A FAST SEARCH WHEN THE LIST AND THE 
C                   DIRECTORY ARE IN THE SAME ORDER.
C            RDSTGD IS FOR A BINARY SEARCH WHEN THE LIST AND THE
C                   DIRECTORY ARE NOT NECESSARILY IN ORDER AND/OR
C                   THE LIST CAN CONTAIN DUPLICATES.
C
C        DATA SET USE 
C            KFILDO   - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (OUTPUT) 
C            IP4      - UNIT NUMBER FOR OUTPUT OF CALL LETTERS.
C                       (OUTPUT)
C            IP5      - UNIT NUMBER FOR OUTPUT OF DIRECTORY INFORMATION
C                       (OUTPUT) 
C            KFILD(J) - UNIT NUMBER FROM WHICH TO READ STATION LIST
C                       (J=1) AND STATION DIRECTORY (J=2).  IT IS 
C                       ASSUMED FILES HAVE BEEN OPENED.  (INPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C                 IP4 = INDICATES WHETHER (>0) OR NOT (=0) THE STATION 
C                       LIST (CALL LETTERS ONLY) WILL BE WRITTEN TO UNIT
C                       IP4.  IF THERE ARE INPUT ERRORS, THE STATION
C                       LIST WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE
C                       UNIT KFILDO AS WELL AS TO UNIT IP4.  (INPUT)
C                 IP5 = INDICATES WHETHER (>0) OR NOT (=0) THE STATION 
C                       DIRECTORY INFORMATION WILL BE WRITTEN TO UNIT
C                       IP5.  IF THERE ARE INPUT ERRORS, THE STATION
C                       LIST WILL BE WRITTEN TO THE DEFAULT OUTPUT FILE
C                       UNIT KFILDO AS WELL AS TO UNIT IP5.  (INPUT) 
C            KFILD(J) = UNIT NUMBER FROM WHICH TO READ STATION LIST
C                       (J=1) AND STATION DIRECTORY (J=2).  IT IS 
C                       ASSUMED FILES HAVE BEEN OPENED.  (INPUT) 
C                 NEW = 0 WHEN OLD 3-LETTER CALL LETTERS ARE TO BE USED;
C                       1 WHEN NEW 4-LETTER ICAO IDENTIFIERS ARE TO BE
C                         USED;
C                       2 WHEN IDS ARE GRID ORIENTED AND DUPLICATES CAN
C                         BE PRESENT;
C                       3 WHEN IDS ARE GRID ORIENTED AND DUPLICATES CAN
C                         NOT ACCOMMODATED.
C                       (INPUT)
C               NALPH = 1 WHEN THE CALL STATIONS USED ARE TO BE
C                         ALPHABETIZED BY GROUP (MORE EXACTLY, PUT IN
C                         THE ORDER THEY EXIST IN THE STATION DIRECTORY.
C                       0 WHEN THE ORDER READ IN IS TO BE PRESERVED.
C                       (INPUT)
C              NCALER = THE NUMBER OF THE CALLING PROGRAM.  HELPS TO
C                       SWITCH AMONG READERS.  (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  (CHARACTER*8)
C                       (OUTPUT)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS LIST IS 
C                       USED IN RDSTGA AND RDSTGN TO RETAIN THE ORIGINAL
C                       LIST IN CCALL( , ).  (CHARACTER*8)  (INTERNAL)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA)  (CHARACTER*20)
C                       (OUTPUT)
C            NELEV(K) = ELEVATION OF STATIONS (K=1,NSTA).  (OUTPUT)
C            IWBAN(K) = WBAN NUMBERS OF STATIONS (K=1,NSTA).  (OUTPUT) 
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (OUTPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (OUTPUT)
C           ITIMEZ(K) = THE TIME ZONE INDICATOR FROM THE STATION 
C                       DIRECTORY (K=1,NSTA)  (OUTPUT)
C           ISDATA(K) = WORK ARRAY (K=1,NSTA).  (INTERNAL)
C              NGP(J) = SIZE OF KGP GROUPS OF STATIONS (J=1,KGP), MAX
C                       OF ND1.  (OUTPUT)
C            IPACK(K) = WORK ARRAY (K=1,NSTA).  (INTERNAL)
C                 ND1 = SIZE OF ARRAYS CCALL( , ), NAME( ),
C                       NELEV( ), IWBAN( ), STALAT( ), STALON( )
C                       IFOUND( ), AND NGP( ).  THIS IS THE MAXIMUM
C                       NUMBER OF STATIONS IN THE LIST TO BE RETURNED.
C                       (INPUT)
C                 KGP = THE NUMBER OF VALUES RETURNED IN NGP( ).
C                       (OUTPUT) 
C                NSTA = THE TOTAL NUMBER OF STATIONS RETURNED IN
C                       CCALL( , ) AND OTHER OUTPUT ARRAYS.  (OUTPUT)
C                 ND5 = TO PASS TO CALLED ROUTINES.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                       0  = GOOD RETURN.
C                       SEE CALLED ROUTINES FOR OTHER VALUES.
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            RDSTGN, RDSTGA, RDSTGF, RDSTGD 
C
      CHARACTER*8 CCALL(ND1,6),CCALLD(ND5)
      CHARACTER*20 NAME(ND1)
C
      DIMENSION NELEV(ND1),IWBAN(ND1),STALAT(ND1),STALON(ND1),
     1          IFOUND(ND1),NGP(ND1),IPACK(ND1),ITIMEZ(ND1),
     2          ISDATA(ND1)
      DIMENSION IGLIST(ND1)
C        IGLIST( ) IS AN AUTOMATIC ARRAY.
      DIMENSION KFILD(2)
C
      IF(NEW.LE.1)THEN
C
         IF(NCALER.EQ.171)THEN
C
C           RDSTGN AND RDSTGA ARE FOR ICAO OR SAO LOCATION IDENTIFIERS.
C
            IF(NALPH.EQ.0)THEN
               CALL RDSTGN(KFILDO,IP4,IP5,KFILD,NEW,CCALL,
     1                     NAME,NELEV,IWBAN,STALAT,STALON,ISDATA,
     2                     NGP,ND1,KGP,NSTA,IER)
C
            ELSEIF(NALPH.EQ.1)THEN
               CALL RDSTGA(KFILDO,IP4,IP5,KFILD,NEW,CCALL,CCALLD,
     1                     NAME,NELEV,IWBAN,STALAT,STALON,ISDATA,
     2                     NGP,IPACK,ND1,KGP,NSTA,IER)
C                 CCALLD( ) IS TREATED HERE AS IF IT HAD THE SAME
C                 DIMENSIONS AS CCALL( , ).  THIS IS OK, BECAUSE
C                 ND5 IS GE ND1.
            ELSE
C
               WRITE(KFILDO,110)NALPH,NEW
 110           FORMAT(/' ****THE VALUE OF NALPH =',I4,' IS NOT 0 OR 1',
     1                 ' WHEN NEW =',I4,'.  STOP IN RDSTG AT 110.')
               STOP 110
           ENDIF
C*******
C           PUT TEST FOR OTHER CALLING PROGRAMS HERE, OR MODIFY
C           TEST ON NCALER ABOVE.
C*******
         ELSE
C
            WRITE(KFILDO,115)NCALER
 115        FORMAT(/' ****THE VALUE OF NCALER =',I4,' IS NOT ONE OF',
     1              ' THE POSSIBLE CALLING PROGRAMS',
     2              ' .  STOP IN RDSTG AT 115.')
            STOP 115
         ENDIF
C
C           RDSTGD IS FOR GRIDPOINT LOCATIONS WITH DUPLICATES.
C
      ELSEIF(NEW.EQ.2)THEN
         CALL RDSTGD(KFILDO,IP4,IP5,KFILD,NEW,CCALL,
     1               NAME,NELEV,IWBAN,STALAT,STALON,ITIMEZ,ISDATA,
     2               NGP,ND1,KGP,NSTA,ND5,IER)
      ELSEIF(NEW.EQ.3)THEN
C
C           RDSTGF IS FOR GRIDPOINT LOCATIONS WITHOUT DUPLICATES.
C
         CALL RDSTGF(KFILDO,IP4,IP5,KFILD,NEW,CCALL,
     1               NAME,NELEV,IWBAN,STALAT,STALON,ISDATA,
     2               NGP,ND1,KGP,NSTA,IER)
      ELSE
C
         WRITE(KFILDO,120)NEW
 120     FORMAT(/' ****THE VALUE OF NEW =',I4,
     1              ' IS NOT 0, 1, 2, OR 3.  STOP IN RDSTG AT 120.')
         STOP 120
      ENDIF
C
      RETURN
      END
