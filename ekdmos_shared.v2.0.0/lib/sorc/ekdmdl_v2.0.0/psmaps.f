      SUBROUTINE PSMAPS(KFILDO,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1                  XI,YJ,SINL,XMAPF,NSTA,IER)
C
C        JULY     2002   GLAHN   TDL   MOS-2000 
C                                ADAPTED FROM PSMAPF
C
C        PURPOSE 
C            TO RETURN THE SIN OF THE LATITUDE AND THE MAP FACTOR FOR
C            A SET OF NSTA POINTS WHOSE X, Y COODINATES ARE IN 
C            YI( ) AND XI( ) FOR A POLAR STEREOGRAPHIC MAP.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INPUT)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES
C                       WEST.  DO NOT USE NEGATIVE.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS
C                       CORRECT.  DO NOT USE NEGATIVE.  (INPUT)
C              ALATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES NORTH.  (INPUT)
C              ALONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF
C                       THE GRID IN DEGREES WEST.  DO NOT USE NEGATIVE.
C                       (INPUT)
C               XI(K) = THE X LOCATION OF THE POINTS (K=1,NSTA).
C                       (INPUT)
C               YJ(K) = THE Y LOCATION OF THE POINTS (K=1,NSTA). 
C                       (INPUT)
C             SINL(K) = SINE OF LATITUDE AT THE POINT XI(K),YJ(K)
C                       (K=1,NSTA).  (OUTPUT)
C            XMAPF(K) = THE MAP FACTOR AT THE POINT XI(K),YJ(K)
C                       (K=1,NSTA).  (OUTPUT)
C                NSTA = NUMBER OF VALUES BEING DEALT WITH.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                         0 = GOOD RETURN.
C                       184 = PROBLEM WITH EITHER XMESHL, ORIENT,
C                             XLAT, OR ALONLL.
C                 XNP = THE IX POSITION OF THE NORTH POLE RELATIVE TO
C                       THE LOWER LEFT CORNER OF THE GRID.  (INTERNAL)
C                 YNP = THE JY POSITION OF THE NORTH POLE RELATIVE TO
C                       THE LOWER LEFT CORNER OF THE GRID.  (INTERNAL)
C              SINRXL = SINE OF THE LATITUDE AT WHICH XMESHL IS CORRECT.
C                       (INTERNAL)
C              POL2EQ = DISTANCE FROM THE NORTH POLE TO THE EQUATOR IN 
C                       GRID UNITS.  NOTE THAT THIS IS THE DISTANCE
C                       ON THE IMAGE PLANE, NOT ON THE EARTH.
C                       (INTERNAL)
C              P2EQSQ = DISTANCE FROM THE NORTH POLE TO THE EQUATOR IN
C                       GRID UNITS SQUARED.  NOTE THAT THIS IS THE
C                       DISTANCE ON THE IMAGE PLANE, NOT ON THE EARTH.
C                       (INTERNAL)
C                  PI = PI.  SET BY PARAMETER.  (INTERNAL)
C               RERTH = RADIUS OF EARTH IN M.  SET BY PARAMETER.
C                       (INTERNAL)
C              RADPDG = RADIANS PER DEGREE.  SET BY PARAMETER.
C                       (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      PARAMETER (PI=3.14159)
      PARAMETER (RERTH=6371200.)
      PARAMETER (RADPDG=PI/180.)
C
      DIMENSION XI(NSTA),YJ(NSTA),SINL(NSTA),XMAPF(NSTA)
C
      IER=0
C
      IF(XMESHL.LE.0..OR.ORIENT.LT.0..OR.XLAT.LT.0..OR.ALONLL.LT.0.)THEN
         WRITE(KFILDO,100)XMESHL,ORIENT,XLAT,ALONLL
 100     FORMAT(/' ****PROBLEM IN PSMAPS WITH EITHER'/
     1           '     XMESHL =',F12.4,',',
     2           '     ORIENT =',F12.4,',',
     3           '     XLAT   =',F12.4,', OR',
     4           '     ALONLL =',F12.4,'.')
         IER=184
         GO TO 200
      ENDIF
C
      CALL PSLLIJ(KFILDO,90.,0.,XMESHL,ORIENT,XLAT,ALATLL,ALONLL,
     1               XNP,YNP)
C        XNP AND YNP ARE THE NORTH POLE GRIDPOINT POSITIONS
C        CORRESPONDING TO THE GRID BEING USED.  (BECAUSE OF SIMPLICITY
C        OF CALCULATIONS FOR POLAR STEREOGRAPHIC MAP, ONCE THE POLE
C        POSTION IS KNOWN, THE SINE OF THE LATITUDE AND MAP
C        FACTOR ARE EASILY CALCULATED.  TIME HAS NOT BEEN DEVOTED 
C        TO TRYING TO MAKE THIS SIMPLIFICATION FOR THE LAMBERT.)
C
D     WRITE(KFILDO,101)XMESHL,ORIENT,XLAT,ALATLL,ALONLL,XNP,YNP
D101  FORMAT(/' ****AT 101 IN PSMAPS--',
D    1        'XMESHL,ORIENT,XLAT,ALATLL,ALONLL,XNP,YNP',7F10.1)
C
      POL2EQ=RERTH*(1.+SIN(XLAT*RADPDG))/XMESHL
C        POL2EQ = THE DISTANCE FROM THE POLE TO THE EQUATOR IN 
C        GRID UNITS ON THE IMAGE PLANE FOR A POLAR STEREOGRAPHIC MAP,
C        ON WHICH THE XMESHL APPLIES AT XLAT.
C
D     WRITE(KFILDO,215)XMESHL,XLAT,ORIENT,ALATLL,ALONLL,POL2EQ,
D    1                 RADPDG,XNP,YNP
D215  FORMAT(/' IN PSMAPS'
D    1     /'      XMESHL       XLAT     ORIENT     ALATLL     ALONLL',
D    2       '     POL2EQ     RADPDG        XNP        YNP'/' '9F11.3)
C
      SINRXL=SIN(XLAT*RADPDG)
      P2EQSQ=POL2EQ**2
C
      DO 150 K=1,NSTA
      DYSQ=(YNP-YJ(K))**2
      DXSQ=(XNP-XI(K))**2
      SINL(K)=(P2EQSQ-(DXSQ+DYSQ))/(P2EQSQ+(DXSQ+DYSQ))
      XMAPF(K)=(1.+SINRXL)/(1.+SINL(K))
C
D     WRITE(KFILDO,248)XI(K),YJ(K),XNP,YNP,DXSQ,DYSQ,
D    1                 SINL(K),XMAPF(K)
D248  FORMAT(' XI(K),YJ(K),XNP,YNP,DXSQ,DYSQ,SINL(K),XMAPF',
D    1       ' '2F8.2,6F11.2)
C
 150  CONTINUE
C
 200  RETURN
      END
