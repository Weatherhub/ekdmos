      SUBROUTINE TANDID(KFILDO,KFILCP,KFILPF,PFCORR,IP7,IP9,IDTAND,
     1                  IDTPAR,ISCALD,PLAIN,ICAT,MTANDS,NSETS,ND3,ND11,
     2                  ISTOP,IER)
C
C        SEPTEMBER 1998   GLAHN   TDL   MOS-2000
C        SEPTEMBER 1998   GLAHN   ADDED IDTPAR( ) 
C        SEPTEMBER 1998   GLAHN   ADDED CHANGING CCC = XXX TO 2XX
C        JANUARY   1999   GLAHN   ADDED PREDICTAND/FORECAST TABLE
C        FEBRUARY  1999   GLAHN   NCOUNT INITIALIZED DIFFERENTLY
C        MAY       1999   GLAHN   ADDED ICAT( , )
C        APRIL     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                 CONFORM TO FORTRAN 90 STANDARDS
C                                 ON THE IBM SP
C        MARCH     2007   GLAHN   COMMENT CHANGE
C        MARCH     2007   GLAHN   ADDED IER RETURN COMMENT
C
C        PURPOSE
C            TO FIND PLAIN LANGUAGE AND SCALING CONSTANTS FOR
C            PREDICTANDS FOR U700 AND U900.  THE CCCFFF OF EACH
C            PREDICTAND IS CHANGED THROUGH A CORRESPONDENCE TABLE
C            TO A FORECAST CCCFFF.  ALL OTHER PORTIONS OF THE ID
C            ARE LEFT INTACT.
C   
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILCP    - UNIT NUMBER FOR VARIABLE CONSTANT FILE.
C                        (INPUT)
C            KFILPF    - UNIT NUMBER FOR READING PREDICTAND/FORECAST
C                        CORRESPONDENCE TABLE.  (INPUT)
C            IP7       - THE UNIQUE PREDICTAND LIST IN SUMMARY FORM. 
C                        THIS LIST INCLUDES THE PARSED ID'S IN 
C                        IDPARS( , ).  (OUTPUT)
C            IP9       - THE PREDICTAND LIST IN SUMMARY FORM, NOT
C                        INCLUDING THE PARSED ID'S IN IDPARS( , ),
C                        BUT INCLUDING THE PLAIN LANGUAGE INFORMATION. 
C                        (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILCP = UNIT NUMBER FOR PREDICTAND CONSTANT FILE.  THIS
C                       CONTAINS DEFAULT VALUES FOR CERTAIN CONSTANTS. 
C                       THESE INCLUDE PLAIN LANGUAGE NAMES.  (INPUT)
C              KFILPF = UNIT NUMBER FOR READING PREDICTAND/FORECAST
C                       CORRESPONDENCE TABLE AND THE PROCESSING
C                       INDICATOR.  (INPUT)
C              PFCORR = NAME OF PREDICTAND/FORECAST FILE. 
C                       (CHARACTER*60) (INPUT)
C                 IP7 = WHEN IP7 NE 0, THE PREDICDTAND LIST 
C                       IS WRITTEN TO UNIT IP7, INCLUDING PARSED IDS.
C                       (INPUT)
C                 IP9 = WHEN IP9 NE 0, THE PREDICTAND LIST 
C                       IS WRITTEN TO UNIT IP9, NOT INCLUDING THE 
C                       PARSED ID'S IN IDPARS( , ), BUT INCLUDING 
C                       THE PLAIN LANGUAGE INFORMATION.  (INPUT)
C      IDTAND(J,NN,I) = THE PREDICTAND ID'S (J=1,4), FOR PREDICTAND NN 
C                       (NN=1,MTANDS(I)), AND EQUATION SET I (I=1,NSETS).
C                       (INPUT/OUTPUT)
C      IDTPAR(J,NN,I) = THE PARSED PREDICTAND ID'S (J=1,15), FOR
C                       PREDICTAND NN (NN=1,MTANDS(I)), AND EQUATION
C                       SET I (I=1,NSETS).  (OUTPUT)
C        ISCALD(NN,I) = SCALING FACTORS FOR PREDICTAND NN 
C                       (NN=1,MTANDS(I)), AND EQUATION SET I (I=1,NSETS).
C                       (OUTPUT)
C         PLAIN(NN,I) = THE PLAIN LANGUAGE DESCRIPTION OF THE PREDICTAND NN 
C                       (NN=1,MTANDS(I)), AND EQUATION SET I (I=1,NSETS).
C                       (CHARACTER*32)  (OUTPUT)
C          ICAT(NN,I) = THE POSTPROCESSING INDICATOR FOR THIS EQUATION
C                       SET I (I=1,NSETS) AND PREDICTAND (NN=1,MTANDS(I)).
C                       FOR EXAMPLE, THE VALUE 5 WOULD REFER TO 
C                       SUBROUTINE CAT5.  (INPUT)
C           MTANDS(I) = THE NUMBER OF PREDICTANDS FOR EACH EQUATION
C                       SET I (I=1,NSETS).  (OUTPUT)
C               NSETS = THE NUMBER OF SETS OF EQUATIONS.
C                       (INPUT)
C                 ND3 = MAXIMUM NUMBER OF PREDICTANDS IN EQUATION.
C                       (INPUT)
C                ND11 = MAXIMUM NUMBER OF SETS OF EQUATIONS.
C                       (INPUT)
C               ISTOP = INCREMENTED BY 1 WHEN AN ERROR OCCURS.
C                       (INPUT/OUTPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN
C                       172 = ERROR READING FORECAST TABLE.
C         IDPARS(J,N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C           TRESHL(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C            IDT(J,N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C             JD(J,N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C              ISC(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C            SMULT(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C             SADD(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C           ORIGIN(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C             CINT(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C            ICATC(N) = WORK ARRAY FOR PRINTING.  (INTERNAL) (AUTOMATIC)
C 
C        NONSYSTEM SUBROUINES USED 
C            PRSID2, SETPLN
C
      CHARACTER*12 UNITS(ND3*ND11)
C        UNITS( ) IS AN AUTOMATIC ARRAY.
      CHARACTER*32 PLAIN(ND3,ND11)
      CHARACTER*32 PL(ND3*ND11)
C        PL( ) IS AN AUTOMATIC ARRAY.
      CHARACTER*60 PFCORR
C
      DIMENSION MTANDS(ND11)
      DIMENSION IDTAND(4,ND3,ND11),ISCALD(ND3,ND11),
     1          IDTPAR(15,ND3,ND11),ICAT(ND3,ND11)
      DIMENSION IDT(4,ND3*ND11),IDPARS(15,ND3*ND11),
     1          TRESHL(ND3*ND11),JD(4,ND3*ND11),
     2          ISC(ND3*ND11),SMULT(ND3*ND11),SADD(ND3*ND11),
     3          ORIGIN(ND3*ND11),CINT(ND3*ND11),ICATC(ND3*ND11)
C        IDT( , ), IDPARS( , ), TRESH( ), JD( , ), SMULT( ), ISC( ),
C        SADD( ), ORIGIN( ), CINT( ), AND ICATC( ) ARE AUTOMATIC ARRAYS.
C
D     CALL TIMPR(KFILDO,KFILDO,'START TANDID        ')
      IER=0
      NCOUNT=0
      ND311=ND3*ND11
C
C        CHANGE PREDICTAND IDS TO FORECAST IDS.
C
      REWIND KFILPF
C
 119  READ(KFILPF,120,IOSTAT=IOS,ERR=121,END=130)ICCCFFF,MCCCFFF,ICATT
 120  FORMAT(I6,2X,I6,I4)
      GO TO 123
C
 121  WRITE(KFILDO,122)IOS,PFCORR
 122  FORMAT(/,' ****ERROR READING PREDICTAND/FORECAST TABLE',
     1         ' IN TANDID AT 122, IOSTAT =',I5,/,
     2         '     FILE NAME = ',A60)
      IER=172
      ISTOP=ISTOP+1
      GO TO 170
C
 123  DO 125 I=1,NSETS
      DO 124 NN=1,MTANDS(I)
C
D     WRITE(KFILDO,1235)I,NN,ICCCFFF,MCCCFFF,ICATT,
D    1                  IDTAND(1,NN,I)
D1235 FORMAT(' AT 1235 IN TANDID--I,NN,ICCCFFF,MCCCFFF,',
D    1                 'IDTAND(1,NN,I)'8I10)
      IF(ICCCFFF.NE.IDTAND(1,NN,I)/1000)GO TO 124
C        THERE IS A MATCH.
      IDTAND(1,NN,I)=IDTAND(1,NN,I)-(IDTAND(1,NN,I)/1000)*1000
     1               +MCCCFFF*1000
      ICAT(NN,I)=ICATT
 124  CONTINUE
 125  CONTINUE
C
      GO TO 119
C
C        PARSE PREDICTAND IDS INTO IDPARS( , , ).
C
 130  DO 145 I=1,NSETS
C
      DO 144 NN=1,MTANDS(I)
      NCOUNT=NCOUNT+1
C
C        TEST ARRAY SIZE.  THIS IS A SAFETY; CHECK
C        SHOULD BE IN CALLING PROGRAM.
C
      IF(NCOUNT.GT.ND311)THEN
         WRITE(KFILDO,131)NCOUNT,ND311
 131     FORMAT(/,' ****NUMBER OF PREDICTANDS EXCEEDS',
     1            ' NUMBER ALLOWED =',I4,'.',/,
     2            '     STOP IN TANDID AT 131.')
         STOP 131
      ENDIF
C
      IDT(1,NCOUNT)=IDTAND(1,NN,I)
      IDT(2,NCOUNT)=IDTAND(2,NN,I)
      IDT(3,NCOUNT)=IDTAND(3,NN,I)
      IDT(4,NCOUNT)=IDTAND(4,NN,I)
      CALL PRSID2(KFILDO,IDT(1,NCOUNT),IDPARS(1,NCOUNT),TRESHL(NCOUNT))
C
C        FOR ALL PROGRAMS EXCEPT U201, IDPARS(15, ) AND IT'S ROLE IN
C        ID(4) HAS NO MEANING.  THE INTERPOLATED INPUT FILE WILL NOT
C        INCLUDE IT.  THEREFORE, SET IT TO ZERO.
C
      IDPARS(15,NCOUNT)=0
      IDT(4,NCOUNT)=(IDT(4,NCOUNT)/10)*10
C
C        PREPARE "BASIC" VARIABLE ID'S.  FOR PROGRAMS READING U201
C        OUTPUT (E.G., U600), JD( , , ) = ID( , , ) EXCEPT IN
C        ID(1, , ) THE BINARY INDICATOR IS OMITTED AND IN JD(4, , )
C        THE THRESHOLDS AND IDPARS(15, ) ARE OMITTED.
c
      JD(1,NCOUNT)=IDPARS(1,NCOUNT)*1000000+
     1           IDPARS(2,NCOUNT)*1000+
     2           IDPARS(4,NCOUNT)
      JD(2,NCOUNT)=IDT(2,NCOUNT)
      JD(3,NCOUNT)=IDT(3,NCOUNT)
      JD(4,NCOUNT)=IDPARS(13,NCOUNT)*100+
     1           IDPARS(14,NCOUNT)*10
 144  CONTINUE
C
 145  CONTINUE
C
C        PUT PLAIN LANGUAGE AND PARSED IDS WITH THE PREDICTANDS.
C        THE CONSTANT FILE HAS BEEN USED BEFORE, SO MUST REWIND IT.
C
      REWIND KFILCP
C        THE FILE IS REWOUND IN CASE IT HAS BEEN READ PREVIOUSLY.
      CALL SETPLN(KFILDO,KFILCP,
     1            IDT,IDPARS,JD,ISC,SMULT,SADD,
     2            ORIGIN,CINT,PL,UNITS,ND3*ND11,NCOUNT,ISTOP,IER)
C        ISTOP HAS BEEN INCREMENTED WITH DIAGNOSTIC IN SETPLN AS
C        NECESSARY.  THERE IS NO NON-ZERO IER RETURN.
      NCOUNT=0
C
      DO 148 I=1,NSETS
      DO 147 NN=1,MTANDS(I)
      NCOUNT=NCOUNT+1
      PLAIN(NN,I)=PL(NCOUNT)
      ISCALD(NN,I)=ISC(NCOUNT)
      ICATC(NCOUNT)=ICAT(NN,I)
C
      DO 146 J=1,15
      IDTPAR(J,NN,I)=IDPARS(J,NCOUNT)
 146  CONTINUE
C
 147  CONTINUE
 148  CONTINUE

      IF(IP7.EQ.0)GO TO 150
C
C        WRITE THE PREDICTAND ID'S TO UNIT NO. IP7.
C
      WRITE(IP7,149)NCOUNT,((IDT(J,L),J=1,4),TRESHL(L),
     1              (IDPARS(J,L),J=1,15),L=1,NCOUNT)
 149  FORMAT(/,' ',I4,
     1         ' PREDICTANDS, THRESHOLDS, AND PARSED VALUES       ',
     2         '      (1)  (2)  (3)  (4)  (5)  (6)  (7)  (8)  (9)',
     3         ' (10) (11) (12)(13)(14)(15)',/,
     4       (' ',I9.9,2I10,I11,F12.4,5X,12I5,3I4))
C
 150  IF(IP9.EQ.0)GO TO 170
C
C        WRITE THE PREDICTAND ID'S INCLUDING PLAIN LANGUAGE
C        TO UNIT NO. IP9.
C
      WRITE(IP9,160)NCOUNT
 160  FORMAT(/,' ',I4,' PREDICTANDS AND INFORMATION FROM VARIABLE',
     1              ' CONSTANT FILE AND PREDICTAND-FORECAST FILE')
      WRITE(IP9,161)(L,(IDT(J,L),J=1,4),PL(L),ICATC(L),
     1               ISC(L),L=1,NCOUNT)
 161  FORMAT('  NO.     ID(1)     ID(2)     ID(3)      ID(4)',
     1       '       PLAIN LANGUAGE ID         ICAT ISCALD',
     2       ' (FOR PACKING)',/,
     3      (' ',I4,1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,2X,A32,I2,I6))
C
 170  CONTINUE
      RETURN
      END
