      SUBROUTINE RDC(KFILDO,IPN,KFIL,CDATA,ND,CTEMP,NT,FMT,NVAL,CTERM,
     1               IER) 
C 
C        JANUARY 1995   GLAHN   TDL   MOS-2000 
C        APRIL   2000   DALLAVALLE    MODIFIED FORMAT STATEMENTS TO
C                               CONFORM TO FORTRAN 90 STANDARDS
C                               ON THE IBM SP
C        MAY     2000   GLAHN   MODIFIED TO NOT WRITE IPN WHEN LE 0    
C
C        PURPOSE 
C            TO READ A LIST OF CHARACTER DATA ACCORDING TO A SPECIFIED 
C            FORMAT AND RETURN THE LIST IN ARRAY CDATA( ).
C            THE LIST IS TERMINATED BY A TERMINATOR CTERM. 
C            THIS ROUTINE WORKS ONLY FOR A CHARACTER VARIABLE OF SIZE 8.
C            BLANKS (ALL 8 CHARACTERS = BLANK) ARE ELIMINATED FROM THE LIST.
C            FMT MUST BE DECLARED CHARACTER OR BE A CHARACTER STRING IN
C            THE CALLING PROGRAM. FMT WILL INDICATE
C            HOW MANY VALUES TO READ PER RECORD (NT OR LESS); HOWEVER,
C            THE RECORD NEED NOT HAVE THAT MANY VALUES.    
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            IPN    - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            KFIL   - UNIT NUMBER FROM WHICH TO READ DATA.  (INPUT) 
C 
C        VARIABLES 
C 
C            INPUT 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. 
C                 IPN = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  WHEN
C                       DIAGNOSTICS ARE WRITTEN TO KFILDO, THEY WILL
C                       ALSO BE WRITTEN TO UNIT NO. IPN UNLESS IPN = KFILDO
C                       OR IPN <= 0.
C                KFIL = UNIT NUMBER FROM WHICH TO READ DATA. 
C                  ND = SIZE OF ARRAY CDATA( ). 
C            CTEMP( ) = TEMPORARY ARRAY THAT MUST BE OF AT LEAST SIZE NT.
C                       (CHARACTER*8) 
C                  NT = NUMBER OF WORDS PER RECORD INDICATED IN FMT( ). 
C              FMT( ) = CONTAINS FORMAT OF DATA.  THIS MUST PERTAIN
C                       TO CHARACTER DATA.  (CHARACTER*(*)) 
C                NVAL = COUNT OF ELEMENTS IN ARRAY RETURNED. 
C               CTERM = TERMINATOR.  (CHARACTER*8) 
C
C            OUTPUT 
C            CDATA( ) = ARRAY IN WHICH DATA ARE RETURNED (J=1,NVAL). 
C                 IER = STATUS RETURN.
C                       0  = GOOD RETURN.
C                       20 = ERROR OR END OF FILE ON UNIT KFIL.
C                       21 = LIST TOO LONG FOR DIMENSION ND ON UNIT KFIL.
C                       (CHARACTER*8) 
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE. 
C
      CHARACTER*(*) FMT
      CHARACTER*8 CDATA(ND),CTEMP(NT),CTERM,BLANK
C
      DATA BLANK/' '/
C
      IER=0 
      NVAL=0 
C
C        READ A RECORD.
C      
 115  READ(KFIL,FMT,IOSTAT=IOS,ERR=116,END=118)CTEMP 
C        MORE VALUES IN THE RECORD THAN NT DOES NOT CAUSE AN ERROR.
C        ONLY NT WILL BE READ.  A RECORD TOO LONG MAY CAUSE THE
C        TERMINATOR TO BE SKIPPED.  ALSO, FEWER VALUES IN THE RECORD
C        THAN NT DOES NOT CAUSE AN ERROR, EVEN THE LAST VALUE CAN
C        BE TRUNCATED (E.G., 'OKC     ' CAN BE 'OKC' OR 'OKC '.
      GO TO 119 
C 
 116  IER=20 
D     WRITE(KFILDO,1160)NT,CTERM,FMT,CTEMP
D1160 FORMAT(' ****NT, CTERM, FMT, CTEMP,',I6,A10,12(1X,A8))
      WRITE(KFILDO,1165)KFIL,IOS,IER 
      IF(IPN.NE.KFILDO.AND.IPN.GT.0)WRITE(IPN,1165)KFIL,IOS,IER
 1165 FORMAT(/,' ****ERROR ON UNIT NO.',I3,'.  IOSTAT =',I5,/,
     1         '     RETURN FROM RDC AT 1165 WITH IER = ',I3,'.')
      GO TO 135 
C 
 118  IER=20
D     WRITE(KFILDO,1180)NT,CTERM,FMT,CTEMP
D1180 FORMAT(' ****NT, CTERM, FMT, CTEMP,',I6,A10,12(1X,A8))
      WRITE(KFILDO,1185)KFIL,IOS,IER 
      IF(IPN.NE.KFILDO.AND.IPN.GT.0)WRITE(IPN,1185)KFIL,IOS,IER 
 1185 FORMAT(/,' ****END OF FILE ON UNIT NO.',I3,'.  IOSTAT =',I5,/,
     1         '     RETURN FROM RDC AT 1185 WITH IER = ',I3,'.')
      GO TO 135
C
C        PROCESS DATA IN REOCRD.
C 
 119  DO 125 K=1,NT
D     WRITE(KFILDO,1195)K,NVAL,BLANK,CTERM,CTEMP(K)
D1195 FORMAT(' K, NVAL, BLANK, CTERM, CTEMP(K)'2I4,3(1XA8))  
      IF(CTEMP(K).EQ.BLANK)GO TO 125 
C        A BLANK CHARACTER STRING (OF 8 CHARACTERS) IS NOT KEPT.
      IF(CTEMP(K).EQ.CTERM)GO TO 135 
C        THE LIST IS TERMINATED WITH THE TERMINATOR CTERM. 
      NVAL=NVAL+1 
      IF(NVAL.GT.ND)GO TO 130 
      CDATA(NVAL)=CTEMP(K) 
 125  CONTINUE 
C
C        READ ANOTHER RECORD.
      GO TO 115 
C 
C        THE LIST IS TOO LONG FOR THE ARRAY CDATA( ).
C
 130  NVAL=NVAL-1 
      IER=21 
      WRITE(KFILDO,131)KFIL,IER
      IF(IPN.NE.KFILDO.AND.IPN.GT.0)WRITE(IPN,131)KFIL,IER 
 131  FORMAT(/,' ****LIST TOO LONG ON UNIT NO.',I3,
     1         '.  RETURN FROM RDC AT 131 WITH IER = ',I3,'.') 
C****      WRITE(KFILDO,132)(CDATA(J),J=1,NVAL),CTEMP 
C**** 132  FORMAT(' ',10(A8,1X))      
C 
 135  RETURN 
      END 
