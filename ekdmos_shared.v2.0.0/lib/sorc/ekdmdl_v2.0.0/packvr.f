      SUBROUTINE PACKVR(KFILDO,KFILX,CFILX,ID,IDPARS,
     1                  JP,ISCALD,ISCALE,
     2                  IPLAIN,PLAIN,NDATE,NYR,NMO,NDA,NHR,
     3                  CCALL,ISDATA,SDATA,ND1,NSTA,IPACK,ND5,MINPK,
     4                  IS0,IS1,IS2,IS4,ND7,XMISSP,XMISSS,
     5                  IPX,NWORDS,NTOTVB,NTOTVR,
     6                  L3264B,L3264W,ISTOP,IER)
C
C        OCTOBER   2004   GLAHN   TDL   MOS-2000
C                                 MODIFED FROM PACKV
C        JUNE      2012   ENGLE   MODIFIED CALL TO INCLUDE PLAIN.
C                                 PLAIN LANGUAGE IS NOW PACKED BY
C                                 USING IACHAR FUNCTION.
C
C        PURPOSE
C           TO PACK AND WRITE VECTOR DATA TO A MOS-2000 RANDOM
C           ACCESS FILE.  THE FILE NUMBER MUST BE 49; OTHERWISE.
C           PACKED DATA ARE WRITTEN UNLESS THERE IS AN ERROR.
C           PRINT IS UNDER CONTROL OF IPX AND JP(3).  THE "MODEL
C           NUMBER" IS ELIMINATED FOR WRITING FOR "CONSTANT"
C           VARIABLES WITH CCC IN THE RANGE 400 TO 499.
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILX  - UNIT NUMBER FOR TDL FILE.  (OUTPUT) 
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C               KFILX = UNIT NUMBER FOR TDL FILE.  (INPUT) 
C               CFILX = THE NAME OF THE FILE CORRESPONDING TO KFILX.
C                       (CHARACTER*60)  (INPUT)
C               ID(J) = THE INTEGER VARIABLE ID (J=1,4)  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       VARIABLE
C                       ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JP(J) = JP( ) INDICATES WHETHER (>0) OR NOT (=0) THE 
C                       VARIABLE WILL BE OUTPUT FOR VIEWING.
C                       J=1--NOT USED,
C                       J=2--NOT USED, AND
C                       J=3--INTERPOLATED (VECTOR) VALUES.
C                       THIS ALLOWS INDIVIDUAL VARIABLE CONTROL ON
C                       THE PRINT PARAMETER IPX.  (INPUT)
C              ISCALD = THE DECIMAL SCALING CONSTANT TO USE WHEN
C                       PACKING THE DATA.  (INPUT)
C              ISCALE = THE BINARY SCALING CONSTANT TO USE WHEN
C                       PACKING THE DATA.  NORMALLY, THIS IS 0.  (INPUT)
C         IPLAIN(L,J) = 32 CHARACTERS (L=1,L3264W) (J=1,4) OF PLAIN
C                       LANGUAGE DESCRIPTION OF VARIABLE.
C                       NOTE THAT THIS REQUIRES TWO 32-BIT WORDS TO HOLD
C                       THE DESCRIPTION BUT ONLY ONE 64-BIT WORD.
C                       (INPUT)
C               PLAIN = THE PLAIN LANGUAGE DESCRIPTION OF THE VARIABLE 
C                       EQUIVALENCED TO IPLAIN( , ).  (CHARACTER*32)
C                       (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH VARIABLES ARE BEING
C                       DEALT WITH.  (INPUT)
C                 NYR = YEAR, 4 DIGITS.  (INPUT)
C                 NMO = MONTH.  (INPUT)
C                 NDA = DAY OF MONTH.  (INPUT)
C                 NHR = HOUR, 2 DIGITS.  (INPUT)
C            CCALL(K) = 8 STATION CALL LETTERS (K=1,NSTA).  USED FOR
C                       PRINTOUT ONLY.  (CHARACTER*8)  (INPUT)
C           ISDATA(K) = USED IN PACK1D (K=1,NSTA).  (INTERNAL)
C            SDATA(K) = DATA FOR WRITING (K=1,NSTA).  (INPUT)
C                 ND1 = DIMENSION OF ISDATA( ) AND SDATA( ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS IN CCALL( ).  (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ).  (INPUT)
C               MINPK = MINIMUM GROUP SIZE WHEN PACKING THE VALUES.
C                       (INPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,29).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4). 
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C              XMISSP = PRIMARY MISSING VALUE.  (INPUT)
C              XMISSP = SECONDARY MISSING VALUE.  (INPUT)
C                 IPX = INDICATES WHETHER (>1) OR NOT (=0) DATA
C                       VALUES WILL BE WRITTEN TO UNIT IPX FOR VIEWING
C                       WHEN JP(3) NE 0.  (INPUT)
C              NWORDS = THE NUMBER OF WORDS IN IPACK( ), CALCULATED
C                       FROM IOCTET.  (OUTPUT)
C              NTOTVB = THE TOTAL NUMBER OF BYTES WRITTEN TO THE RA
C                       FILE ON UNIT NO. KFILX.  IT IS UPDATED WHEN
C                       THE DATA IN IPACK( ) ARE WRITTEN.
C                       (INPUT-OUTPUT)
C              NTOTVR = THE TOTAL NUMBER OF RECORDS WRITTEN TO THE RA
C                       FILE ON UNIT NO. KFILX.  IT IS UPDATED WHEN 
C                       THE DATA IN IPACK( ) ARE WRITTEN. 
C                       (INPUT-OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).  
C                       (INPUT)
C               ISTOP = INCREMENTED BY ONE EACH TIME AN ERROR IS 
C                       ENCOUNTERED.  (INPUT-OUTPUT)
C                 IER = STATUS RETURN.
C                        0 = GOOD RETURN.
C                       16 = ND7 NOT LARGE ENOUGH.  SET ND7 GE 54.
C                       SEE ROUTINES PACK1D, UNPKBG, AND RWTDLM 
C                       FOR OTHER VALUES.  (INTERNAL-OUTPUT)
C              IOCTET = THE PACKED RECORD SIZE IN OCTETS (BYTES).
C                       PROVIDED BY PACK1D.  (INTERNAL)
C                 FMT = FORMAT FOR WRITING DATA.  MODIFIED TO WRITE
C                       AT ACCURACY PACKED.  (CHARACTER*12)  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            TIMPR, UNPKBG, PACK2D, RWTDLM
C
      CHARACTER*60 CFILX
C
      CHARACTER*8 CCALL(ND1)
      CHARACTER*13 FMT
      CHARACTER*32 PLAIN
C
      DIMENSION ISDATA(ND1),SDATA(ND1)
      DIMENSION IPACK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION IPLAIN(L3264W,4)
      DIMENSION ID(4),IDPARS(15),JP(3)
C
      DATA FMT/'(1X,A8,F11.2)'/
C
      IER=0
D     WRITE(KFILDO,100)ID,IPX,(JP(J),J=1,3)
D100  FORMAT(/' **** ENTERING PACKVR, ID( ) =',4I12,
D    1        '  IPX, JP(3) ='4I4)
C
C        VERIFY UNIT NUMBER IS 49.
C
      IF(KFILX.NE.49)THEN
         WRITE(KFILDO,105)KFILX,(ID(J),J=1,4)
 105     FORMAT(/' ****UNIT NUMBER =',I4,' IN PACKVR IS NOT 49.',
     1           '  WRITING DATA FOR ID = ',4I12,' NOT DONE.')
         ISTOP=ISTOP+1
         GO TO 400
      ENDIF
C
C        WRITE THE DATA FOR VIEWING.
C
      IF(IPX.EQ.0)GO TO 365
      IF(JP(3).EQ.0)GO TO 365
C
C     WRITE THE DATA VALUES TO UNIT IPX TO THE ACCURACY
C     THEY WILL BE PACKED.
C
      WRITE(IPX,355)(ID(I),I=1,4),PLAIN,NDATE
 355  FORMAT(/,' DATA VALUES TO THE ACCURACY PACKED',
     1         ' FOR VARIABLE ',1X,I9.9,1X,I9.9,1X,I9.9,1X,
     2         I10.3,2X,A32,/,' NDATE =',I12)
C 
      FACTOR=(10.**ISCALD)*(2.**ISCALE)
      IF(ISCALD.LE.5)GO TO 357
      WRITE(KFILDO,356)ISCALD
 356  FORMAT(/,' ****ISCALD =',I5,' IN PACKVR GT 5.',
     1         '  MUST BE AN ERROR.  PROCEEDING.')
      ISTOP=ISTOP+1
C
 357  FMT(12:12)='6'
      IF(FACTOR.LE.100000.)FMT(12:12)='5'
      IF(FACTOR.LE. 10000.)FMT(12:12)='4'
      IF(FACTOR.LE.  1000.)FMT(12:12)='3'
      IF(FACTOR.LE.   100.)FMT(12:12)='2'
      IF(FACTOR.LE.    10.)FMT(12:12)='1'
      IF(FACTOR.LE.     1.)FMT(12:12)='0'
C
      DO 360 K=1,NSTA
C     
      IF(XMISSP.EQ.0.)GO TO 358
      IF(SDATA(K).EQ.XMISSP)THEN
         PDATA=XMISSP
         GO TO 359
      ELSEIF(XMISSS.NE.0..AND.SDATA(K).EQ.XMISSS)THEN
         PDATA=XMISSS
         GO TO 359
      ENDIF
C
 358  PDATA=NINT(SDATA(K)*FACTOR)/FACTOR
 359  WRITE(IPX,FMT)CCALL(K),PDATA
 360  CONTINUE
C
C        PACK THE DATA.  ELIMINATE THE "MODEL NUMBER" FROM 
C        THE "CONSTANT" VARIABLES.
C
 365  IDPAW4=IDPARS(4)
      IDW1=ID(1)
      IF(IDPARS(1).LT.400.OR.IDPARS(1).GT.499)GO TO 370
      IDPAW4=0
      IDW1=IDPARS(1)*1000000+IDPARS(2)*1000+IDPARS(3)*100
C
C        LOAD IS1( ) FOR PACKING.  ALL OTHER VALUES ARE PROVIDED
C        BY THE PACKING ROUTINES.
C
 370  IS1(2)=0
C        IS1(2) = 0 SIGNIFIES VECTOR DATA.
      IS1(3)=NYR
      IS1(4)=NMO
      IS1(5)=NDA
      IS1(6)=NHR
      IS1(7)=0
      IS1(8)=NDATE
      IS1(9)=IDW1
      IS1(10)=ID(2)
      IS1(11)=ID(3)
      IS1(12)=ID(4)
      IS1(13)=IDPARS(12)
      IS1(14)=0
      IS1(15)=IDPAW4
      IS1(16)=0
      IS1(17)=ISCALD
      IS1(18)=ISCALE
      IS1(19)=0
      IS1(20)=0
      IS1(21)=0
      IS1(22)=32
      IF(ND7.GE.54)GO TO 375
C        IS1( ) WILL BE OVERFLOWED IN NEXT SECTION.
      WRITE(KFILDO,374)
 374  FORMAT(/' ****IS1( ) IN UNPACK ABOUT TO BE OVERFLOWED.',
     1        '  INCREASE ND7 TO 54.')
      IER=16
      ISTOP=ISTOP+1
      GO TO 400
C
 375  LOC=1
C        LOC = WORD POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
      IPOS=1
C        IPOS = BIT POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
C
CINTEL
C
C        USE IACHAR FUNCTION TO PUT ONE CHARACTER (BYTE) FROM
C        PLAIN INTO ONE IS1( ) WORD.
C
      DO J=1, 32
         IS1(J+22)=IACHAR(PLAIN(J:J))
      END DO
C      DO 380 J=1,32
C      CALL UNPKBG(KFILDO,IPLAIN(1,1),4*L3264W,LOC,IPOS,
C     1            IS1(J+22),8,L3264B,IER,*900)
CC        NOTE THAT THIS PUTS ONE BYTE PER IS1( ) WORD.
CC        RETURN HERE ONLY WHEN IER = 0; OTHERWISE, TO 900.
C 380  CONTINUE
CINTEL
C
C***D     WRITE(KFILDO,388)IS0,IS1,IS2
C***D388  FORMAT(/' AT 388 IN PACKVR--IS0,IS1,IS2'/(5(10I10/),4I10/))
C***D     WRITE(KFILDO,389)NSTA,ND5,ND7,L3264B,
C***D    1      (SDATA(K),K=1,NSTA)
C***D389  FORMAT(/' AT 389 IN PACKVR--NSTA,ND5,ND7,L3264B,SDATA'/
C***D    2         4I10/
C***D    3         (20F6.1))
      CALL PACK1D(KFILDO,SDATA,ISDATA,NSTA,IS0,IS1,IS2,IS4,
     1            ND7,XMISSP,XMISSS,IPACK,ND5,
     2            MINPK,LX,IOCTET,L3264B,IER)
      IF(IER.NE.0)ISTOP=ISTOP+1
      IF(IER.NE.0)GO TO 400
C
C        WRITE THE PACKED DATA.
C
      NWORDS=IOCTET*8/L3264B
C        IOCTET IS RETURNED FROM PACK1D AS EVENLY DIVISIBLE BY 8
C        BY PADDING IPACK( ) IF NECESSARY.  THEREFORE, TRUNCATION
C        DOES NOT OCCUR IN COMPUTATION OF NWORDS.  THIS IS USED
C        ONLY TO RETURN TO CALLING ROUTINE.
C
D     WRITE(KFILDO,125)(IS2(J),J=1,ND7)
D125  FORMAT(/' IN PACKVR AT 125--IS2( )'/(10I10))
C
C        WRITE THE DATA.
C      
      CALL WRTDLM(KFILDO,KFILX,CFILX,ID,IPACK,(IOCTET*8)/L3264B,
     1            2,0,L3264B,IER)
C        RECORD WRITTEN WITH REPLACEMENT, AND NOT CHECKING FOR
C        DUPLICATES.
C
      IF(IER.EQ.0)THEN
         NTOTVB=NTOTVB+IOCTET
         NTOTVR=NTOTVR+1
C
D        WRITE(KFILDO,158)KFILX,NTOTVB,NTOTVR
D158     FORMAT(/' WRITING DATA IN PACKVR ON UNIT NO.',I4/
D    1        '    NTOTVB ='I10  '   TOTAL BYTES   WRITTEN TO RANDOM',
D    2                           ' ACCESS FILE'/
D    3        '    NTOTVR ='I10  '   TOTAL RECORDS WRITTEN TO RANDOM',
D    4                           ' ACCESS FILE')
      ELSE
         ISTOP=ISTOP+1
      ENDIF
C
 400  CONTINUE
      RETURN
C
 900  WRITE(KFILDO,901)IER
 901  FORMAT(/,' ****ERROR IN UNPKBG IN PACKVR, IER =',I4)
      ISTOP=ISTOP+1
      RETURN
      END
