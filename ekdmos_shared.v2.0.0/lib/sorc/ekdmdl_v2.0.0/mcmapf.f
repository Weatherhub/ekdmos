      SUBROUTINE MCMAPF(KFILDO,XMESHL,XLAT,XLATLL,XLONLL,
     1                  FDSINS,FDMS,NX,NY)
C
C        JUNE     2002   GLAHN   TDL   MOS-2000 
C
C        PURPOSE 
C            TO RETURN THE SIN OF THE LATITUDE AND THE MAP FACTOR FOR
C            EACH GRIDPOINT FOR A MERCATOR MAP.  MAP FACTOR EQUATION
C            GIVEN IN GLAHN, MONTHLY WEATHER REVIEW, VOL 118, NO. 12,
C            PP. 2781-2783.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INPUT)
C                XLAT = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS CORRECT.
C                       (INPUT)
C              XLATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES NORTH.  (INPUT)
C              XLONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES WEST.  (INPUT)
C       FDSINS(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).  (OUTPUT)
C         FDMS(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (OUTPUT)
C                       TO SAVE THE ARRAY.  SEE FDSINS( , ) 
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            MCIJLL
C
      PARAMETER (PI=3.14159,
     1           RADPDG=PI/180.)
c
      DIMENSION FDSINS(NX,NY),FDMS(NX,NY)
C
      COSLAT=COS(XLAT)
C
C        CALCULATE THE LATITUDE AND LONGITUDE OF EACH GRIDPOINT, THEN
C        THE SINE OF THE LATITUDE AND THE MAP FACTOR.
C
      DO 130 JY=1,NY
      DO 120 IX=1,NX
      XI=IX
      YJ=JY
      CALL MCIJLL(KFILDO,XI,YJ,XMESHL,XLAT,
     1            XLATLL,XLONLL,ALAT,ALON)
      FDSINS(IX,JY)=SIN(RADPDG*ALAT)
      FDMS(IX,JY)=COSLAT/COS(ALAT)
 120  CONTINUE
 130  CONTINUE
C
      RETURN
      END      
      
      
