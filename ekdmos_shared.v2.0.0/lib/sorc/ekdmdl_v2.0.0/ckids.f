      SUBROUTINE CKIDS(KFILDO,N,ID,IDPARS,NTYPVR,IER)
C 
C        MAY       1994   GLAHN   TDL   MOS-2000 
C        FEBRUARY  1997   GLAHN   ADDED TO INTERPOLATION CHECK FOR OBS
C                                 WITH CCC IN RANGE 700-799.
C        JANUARY   1998   GLAHN   COMMENTS TO INDICATE NTYPVR = 3 IS OK.
C        APRIL     1998   GLAHN   DISABLED CHECKS WHEN AEV DATA
C        APRIL     1998   GLAHN   DISABLED INTERPOLATION CHECK FOR TRIG
C        APRIL     1999   GLAHN   EXPANDED PRECIP IDS AND ADDED CHECK
C                                 FOR INTERPOLATION WHEN GRID BINARY
C        APRIL     1999   GLAHN   ADDED CHECK FOR SMOOTHING
C        MARCH     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                 CONFORM TO FORTRAN 90 STANDARDS
C                                 ON IBM SP
C        DECEMBER  2000   GLAHN   REMOVED REFERENCE TO U850 FOR NTYPVR=3
C                                 FROM COMMENTS, SHORTENED COMMENTS TO
C                                 72 CHARACTERS; SPELL CHECKED
C        OCTOBER   2001   COSGROVE CHANGED CHECK OF PRECIP SMOOTHING IDS TO
C                                 ACCEPT ALL IDS FROM 200 TO 285.
C        JUNE      2002   GLAHN   ADDED INTERPOLATION VALUE OF 4
C        OCTOBER   2002   HUGHES  RECONCILED VERSIONS FROM COSGROVE, ANTOLIK
C                                 SFANOS, AND GLAHN.  MOST OF THE CHANGES HAD
C                                 BEEN INCLUDED IN THE COSGROVE OPERATIONAL
C                                 VERSION, BUT WITH DIFFERENT SYNTAX.
C        MARCH     2003   GLAHN   EXEMPTED CCC=9XX FROM INTERPOLATION
C                                 CHECK
C        MAY       2003   YAN     ADDED BINARY VARIABLE IDPARS(3)=2
C                                 WHEN NTYPVR=1.
C        JUNE      2003   GLAHN   CHANGED COMMENT ABOUT IDPARS(13) CHECKING
C                                 FOR CONSTANTS IN 400-499 RANGE
C        JUNE      2003   GLAHN   ADDED SEVERAL PRECIP CHECKS AT 110
C        JULY      2003   GLAHN   ADDED INTERPOLATION VALUE OF 5
C        JULY      2003   MALONEY RECONCILED IBM AND GDP VERSIONS; A FEW
C                                 MINOR COMMENTS CHANGES
C        AUGUST    2003   GLAHN   EXEMPTED CCC=2XX FROM INTERPOLATION
C                                 CHECK
C        SEPTEMBER 2003   GLAHN   ADDED CHECK FOR BINARY INDICATOR = 5
C                                 FOR CCC = 2XX, 4XX, AND 7XX; SOME
C                                 GO TO 900 CHANGED TO GO TO 200
C        OCTOBER   2003   GLAHN   CHANGED TO ACCOMMODATE CHECK ON 
C                                 IDPARS(3) FOR CCC=2XX AT 104; CHANGED
C                                 "PREDICTOR" TO "VARIABLE" IN MOST
C                                 PLACES
C        DECEMBER  2003   GLAHN   ADDED SD FFF=410 TO PRECIPITATION
C                                 TYPE INTERPOLATION AT 110
C        DECEMBER  2003   GLAHN   ELIMINATED LAST FFF=410 CHANGE
C        APRIL     2004   WIEDENFELD RECONCILED IBM AND GDP VERSIONS
C        APRIL     2004   GLAHN   INSERTED FORMAT 107 TO SOFTEN
C                                 DIAGNOSTIC FOR INTERPOLATION VALUES
C        APRIL     2004   GLAHN   MODIFIED FORMAT 107
C        JANUARY   2007   RUDACK  ADDED CHECK FOR TEMPORAL QUADRATIC
C                                 INTEROLATION VALUE OF 9 
C
C        PURPOSE 
C            TO CHECK CERTAIN INFORMATION IN A MOS ID AND IT'S 
C            PROCESSING INFORMATION.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT)
C                   N = VARIABLE NUMBER.  USED FOR PRINT ONLY. 
C                       (INPUT) 
C               ID(J) = THE VARIABLE ID'S (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       VARIABLE ID'S CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C              NTYPVR = 1 WHEN THE BINARY VARIABLE IDPARS(3) CAN TAKE
C                         A VALUE OF ONLY 0, 1, 2, OR 5.  NORMALLY USED
C                         FOR VARIABLES FROM U201.
C                       OTHERWISE, WHEN THE BINARY VARIABLE IDPARS(3) 
C                       CAN TAKE A VALUE OF 3 IN ADDITION TO 0, 1, 2,
C                       OR 5.  NORMALLY USED IN U600 FOR PREDICTANDS.
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       43 = PRECIPITATION AMOUNT FIELD DOES NOT HAVE
C                            AN INTERPOLATION VALUE OF 3.
C                       44 = NON-PRECIPITATION AMOUNT FIELD HAS AN 
C                            INTERPOLATION VALUE OF 3.
C                       45 = 1)  A BINARY INDICATOR IS NOT 0, 1, OR 5;
C                            2)  A GRID BINARY DOES NOT HAVE AN 
C                                INTERPOLATION INDICATOR OF 0, 1, OR 2; 
C                            3)  SMOOTHING IS NOT 0, 1, 2, 3, 4, OR 5;
C                            4)  CCC = 2XX, 4XX, OR 7XX HAS A GRID
C                                INDICATOR OF 5
C                       46 = INTERPOLATION INDICATOR NOT 1, 2, 3, 4,
C                            OR 5.
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      DIMENSION ID(4),IDPARS(15)
C
      IER=0
C
C        DO NOT CHECK IDS IF THE "MODEL NUMBER" IDPARS(4, ) =
C        80, 81, OR 82.  THESE ARE AEV FORECASTS AND DON'T
C        OBEY THE BINARY AND INTERPOLATION RULES.
C 
      IF(IDPARS(4).EQ.80.OR.
     1   IDPARS(4).EQ.81.OR.
     2   IDPARS(4).EQ.82)GO TO 200
C
C        CHECK SMOOTHING INDICATOR.  IT MUST BE 0, 1, 2, 3, 4, OR 5.
C
      IF(IDPARS(14).EQ.0.OR.
     1   IDPARS(14).EQ.1.OR.
     2   IDPARS(14).EQ.2.OR.
     3   IDPARS(14).EQ.3.OR.
     4   IDPARS(14).EQ.4.OR.
     5   IDPARS(14).EQ.5)GO TO 102
      WRITE(KFILDO,101)N,(ID(J),J=1,4)
 101  FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,' INDICATES INCORRECT SMOOTHING.')
      IER=45
C        ALLOW IT TO CONTINUE CHECKING.
C    
C        CHECK INTERPOLATION VALUE OF 0, 1, OR 2 FOR A GRID
C        BINARY.
C
 102  IF(IDPARS(3).NE.5)GO TO 104
      IF(IDPARS(13).EQ.0.OR.
     1   IDPARS(13).EQ.1.OR.
     2   IDPARS(13).EQ.2)GO TO 200
      WRITE(KFILDO,103)N,(ID(J),J=1,4)
 103  FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,
     2       ' INDICATES INCORRECT INTERPOLATION FOR GRID BINARY.')
      IER=45
      GO TO 200
C     
C        CHECK THAT BINARY INDICATOR IS EITHER 0, 1, 0R 5 FOR
C        NTYPVR=1 AND 0, 1, 2, 3, OR 5 FOR NTYPVR GE 2 OR MOS FORECAST
C        (CCC=2XX).
C
 104  IF(NTYPVR.EQ.2.OR.IDPARS(1)/100.EQ.2)THEN
C
         IF(IDPARS(3).EQ.0.OR.
     1      IDPARS(3).EQ.1.OR.
     2      IDPARS(3).EQ.2.OR.
     3      IDPARS(3).EQ.3.OR.
     4      IDPARS(3).EQ.5)GO TO 110
         WRITE(KFILDO,105)N,(ID(J),J=1,4)
 105     FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,' INDICATES INCORRECT BINARY INDICATOR.')
         IER=45
C
      ELSE
         IF(IDPARS(3).EQ.0.OR.
     1      IDPARS(3).EQ.1.OR.
     2      IDPARS(3).EQ.2.OR.
     3      IDPARS(3).EQ.5)GO TO 110
         WRITE(KFILDO,107)N,(ID(J),J=1,4),IDPARS(3) 
 107     FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1          1X,I10.3,' HAS UNUSUAL BINARY INDICATOR =',I3)
         IER=45
      ENDIF
C
C        CHECK THAT PRECIPITATION AMOUNT FIELDS HAVE AN INTERPOLATION
C        VALUE OF 3, EXCEPT A GRID BINARY SHOULD NOT HAVE A VALUE
C        OF 3.
C
 110  IF(IDPARS(1).NE.003)GO TO 120
C        THIS IS A MOISTURE VARIABLE.
      IF((IDPARS(2).LT.200).OR.
     1   (IDPARS(2).GE.299))GO TO 120
C        THIS IS PRECIPITATION AMOUNT, EITHER TOTAL, CONVECTIVE, OR
C        NONCONVECTIVE.
      IF((IDPARS(13).EQ.3).OR.(IDPARS(13).EQ.9)) GO TO 200
C        THIS IS A PRECIPITATION AMOUNT FIELD, AND INTERPOLATION IS NOT
C        INDICATED CORRECTLY.
      WRITE(KFILDO,115)N,(ID(J),J=1,4)
 115  FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,' INDICATES PRECIPITATION AMOUNT,',/,
     2       '     BUT INTERPOLATION TYPE OTHER THAN 3 OR 9 IN CKIDS.')
      IER=43
      GO TO 200
C
C        CHECK THAT NON PRECIPITATION AMOUNT FIELDS DO NOT HAVE AN 
C        INTERPOLATION VALUE OF 3.
C
 120  IF(IDPARS(13).NE.3) GO TO 130
C        THIS IS A NON PRECIPITATION AMOUNT FIELD, AND INTERPOLATION IS 
C        INDICATED INCORRECTLY AS PERTAINING TO PRECIPITATION AMOUNT.
      WRITE(KFILDO,125)N,(ID(J),J=1,4)
 125  FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,' INDICATES NON-PRECIPITATION AMOUNT,',/,
     2       '     BUT INTERPOLATION TYPE OF 3 IN CKIDS.')
      IER=44
      GO TO 200
C
C        CHECK FOR INTERPOLATION TYPE OF 1, 2, 3, 4, OR 5 EXCEPT FOR
C        OBSERVATIONS WITH CCC IN RANGE 700-799, TRIG FUNCTIONS, AND
C        STRATIFICATION VARIABLES CCC = 8XX..
C
 130  IF(IDPARS(13).EQ.1.OR.
     1   IDPARS(13).EQ.2.OR.
     2   IDPARS(13).EQ.3.OR.
     3   IDPARS(13).EQ.4.OR.
     4   IDPARS(13).EQ.5.OR.
     5   IDPARS(13).EQ.9)GO TO 200
      IF(IDPARS(1).GE.400.AND.IDPARS(1).LE.499)GO TO 200
C        FOR CONSTANTS, INTERPOLATION MAY NOT BE NEEDED.  FOR MORE
C        COMPLEX VARIABLES REQUIRING CONSTANTS, AND HAVE A CCC IN THE
C        400-499 RANGE, ANY VALUE IS ACCEPTED HERE.
      IF(IDPARS(1).GE.700.AND.IDPARS(1).LE.799)GO TO 200
C        FOR OBSERVATIONS, INTERPOLATION IS NOT NEEDED.
      IF(IDPARS(1).GE.200.AND.IDPARS(1).LE.299)GO TO 200
C        FOR STATISTICAL FORECASTS, INTERPOLATION IS NOT NEEDED.
      IF(IDPARS(1).EQ.010)GO TO 200
C        TRIG FUNCTIONS ARE NOT INTERPOLATED.
      IF(IDPARS(1)/100.EQ.9)GO TO 200
C        FOR STRATIFICATION VARIABLES, INTERPOLATION MAY NOT BE NEEDED.
      WRITE(KFILDO,131)N,(ID(J),J=1,4)
 131  FORMAT(' ****PREDICTOR ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1       1X,I10.3,
     2       ' DOES NOT HAVE A VALID INTERPOLATION INDICATOR IN CKIDS.')
      IER=46
C
C        CHECK THAT OBVIOUS VECTOR DATA CCC = 2XX, 4XX, AND 7XX
C        DO NOT HAVE A GRID BINARY INDICATOR OF 5.  IF SO, SET TO 1.
C
 200  IF((IDPARS(1).GE.200.AND.IDPARS(1).LE.299).OR.
     1   (IDPARS(1).GE.400.AND.IDPARS(1).LE.499).OR.
     2   (IDPARS(1).GE.700.AND.IDPARS(1).LE.799))THEN
C
         IF(IDPARS(3).EQ.5)THEN
            WRITE(KFILDO,118)N,(ID(J),J=1,4)
 118        FORMAT(' ****VARIABLE ID NO.',I4,1X,I9.9,1X,I9.9,1X,I9.9,
     1          1X,I10.3,' INDICATES GRID BINARY = 5.  IT IS SET TO 0.'/
     2             '     THIS MAY NOW BE A DUPLICATE.')
            IDPARS(3)=0
            IER=45
         ENDIF
C
      ENDIF

 900  RETURN
      END
