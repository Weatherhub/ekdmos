      SUBROUTINE MAPLAT(KFILDO,JD,NGRIDC,
     1                  FDSIN,FDM,FDSINS,FDMS,NX,NY,IER)
C
C        FEBRUARY 1995   GLAHN   TDL   MOS-2000 
C        OCTOBER  1996   GLAHN   ENHANCED DIAGNOSTIC
C        JUNE     1997   GLAHN   D COMPILE OPTION COMMENTED OUT
C        JUNE     2002   GLAHN   ADDED MERCATOR CAPABILITY
C        NOVEMBER 2002   COSGROVE MODIFIED IF TEST FOR EQUALITY OF GRID
C                                SPECIFICATIONS.  THIS IS BASED ON A 
C                                CHANGE MADE BY DREWRY IN MAY 2000, BUT
C                                THAT FIX NEEDED A LITTLE FIXING.
C        MAY      2003   GLAHN   REMOVED DUPLICATIVE TESTS ABOVE 135;
C                                XMINDIF PUT INTO DATA STATEMENT
C
C        PURPOSE 
C            TO COMPUTE SINE OF THE LATITUDE AND MAP FACTOR FOR
C            VARIOUS ROUTINES.  POLAR STEREOGRAPHIC, LAMBERT AND
C            MERCATOR ARE SUPPORTED.  THESE FIELDS ARE CALCULATED IN
C            EITHER LMMAPF, PSMAPF, OR MCMAPF ON INITIAL ENTRY AND 
C            SAVED IN  FDSINS( , ) AND FDMS( , ), RESPECTIVELY.
C            THEY ARE NOT RECALCULATED UNLESS THE MAP
C            CHARACTERISTICS CHANGE.  THIS IS EFFICIENT UNDER THE 
C            ASSUMPTION THAT MOST USES WILL BE FOR ONE MODEL/GRID.
C            (THE ARCHIVE COULD CHANGE AT SOME POINT; THAT WOULD BE OK.)
C            A DIAGNOSTIC IS PRINTED FOR ONLY THE FIRST 10 COMPUTATIONS
C            TO ALERT THE USER THAT THIS HAS HAPPENED.  IT IS NOT
C            TREATED AS AN ERROR.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       USED ONLY FOR PRINTING.  (INPUT)
C           NGRIDC(L) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR THIS GRID.
C                       (INPUT)
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000 (NOT
C                            USED FOR MERCATOR, BUT SHOULD BE CONSISTENT
C                            FROM CALL TO CALL),
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C        FDSIN(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).  (OUTPUT)
C          FDM(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (OUTPUT)
C       FDSINS(IX,JY) = USED TO SAVE THE SIN OF THE LATITUDE 
C                       (IX=1,NX) (JY=1,NY).  THE USER MUST NOT USE 
C                       THIS ARRAY EXCEPT IN CALLING MAPLAT.
C                       IT WOULD BE BETTER TO "HIDE" THIS VARIABLE BY
C                       BY USING AN ALLOCATE STATEMENT (THE SIZE SHOULDN'T
C                       BE HARD WIRED), BUT THE CRAY FORTRAN DOES NOT
C                       SUPPORT THIS.
C                       (INPUT/OUTPUT)
C         FDMS(IX,JY) = USED TO SAVE THE MAP FACTOR (IX=1,NX) (JY=1,NY).
C                       THE USER MUST NOT USE THIS ARRAY EXCEPT
C                       IN CALLING MAPLAT.
C                       IT WOULD BE BETTER TO "HIDE" THIS VARIABLE BY
C                       BY USING AN ALLOCATE STATEMENT (THE SIZE SHOULD'T
C                       BE HARD WIRED), BUT THE CRAY FORTRAN DOES NOT
C                       SUPPORT THIS.  (INPUT/OUTPUT)
C                  NX = THE DIMENSION OF THE GRIDS IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRIDS IN THE JY DIRECTION.
C                       (INPUT).
C                 IER = STATUS RETURN.
C                        0 = GOOD RETURN.
C                       60 = MAP PROJECTION NUMBER NOT EXPECTED.
C              XMESHL = GRID LENGTH IN M AT THE LATITUDE XLAT.  (INTERNAL)
C                XLAT = THE LATITUDE IN DEGREES AT WHICH XMESHL IS CORRECT.
C                       (INTERNAL)
C              ORIENT = THE VERTICAL LONGITUDE ON THE GRID IN DEGREES WEST.
C                       (INTERNAL)
C              XLATLL = THE LATITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES NORTH.  (INTERNAL)
C              XLONLL = THE LONGITUDE OF THE LOWER LEFT CORNER POINT OF THE
C                       GRID IN DEGREES WEST.  (INTERNAL)
C               XLATS = THE LATITUDE IN DEGREES N AT WHICH XMESHL IS CORRECT
C                       SAVED FROM LAST ENTRY.  (INTERNAL)
C                XNPS = THE IX POSITION OF THE NORTH POLE RELATIVE TO THE
C                       LOWER LEFT CORNER OF THE GRID SAVED FROM THE LAST
C                       ENTRY.  (INTERNAL)
C                YNSP = THE JY POSITION OF THE NORTH POLE RELATIVE TO THE
C                       LOWER LEFT CORNER OF THE GRID SAVED FROM THE LAST
C                       ENTRY.  (INTERNAL)
C                 NXS = THE DIMENSION OF THE GRID IN THE IX DIRECTION SAVED
C                       FROM THE LAST ENTRY.  (INTERNAL).
C                 NYS = THE DIMENSION OF THE GRID IN THE JY DIRECTION SAVED
C                       FROM THE LAST ENTRY.  (INTERNAL).
C             XMINDIF = MAXIMUM ALLOWABLE ABSOLUTE DIFFERENCE WHEN CHECKING
C                       VARIABLES IN NGRIDC( ).  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            PSLLIJ, LMLLIJ
C
      DIMENSION JD(4)
      DIMENSION NGRIDC(6)
      DIMENSION FDSIN(NX,NY),FDM(NX,NY),FDSINS(NX,NY),FDMS(NX,NY)
C
      DATA MAPPS/0/,
     1     XMESHS/0/,
     2     XLATS/0/,
     3     ORIENS/0/,
     4     ALATLS/0/,
     5     ALONLS/0/,
     6     NXS/0/,
     7     NYS/0/
      DATA NFIRST/0/
      DATA XMINDIF/0.0001/
C
      IER=0
C
      MAPP=NGRIDC(1)
      XMESHL=NGRIDC(2)/1000. 
      XLAT=NGRIDC(3)/10000.
      ORIENT=NGRIDC(4)/10000.
      XLATLL=NGRIDC(5)/10000.
      XLONLL=NGRIDC(6)/10000.
C
C        CHECK TO SEE WHETHER CALCULATIONS ARE NEEDED.
C        11/2002 - CHANGED THIS IF TEST BECAUSE IT WAS EQUIVALENCING
C        REAL VALUES.  RAN WITH "EQ" ON HP BUT NOT ON IBM.
C        5/12/03 - REMOVED DUPLICATIVE "EQ" TESTS ON REALS.
C
      IF ((MAPP .EQ. MAPPS) .AND.
     1    (ABS(XMESHL-XMESHS).LT.XMINDIF).AND.
     2    (ABS(XLAT-XLATS).   LT.XMINDIF).AND.
     3    (ABS(ORIENT-ORIENS).LT.XMINDIF).AND.
     4    (ABS(XLATLL-ALATLS).LT.XMINDIF).AND.
     5    (ABS(XLONLL-ALONLS).LT.XMINDIF).AND.
     6    (NX.EQ.NXS).AND.(NY.EQ.NYS)) GO TO 260
C
      NFIRST=NFIRST+1
      IF(NFIRST.GT.10)GO TO 140
C
      WRITE(KFILDO,135)
 135  FORMAT(/' SINE OF LATITUDE AND MAP FACTOR BEING CALCULATED',
     1        ' IN MAPLAT.')
      IF(NFIRST.EQ.10)WRITE(KFILDO,1350)
 1350 FORMAT(' THIS DIAGNOSTIC WILL NOT BE REPEATED.')      
      WRITE(KFILDO,136)MAPP, XMESHL,XLAT, ORIENT,XLATLL,XLONLL,NX, NY,
     1                 MAPPS,XMESHS,XLATS,ORIENS,ALATLS,ALONLS,NXS,NYS
 136  FORMAT('     MAPP      XMESHL   XLAT  ORIENT   XLATLL   XLONLL',
     1       '  NX  NY'/
     2       '     MAPPS     XMESHS   XLATS ORIENS   ALATLS   ALONLS',
     3       ' NXS NYS'/
     4       ' ',I8,F12.3,F7.3,F8.3,F9.4,F9.4,2I4,
     5       '    NEW GRID CHARACTERISTICS'/
     6       ' ',I8,F12.3,F7.3,F8.3,F9.4,F9.4,2I4,
     7       '    OLD GRID CHARACTERISTICS')
C
C        CALCULATIONS ARE NEEDED.  FIRST SAVE VALUES FOR NEXT TIME.
C
 140  MAPPS=MAPP
      XMESHS=XMESHL
      XLATS=XLAT
      ORIENS=ORIENT
      ALATLS=XLATLL
      ALONLS=XLONLL
      NXS=NX
      NYS=NY
C
C        CALCULATE SINE OF LATITUDE IN FDSINS( , ) AND 
C        MAP FACTOR IN FDMS( , ).   
C
      IF(MAPP.EQ.3)THEN
         CALL LMMAPF(KFILDO,XMESHL,ORIENT,XLAT,XLATLL,XLONLL,
     1               FDSINS,FDMS,NX,NY,IER)
C
         IF(IER.NE.0)THEN
            MAPPS=999
C              MAPPS SET TO 999 TO SIGNAL THAT FDSINS( , ) AND
C              FDMS( , ) CAN'T BE USED NEXT TIME.
            GO TO 280
         ENDIF
C
      ELSEIF(MAPP.EQ.5)THEN
         CALL PSMAPF(KFILDO,XMESHL,ORIENT,XLAT,XLATLL,XLONLL,
     1               FDSINS,FDMS,NX,NY,IER)
C
         IF(IER.NE.0)THEN
            MAPPS=999
C              MAPPS SET TO 999 TO SIGNAL THAT FDSINS( , ) AND
C              FDMS( , ) CAN'T BE USED NEXT TIME.
            GO TO 280
         ENDIF
C
      ELSEIF(MAPP.EQ.7)THEN
         CALL MCMAPF(KFILDO,XMESHL,XLAT,XLATLL,XLONLL,
     1               FDSINS,FDMS,NX,NY,IER)
C
         IF(IER.NE.0)THEN
            MAPPS=999
C              MAPPS SET TO 999 TO SIGNAL THAT FDSINS( , ) AND
C              FDMS( , ) CAN'T BE USED NEXT TIME.
            GO TO 280
         ENDIF
C
      ELSE
         WRITE(KFILDO,212)MAPP,(JD(J),J=1,4)
 212     FORMAT(/' ****MAP PROJECTION NUMBER =',I3,' NOT EXPECTED.',
     1           '  PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2           ' NOT COMPUTED IN MAPLAT.')
         IER=60
C
      ENDIF
C
C        PUT SAVE ARRAYS INTO FDSIN( , ) AND FDM( , ) FOR RETURN.
C        THEY ARE SINGLE DIMENSIONED ARRAYS IN THIS ROUTINE.
C
 260  DO 270 JY=1,NY
      DO 269 IX=1,NX
C***D     WRITE(KFILDO,268)IX,JY,FDSINS(IX,JY),FDMS(IX,JY)
C***D268  FORMAT(/' IN MAPLAT, IX,JY,FDSINS(IX,JY),FDMS(IX,JY)',
C***D    1        2I4,2F10.2)
      FDSIN(IX,JY)=FDSINS(IX,JY)
      FDM(IX,JY)=FDMS(IX,JY)
 269  CONTINUE
 270  CONTINUE
C
 280  RETURN
      END
