      SUBROUTINE FORIER(KFILDO,IDPARS,JD,NDATE,
     1                  SINCOS,NSTA,ISTAV,IER)
C
C        MARCH 1995   GLAHN   TDL   MOS-2000 
C        APRIL 2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                             CONFORM TO FORTRAN 90 STANDARDS
C                             ON THE IBM SP
C        JUNE  2002   GLAHN   MODIFIED TO USE IDPARS(7) TO SHIFT THE
C                             PHASE OF THE HARMONIC
C
C        PURPOSE 
C            TO COMPUTE THE SIN OR COS OR THE DAY OF THE YEAR (FIRST
C            HARMONIC) OR THE SIN OR COS OF TWICE THE DAY OF THE YEAR
C            (2ND HARMONIC).  THE DAY OF THE YEAR CAN BE EITHER THE
C            BASIC DATE NDATE OR WITH THE PROJECTION ADDED.  THIS LATTER
C            IS USEFUL FOR LONG PROJECTIONS, BUT SHOULD BE USED SPARINGLY.
C            THAT IS, FOR A PARTICULAR BASIC DATE, ONE WOULD NOT WANT TO
C            COMPUTE AND PUT ON THE OUTPUT TAPE RECORDS FOR EACH OF THE
C            POSSIBLE PROJECTIONS OUT TO 6 OR 8 DAYS.  RATHER, ONE SUCH
C            PROJECTION, SUCH AS 5 DAYS = 120 HOURS, SHOULD SUFFICE.
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C               010 201 SIN DOY
C               010 202 COS DOY
C               010 203 SIN 2*DOY
C               010 204 CON 2*DOY
C               010 205 SIN DOY + IDPARS(12)
C               010 206 COS DOY + IDPARS(12)
C               010 207 SIN 2*DOY + IDPARS(12)
C               010 208 CON 2*DOY + IDPARS(12)
C            IDPARS(7) CAN BE USED TO SHIFT THE PHASE OF THE HARMONIC.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--NORMALLY, LTLTLTLT (TOP OF LAYER); FOR FORIER
C                            THIS IS THE NUMBER OF DAYS TO SHIFT THE HARMONIC
C                            FROM ITS NORMAL PHASE,
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       JD( ) IS USED TO IDENTIFY THE BASIC MODEL FIELDS.
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C           SINCOS(K) = CALCULATED SIN OR COS FOR NSTA LOCATIONS.  (OUTPUT)
C                NSTA = NUMBER OF STATIONS.  USED AS THE DIMENSION 
C                       OF SINCOS( ).  (INPUT)
C               ISTAV = 1 SINCE THE DATA RETURNED ARE STATION DATA.  (OUTPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       103 = IDPARS(1) DOES NOT INDICATE GEOCLIMATIC
C                             DATA OR IDPARS(2) DOES NOT INDICATE SIN OR COS.
C                             DATA ARE RETURNED AS MISSING.
C                       (OUTPUT)
C             LDPM(J) = THE NUMBER OF DAYS IN THE YEAR PRECEEDING THE
C                       MONTH J (J=1,12).  (INTERNAL)
C                 NHR = THE HOUR (CYCLE) IN NDATE.  (INTERNAL)
C                NDAY = THE NUMBER OF THE DAY OF THE MONTH IN NDATE.
C                       (INTERNAL)
C                 MON = THE NUMBER OF THE MONTH IN NDATE.  (INTERNAL)
C                NDOY = THE JULIAN DAY OF THE YEAR, JAN. 1 = 1, ETC.
C                       WHEN THE PROJECTION IS ADDED, NDOY INCLUDES THAT.
C                       (INTERNAL)
C              RADDOY = THE RADIANS FROM WHICH TO COMPUTE THE SIN AND COS,
C                       SUCH THAT RADDOY VARIES FROM 0 TO 2PI OVER 365 DAYS.
C                       NO ALLOWANCE IS MADE ON MARCH 1 FOR LEAP YEAR.
C                       (INTERNAL)
C                 VAL = THE SIN OR COS TO STORE INTO SINCOS( ).  THIS
C                       IS INITIALLY SET TO 9999 FOR SAFETY.  (INTERNAL)
C                  PI = PI.  SET BY PARAMETER.  (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            NONE
C
      PARAMETER (PI=3.14159)
C
      DIMENSION SINCOS(NSTA)
      DIMENSION JD(4),IDPARS(15)
      DIMENSION LDPM(12)
C
      DATA LDPM/0,31,59,90,120,151,181,212,243,273,304,334/
C
D     CALL TIMPR(KFILDO,KFILDO,'START FORIER        ')
      IER=0 
      ISTAV=1
      VAL=9999.
C
      IF(IDPARS(1).EQ.010)GO TO 105
      WRITE(KFILDO,101)(JD(J),J=1,4)
 101  FORMAT(/,'****IDPARS(1) DOES NOT INDICATE GEOCLIMATIC VARIABLE',
     1       /,'     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2        ' NOT COMPUTED IN FORIER.')
      IER=103
      GO TO 800
C
 105  NDAY=MOD(NDATE/100,100)
      MON=MOD(NDATE/10000,100)
      NHR=MOD(NDATE,100)
      NDOY=NDAY+LDPM(MON)
      IF(IDPARS(2).GE.205.AND.IDPARS(2).LE.208)
     1          NDOY=NDOY+(IDPARS(12)+NHR)/24
C        THE ABOVE ADDS THE PROJECTION, DEPENDING ON IDPARS(2).
C        NOTE THAT IT TRUNCATES THE SUM OF THE PROJECTION AND
C        CYCLE TIME TO WHOLE DAYS.
      NDOY=NDOY+IDPARS(7)
C        IDPARS(7) IS THE NUMBER OF DAYS TO SHIFT THE HARMONIC.
      NDOY=MOD(NDOY,365)
C        THE ABOVE KEEPS NDOY TO 365 IN CASE OF LEAP YEAR,IN CASE
C        THE PROJECTION IS ADDED TO THE DATE, OR THERE IS A SHIFT
C        IN PHASE BY IDPARS(7) (E.G., 367 BECOMES 2).
      RADDOY=NDOY*(2.*PI/365.)
D     WRITE(KFILDO,110)NDATE,NDAY,MON,NHR,NDOY,RADDOY
D110  FORMAT(/' FORIER,NDATE,NDAY,MON,NHR,NDOY,RADDOY'I12,4I6,F6.3)
      IF(IDPARS(2).EQ.201.OR.IDPARS(2).EQ.205)THEN
         VAL=SIN(RADDOY)
      ELSEIF(IDPARS(2).EQ.202.OR.IDPARS(2).EQ.206)THEN
         VAL=COS(RADDOY)      
      ELSEIF(IDPARS(2).EQ.203.OR.IDPARS(2).EQ.207)THEN
         VAL=SIN(2.*RADDOY)      
      ELSEIF(IDPARS(2).EQ.204.OR.IDPARS(2).EQ.208)THEN
         VAL=COS(2.*RADDOY)
      ELSE
         WRITE(KFILDO,230)(JD(J),J=1,4)
 230     FORMAT(/,'****IDPARS(2) DOES NOT INDICATE SIN OR COS',
     1          /,'     PREDICTOR ',I9.9,1X,I9.9,1X,I9.9,I3,
     2           ' NOT COMPUTED IN FORIER.')
      IER=103
      GO TO 800
         
      ENDIF      
C      
      DO 220 K=1,NSTA
      SINCOS(K)=VAL
 220  CONTINUE
C
      GO TO 900
C
C        SET OUTPUT FIELD TO MISSING WHEN AN ERROR HAS OCCURRED.
C        THE ABOVE LOOP WOULD HAVE ALSO DONE THIS, BUT THE LOOP
C        BELOW IS INCLUDED TO BE CONSISTENT WITH OTHER SIMILAR
C        ROUTINES.
C
 800  DO 801 K=1,NSTA
      SINCOS(K)=9999.
 801  CONTINUE
C
 900  RETURN
      END      
