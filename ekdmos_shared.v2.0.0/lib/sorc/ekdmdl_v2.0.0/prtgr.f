      SUBROUTINE PRTGR(KFILDO,GRID,NX,NY,CINT,ORIGIN,SMULT,SADD,IOPT,
     1                 TITLE,IER) 
C 
C        MAY    1993   GLAHN   TDL   HP9000
C        MAY    2000   GLAHN   COMMAS INSERTED IN FORMAT STATEMENT
C        AUGUST 2000   GLAHN   CHANGED FORMAT STATEMENT AT 100;
C                              SUBSTITUTED "CTRL-L" FOR "1" TO GIVE NEW
C                              PAGE AT 140
C        APRIL  2001   MALONEY CHANGED SOME VARIABLES FROM INTEGERS TO
C                              CHARACTER*1 TO WORK WITH BESEL FROM IBM
C        APRIL  2002   GLAHN   ADDED PRINT OF TITLE ON ABORT AT 214
C        MAY    2002   GLAHN   STOPPED USE OF SMULT AND SADD WHEN
C                              DATA = 9999.
C        APRIL  2007   SMB     MERGED OPERATIONAL VERSION WITH MALONEY'S
C                              CHANGED WITH HRG VERSION.
C 
C        PURPOSE 
C            DISPLAYS TWO-DIMENSIONAL GRIDS OR SUB-GRIDS, WITH OR 
C            WITHOUT CONTOURING ON A LINE PRINTER.  A DOUBLE 
C            QUADRATIC INTERPOLATION USING BESSEL'S CENTRAL 
C            DIFFERENCE FORMULA IS USED TO CONSTRUCT CONTOURS. 
C            THIS ROUTINE WAS WRITTEN BY PERROTTI FOR THE IBM 360/195
C            IN 1980.  IT WAS CONVERTED TO THE DG ECLIPSE BY PERROTTI
C            AND MODIFIED BY BY GLAHN 1985-87.
C 
C        DATA SET USE 
C            KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C 
C        VARIABLES 
C 
C            INPUT 
C              KFILDO = UNIT NUMBER FOR OUTPUT. 
C           GRID( , ) = INPUT DATA ARRAY.  DATA ARE STORED IN NMC 
C                       CONVENTION.  STARTING POINT GRID(1,1) ON THE MAP 
C                       IS LOWER LEFT WITH DATA STORED LEFT TO RIGHT 
C                       (THE IX DIRECTION), THEN THE NEXT ROW UP, ETC. 
C                  NX = FIRST DIMENSION OF GRID( , ). 
C                  NY = SECOND DIMENSION OF GRID( , ). 
C                CINT = CONTROUR INTERVAL.  IF CINT = 0, CONTOURING 
C                       IS NOT DONE.  THIS APPLIES TO THE ORIGINAL DATA,
C                       NOT AFTER MULTIPLICATION BY SMULT. 
C              ORIGIN = VALUE WHERE CONTOURING IS TO BEGIN. THIS APPLIES
C                       TO THE ORIGINAL DATA, NOT AFTER MULTIPLICATION BY
C                       SMULT.  
C               SMULT = MULTIPLICITIVE SCALING FACTOR FOR PRINTING VALUES
C                       AT GRIDPOINTS. 
C                SADD = ADDITIVE SCALING FACTOR FOR PRINTING VALUES
C                       AT GRIDPOINTS. 
C             IOPT( ) = ADDITIONAL OPTIONS TABLE. 
C                       1 - 0 OPTION TABLE NOT USED/1 USED. 
C                       2 - SUB ARRAY MIN IX VALUE. 
C                       3 - SUB ARRAY MAX IX VALUE. 
C                       4 - SUB ARRAY MIN JY VALUE. 
C                       5 - SUB ARRAY MAX JY VALUE. 
C                       6 - 1 IF ALL INTERPOLATION IS TO BE BILINEAR. 
C                           OTHERWISE, INTERPOLATION IS TO BE 
C                           BIQUADRATIC WHERE POSSIBLE. 
C                       7 - NOT USED. 
C                       8 - PAGE WIDTH IN GRID POINTS. 
C                       SUB GRID DIMENSIONS ARE MADE CONSISTENT FOR PRINT 
C                       IF IOPT(2), IOPT(3), IOPT(4), AND IOPT(5) ARE 
C                       INCONSISTENT. 
C            TITLE( ) = *-CHARACTER TITLE TO BE PRINTED AT BOTTOM OF 
C                       EACH PAGE.  MAXIMUM OF 74 CHARACTERS.
C                       (CHARACTER*(*)) 
C 
C            OUTPUT 
C                 IER = ERROR RETURN.  (OUTPUT)
C                       0  = GOOD RETURN.
C                       90 = OVERFLOW IN SCALING. 
C                       (IER ADDED 7/10/94.)
C            INTERNAL 
C            LINE1( ) = HOLDS TOP AND BOTTOM LABELS FOR MAP. 
C            LINE2( ) = HOLDS VALUES TO PLOT, THEN FIRST OF 3 ROWS OF 
C                       CONTOURS BETWEEN A PAIR OF GRIDPOINT ROWS. 
C            LINE3( ) = HOLDS 2ND OF 3 ROWS OF CONTOURS. 
C            LINE4( ) = HOLDS 3RD OF 3 ROWS OF CONTOURS. 
C              CONINT = RECIPROCAL OF CONTOUR INTERVAL, IF CINT NE 0. 
C               IXMIN = MINIMUM IX VALUE TO PLOT.  DEFAULT = 1. 
C                       IF IOPT(1) AND IOPT(2) NE 0, IXMIN = IOPT(2). 
C               IXMAX = MAXIMUM IX VALUE TO PLOT.  DEFAULT = NX. 
C                       IF IOPT(1) AND IOPT(3) NE 0, IXMAX = IOPT(3). 
C               JYMIN = MINIMUM JY VALUE TO PLOT.  DEFAULT = 1. 
C                       IF IOPT(1) AND IOPT(4) NE 0, JYMIN = IOPT(4). 
C               JYMAX = MAXIMUM JY VALUE TO PLOT.  DEFAULT = NY. 
C                       IF IOPT(1) AND IOPT(5) NE 0, JYMAX = IOPT(5). 
C                IXMN = MINIMUM VALUE OF IX TO PLOT FOR THE PARTICULAR 
C                       PAGE BEING WORKED ON. 
C                IXMX = MAXIMUM VALUE OF IX TO PLOT FOR THE PARTICULAR 
C                       PAGE BEING WORKED ON. 
C               IPAGE = PAGE WIDTH IN GRIDPOINTS.  DEFAULT = 24. 
C                       IF IOPT(1) AND IOPT(8) NE 0, IPAGE = IOPT(8). 
C              LINEAR = TRUE FOR BILINEAR INTERPOLATION FOR ALL POINTS. 
C                       FALSE FOR BIQUADRATIC INTERPOLATION WHERE 
C                       POSSIBLE.  DEFAULT = FALSE.  (LOGICAL) 
C               IXDIF = NUMBER OF GRIDPOINTS TO PLOT ON THE PAGE BEING 
C                       WORDED ON. 
C              ENDPLT = TRUE IF THE PAGE BEING WORKED ON IS THE LAST 
C                       PAGE.  (LOGICAL) 
C                ICOL = NUMBER OF CHARACTERS TO WRITE ACROSS TOP AND 
C                       BOTTOM OF MAP (LINE1(K),K=1,ICOL). 
C                KCOL = NUMBER OF CHARACTERS TO WRITE FOR PLOTTED DATA 
C                       (LINE2(K),K=1,KCOL). 
C                LCOL = NUMBER OF CHARACTERS TO WRITE FOR CONTOURS 
C                       (LINEX(K),K=1,LCOL WHERE X = 2, 3, OR 4). 
C                  IX = THE NUMBER OF THE GRIDPOINT IN THE IX DIRECTION 
C                       BEING REFERENCED IN GRID( , ). 
C                  JY = THE NUMBER OF THE GRIDPOINT IN THE JY DIRECTION 
C                       BEING REFERENCED IN GRID( , ). 
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            SETUP, BESEL 
C 
      CHARACTER*(*) TITLE
      LOGICAL NOCNTR,LINEAR,ENDPLT 
C
      DIMENSION GRID(NX,NY)
      DIMENSION IOPT(8)
      CHARACTER*1 LINE1(133),LINE2(133),LINE3(133),LINE4(133)
      CHARACTER*1 NUM(10),MINUS,NPLUS,NBLK
C 
      DATA NUM/'0','1','2','3','4','5','6','7','8','9'/ 
      DATA MINUS/'-'/,
     1     NPLUS/'+'/,
     2     NBLK/' '/ 
C 
      IER=0
C
D     WRITE(KFILDO,100)NX,NY,(IOPT(J),J=1,8)
D100  FORMAT(' IN PRTRGR AT 100--NX,NY,IOPT =',10I4)
C
      DO 101 J=1,133 
      LINE1(J)=NBLK 
      LINE2(J)=NBLK 
      LINE3(J)=NBLK 
      LINE4(J)=NBLK 
 101  CONTINUE 
C 
C        CALCULATE RECIPROCAL OF CONTOUR INTERVAL 
      IF(CINT.NE.0.)CONINT=1./CINT 
C        SET DEFAULT VALUES 
      CALL SETUP(KFILDO,IOPT,NX,NY,IXMIN,IXMAX,JYMIN,JYMAX,IPAGE,LINEAR) 
C        CONTOURING NOT DONE IF CINT = 0 
      NOCNTR=CINT.EQ.0. 
      IXMN=IXMIN 
      IXMX=IXMAX 
      IXDIF=IXMAX-IXMIN+1 
C 
C        CONTOURING LOOP STARTS HERE 
C 
 130  ENDPLT=IXDIF.LE.IPAGE 
      WRITE(KFILDO,140)
C        THIS IS A "CTRL L" IN THE FIRST COLUMN FOR A PAGE BREAK. 
 140  FORMAT('') 
C        ESTABLISH PAGE LIMITS (WIDTH IN GRIDPOINTS) 
      IF(ENDPLT)GO TO 150 
      IXMX=IXMN+IPAGE-1 
      ICOL=IPAGE*5+6 
      GOTO 160 
C 
 150  IXMX=IXMN+IXDIF-1 
      ICOL=IXDIF*5+6 
 160  KCOL=ICOL 
      LCOL=ICOL 
      IF(ENDPLT)KCOL=ICOL+4 
      IF(ENDPLT)LCOL=ICOL-4 
      K=IXMN 
C 
C        LOAD I-LABELS ACROSS TOP OF MAP 
C 
      DO 170 J=7,ICOL,5 
C        IF I LABEL .GT. 99 - PRINT LAST TWO DIGITS 
      IF(K.GT.99)K=MOD(K,100) 
      LINE1(J)=NPLUS 
      L=K/10 
      LINE1(J+1)=NUM(L+1) 
      L=K-L*10 
      LINE1(J+2)=NUM(L+1) 
      K=K+1 
 170  CONTINUE 
      WRITE(KFILDO,180)(LINE1(I),I=1,ICOL) 
 180  FORMAT(1H ,133A1) 
      WRITE(KFILDO,190) 
 190  FORMAT(/) 
      JY=JYMAX 
      KOUNT=-1 
C 
C        LOAD J-LABELS FOR PRINTING ON LEFT AND RIGHT OF PAGE 
C 
      DO 300 KNT=JYMIN,JYMAX 
C        IF J LABEL .GT. 99 - PRINT LAST TWO DIGITS 
      JJ=JY 
      IF(JJ.GT.99)JJ=MOD(JJ,100) 
      LINE2(ICOL+1)=NBLK 
      LINE2(2)=NPLUS 
      LINE2(ICOL+2)=LINE2(2) 
      L=JJ/10 
      LINE2(3)=NUM(L+1) 
      LINE2(ICOL+3)=LINE2(3) 
      L=JJ-L*10 
      LINE2(4)=NUM(L+1) 
      LINE2(ICOL+4)=LINE2(4) 
      IX=IXMN 
C 
      DO 220 J=7,ICOL,5 
C        SCALE RAW DATA FOR PRINTING
C
      IF(GRID(IX,JY).EQ.9999.)THEN
         PLOT=9999.
      ELSE 
         PLOT=GRID(IX,JY)*SMULT+SADD 
      ENDIF
C
      IF(PLOT.LT.32767.)GO TO 215 
      WRITE(KFILDO,214)SMULT,SADD,TITLE 
 214  FORMAT(/,' ****SCALING CAUSED OVERFLOW.  SMULT =',F10.2,
     1                                      4X,'SADD =',F10.2,/,
     2        '     CONTOURING ABORTED AT 214 FOR GRID',2X,A74) 
C
      DO 2145 JJJ=1,NY
      WRITE(KFILDO,2140)(GRID(III,JJJ),III=1,NX)
 2140 FORMAT(/(10F12.2))
 2145 CONTINUE
C      
      IER=90 
      GO TO 330
C 
 215  LINE2(J)=NPLUS 
      IF(PLOT.LT.0.)LINE2(J)=MINUS 
C        LOAD SCALED DATA LINE INTO MAP 
      IF(PLOT.GE.0.)L1=ABS(PLOT+.5) 
      IF(PLOT.LT.0.)L1=ABS(PLOT-.5) 
      L2=MOD(L1,10000)/1000 
      LINE2(J+1)=NUM(L2+1) 
      L2=MOD(L1,1000)/100 
      LINE2(J+2)=NUM(L2+1) 
      L2=MOD(L1,100)/10 
      LINE2(J+3)=NUM(L2+1) 
      L2=MOD(L1,10) 
      LINE2(J+4)=NUM(L2+1) 
      IX=IX+1 
 220  CONTINUE 
C 
      WRITE(KFILDO,180)(LINE2(KK),KK=1,KCOL) 
C        CHECK TO SEE IF CONTOURING IS TO BE DONE 
      IF(NOCNTR)WRITE(KFILDO,225) 
 225  FORMAT(//) 
      IF(NOCNTR)GO TO 290 
C        IF KNT EQ JYMAX, JY EQ JYMIN.  INTERPOLATING AND PRINTING IS 
C        NOT DONE. 
      IF(KNT.EQ.JYMAX)GO TO 300 
C 
C        DO INTERPOLATION FOR ONE ROW AND LOAD CHARACTERS IN LINE2( ), 
C        LINE3( ), AND LINE4( ). 
C 
      CALL BESEL(GRID,NX,NY,JY,IXMN,IXMX,IXMIN,IXMAX,JYMIN,JYMAX, 
     1           ORIGIN,CONINT,LINEAR,LINE2,LINE3,LINE4) 
      WRITE(KFILDO,180)(LINE2(INT),INT=1,LCOL) 
      WRITE(KFILDO,180)(LINE3(INT),INT=1,LCOL) 
      WRITE(KFILDO,180)(LINE4(INT),INT=1,LCOL) 
 290  JY=JY+KOUNT 
 300  CONTINUE 
C 
C        PRINT I-LABELS ACROSS BOTTOM OF MAP 
C 
      WRITE(KFILDO,190) 
      WRITE(KFILDO,180)(LINE1(I),I=1,ICOL) 
      WRITE(KFILDO,310)
 310  FORMAT(' ')
C***      WRITE(KFILDO,190)
C         SUBSTITUTED FORMAT 310 FOR 190 8/26/00.  REMOVES
C         A LINE BELOW MAP BEFORE TITLE.
      WRITE(KFILDO,320)TITLE 
 320  FORMAT(T7,A74) 
C        IF NO MORE DATA --- BAIL-OUT 
      IF(ENDPLT)GO TO 330 
C 
      IXMN=IXMX+1 
      IXDIF=IXDIF-IPAGE 
      GO TO 130 
C 
 330  RETURN 
      END 
