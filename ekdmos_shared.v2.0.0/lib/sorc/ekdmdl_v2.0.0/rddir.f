      SUBROUTINE RDDIR(KFILDO,KFILIN,IP12,NAMIN,NDATE,
     1                 CCALL,INDEXC,ND1,NSTA,CCALLD,ND5,MSTA,
     2                 L3264B,L3264W,IER)
C
C        OCTOBER   1996   GLAHN   TDL   MOS-2000 
C        JANUARY   1997   GLAHN   ND5 CHECK CHANGED FROM ND1,
C                                 READING CHANGED TO MIN(ND5,...)
C        FEBRUARY  1997   GLAHN   FORMATTING CHANGE AT STATEMENT 130,
C                                 AND A COUPLE OF COMMENTS
C        JULY      1997   GLAHN   ADDED SUBSTITUTE STATIONS IN CCALL( , )
C                                 ADDED ND1 TO ARGUMENT LIST FOR FINDST
C        SEPTEMBER 1997   GLAHN   MODIFIED DIAGNOSTIC STATEMENT 116
C        SEPTEMBER 1997   GLAHN   REMOVED DIAGNOSTIC FOR EOF
C        SEPTEMBER 1997   GLAHN   CHANGES FOR TRAILER USE CHANGE
C        FEBRUARY  1998   GLAHN   ADDED TEST FOR MSTA=0
C        OCTOBER   1998   GLAHN   INDEXC( ) SET = 99999999 FOR MISSING
C        APRIL     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                      CONFORM TO FORTRAN 90 STANDARDS
C                                      ON THE IBM SP
C        DECEMBER  2003   GLAHN   INSERTED TIMPR
C
C        PURPOSE
C           TO READ CALL LETTERS DIRECTORY INTO CCALLD( ) AND TO
C           PROVIDE INDICES FOR THE CALL LETTERS IN CCALL( , )
C           TO THE POSITIONS IN CCALLD( ).  THE INDICES ARE STORED IN 
C           INDEXC( ).
C   
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILIN - UNIT NUMBER OF FILE FROM WHICH TO READ THE CALL
C                     LETTERS DIRECTORY.  (INPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILIN = UNIT NUMBER OF FILE FROM WHICH TO READ THE CALL
C                       LETTERS DIRECTORY.  (INPUT)
C                IP12 = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                       THE FILE WHOSE UNIT NUMBER IS IP12.  (INPUT)
C               NAMIN = THE NAME OF THE FILE WHOSE CALL LETTERS ARE IN
C                       CCALLD( ).  USED FOR PRINTOUT ONLY.  (CHARACTER*60)
C                       (INPUT)
C               NDATE = DATE BEING PROCESSED IN FORM YYYYMMDDHH.  (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (K=1,NSTA),
C                       PRIMARY STATION (J=1) AND 5 SUBSTITUTE STATIONS
C                       (J=2,6).  THE LOCATION OF EACH STATION IN THE
C                       CCALLD( ) RECORD IS PLACED INTO INDEXC( ). 
C                       (CHARACTER*8)  (INPUT)
C           INDEXC(K) = LOCATIONS OF THE STATIONS CORRESPONDING TO
C                       CCALL(K, ) (K=1,NSTA).  IF THE STATION CAN'T
C                       BE FOUND IN THE DIRECTORY, INDEXC( ) IS SET
C                       TO 99999999.  NOTE THAT THIS MUST BE LARGER
C                       THAN THE MAXIMUM NUMBER OF STATIONS BEING
C                       DEALT WITH (GRIDPOINTS IN THE CASE OF GRIDPOINT
C                       DEVELOPMENT).  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       (INPUT)
C                NSTA = THE NUMBER OF STATIONS IN CCALL( , ).  THIS WORKS
C                       OK WHEN NSTA = 0.  (INPUT)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,MSTA) READ IN FROM
C                       FILE KFILIN.  (CHARACTER*8)  (INTERNAL/OUTPUT)
C                 ND5 = DIMENSION OF CCALLD( ).  (INPUT)
C                MSTA = NUMBER OF STATIONS IN THE DIRECTORY RECORD
C                       OF THE FILE BEING READ.  TAKEN FROM THE RECORD
C                       SIZE.  NOTE THAT THIS CAN EXCEED NSTA AND/OR ND1.
C                       (INTERNAL/OUTPUT)
C              L3264B = THE NUMBER OF BITS IN THE WORD OF THE MACHINE
C                       BEING USED, 32 OR 64.  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).  (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       120 = ONE OR MORE STATIONS CAN'T BE FOUND IN
C                             CCALLD( ) FROM THE INPUT IN FINDST OR
C                             BECAUSE MSTA = 0.
C                       140 = ERROR READING CALL LETTERS RECORD.
C                       145 = SIZE OF CALL LETTERS RECORD EXCEEDS ARRAY SIZE
C                             CCALLD(ND5). 
C                       146 = END OF FILE FOUND.
C                       SEE OTHER ROUTINES FOR OTHER VALUES.  (OUTPUT)
C              NBYTES = NUMBER OF BYTES IN RECORD, FOLLOWING THE INITIAL
C                       8 BYTES CONTAINING NBYTES ITSELF.   (INTERNAL)
C
C        NONSYSTEM SUBROUINES USED 
C            FINDST, TIMPR
C
      CHARACTER*8 CCALL(ND1,6),
     1            CCALLD(ND5)
      CHARACTER*60 NAMIN
C
      DIMENSION INDEXC(ND1)
C
      IER=0
C
D     CALL TIMPR(KFILDO,KFILDO,'START RDDIR         ')
C
      IF(L3264B.EQ.32)THEN
         READ(KFILIN,IOSTAT=IOS,ERR=110,END=115)NTRASH,NBYTES,
     1                             (CCALLD(K),K=1,MIN(ND5,NBYTES/8))
C           CCALLD( ) CONTAINS THE CALL LETTERS.
C           THE RECORD CONSISTS OF AN INITIAL 8 BYTES CONTAINING THE NUMBER
C           OF BYTES FOLLOWING.  FOR A 32-BIT MACHINE, THIS IS TWO WORDS.
      ELSE
         READ(KFILIN,IOSTAT=IOS,ERR=110,END=115)NBYTES,
     1                             (CCALLD(K),K=1,MIN(ND5,NBYTES/8))
      ENDIF
C
      GO TO 119
C
 110  WRITE(KFILDO,111)KFILIN,IOS,NAMIN
 111  FORMAT(/,' ****ERROR READING DIRECTORY RECORD',
     1         ' ON UNIT NO.',I3,' IN RDDIR, IOSTAT =',I5,
     2         ' ON FILE = ',A60)
      IER=140
      GO TO 132
C
 115  IER=146
      GO TO 132
C
 119  MSTA=NBYTES/8
D     WRITE(KFILDO,120)NBYTES,L3264B,MSTA
D120  FORMAT(/,' IN RDDIR, NBYTES, L3264B, MSTA =',3I8)
C
      IF(MSTA.NE.0)GO TO 125
      WRITE(KFILDO,121)KFILIN,NAMIN
 121  FORMAT(/,' ****THE NUMBER OF STATIONS READ FROM THE FILE ON UNIT',
     1         ' NO.',I3,' IS ZERO IN RDDIR AT 121',/,
     2         '     FOR FILE ',A60)
      IER=120
      GO TO 132
C
 125  IF(MSTA.LE.ND5)GO TO 150
C
      WRITE(KFILDO,130)KFILIN,MSTA,ND5,NAMIN
 130  FORMAT(/,' ****THE NUMBER OF STATIONS READ FROM THE FILE ON UNIT',
     1         ' NO.',I3,' =',I6,' EXCEEDS ND5 =',I6,' IN RDDIR AT 130',
     2       /,'     FOR FILE ',A60,/,
     3         '     STOP THE PRESSES!')
      IER=145
C
C        SET INDEXC( ) TO 99999999 WHENEVER IT CAN'T BE PROPERLY
C        INITIALIZED.
C
 132  DO 133 K=1,NSTA
      INDEXC(K)=99999999
 133  CONTINUE
C
      GO TO 235
C
C        THE LOCATION OF THE CALL LETTERS (OR GRIDPOINT) IN THE
C        RECORD IS PUT INTO INDEXC( ).  THE MSTA CALL LETTERS
C        ARE FURNISHED IN CCALLD( ).  IF A STATION K CANNOT BE FOUND,
C        INDEXC(K) IS SET TO 99999999.      
C
 150  CALL FINDST(KFILDO,IP12,NAMIN,CCALL,ND1,NSTA,
     1            CCALLD,MSTA,INDEXC,IER)
C        IER WILL = 120 WHEN ONE OR MORE STATIONS CAN'T BE FOUND IN
C        CCALLD( ) AS READ FROM THE INPUT.
D     WRITE(KFILDO,155)(CCALL(K,1),INDEXC(K),K=1,NSTA)
D155  FORMAT(/' CALL LETTERS AND INDICES IN RDDIR'/
D    1      (' 'A8,I8))
C
D     CALL TIMPR(KFILDO,KFILDO,'END   RDDIR         ')
C
 235  RETURN
      END
 
