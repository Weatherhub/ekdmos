      SUBROUTINE PTPMOSPRD(KFILDO,KFIL10,ID,IDPARS,NDATE,
     1                    SDATA,ND1,NSTA,IPACK,IWORK,FD1,FD2,ND2X3,
     2                    LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3                    IS0,IS1,IS2,IS4,ND7,ISTAV,L3264B,IER)
C
C        AUGUST    2004   SCALLION 
C                         & RUDACK    MDL
C        OCTOBER   2005   WIEDENFELD  MDL   MODIFIED NAME OF CODE.
C        MAY       2006   SCALLION    MDL   UPDATED ROUTINE FOLLOWING
C                                           05/12/06 CODE WALKTHROUGH:
C                                           CLEANED UP COMMENTS,
C                                           REMOVED JD, ADDED 'SAVE 
C                                           IENTER' STATEMENT, IMPROVED 
C                                           LOGIC AROUND ERROR CHECKS, 
C                                           AND ADDED A CHECK TO SET
C                                           SDATA TO ZERO IF FDATA IS 
C                                           MISSING IN SECOND PREDICTOR 
C                                           CALCULATION.
C
C        PURPOSE 
C           TO CONSTRUCT THE FOLLOWING INTERACTIVE MOS-2000 IDS FOR LAMP
C           PTYPE DEVELOPMENT:
C              (1)THE PRODUCT OF THE MOS PROBABILITY OF FROZEN PRECIP 
C                 AND THE PROBABILITY OF FREEZING PRECIP
C              (2)THE PRODUCT OF THE MOS PROBABILITY OF FROZEN PRECIP 
C                 AND THE PROBABILITY OF LIQUID PRECIP
C              (3)THE PRODUCT OF THE MOS PROBABILITY OF FREEZING PRECIP 
C                 AND THE BINARY OF OBSERVATION OF PRECIPITATION
C                 OCCURRENCE (1=YES, 0=NO).
C              (4)THE PRODUCT OF THE MOS PROBABILITY OF FROZEN PRECIP 
C                 AND THE BINARY OF OBSERVATION OF PRECIPITATION
C                 OCCURRENCE (1=YES, 0=NO).
C           (3) AND (4) WILL BE BUILT FROM CASES CONDITIONAL ON THE
C           OCCURRENCE OF PRECIPITATION BY CALLING THE OBSPOPO ROUTINE
C           AND MULTIPLYING THE PTYPE PROBABILITY VALUES BY THE BINARY 
C           OF PRECIPITATION OCCURRENCE.
C
C           THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C                    208 - 542 (MOS PROB(FROZEN)*OBSERVED POPO)      (1) 
C                    208 - 543 (MOS PROB(FREEZING)*OBSERVED POPO)    (2)
C                    208 - 547 (MOS PROB(FROZEN)*MOS PROB(FREEZING)) (3)
C                    208 - 548 (MOS PROB(FREEZING)*MOS PROB(LIQUID)) (2)
C
C        DATA SET USE 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE 
C                       (OUTPUT). 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS
C                       (INPUT-OUTPUT). 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE
C                       (INPUT). 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM 
C                       ACCESS (INPUT-OUTPUT).
C               ID(J) = THE VARIABLE ID (J=1,4) (INPUT). 
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID( ) (J=1,15)
C                       (INPUT).
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 
C                            LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED
C                       (INPUT).
C            SDATA(K) = DATA TO RETURN (K=1,ND1) (OUTPUT).
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT
C                       WITH.  DIMENSION OF SDATA (INPUT).
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING
C                       DEALT WITH (INPUT).
C            IPACK(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL).
C            IWORK(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL).
C       FD1(J),FD2(J) = WORK ARRAYS (J=1,ND2X3) 
C                       FD1 CONTAINS MOS PROBABILITY OF FREEZING OR
C                           FROZEN.
C                       FD2 CONTAINS MOS PROBABILITY OF FROZEN OR
C                           LIQUID. (INTERNAL)
C               ND2X3 = DIMENSION OF FD1( ) AND FD2( ) (INPUT).
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS), (INPUT-OUTPUT).
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR 
C                              NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN 
C                              RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE 
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE 
C                              SORTED LIST IN ID( ,N) (N=1,NPRED)
C                              FOR WHICH THIS VARIABLE IS NEEDED, WHEN 
C                              IT IS NEEDED ONLY ONCE FROM 
C                              LSTORE( , ).  WHEN IT IS NEEDED MORE
C                              THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING 
C                              MOSTORE( , ). LATER USED AS A WAY OF 
C                              DETERMINING WHETHER TO KEEP THIS 
C                              VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ), (INPUT). 
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , )
C                       THAT HAVE BEEN USED IN THIS RUN (INPUT).  
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA 
C                       IDENTIFIED IN LSTORE( , ) (J=1,ND10). WHEN
C                       CORE( ) IS FULL, DATA ARE STORED ON DISK
C                       (OUTPUT).
C                ND10 = DIMENSION OF CORE( ), (INPUT).
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE (INPUT).  
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING  COUNT FROM THE BEGINNING OF 
C                       THE PROGRAM.  THIS COUNT IS MAINTAINED IN 
C                       CASE THE USER NEEDS IT (DIAGNOSTICS, ETC.),
C                       (INTERNAL).  
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3)
C                       (INTERNAL).
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+)  
C                       (INTERNAL).
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12)
C                       (INTERNAL).
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4)
C                       (INTERNAL).
C                 ND7 = DIMENSION OF IS0, IS1, IS2, AND IS4. NOT ALL
C                       LOCATIONS ARE USED (INPUT).
C               ISTAV = 1 SINCE THE DATA RETURNED ARE STATION DATA
C                       (OUTPUT).
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64) (INPUT).
C                 IER = STATUS RETURN.
C                        0 = GOOD RETURN.
C                       52 = NWORDS DOES NOT EQUAL NSTA
C                       SEE GFETCH FOR VALUES. (INTERNAL-OUTPUT)
C
C         ADDITIONAL VARIABLES
C              IENTER = NUMBER OF TIMES SUBROUTINE IS ENTERED
C                       (INTERNAL).
C               MD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       FD1( ) AND FD2( ) (J=1,4) (INTERNAL).
C               MISSP = PRIMARY MISSING VALUE INDICATOR. RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED (INTERNAL). 
C               MISSS = SECONDARY MISSING VALUE INDICATOR. RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED (INTERNAL). 
C               NPACK = 2 FOR TDL GRIB PACKED DATA: 1 FOR NOT PACKED.
C                       THIS IS STORED IN LSTORE(7, ) (INTERNAL).
C               NSLAB = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ) FOR THE FIRST FIELD.  THIS IS THE
C                       VALUE OF NSLAB RETURNED.  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED (INTERNAL).  
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE, THAT
C                       THE RECORD HAS BEEN FETCHED. THIS IS STORED 
C                       IN LSTORE(9, ) (INTERNAL).
C              NWORDS = NUMBER OF WORDS RETURNED IN FD1 OR FD2( ), 
C                       (INTERNAL).
C
C        NONSYSTEM SUBROUTINES CALLED 
C            GFETCH,OBSPOPO,PRSID1
C
C
      PARAMETER(NDIM=4)
C    
      DIMENSION FD1(ND2X3),FD2(ND2X3),SDATA(ND1) 
      DIMENSION ID(4),IDPARS(15),MDPARS(15),JD(4),MD(4)
      DIMENSION IPACK(ND2X3),IWORK(ND2X3)
      DIMENSION LSTORE(12,ND9)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION CORE(ND10)
C
      DIMENSION ITABLE(4,NDIM)
      DATA IENTER/0/
      SAVE IENTER
C
      DATA ITABLE/208547,2085453,350001000,450001000,
     1            208548,2085453,450001000,999905000,
     2            208543,708504,2085453,350001000,
     3            208542,708504,2085453,450001000/
C
      IER=0
      ISTAV=1
C
      IENTER=IENTER+1
C
C        INITIALIZING SDATA(J) AS MISSING
C
      DO 100 J=1,ND1
         SDATA(J)=9999.
 100  CONTINUE
C
C        FIND CCCFFF OF ID(1) IN ITABLE(1, ).  IF THE VALUE ENDS WITH 
C        A DIGIT GREATER THAN 5 (208547 OR 208548), WE WILL BE TAKING 
C        THE PRODUCT OF TWO MOS PROBABILITIES.  IF THE CCCFFF VALUE 
C        ENDS WITH A DIGIT LESS THAN 5 (208542 OR 208543), WE ARE 
C        TAKING THE PRODUCT OF A MOS PROBABILITY AND THE OBSERVED 
C        PRECIPITATION OCCURRENCE.  GO TO THE APPROPRIATE PLACE
C        IN THE ROUTINE FOR ID CONSTRUCTION.
C
      DO 105 JJ=1,NDIM
         IF((ITABLE(1,JJ).EQ.IDPARS(1)*1000+IDPARS(2)).AND.
     1      (MOD(ITABLE(1,JJ),10).GE.5)) THEN 
            GOTO 120
         ELSEIF((ITABLE(1,JJ).EQ.IDPARS(1)*1000+IDPARS(2)).AND.
     1          (MOD(ITABLE(1,JJ),10).LT.5)) THEN
            GOTO 400
         ENDIF
 105  CONTINUE
C
      WRITE(KFILDO,107)(ID(L),L=1,4)
 107  FORMAT(/,' ****PTPMOSPRD ENTERED FOR VARIABLE',
     1         2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,
     2       ' NOT ACCOMMODATED.')
      IER=102
      GO TO 800
C
C        CONSTRUCT THE MOS-2000 ID FOR THE PRODUCT OF THE PROBABILITIES
C        FOR EITHER FREEZING PRECIP AND FROZEN PRECIP OR FROZEN PRECIP AND
C        LIQUID PRECIP.
C
 120  MD(1)=ITABLE(2,JJ)*100+IDPARS(4)
      MD(2)=0
      MD(3)=ID(3)
      MD(4)=ITABLE(3,JJ)
C
C       FETCH THE FIRST MOS PTYPE PROBABILITY VALUE (FREEZING OR FROZEN)
C       DATA IS RETURNED TO FD1.
C
      CALL GFETCH(KFILDO,KFIL10,MD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,
     4            IER)
C
      IF((IER.NE.0).AND.(IENTER.GE.1)) THEN
         IF (IENTER.EQ.1) THEN
            WRITE(KFILDO,205) IER,(MD(J),J=1,4)
 205        FORMAT(/,' ****ERROR IN PTPMOSPRD, FOR FIRST PROCESS DATE,',
     1               ' THE FIRST MOS PTYPE PROBABILITY IS MISSING',
     2             /,' POSSIBLE DATA GAP, **** ALL OUTPUT VALUES',
     3               ' ARE SET TO MISSING ****, IER =',I5,
     4               ' FOR VARIABLE',4I12)
         ENDIF
         GO TO 800
      ENDIF
C
      IF(NWORDS.NE.NSTA) THEN
         WRITE(KFILDO,210)NWORDS,NSTA
 210     FORMAT(/,' ****NWORDS =',I6,' RETURNED FROM GFETCH',
     1            ' NOT EQUAL TO NSTA =',I6,' FOR FIRST MOS PTYPE',
     2            ' PROBABILITY VALUE IN PTPMOSPRD. DATA SET TO',
     3            ' MISSING.')
         IER=52
         GO TO 800
      ENDIF
C
      MD(4)=ITABLE(4,JJ)
C
C       FETCH THE SECOND MOS PTYPE PROBABILITY VALUE (FROZEN OR LIQUID)
C       DATA IS RETURNED TO FD2.
C
      CALL GFETCH(KFILDO,KFIL10,MD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD2,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,
     4            IER)
C
      IF((IER.NE.0).AND.(IENTER.GE.1)) THEN
         IF (IENTER.EQ.1) THEN
            WRITE(KFILDO,305) IER,(MD(J),J=1,4)
 305        FORMAT(/,' ****ERROR IN PTPMOSPRD, FOR FIRST PROCESS DATE,',
     1               ' SECOND MOS PTYPE PROBABILITY IS MISSING',
     2             /,' POSSIBLE DATA GAP, **** ALL OUTPUT VALUES',
     3               ' ARE SET TO MISSING ****, IER =',I5,
     4               ' FOR VARIABLE',4I12)
         ENDIF
         GO TO 800
      ENDIF
C
      IF(NWORDS.NE.NSTA) THEN
         WRITE(KFILDO,310)NWORDS,NSTA
 310     FORMAT(/,' ****NWORDS =',I6,' RETURNED FROM GFETCH',
     1            ' NOT EQUAL TO NSTA =',I6,' FOR FIRST MOS PTYPE',
     2            ' PROBABILITY IN PTPMOSPRD. DATA SET TO MISSING.')
         IER=52
         GO TO 800
      ENDIF
C
C        TAKE PRODUCT OF MOS PTYPE PROBABILITIES FOR FREEZING AND FROZEN
C        OR FROZEN AND LIQUID.  STORE TO SDATA.
C
      DO 350 J=1,NSTA
         IF(NINT(FD1(J)).EQ.9997) FD1(J)=0.
         IF(NINT(FD2(J)).EQ.9997) FD2(J)=0.
         IF((NINT(FD1(J)).NE.9999).AND.(NINT(FD2(J)).NE.9999))THEN
            SDATA(J)=FD1(J)*FD2(J)
         ENDIF
 350  CONTINUE
C
      GO TO 850 
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        CONSTRUCT THE MOS-2000 ID FOR THE PRODUCT OF EITHER THE 
C        PROBABILITY OF FREEZING PRECIP AND THE BINARY OF OBSERVED 
C        PRECIPITATION OR THE PROBABILITY OF FROZEN PRECIP AND THE 
C        BINARY OF OBSERVED PRECIPITATION; THIS WILL CAUSE THE 
C        SUBROUTINE TO ONLY RETURN VALUES CONDITIONAL ON THE OCCURENCE 
C        OF PRECIPITATION.
C
C        CONSTRUCT AND FETCH THE MOS-2000 ID FOR OBSERVED PRECIP 
C        OCCURENCE.  THE DATA IS RETURNED IN SDATA( ).
C
 400  MD(1)=ITABLE(2,JJ)*1000
      MD(2)=0
      MD(3)=0
      MD(4)=0
      CALL PRSID1(KFILDO,MD,MDPARS)
      CALL OBSPOPO(KFILDO,KFIL10,MDPARS,MD,NDATE,
     1             SDATA,ND1,NSTA,IPACK,IWORK,FD1,ND2X3,
     2             LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3             IS0,IS1,IS2,IS4,ND7,
     4             ISTAV,L3264B,IER)
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,402)(MD(J),J=1,4)
  402    FORMAT(/,' ****VARIABLE NOT RETRIEVED BY PTPMOSPRD. ',
     1          2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3)
         GO TO 800
      ENDIF
C
C        CONSTRUCT THE MOS-2000 ID FOR PROBABILITY OF FREEZING OR
C        FROZEN PRECIPITATION. THE DATA IS RETURNED IN FD1( ).
C
      MD(1)=ITABLE(3,JJ)*100+IDPARS(4)
      MD(2)=0
      MD(3)=ID(3)
      MD(4)=ITABLE(4,JJ)
      CALL GFETCH(KFILDO,KFIL10,MD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,
     4            IER)
C
      IF((IER.NE.0).AND.(IENTER.GE.1)) THEN
         IF (IENTER.EQ.1) THEN
            WRITE(KFILDO,405) IER,(MD(J),J=1,4)
 405        FORMAT(/,' ****ERROR IN PTPMOSPRD, FOR FIRST PROCESS DATE,',
     1               ' MOS PTYPE PROBABILITY IS MISSING',
     2             /,' POSSIBLE DATA GAP, **** ALL OUTPUT VALUES',
     3               ' ARE SET TO MISSING ****, IER =',I5,
     4               ' FOR VARIABLE',4I12)
         ENDIF
         GO TO 800
      ENDIF
C
      IF(NWORDS.NE.NSTA) THEN
         WRITE(KFILDO,410)NWORDS,NSTA
 410     FORMAT(/,' ****NWORDS =',I6,' RETURNED FROM GFETCH',
     1            ' NOT EQUAL TO NSTA =',I6,' FOR FIRST MOS PTYPE',
     2            ' PROBABILITY IN PTPMOSPRD. DATA SET TO MISSING.')
         IER=52
         GO TO 800
      ENDIF
C
C        TAKE PRODUCT OF THE MOS PTYPE PROBABILITY OF FREEZING OR
C        FROZEN PRECIP AND OBS POPO.  STORE TO SDATA
C
      DO 550 J=1,NSTA
         IF(NINT(SDATA(J)).EQ.9997) SDATA(J)=0.
         IF(NINT(FD1(J)).EQ.9997) FD1(J)=0.
         IF((NINT(SDATA(J)).NE.9999).AND.(NINT(FD1(J)).NE.9999))THEN
            SDATA(J)=SDATA(J)*FD1(J)
         ELSE
            SDATA(J)=0.
         ENDIF
 550  CONTINUE
      GOTO 850 
C
 800  DO 810 J=1,NSTA
         SDATA(J)=9999.
 810  CONTINUE
C
 850  RETURN
      END     
