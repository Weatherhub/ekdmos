      SUBROUTINE EXPVAL(KFILDO,KFIL10,IP12,KFILRA,RACESS,NUMRA,
     1                  ID,IDPARS,JD,ITAU,
     2                  NDATE,MDATE,CCALL,ISDATA,XDATA,ND1,NSTA,
     3                  ICALLD,CCALLD,IPACK,IWORK,DATA,ND5,
     4                  LSTORE,ND9,LITEMS,CORE,ND10,
     5                  NBLOCK,NFETCH,
     6                  IS0,IS1,IS2,IS4,ND7,
     7                  L3264B,L3264W,IER)
C 
C        JULY      1999   RUDACK   TDL   MOS-2000
C        AUGUST    1999   RUDACK   CHANGED CALL TO GFETCH TO RETVEC
C        SEPTEMBER 1999   GLAHN    ADDED YDATA( )
C        NOVEMBER  2002   HUGHES   ADDED COMMAS TO FORMAT STATEMENTS
C                                  REMOVED A WRITE STATEMENT USED FOR
C                                  DEGBUGGING AND DELETED EXTRA BLANK 
C                                  LINES BETWEEN CODE.
C                                  THERE HAVE BEEN MANY CHANGES, POSSIBLY BY
C                                  RUDACK, (DIFFERENT IDS, NEW TABLES), SO
C                                  THAT IT NO LONGER PROCESSES THE SAME 
C                                  VARIABLES AS THE VERSION THAT WAS ON THE IBM.
C                                  THE ORIGINAL VERSION HAS BEEN KEPT ON THE 
C                                  IBM IN MOSLIB AS EXVALU.F.OLD UNTIL
C                                  SOMEONE WISER THAN ME DECIDES IT IS NO
C                                  LONGER NEEDED.
C        JAUNARY   2007   MALONEY  MODIFIED TO USE IDS 203260  
C                                  AND 203360 PER PLAN FOR 
C                                  PRECIPITATION AMOUNT.  LEFT THE 
C                                  203240/340/440 ALONE SO WEIBULL
C                                  WILL BE UNAFFECTED.
C        JANUARY   2007   ANTOLIK  NAME CHANGED FROM EXVALU AND CODE    
C                                  SUBSTANTIALLY REWRITTEN TO MAKE THIS
C                                  WORK FOR MORE GENERAL APPLICATIONS.
C                                  CUMULATIVE AND DISCRETE PROBABILITIES 
C                                  ARE NOW ACCOMODATED; LOGIC TO DO THE 
C                                  ACTUAL EXPECTED VALUE CALCULATIONS HAS 
C                                  BEEN ISOLATED FROM THE REST OF THE CODE
C                                  AND ASSUMES THAT THE PROBABILITIES 
C                                  HAVE BEEN CHECKED FOR CONSISTENCY AND/OR
C                                  MONOTONICITY. "BACKUP" PROBABILITIES  
C                                  ARE SUPPORTED. IDS 203240/340/440 HAVE 
C                                  BEEN REMOVED SINCE THESE ARE NOW OBSOLETE.
C                                  IDS FOR CLOUD AMOUNT EXPECTED VALUES FROM
C                                  DISCRETE PROBABILITIES (208385) ADDED.
C        FEBRUARY  2007   MALONEY  ADDED LOOP TO CHECK ALL DATA FOR MISSINGS
C                                  PRIOR TO EXPECTED VALUE CALCULATIONS.
C
C        PURPOSE:
C            CALCULATES THE EXPECTED VALUES FROM A SET OF INPUT PROBABILITES.
C            INPUT PROBABILITIES MAY BE CUMULATIVE OR DISCRETE, AND ARE     
C            ASSUMED TO COMPLETELY EXHAUST THE EVENT.
C            IT ALSO IS ASSUMED THAT PROBABILIBIES HAVE ALREADY
C            BEEN MESSAGED TO ENSURE MONOTONIC BEHAVIOR; CONSISTENCY-
C            CHECKED PROBABILITIES, IF AVAILABLE, ARE ACCOMODATED.
C
C            IDPARS(1) & IDPARS(2) OF PROBABILITIES  ARE MAPPED
C                          INTO       FROM
C                          203260    203220
C                          203360    203330/203320
C                          208385    208384
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C            IP12   - INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                     STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                     THE FILE WHOSE UNIT NUMBER IS IP12.  (OUTPUT)
C         KFILRA(J) - THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                     ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C                IP12 = INDICATES WHETHER (>0) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE EXTERNAL RANDOM ACCESS FILES
C                       WILL BE LISTED TO UNIT IP12.  (INPUT)
C           KFILRA(J) = THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                       ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES ASSOCIATED WITH KFILRA(J) (J=1,NUMRA).
C                       (CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF VALUES IN KFILRA( ) AND RACESS( ).
C                       (INPUT)
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN 
C                            TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE
C                       PORTIONS PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       JD( ) IS USED TO IDENTIFY THE BASIC MODEL FIELDS
C                       AS READ FROM THE ARCHIVE.  (INPUT)
C                ITAU = THE NUMBER OF HOURS AHEAD TO FIND A VARIABLE.
C                       THIS HAS ALREADY BEEN CONSIDERED IN MDATE, BUT
C                       IS NEEDED FOR CALL TO RETVEC.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.
C                       (INPUT)
C               MDATE = NDATE UPDATED WITH ITAU( ).  (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ).  EQUIVALENCED TO ICALL( , , ). 
C                       (CHARACTER*8)  (INPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C            XDATA(K) = RETURNED EXPECTED VALUE (K=1,NSTA).  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT 
C                       WITH.  DIMENSION OF XDATA( ).  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT
C                       WITH.  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).  THIS ARRAY IS USED 
C                       TO READ THE STATION DIRECTORY FROM A MOS-2000
C                       EXTERNAL FILE.  EQUIVALENCED TO CCALLD( ). 
C                       (CHARACTER*8)  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  EQUIVALENCED
C                       TO ICALLD( , ).  (INTERNAL)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(K) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ).
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE 
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH 
C                              THIS VARIABLE IS NEEDED, WHEN IT IS 
C                              NEEDED ONLY ONCE FROM LSTORE( , ). 
C                              WHEN IT IS NEEDED MORE THAN ONCE, THE 
C                              VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING 
C                              MOSTORE( , ).  LATER USED AS A WAY OF
C                              DETERMINING WHETHER TO KEEP THIS 
C                              VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , )
C                       THAT HAVE BEEN USED IN THIS RUN.  (INPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETIREVE THE DATA 
C                       IDENTIFIED IN LSTORE( , ) (J=1,ND10).  WHEN 
C                       CORE( ) IS FULL DATA ARE STORED ON DISK.
C                       (INPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.
C                       GFETCH KEEPS TRACK OF THIS AND RETURNS THE
C                       VALUE.  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3). 
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+). 
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12). 
C                       (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4). 
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).  
C                       CALCULATED BY PARAMETER, BASED ON L3464B.
C                       (INPUT)
C                 IER = STATUS RETURN.
C                          0 = GOOD RETURN.
C                        102 = ID NOT ACCOMMODATED.
C                       SEE RETVEC FOR OTHER VALUES.  (OUTPUT)
C               LD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       XDATA( ) (J=1,4).  (INTERNAL)
C           LDPARS(J) = PARSED VALUES CORRESPONDING TO LD( ) (J=1,15)
C                       (INTERNAL)
C          YDATA(K,J) = WORK ARRAY WHICH STORES THE PROBABILITIES FROM 
C                       RETVEC. (K=1,ND1,J=1,NCAT) (AUTOMATIC)
C         ITABLE(I,J) = VALUES OF CCCFFFB ACCOMMODATED. (INTERNAL) 
C                       I=1--OUTPUT VARIABLE.
C                       I=2--COMPONENT PROBABILITIES USED TO CALCULATE
C                            EXPECTED VALUE.
C                       I=3--"BACKUP" PROBABILITIES, IF AVAILABLE.
C                       (J=1,NDIM)
C         JTABLE(I,J) = VALUES OF NCAT AND THRESH FOR EACH COMPONENT 
C                       PROBABILITY CORRESPONDING TO I=(2,3) AND 
C                       J=JJ OF ITABLE. (INTERNAL)
C         XTABLE(I,J) = MIDPOINT VALUES FOR PROBABILITY CATEGORIES.
C                       (I=1,NCAT; J=1,NDIM)  (INTERNAL)
C                NCAT = NUMBER OF FORECAST PROBABILITY CATEGORIES. SET IN 
C                       JTABLE(1).                        
C                NDIM = SECOND DIMENSION OF ITABLE(I,J).  EQUAL TO TOTAL
C                       NUMBER OF DIFFERENT VARIABLE IDS HANDLED BY THE
C                       PROGRAM.  SET BY PARAMETER.  (INTERNAL)
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            RETVEC, PRSID1
C
      PARAMETER(NDIM=3)
      PARAMETER(MAXCAT=8)
C
      CHARACTER*8 CCALL(ND1,6)
      CHARACTER*8 CCALLD(ND5)
      CHARACTER*60 RACESS(5)
C
      DIMENSION XDATA(ND1),ISDATA(ND1),YDATA(ND1,MAXCAT)
C        YDATA( ) IS AN AUTOMATIC ARRAY.
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10),ICALLD(L3264W,ND5)
      DIMENSION LD(4),LDPARS(15),KFILRA(5)
      DIMENSION ITABLE(3,NDIM),JTABLE(MAXCAT+1,NDIM)
      DIMENSION XTABLE(MAXCAT,NDIM)
C
C        ITABLE CONTAINS CCCFFB OF WORD1
C        FOR ITABLE, THE COLUMNS CONTROL THE WORD1 MAPPING:
C                  INTO    FROM   'BACKUP'                 
C
      DATA ITABLE/2032600,2032201,9999999,
     1            2033600,2033301,2033201,
     2            2083850,2083843,9999999/
C     
C        JTABLE CONTAINS NCAT AND JD(4) VALUES FOR THE NECESSARY
C        COMPONENT PROBABILITIES CORRESPONDING TO WORD1 OF THE
C        MOS2000 ID GIVEN BY ITABLE(2, ). THE COLUMNS ARE:
C                             NCAT   JD(4,J=1,NCAT)           
C  
      DATA (JTABLE(I,1),I=1,6)/5,950052000,950051000,245000000,
     1                          495000000,995000000/
      DATA (JTABLE(I,2),I=1,7)/6,950052000,950051000,245000000,
     1                          495000000,995000000,199501000/
      DATA (JTABLE(I,3),I=1,6)/5,150001000,250001000,400001000,
     1                          700001000,999905000/
C     
C        XTABLE CONTAINS NCAT MIDPOINT VALUES FOR EACH VARIABLE
C        GIVEN IN ITABLE(2, ).  THESE ARE USED AS SURROGATES FOR 
C        THE MEAN VALUES IN EACH CATEGORY. MEAN VALUE FOR UPPERMOST
C        CATEGORY HAS BEEN ASSIGNED TO SOMETHING "REASONABLE".
C        IN REALITY, THESE COULD DIFFER FOR EACH STATION AND 
C        SHOULD BE CONSTANTS CALCULATED FROM THE DEPENDENT
C        SAMPLE OR OTHER LONG-TERM CLIMATOLOGY.
C
      DATA XTABLE/0.050,0.175,0.375,0.750,1.25,0.,0.,0.,
     1            0.050,0.175,0.375,0.750,1.50,2.1,0.,0.,
     2            0.025,0.15,0.375,0.685,0.935,0.,0.,0./
C
C                 0.050,0.175,0.375,0.750,1.50,3.0,0.,0./
C        THESE WERE THE "OLD" VALUES FOR 24-H QPF, 3" MAY BE TOO HIGH  
C
      IER=0
C      
C        VERIFY PROCESSING.  FIND THE ID(1) IN ITABLE(1, ).
C
      DO 105 JJ=1,NDIM      
         IF(ITABLE(1,JJ).EQ.(IDPARS(1)*10000+IDPARS(2)*10+IDPARS(3)))
     1    GOTO 108
 105  CONTINUE
C     
      WRITE(KFILDO,106)(JD(L),L=1,4)
 106  FORMAT(' ****EXPVAL ENTERED FOR VARIABLE',
     1        2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,
     2        ' NOT ACCOMMODATED.')
      IER=102
      GO TO 900
C
C        SET XDATA( ) AND YDATA( ) TO MISSING PRIOR TO EVALUATION. 
C 
 108  DO 111 K=1,ND1
         DO 110 J=1,MAXCAT
            YDATA(K,J)=9999.
 110     CONTINUE
         XDATA(K)=9999.
 111  CONTINUE
C
C        FLOW CONTROL IS NOW DONE BY JJ, THE LOOP INDEX OF THE 
C        OUTPUT VARIABLE IN ITABLE.  ONCE THIS IS KNOWN, ALL PERTINENT
C        PROCESSING INFORMATION IS IN THE CORRESPONDING LINES OF JTABLE
C        AND XTABLE.  THIS ELIMINATES THE NEED FOR A SEPARATE BRANCH FOR
C        EACH VARIABLE AS IN THE ORIGINAL EXVALU.
C
C        GET THE NCAT PROBABILITIES FOR THE PRIMARY COMPONENT.
C     
 120  NCAT=JTABLE(1,JJ)
      LD(1)=ITABLE(2,JJ)*100+IDPARS(4)
      LD(2)=JD(2)
      LD(3)=JD(3)
C
      DO 165 J=1,NCAT
         LD(4)=JTABLE(J+1,JJ)
C
         CALL PRSID1(KFILDO,LD,LDPARS)
         IF((LDPARS(3).LT.1).OR.(LDPARS(3).GT.3))THEN
           WRITE(KFILDO,130)LDPARS(3)
 130       FORMAT(/,'BINARY INDICATOR B= ',I1,' OUT OF RANGE') 
           GOTO 160
         ENDIF
         CALL RETVEC(KFILDO,KFIL10,IP12,KFILRA,RACESS,NUMRA,
     1               LD,LDPARS,JD,ITAU,
     2               NDATE,MDATE,CCALL,ISDATA,YDATA(1,J),ND1,NSTA,
     3               ICALLD,CCALLD,IPACK,IWORK,DATA,ND5,
     4               LSTORE,ND9,LITEMS,CORE,ND10,
     5               NBLOCK,NFETCH,
     6               IS0,IS1,IS2,IS4,ND7,
     7               L3264B,L3264W,IER)
C
C          JD( ) IS NOT ACTUALLY USED IN RETVEC.  IT IS USED
C          IN CALL TO CONST, BUT CONST DOES NOT USE IT EITHER.
C
         IF(IER.NE.0)THEN
C
C        THE FETCHING FOR THE PRIMARY SET OF PROBABILITIES HAS
C        FAILED.  TRY TO GET BACKUP PROBABILITIES (USUALLY MONOTONIC),
C        IF THESE EXIST.
C
           IF(ITABLE(3,JJ).GT.9999998)GOTO 160
C
C        SINCE THE FORECAST IN ITABLE(2,JJ) WAS MISSING, TRY FOR THE 
C        'BACKUP' FORECAST LISTED IN ITABLE(3,JJ).
C        NOTE: THE LOGIC HERE FOLLOWS THAT OF CMPPRB, I.E. CHECKING
C        IS DONE ONLY FOR THE FIRST OF THE NCAT PROBABILITIES; ONCE
C        YOU HAVE MADE THIS SWITCH TO THE BACKUP FORECAST FOR THE
C        FIRST CATEGORY OF AN ID, ALL THE OTHER CATEGORIES WILL
C        JUST AUTOMATICALLY CHECK FOR THE 'BACKUP' FORECAST
C
C
           WRITE(KFILDO,135)NCAT-1
 135       FORMAT(' PRIMARY PROBABILITIES NOT AVAILABLE; CHECKING ',
     1            'FOR BACKUP (MONOTONIC) FORECAST.'/' IF FOUND, ',
     2            'THE ',I2,' OTHER PROBABILITIES FOR THIS CCCFFF ',
     3            'AND PROJ. WILL ALSO'/' USE THE BACKUP FORECASTS.')
C
           LD(1)=ITABLE(3,JJ)*100+IDPARS(4)
C
C        TRY TO FETCH THE 'BACKUP' PROBABILITIES.
C
           CALL PRSID1(KFILDO,LD,LDPARS)
         IF((LDPARS(3).LT.1).OR.(LDPARS(3).GT.3))THEN
           WRITE(KFILDO,140)LDPARS(3)
 140       FORMAT(/,'BINARY INDICATOR B= ',I1,' OUT OF RANGE') 
           GOTO 160
         ENDIF
           CALL RETVEC(KFILDO,KFIL10,IP12,KFILRA,RACESS,NUMRA,
     1                 LD,LDPARS,JD,ITAU,
     2                 NDATE,MDATE,CCALL,ISDATA,YDATA(1,J),ND1,NSTA,
     3                 ICALLD,CCALLD,IPACK,IWORK,DATA,ND5,
     4                 LSTORE,ND9,LITEMS,CORE,ND10,
     5                 NBLOCK,NFETCH,
     6                 IS0,IS1,IS2,IS4,ND7,
     7                 L3264B,L3264W,IER)
C
           IF(IER.NE.0)GOTO 160
         ENDIF
         GOTO 165
C
C       NEITHER SET OF PROBABILITIES RETRIEVED.
C       WRITE ERROR MESSAGE AND EXIT.
C
 160     WRITE(KFILDO,161)(LD(L),L=1,4)
 161     FORMAT(/,' ****VARIABLE NOT RETRIEVED BY RETVEC IN ',
     1          'EXPVAL',2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3)
         GOTO 900
C      
 165  CONTINUE
C 
C        200 SECTION IDENTIFIES XMEAN FOR THE GIVEN VARIABLE AND
C        DETERMINES THE EXPECTED VALUE FOR EACH STATION.
C 
      DO 280 K=1,NSTA         
         SUM=0.0
         DO 270 J=1,NCAT   
C
C        FIRST, CHECK ALL PROBABILITIES FOR MISSINGS.  
C        IF ANY PROBABILITY IS MISSING FOR A STATION, THEN ALL 
C        SHOULD BE, AND THE RETURNED EXPECTED VALUE MUST BE RESET   
C        TO MISSING.
C
           IF(NINT(YDATA(K,J)).EQ.9997) YDATA(K,J)=0.0
C
           IF(NINT(YDATA(K,J)).EQ.9999) THEN      
             XDATA(K)=9999.
             GOTO 280
           ENDIF           
 270     CONTINUE
C
C           HAVING CHECKED ALL PROBABILITIES FOR MISSINGS,
C           NOW DO THE EXPECTED VALUE CALCULATIONS.
C
         DO 275 J=1,NCAT   
C
C        LOAD XMEAN.  USE XTABLE(J,JJ).  THEORETICALLY, THESE
C        VALUES SHOULD HAVE UNIQUE VALUES FOR EACH STATION AND
C        SEASON.  BUT, THAT'S AN ENHANCEMENT FOR THE FUTURE.
C
           XMEAN=XTABLE(J,JJ)
C
C        CONVERT PROBABILITIES TO DISCRETE, UNLESS LDPARS(3)=3
C
           IF(LDPARS(3).EQ.1) THEN
             IF(J.LT.NCAT) THEN
                DPROB=(YDATA(K,J)-YDATA(K,J+1))
             ELSE
                DPROB=YDATA(K,J)
             ENDIF
           ELSEIF(LDPARS(3).EQ.2) THEN
             IF(J.EQ.1) THEN
                DPROB=YDATA(K,J)
             ELSE
                DPROB=(YDATA(K,J)-YDATA(K,J-1))
             ENDIF
           ELSE
             DPROB=YDATA(K,J)
           ENDIF
C               
            SUM=SUM+DPROB*XMEAN
C
 275     CONTINUE
C
         XDATA(K)=SUM
C
 280  CONTINUE 
      GOTO 999
C     
C        THIS PREDICTOR CANNOT BE COMPUTED.  SET XDATA TO MISSING.
C     
 900  DO 910 K=1,ND1  
         XDATA(K)=9999.
 910  CONTINUE
C
 999  RETURN
      END
