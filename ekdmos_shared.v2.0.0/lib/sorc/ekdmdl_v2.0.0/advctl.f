      SUBROUTINE ADVCTL(KFILDO,KFIL10,IDPARS,ICCCFFF,NDATE,
     1                  NGRIDC,ND11,NSLAB,IPACK,IWORK,FDAV,ND5,
     2                  LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3                  IS0,IS1,IS2,IS4,ND7,
     4                  FD1,FD2,FD3,FD4,FD5,FD6,FDSINS,FDMS,ND2X3,
     5                  ISTAV,L3264B,MISTOT,IER)
C
C        MAY      2004   GLAHN      MDL MOS-2000 
C                                       ADAPTED FROM ADVCTW
C        JANUARY  2005   WIEDENFELD MDL MODIFIED CALL TO GWIND FOR 
C                                       V COMPONENT TO USE FD1
C                                       INSTEAD OF TWO FD3'S.
C        JUNE     2006   WIEDENFLED MDL CLEANED UP CODE AND DOC BLOCK.
C                                       ADDED CALL TO BASICP TO CREATE
C                                       JD ARRAY.
C
C        PURPOSE
C            TO COMPUTE ADVECTION OF A QUANTITY, Q, FROM GEOSTROPHIC
C            U- AND V- WIND COMPONENTS ON AN ISOBARIC SURFACE.  UNITS
C            ARE Q/SEC.  SINCE THE ACTUAL GRID DIMENSIONS
C            LX AND LY ARE NOT KNOWN UNTIL THE DATA ARE FETCHED, THE
C            SPATIAL GRID COMPUTATIONS ARE MADE IN SUBROUTINES MAPLAT
C            ADVCTW1.
C
C        DATA SET USE 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS
C                     (INPUT-OUTPUT) 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE
C                     (OUTPUT)
C 
C        VARIABLES 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS
C                       (INPUT)
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE
C                       (INPUT)
C           IDPARS(J) = PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID(J) (J=1,15) DEFINED IN
C                       THE CALLING PROGRAM (INPUT)
C                       J=1 -- CCC (CLASS OF VARIABLE)
C                       J=2 -- FFF (SUBCLASS OF VARIABLE)
C                       J=3 -- B (BINARY INDICATOR)
C                       J=4 -- DD (DATA SOURCE, MODEL NUMBER)
C                       J=5 -- V (VERTICAL APPLICATION)
C                       J=6 -- LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 
C                              LAYER)
C                       J=7 -- LTLTLTLT (TOP OF LAYER)
C                       J=8 -- T (TRANSFORMATION)
C                       J=9 -- RR (RUN TIME OFFSET -- PREVIOUS CYCLE.
C                              IT IS ALWAYS A POSITIVE NUMBER AND
C                              COUNTED BACKWARDS IN TIME)
C                       J=10 -- OT (TIME APPLICATION)
C                       J=11 -- OH (TIME PERIOD IN HOURS)
C                       J=12 -- TAU (PROJECTION IN HOURS)
C                       J=13 -- I (INTERPOLATION TYPE)
C                       J=14 -- S (SMOOTHING INDICATOR)
C                       J=15 -- G (GRID INDICATOR)
C             ICCCFFF = CONTAINS IDPARS(1) AND IDPARS(2) ID FOR THE U
C                       WIND COMPONENT BEING COMPUTED IN UNITS OF M/S.
C                       THE OTHER INFORMATION ABOUT THE WINDS IS IN
C                       IDPARS( ).  (INPUT)
C               NDATE = DATE/TIME FOR WHICH THE PREDICTOR IS NEEDED
C                       (INPUT)
C         NGRIDC(L,M) = HOLDING THE GRID CHARACTERISTICS (L=1,6) FOR
C                       EACH GRID COMBINATION (M=1,NGRID) (INPUT-OUTPUT)
C                       L=1 -- MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                              STEREOGRAPHIC)
C                       L=2 -- GRID LENGTH IN METERS
C                       L=3 -- LATITUDE AT WHICH GRID LENGTH IS CORRECT
C                              *1000
C                       L=4 -- GRID ORIENTATION IN DEGREES *1000
C                       L=5 -- LATITUDE OF LL CORNER IN DEGREES *1000
C                       L=6 -- LONGITUDE OF LL CORNER IN DEGREES *1000
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH IN THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ) (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , , ) AND IN
C                       NGRIDC( , ) DEFINING THE CHARACTERISTICS OF THE
C                       GRID.  IT PERTAINS TO THE FIELD IN FDAV. (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5) (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5) (INTERNAL)
C             FDAV(J) = DATA ARRAY TO HOLD VARIABLE Q TO BE ADVECTED
C                       (INPUT) AND THE ADVECTED VARIABLE (OUTPUT)
C                       (Q/SEC) (J=1,ND5).  (INPUT/OUTPUT)
C                 ND5 = DIMENSION OF IPACK( ),IWORK( ), AND FDAV( )
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS) (INPUT-OUTPUT)
C                       L=1,4-- THE 4 IDS FOR THE DATA
C                       L=5  -- LOCATION OF STORED DATA.  WHEN IN CORE,
C                               THIS IS THE LOCATION IN CORE( ) WHERE
C                               THE DATA START.  WHEN ON DISK, THIS IS
C                               MINUS THE RECORD NUMBER WHERE THE DATA
C                               START.
C                       L=6  -- THE NUMBER OF 4-BYTE WORDS STORED
C                       L=7  -- 2 FOR DATA PACKED IN TDL GRIB FORMAT,
C                               1 OTHERWISE
C                       L=8  -- DATE/TIME OF THE DATA IN FORMAT
C                               YYYYMMDDHH
C                       L=9  -- NUMBER OF TIMES DATA HAVE BEEN RETRIEVED
C                       L=10 -- NUMBER OF THE SLAB IN DIR( , ,L) AND IN
C                               NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                               OF THE GRID
C                       L=11 -- NUMBER OF PREDICTORS IN THE SORTED LIST
C                               IN IS( ,N) (N=1,NPRED) FOR WHICH THIS
C                               VARIABLE IS NEEDED ONLY ONCE FROM
C                               LSTORE( , ).  WHEN IT IS NEEDED MORE
C                               THAN ONCE, THE VALUE IS SET TO 7777.
C                       L=12 -- USED INITIALLY IN ESTABLISHING
C                               MSTORE( , ).  LATER USED TO DETERMINE
C                               WHETHER TO KEEP THIS VARIABLE
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ) (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , )
C                       THAT HAVE BEEN USED IN THIS RUN (INPUT)
C             CORE(J) = ARRAY TO STORE OR RETRIEVE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL,
C                       DATA ARE STORED ON DISK.  UPON RETURN TO THE
C                       CALLING PROGRAM, THE ARRAY WILL BE IN THE SIZE
C                       OF LX*LY (OUTPUT).
C                ND10 = DIMENSION OF CORE( ) (INPUT)
C              NBLOCK = BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM DISK
C                       FILE (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.  IT IS
C                       A RUNNING COUNT FROM THE BEGINNING OF THE MAIN
C                       PROGRAM.  THIS COUNT IS MAINTAINED IN CASE THE
C                       USER NEEDS IT FOR DIAGNOSTICS, ETC. (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 IDS (J=1,3) (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 IDS (J=1,22+) (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 IDS (J=1,12)
C                       IS2(3) AND IS2(4) ARE USED BY THE CALLING
C                       PROGRAM AS GRID DIMENSION (INTERNAL-OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 IDS (J=1,4) (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED. (INPUT)
C              FD1(J) = WORK ARRAY TO HOLD MAP FACTOR (J=1,ND2X3) (INTERNAL)
C              FD2(J) = WORK ARRAY TO HOLD U WIND (J=1,ND2X3) (INTERNAL)
C              FD3(J) = WORK ARRAY TO HOLD V WIND (J=1,ND2X3) (INTERNAL)
C              FD4(J) = WORK ARRAY TO HOLD THE SINE OF THE LATITUDE
C                       (J=1,ND2X3) (INTERNAL)
C              FD5(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL)
C              FD6(J) = WORK ARRAY (J=1,ND2X3) (INTERNAL)
C           FDSINS(J) = ARRAY TO SAVE THE SINE OF THE LATITUDE IN
C                       SUBROUTINES MAPLAT (J=1,ND2X3).  IT IS
C                       ONLY USED AS A PLACEHOLDER FOR SUBROUTINE CALLS.
C                       (INTERNAL)
C             FDMS(J) = ARRAY TO SAVE THE MAP FACTOR IN SUBROUTINE
C                       MAPLAT (J=1,ND2X3)  IT IS ONLY USED AS A
C                       PLACEHOLDER FOR SUBROUTINE CALLS. (INTERNAL)
C               ND2X3 = DIMENSION OF FD1( ), FD2( ), FD3( ), FD4( ),
C                       FDMS( ), AND FDSINS( ) (INPUT)
C               ISTAV = 0 -- WHEN THE DATA RETURNED ARE GRID DATA OR
C                            DATA ARE NOT AVAILABLE FOR RETURN
C                       1 -- WHEN THE DATA RETURNED ARE STATION DATA
C                       (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS DEPENDING ON THE
C                       MACHINE BEING USED (EITHER 32 OR 64) (INPUT)
C                 I,J = LOOP COUNT (INTERNAL)
C              MISTOT = TOTAL NUMBER OF MISSING ITEMS ENCOUNTED IN
C                       UNPACKING GRIDS (INPUT-OUTPUT)
C                 IER = STATUS RETURN
C                         0 = GOOD RETURN
C                       100 = GRID CHARACTERISTICS ARE DIFFERENT FOR THE
C                             2 FIELDS.
C                       SEE GFETCH, GWIND, AND MAPLAT FOR OTHER VALUES
C                       WHEN IER.NE.0 AND DATA ARE RETURNED AS 
C                       MISSING (INTERNAL-OUTPUT)
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).  THIS IS
C                       THE SAME AS ID(J) EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3)
C                       T = IDPARS(8)
C                       I = IDPARS(13)
C                       S = IDPARS(14)
C                       G = IDPARS(15)
C                       JD( ) IS USED TO HELP IDENTIFY THE BASIC MODEL
C                       FIELDS AS READ FROM THE ARCHIVE. (INPUT)
C               LD(J) = WORK ARRAY HOLDING THE 4 ID WORDS OF U WIND
C                       BEING FETCHED (J=1,4) (INTERNAL)
C               LX,LY = DIMENSIONS OF THE GRID RETURNED FROM CALLING
C                       GFETCH FOR U WIND (INTERNAL)
C               MD(J) = WORK ARRAY HOLDING THE 4 ID WORDS OF V WIND
C                       BEING FETCHED (J=1,4) (INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS
C                       ZERO FROM CALLING GFETCH WHEN THERE IS NO
C                       MISSING VALUE.  (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS
C                       ZERO FROM CALLING GFETCH WHEN THERE IS NO
C                       MISSING VALUE.  (INTERNAL)
C               MX,MY = DIMENSIONS OF THE GRID RETURNED FROM CALLING
C                       GFETCH FOR V WIND (INTERNAL)
C               NPACK = 2 FOR TDL GRIB PACKED DATA; 1 FOR NOT PACKED.
C                       THIS IS RETURNED FROM CALLING GFETCH (INTERNAL)
C              NSLABU = SAME AS NSLAB.  RETURNED FROM CALLING GWIND FOR
C                       U WIND (INTERNAL)
C              NSLABV = SAME AS NSLAB.  RETURNED FROM CALLING GWIND FOR
C                       V WIND (INTERNAL)
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE, THAT
C                       THE RECORD HAS BEEN FETCHED.  THIS IS STORED IN 
C                       LSTORE(9, ). (INTERNAL)
C              NWORDS = NUMBER OF WORDS RETURNED IN FD2( ) AND FD3( ). 
C                       THIS IS RETURNED FROM CALLING GFETCH. (INTERNAL)
C              XMESHL = GRID LENGTH IN METER AT THE LATITUDE XLAT
C                       (INTERNAL)
C
C        NONSYSTEM SUBROUTINES USED
C            GWIND, MAPLAT, ADVCTW1, BASICP
C
      INTEGER JD(4),IDPARS(15)
      INTEGER IPACK(ND5),IWORK(ND5)
      INTEGER IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      INTEGER LSTORE(12,ND9)
      INTEGER NGRIDC(6,ND11)
      INTEGER LD(4),MD(4),LDPARS(15),MDPARS(15)
C
      REAL FDAV(ND5)
      REAL FD1(ND2X3),FD2(ND2X3),FD3(ND2X3),FD4(ND2X3),FD5(ND2X3),
     1     FD6(ND2X3),FDMS(ND2X3),FDSINS(ND2X3)
      REAL CORE(ND10)
C
      IER=0
      ISTAV=0
      NX=IS2(3)
      NY=IS2(4)
C
C        TO OBTAIN U WINDS IN COMPUTING ADVECTION
C        CREATE ID FOR FETCHING U-WINDS.
C
      LD(1)=ICCCFFF*1000+IDPARS(4)
      LD(2)=IDPARS(7)
      LD(3)=IDPARS(9)*1000000+IDPARS(12)
      LD(4)=0
C
C        CALL PRSID1 TO PARSE IDS LD( ) TO LDPARS( )    
C
      CALL PRSID1(KFILDO,LD,LDPARS)
C
C        CALL GWIND TO FETCH U-WINDS IN FD2.
C
      CALL GWIND(KFILDO,KFIL10,LDPARS,LD,NDATE,
     1           NGRIDC,ND11,NSLABU,IPACK,IWORK,FD2,ND2X3,
     2           LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3           IS0,IS1,IS2,IS4,ND7,
     4           FD3,FD4,FD5,FD6,FDSINS,FDMS,ND2X3,
     5           ISTAV,L3264B,MISTOT,IER)
C
      IF(IER.NE.0)THEN
	 WRITE(KFILDO,100)IER
 100     FORMAT('     U-WIND FETCH FAILED IN GWIND.  IER=',I3)
	 GO TO 900
      ENDIF
C
      LX=IS2(3)
      LY=IS2(4)
C
C        TO OBTAIN V WINDS IN COMPUTING ADVECTION.
C        CREATE ID FOR FETCHING V-WINDS
C
      MD(1)=(ICCCFFF+100)*1000+IDPARS(4)
      MD(2)=IDPARS(7)
      MD(3)=IDPARS(9)*1000000+IDPARS(12)
      MD(4)=0
C
C        CALL PRSID1 TO PARSE IDS MD( ) TO MDPARS( )    
C
      CALL PRSID1(KFILDO,MD,MDPARS)
C
C        CALL GWIND TO FETCH V-WINDS IN FD3.
C
      CALL GWIND(KFILDO,KFIL10,MDPARS,MD,NDATE,
     1           NGRIDC,ND11,NSLABV,IPACK,IWORK,FD3,ND2X3,
     2           LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3           IS0,IS1,IS2,IS4,ND7,
     4           FD1,FD4,FD5,FD6,FDSINS,FDMS,ND2X3,
     5           ISTAV,L3264B,MISTOT,IER)
C
      IF(IER.NE.0)THEN
	 WRITE(KFILDO,200)IER
 200     FORMAT('     V-WIND FETCH FAILED IN ADVCTL.  IER=',I3)
	 GOTO 900
      ENDIF
C
      MX=IS2(3)
      MY=IS2(4)
C
      IF(NSLAB.NE.NSLABU.OR.NSLAB.NE.NSLABV.OR.
     1   NX.NE.LX.OR.LX.NE.MX.OR.NY.NE.LY.OR.LY.NE.MY)THEN
C           THE GRID CHARACTERISTICS ARE NOT THE SAME.
         IER=100
         WRITE(KFILDO,300)(LD(J),J=1,4),(NGRIDC(J,NSLABU),J=1,6),LX,LY,
     1                    (MD(J),J=1,4),(NGRIDC(J,NSLABV),J=1,6),MX,MY,
     2                    IER
 300     FORMAT(/' ****DIFFERENT GRID CHARACTERISTICS.  PREDICTOR NOT ',
     1           'COMPUTED IN ADVCTL.  VALUES FROM NGRIDC( , ) AND X*Y',
     2           ' ARE: ',2(/2X,3I10.9,I4.3,4X,6I10,4X,I3,'*',I3),
     3           '  IER=',I3)
         GOTO 900
      ENDIF
C
C        SET JD TO ADVECTED ELEMENT ID
C
      CALL BASICP(KFILDO,IDPARS,JD)
C
C        CALL MAPLAT TO COMPUTE SINE OF LATITUDE AND MAP FACTOR (DATA
C        ARRAY FD4 IS BEING BORROWED TO USE HERE).
C
      CALL MAPLAT(KFILDO,JD,NGRIDC(1,NSLAB),FD4,FD1,FDSINS,FDMS,LX,LY,
     1            IER)
      IF(IER.NE.0)THEN
	 WRITE(KFILDO,400)IER
 400     FORMAT(/' ****ATTEMPT TO COMPUTE SINE(LAT) AND MAP FACTOR '
     1          ,'FAILED IN ADVCTL.  IER=',I3)
	 GOTO 900
      ENDIF
C
C        CALL ADVCTW1 TO COMPUTE ADVECTION.
C
      XMESHL=NGRIDC(2,NSLAB)/1000.
      CALL ADVCTW1(KFILDO,XMESHL,FDAV,FD1,FD2,FD3,FD4,LX,LY)
      GO TO 999
C
C        FILL IN THE ADVECTION DATA ARRAY WITH MISSING VALUES
C
 900  DO 901 I=1,ND5
         FDAV(I)=9999.
 901  CONTINUE
C
 999  RETURN
      END
