      SUBROUTINE FFETCH(KFILDO,KFIL,ID,LOCPRD,LSTORE,ND9,LITEMS,
     1                  IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,DATA,NDX,
     2                  NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3                  NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,
     4                  ITIME,IER)
C
C        DECEMBER  2000   GLAHN   TDL   MOS-2000
C                                 MODIFIED FROM GFETCH
C 
C        PURPOSE 
C            TO FETCH DATA FROM EITHER A LINEAR ARRAY CORE( ) IN MEMORY
C            OR FROM DISK.  (THIS IS THE SO-CALLED MOS-2000 INTERNAL
C            STORAGE SYSTEM.)  THE ARRAY LSTORE( ,ND9) HOUSES THE KEYS,
C            THE FIRST 4 WORDS BEING THOSE CORRESPONDING TO ID( ).
C            WHEN LSTORE(1, ) = 0, THE DATA FOR THIS KEY HAVE BEEN DEEMED 
C            NO LONGER NEEDED, AND CANNOT BE RETURNED.  THE DATA ARE
C            RETURNED IN DATA( ) HOWEVER THEY ARE STORED, EITHER 
C            PACKED OR UNPACKED.  DATA IDENTIFIERS ARE ALSO RETURNED
C            IN IS0( ), IS1( ), IS2( ), AND IS4( ) WHEN THE DATA ARE
C            PACKED.  WHEN ITIME NE 0 AND WHEN THE TIME OFFSET RR
C            IN THE PREDICTOR ID(3) GT 0, THE DATE/TIME IS MODIFIED
C            ACCORDINGLY AND RR IS SET TO 0 FOR PURPOSES OF SEARCH.
C            GRIDPOINT DATA ARE ASSUMED TO BE PACKED, ELSE THE
C            GRID CHARACTERISTIS WOULD NOT BE AVAILABLE.  ALSO,
C            VECTOR DATA CAN'T REASONABLY BE PACKED BECAUSE
C            THE DATA MUST MATCH THE STATIONS BEING USED.
C            A DATE/TIME OF ZERO IS USED FOR DATA THAT ARE NOT TO 
C            BE DISCARDED, AND ARE NOT FOR SPECIFIC DATES, SUCH 
C            AS CLIMATOLOGICAL VALUES AND INDICES TO THRESHOLD
C            MATRICES FOR LINEARIZATION ROUTINES.
C
C            FFETCH DIFFERS FROM GFETCH ONLY IN THAT PACKED GRIDPOINT
C            DATA ARE RETURNED STILL PACKED IN FFETCH, RATHER THAN
C            UNPACKED IN GFETCH.  IN FFETCH, THE METADATA ARE 
C            PUT INTO IS0( ), IS1( ), IS2( ), AND IS4( ).
C
C        DATA SET USE 
C           KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C           KFIL   - UNIT NUMBER FOR READING THE DATA INTO DATA( ) 
C                    FROM DISK.  (INPUT)  
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C                KFIL = UNIT NUMBER FOR READING THE DATA INTO DATA( )
C                       FROM DISK.  (INPUT)
C               ID(J) = THE 4-WORD ID OF THE VARIABLE TO FETCH
C                       (J=1,4).  (INPUT)
C              LOCPRD = THE LOCATION OF THE PREDICTOR ID( ) IN THE
C                       PREDICTOR LIST WHEN FFETCH IS ACCESSED FROM PRED23.
C                       WHEN FFETCH IS ACCESSED FROM OPTION OR
C                       COMPUTATION ROUTINES CALLED FROM OPTION,
C                       LOCPRD MUST BE 7777.  THIS MAY BE STORED IN
C                       LSTORE(11, ).  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.  FOR DATA LIKE CLIMATOLOGICAL
C                              VALUES THAT ARE NOT TO BE DISCARDED, THIS
C                              IS ZERO.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                              UPON RETRIEVAL BY FFETCH.
C                       L=10 --NCOMBO, THE NUMBER OF THE SLAB IN DIR( , ,L)
C                              AND IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.  THIS CAN BE 0 FOR VECTOR DATA.
C                       L=11 --THE NUMBER OF THE FIRST PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT DOES NOT NEED
C                              TO BE STORED AFTER DAY 1.  WHEN THE VARIABLE
C                              MUST BE STORED (TO BE ACCESSED THROUGH OPTION)
C                              FOR ALL DAYS, ID(11,N) IS 7777 + THE NUMBER
C                              OF THE FIRST PREDICTOR IN THE SORTED LIST
C                              FOR WHICH THIS VARIABLE IS NEEDED.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ).
C                       (INPUT)
C              IS0(L) = HOLDS THE BYTES DECODED FROM SECTION 0 (L=1,3).
C                       (OUTPUT)
C              IS1(L) = HOLDS THE BYTES DECODED FROM SECTION 1 
C                       (L=1,MAX OF ND7).  (OUTPUT)
C              IS2(L) = HOLDS THE OCTETS DECODED FROM SECTION 2 (L=1,12).
C                       SECTION 2 IS NOT PRESENT WHEN DATA ARE NOT
C                       GRIDPOINT.  (OUTPUT)
C              IS4(L) = HOLDS THE BYTES DECODED FROM SECTION 4 (L=1,4),
C                       EXCEPT FOR THE DATA THEMSELVES.  (OUTPUT)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,NDX).  USED ONLY BY UNPACK
C                       WHEN DATA ARE PACKED AND MUST BE UNPACKED.
C                       (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,NDX).  USED ONLY BY UNPACK
C                       WHEN DATA ARE PACKED AND MUST BE UNPACKED.
C                       (INTERNAL)
C             DATA(J) = THE DATA ARE RETURNED IN DATA( ) (J=1,NWORDS).
C                       IF THEY WERE STORED PACKED, THE UNPACKER IS
C                       CALLED, BUT ONLY TO UNPACK THE METADATA INTO
C                       IS0( ), IS1( ), IS2( ), AND IS4( ).  (OUTPUT)
C                 NDX = DIMENSION OF IPACK( ) AND DATA( ).  MUST BE LARGE 
C                       ENOUGH TO HOLD NWORDS OF DATA FROM CORE( ) OR
C                       FROM DISK.  NORMALLY, NDX WILL BE ND5 IN 
C                       THE CALLING PROGRAM, BUT IF "STATION" DATA 
C                       ARE BEING RETURNED, NDX MAY BE SOME OTHER 
C                       VARIABLE (E.G., ND1).  (INPUT)
C              NWORDS = NUMBER OF WORDS RETURNED IN DATA( ).  THIS IS
C                       STORED IN LSTORE(6, ) (ONLY) WHEN THE DATA
C                       ARE STORED UNPACKED.  MUST BE LE NDX.
C                       FOR UNPACKED DATA, NWORDS COMES FROM LSTORE(6, );
C                       FOR PACKED DATA, NWORDS COMES FROM IS4(3).  (OUTPUT)
C               NPACK = 2 FOR TDL GRIB PACKED DATA; 1 FOR NOT PACKED.
C                       THIS IS STORED IN LSTORE(7, ).  (OUTPUT)
C               NDATE = DATE/TIME OF THE DATA TO BE FETCHED IN FORMAT
C                       YYYYMMDDHH.  THIS IS MATCHED TO THE VALUE 
C                       STORED IN LSTORE(8, ), AFTER POSSIBLE MODIFICATION
C                       BY THE RR IN THE ID.  WHEN THE DATA ARE NOT
C                       ATTACHED TO A SPECIFIC DATE, LIKE CLIMATOLOGICAL
C                       VALUES OR INDICES FOR LINEARIZATION ROUTINES,
C                       NDATE MUST BE INPUT AS ZERO.  (INPUT)
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE, THAT THE 
C                       DATA HAVE BEEN FETCHED.  THIS IS STORED IN
C                       LSTORE(9, ).  (OUTPUT)
C             CORE(J) = THE LINEAR ARRAY WHERE THE DATA ARE STORED,
C                       WHEN SPACE IS AVAILABLE (J=1,ND10).  NORMALLY, THIS 
C                       IS USED ONLY WITHIN GSTORE AND FFETCH.  (INPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE RANDOM DISK FILE.
C                       THIS MUST MATCH THE VALUE PROVIDED TO GSTORE
C                       AND MUST NOT CHANGE DURING THE RUN.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME FFETCH IS ENTERED.  IT IS
C                       A RUNNING COUNT FROM THE BEGINNING OF THE PROGRAM.
C                       THIS COUNT IS MAINTAINED IN CASE THE USER NEEDS 
C                       IT (DIAGNOSTICS, ETC.).  (OUTPUT)
C               NSLAB = FOR U201, THE NUMBER OF THE SLAB IN DIR( , , ) AND
C                       IN NGRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID.  WHEN THE DATA ARE VECTOR,
C                       NSLAB = 0.  FOR OTHER ROUTINES, THIS NUMBER
C                       MAY MEAN SOMETHING ELSE.  FOR INSTANCE, IN U600
C                       IT IS THE "MODEL NUMBER" OR SOURCE OF THE DATA.
C                       SEE LSTORE(10, ).  (OUTPUT) 
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (OUTPUT)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH OF MACHINE BEING USED.
C                       (INPUT)
C               ITIME = SERVES TWO FUNCTIONS.
C                       WHEN EQ 0, AN ADJUSTMENT IN TIME USING RR IS NOT TO
C                       BE MADE; GE 0 IF THE ADJUSTMENT IS TO BE MADE.
C                       U201 ROUTINES REQUIRE THE TIME ADJUSTMENT, BECAUSE
C                       GRIDS FOR PREVIOUS DATE/TIMES MAY BE NEEDED. 
C                       HOWEVER, FOR OTHER PROGRAMS, SUCH AS U600, THE
C                       VARIABLE IS PRESENT FOR THE TIME IN NDATE. 
C                       WHEN LT 0, THIS VALUE IS TO BE STORED IN 
C                       LSTORE(12, ).  THIS IS USED IN VRBL61 AND GTVECT
C                       AND ALLOWS A MAXIMUM TAU TO BE CALCULATED FOR 
C                       PREDICTANDS IN LMSTR6.  (INPUT)
C                 IER = STATUS RETURN.  (OUTPUT)
C                        0 = GOOD RETURN.
C                       47 = DATA CANNOT BE FOUND.
C                       48 = DIMENSION NDX IS NOT LARGE ENOUGH FOR DATA( )
C                            TO HOLD THE DATA.
C                       49 = IS2(3)*IS2(4).NE.IS4(3) FOR GRIDPOINT DATA.
C                       900- = IOSTAT RETURNS FROM SYSTEM ON READING DISK.
C               JD(J) = THE 4-WORD ID OF THE VARAIBLE TO RETRIEVE FROM THE
C                       MOS-2000 INTERNAL STORAGE SYSTEM (J=1,4).  THIS MAY
C                       BE THE SAME AS ID( ), BUT MAY BE MODIFIED WITH RR
C                       (SEE NRR).  (INTERNAL)
C               LDATE = DATE/TIME OF THE DATA TO BE FETCHED IN FORMAT
C                       YYYYMMDDHH AFTER POSSIBLE MODIFICATION BY RR
C                       IN THE ID.  THIS IS MATCHED TO THE VALUE 
C                       STORED IN LSTORE(8, ).  (INTERNAL)
C              IENTER = 0 INITIALLY AND INCREMENTED BY 1 UPON EACH ENTRY.  IT
C                       IS THE INTERNAL COUNT BY WHICH NFETCH IS RETURNED AND
C                       IS IMPERVIOUS TO ACTIONS BY THE USER.  (INTERNAL)
C              LWORDS = WHEN DATA ARE STORED ON DISK, THE NUMBER OF WORDS
C                       STORED.  (INTERNAL)
C                 NRR = RUN TIME OFFSET OF PREDICTOR BEING SEARCHED FOR.
C                       WHEN NONZERO, THE DATE/TIME IS UPDATED BY -NRR.
C                       (INTERNAL)
C                         
C        NON SYSTEM SUBROUTINES CALLED 
C           RDDISK, UNPACK
C
      DIMENSION ID(4),JD(4)
      DIMENSION LSTORE(12,ND9)
      DIMENSION IPACK(NDX),IWORK(NDX),DATA(NDX)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION CORE(ND10)
C
      DATA IENTER/0/
C
C        INCREMENT IENTER AND SET NFETCH.
C
      IER=0
      MISSP=0
      MISSS=0
C
      IENTER=IENTER+1
      NFETCH=IENTER
C
C        MUST USE TIME OFFSET RR IN ID(3) TO ADJUST NDATE, UNLESS
C        ITIME = 0, OR RR = 0.
C
      DO 105 J=1,4
      JD(J)=ID(J)
 105  CONTINUE
C
      LDATE=NDATE
      IF(NDATE.EQ.0)GO TO 106
C        WHEN THE ABOVE TEST IS MET, THE DATE IS NOT TO BE MATCHED.
C        RATHER, THE DATA ARE INDICES OR CLIMATOLOGICAL DATA THAT
C        ARE NEVER DISCARDED.
      IF(ITIME.EQ.0)GO TO 106
C        WHEN ITIME NE 0, RR IS TO BE USED.
      NR=ID(3)/1000000
      NT=NR/100
      NRR=NR-NT*100
      IF(NRR.NE.0)THEN
         CALL UPDAT(NDATE,-NRR,LDATE)
         JD(3)=NT*100000000+(ID(3)-NR*1000000)
      ENDIF
C
 106  CONTINUE
D     WRITE(KFILDO,107)(JD(J),J=1,4),LDATE
D107  FORMAT(/' IN FFETCH,'26X' LOOKING FOR'2XI9.9,2I10.9,I11.3,
D    1        '  FOR DATE'I11)   
C
C        FIND A LOCATION IN LSTORE( , ) MATCHING ID( ), IF ANY.
C
      DO 170 J=1,LITEMS
      IF(LSTORE(8,J).NE.LDATE.AND.
     1   LSTORE(8,J).NE.0)GO TO 170
C        DROP THROUGH HERE MEANS THE NEEDED DATE HAS BEEN FOUND,
C        OR THE NEEDED "DATE" IS ZERO WHICH IS THE CASE FOR
C        'CONSTANT" DATA. 
C
      DO 110 L=1,4
      IF(LSTORE(L,J).NE.JD(L))GO TO 170
 110  CONTINUE
C
C        DROP THROUGH HERE MEANS THAT THE DATA ID HAS BEEN FOUND.
C        WHEN LSTORE(1, ) HAS BEEN SET TO ZERO, MEANING DATA FOR
C        THIS SLOT HAVE BEEN DECLARED NONRETRIEVABLE, THEN
C        LSTORE(1, ) WILL NOT MATCH ID(1).  NOTE THAT THE FIRST
C        ID CANNOT EQUAL 0.
C
C***D     WRITE(KFILDO,114)(LSTORE(M,J),M=1,12)
C***D114  FORMAT(/' FFETCH AT 114  '3I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11/
C***D    1       ('                '3I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11)) 
C***D     WRITE(KFILDO,115)((LSTORE(M,N),M=1,12),N=1,LITEMS)
C***D115  FORMAT(/' FFETCH AT 115  '3I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11/
C***D    1       ('                '3I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11)) 
      NWORDS=LSTORE(6,J)
C
C        DETERMINE WHETHER DATA ARE STORED PACKED OR UNPACKED.
C
      IF(LSTORE(7,J).EQ.2)GO TO 120
C        A DROP THROUGH HERE MEANS THE DATA ARE NOT PACKED.
C        THEREFORE, DATA ARE VECTOR.
C
C        DETERMINE WHETHER DATA ARE STORED IN CORE OR ON DISK
C        AND WHETHER NDX IS LARGE ENOUGH.
C
      IF(LSTORE(5,J).LT.0)GO TO 117
      IF(NDX.LT.NWORDS)GO TO 177
C
C        FETCH FROM CORE( ).
C
      DO 116 M=1,NWORDS
      DATA(M)=CORE(LSTORE(5,J)+M-1)
 116  CONTINUE
C
      IS2(3)=NWORDS
      IS2(4)=1
C        IS2(3) AND IS2(4) ARE NX AND NY IN SOME CALLING PROGRAMS.
      GO TO 160
C
C        FETCH FROM DISK, PROVIDED NDX IS LARGE ENOUGH.
C
 117  LWORDS=LSTORE(6,J)
      IF(NDX.LT.LWORDS)GO TO 176
      CALL RDDISK(KFILDO,KFIL,-LSTORE(5,J),DATA,LWORDS,
     1            NBLOCK,NOREC,IER)
      IF(IER.NE.0)GO TO 180
C        IER NE 0 SHOULD BE TREATED AS A FATAL ERROR.  A DIAGNOSTIC
C        WILL HAVE BEEN PRINTED BY RDDISK.
      IS2(3)=NWORDS
      IS2(4)=1
C        IS2(3) AND IS2(4) ARE NX AND NY IN SOME CALLING PROGRAMS.
      GO TO 160
C
C        DATA ARE PACKED.
C        DETERMINE WHETHER DATA ARE STORED IN CORE OR ON DISK.
C
 120  IF(LSTORE(5,J).LT.0)GO TO 127 
C
C        FETCH FROM CORE( ).
C
      DO 122 M=1,NWORDS
      DATA(M)=CORE(LSTORE(5,J)+M-1)
 122  CONTINUE
C
C        UNPACK IDS ONLY.  THE PACKED DATA ARE STILL IN DATA( ).
C
      CALL UNPACK(KFILDO,DATA,IPACK,DATA,NDX,
     1            IS0,IS1,IS2,IS4,ND7,MISSP,MISSS,1,L3264B,
     2            IER)
      IF(IER.NE.0)GO TO 180
C        IER NE 0 SHOULD BE TREATED AS A FATAL ERROR.  A DIAGNOSTIC
C        WILL HAVE BEEN PRINTED BY UNPACK.
      NWORDS=IS4(3)
C
      IF(IS2(3)*IS2(4).NE.NWORDS)THEN
C           THIS TEST WAS ADDED 10/9/98 AND IS NOT NECESSARY IF
C           PACKING WAS DONE CORRECTLY.
         WRITE(KFILDO,125)(ID(M),M=1,4)
 125     FORMAT(/,' ****IS2(3)*IS2(4) DOES NOT EQUAL IS4(3) FOR',
     1            ' GRIDPOINT DATA IN FFETCH.',/,
     2            '     VARIABLE NOT RETURNED = ',3(I9.9,1X),I3)
         IER=49
         GO TO 180
      ENDIF
C
      GO TO 160
C
C        FETCH FROM DISK AND UNPACK, PROVIDED NDX IS LARGE ENOUGH.
C
 127  LWORDS=LSTORE(6,J)
      IF(NDX.LT.LWORDS)GO TO 176
C
      CALL RDDISK(KFILDO,KFIL,-LSTORE(5,J),DATA,LWORDS,
     1            NBLOCK,NOREC,IER) 
      IF(IER.NE.0)GO TO 180
C
      CALL UNPACK(KFILDO,DATA,IWORK,DATA,NDX,
     1            IS0,IS1,IS2,IS4,ND7,MISSP,MISSS,3,L3264B,
     2            IER)
C        SUBROUTINE UNPACK WILL NOT OVERFLOW DATA.
      IF(IER.NE.0)GO TO 180
C        IER NE 0 SHOULD BE TREATED AS A FATAL ERROR.  A DIAGNOSTIC
C        WILL HAVE BEEN PRINTED BY UNPACK.
      NWORDS=IS4(3)
C
C        THE STATEMENTS FROM HERE TO 200 ARE FOR BOTH CORE AND DISK 
C        STORAGE AND FOR EITHER PACKED OR UNPACKED DATA.
C
 160  NPACK=LSTORE(7,J)
      NTIMES=LSTORE(9,J)+1
      NSLAB=LSTORE(10,J)
      LSTORE(9,J)=NTIMES
      IF(ITIME.LT.0.AND.LSTORE(12,J).EQ.0)LSTORE(12,J)=ITIME
C****************
C        THE ABOVE TEST ON LSTORE(12,J)=0 WAS ADDED FEB. 1, 1998
C        FOR U850.  IT HAS NOT BEEN TESTED WITH U201, U600, OR U660.
C        THE FOLLOWING STATEMENTS ARE USED FOR INITIALIZING LSTORE(11, )
      IF(LOCPRD.LT.7777)THEN
         IF(LSTORE(11,J).EQ.7777.OR.
     1      LSTORE(11,J).EQ.0)LSTORE(11,J)=LSTORE(11,J)+LOCPRD
      ELSE IF(LOCPRD.EQ.7777)THEN
         IF(LSTORE(11,J).LT.7777)LSTORE(11,J)=LSTORE(11,J)+LOCPRD
      ENDIF
C
C***D     WRITE(KFILDO,165)NFETCH,LOCPRD,(LSTORE(L,J),L=1,12)
C***D165  FORMAT(/' FFETCH  '2I4,I11.9,2I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I10) 
      GO TO 200
C
 170  CONTINUE
C
C        A FALL THROUGH HERE MEANS DATA HAVE NOT BEEN FOUND.
C
      IER=47
C        DATA WERE NOT FETCHED.
      NWORDS=0
C        SET NWORDS = 0 WHEN NO DATA RETURNED.
D     WRITE(KFILDO,175)(JD(J),J=1,4),LDATE,IER
D175  FORMAT(' ****DATA NOT FOUND BY FFETCH.',
D    1       '        LOOKING FOR'2XI9.9,2I10.9,I11.3,
D    2       '  FOR DATE'I11,'  IER ='I5,)
      GO TO 180
C
C        NDX IS NOT LARGE ENOUGH TO HOLD THE DATA.  THIS IS
C        A FATAL ERROR.
C
 176  WRITE(KFILDO,178)NDX,LWORDS,(JD(L),L=1,4)
      GO TO 179
C
 177  WRITE(KFILDO,178)NDX,NWORDS,(JD(L),L=1,4)
 178  FORMAT(/,' ****DIMENSION NDX =',I6,' NOT LARGE ENOUGH TO HOLD',
     1         ' RECORD OF',I6,' WORDS.',/,
     2         '     DATA NOT RETURNED BY FFETCH.  ID( ) =',
     3          1X,I9.9,2I10.9,I11.3)
 179  IER=48
C        IER=48 SHOULD PROBABLY BE A FATAL ERROR.
C
 180  DO 190 M=1,NDX
      DATA(M)=9999.
 190  CONTINUE
C
 200  CONTINUE
C
C***D     WRITE(KFILDO,201)NFETCH,((LSTORE(L,J),L=1,12),J=1,LITEMS)
C***D201  FORMAT(/' FFETCH  'I4,I11.9,2I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11/
C***D    1      ('             'I11.9,2I10.9,I10.3,I8,I8,I2,I11,2I4,I5,I11)) 
C
      RETURN
      END   
