      SUBROUTINE VORTH1(KFILDO,XMESHL,VORT,FDSIN,FDM,FDH,NX,NY)
C
C        NOVEMBER 1994   GLAHN   TDL   MOS-2000 
C
C        PURPOSE 
C            TO COMPUTE THE RELATIVE VORTICITY FROM HEIGHTS FOR EACH
C            GRIDPOINT.  UNITS ARE /SEC * 100000.  NOTE THAT THIS
C            ROUTINE WORKS ONLY FOR THE NORTHERN HEMISPHERE.  IN
C            THE CALCULATION, THE SINE OF THE LATITUDE IS ZERO
C            AT THE EQUATOR, AND DIVISION BY THIS VALUE GIVES
C            UNREALISTIC RESULTS NEAR THE EQUATOR.  IN THE SOUTHERN
C            HEMISPHERE, THE SINE IS NEGATIVE IN FDSIN( , ) AND
C            THE SIGN OF THE VORTICITY IS (PROBABLY) REVERSED.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
c                     (OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
c                       (INPUT) 
C              XMESHL = GRID LENGTH IN M AT LATITUDE XLAT.  (INPUT)
C         VORT(IX,JY) = RELATIVE VORTICITY (IX=1,NX) (JY=1,NY).
c                       (OUTPUT)
C        FDSIN(IX,JY) = SIN OF THE LATITUDE (IX=1,NX) (JY=1,NY).
c                       (INPUT)
C          FDM(IX,JY) = MAP FACTOR (IX=1,NX) (JY=1,NY).  (INPUT)
C          FDH(IX,JY) = GEOPOTENTIAL HEIGHTS IN M (IX=1,NX) (JY=1,NY).
C                       (INPUT)
C                  NX = THE DIMENSION OF THE GRID IN THE IX DIRECTION.
C                       (INPUT).
C                  NY = THE DIMENSION OF THE GRID IN THE JY DIRECTION.
C                       (INPUT).
C              OMEGA2 = EARTH'S ROTATION PERIOD IN RADIANS/SEC *2.
C                       SET BY PARAMETER.
C               AGRAG = ACCELERATION OF GRAVITY IN M/SEC/SEC.
C                       SET BY PARAMETER.
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            EXTRAP
C
      PARAMETER (OMEGA2=14.584E-5)
      PARAMETER (AGRAV=9.81)
C
      DIMENSION VORT(NX,NY),FDSIN(NX,NY),FDM(NX,NY),FDH(NX,NY)
C
      F=AGRAV/(OMEGA2*XMESHL**2)
C
C        COMPUTE THE VORTICITY IN VORT( , ).  NOTE THAT THIS
C        COMPUTATION IS NOT FOR USE NEAR THE EQUATOR BECAUSE
C        OF THE DIVISION BY THE SINE OF THE LATITUDE.
C
      DO 270 JY=2,NY-1
      DO 269 IX=2,NX-1
      H=FDH(IX+1,JY)+FDH(IX-1,JY)+FDH(IX,JY+1)+FDH(IX,JY-1)-4*FDH(IX,JY)
      VORT(IX,JY)=H*F*FDM(IX,JY)**2/FDSIN(IX,JY)
 269  CONTINUE
 270  CONTINUE
C
C        EXTRAPOLATE LINEARLY TO BOUNDARY POINTS.
C
      CALL EXTRAP(KFILDO,VORT,NX,NY)
C
C       MAKE UNITS OF RETURNED FIELD PER SECOND X 10*5.
C
      DO 290 JY=1,NY
      DO 289 IX=1,NX
      VORT(IX,JY)=VORT(IX,JY)*100000.
 289  CONTINUE
 290  CONTINUE
C
      RETURN
      END
