      SUBROUTINE PAWRA(KFILDO,KFILX,CFILX,ID,
     1                 RECORD,IC,ND1,NSTA,IPACK,ND5,MINPK,
     2                 IS0,IS1,IS2,IS4,ND7,
     3                 IPLAIN,PLAIN,NCHAR,
     4                 XMISSP,XMISSS,LX,IOCTET,
     5                 L3264B,L3264W,IER)
C
C        NOVEMBER  1996   GLAHN   TDL   MOS-2000
C        MARCH     1998   GLAHN   CHANGED TO WRITING WITH REPLACEMENT
C        SEPTEMBER 2000   GLAHN   IMPROVED COMMENTS BEFORE LOOP 110;
C                                 LINE LENGTHS SHORTENED TO 72 CHAR
C        JUNE      2012   ENGLE   MODIFIED CALL TO INCLUDE PLAIN.
C                                 PLAIN LANGUAGE IS NOW PACKED BY
C                                 USING IACHAR FUNCTION.
C 
C        PURPOSE 
C            TO PREPARE FOR PACKING, PACK, AND WRITE A RECORD TO
C            A MOS-2000 EXTERNAL FILE.  CREATED FOR U351, BUT SHOULD
C            BE USEFUL FOR OTHER SIMILAR ROUTINES.
C
C        DATA SET USE 
C            KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT) 
C            KFILX     - UNIT NUMBER FOR THE OUTPUT RANDOM ACCESS FILE.
C                        (INPUT/OUTPUT)
C
C        VARIABLES 
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C               KFILX = UNIT NUMBER OF THE OUTPUT RANDOM ACCESS FILE.
C                       (INPUT)
C               CFILX = THE NAME OF THE OUTPUT RANDOM ACCESS FILE.
C                       (CHARACTER*60)  (INPUT)
C               ID(J) = MOS ID OF VARIABLE TO WRITE (J=1,4).  (INPUT)
C           RECORD(K) = HOLDS CONSTANT DATA FOR WRITING (K=1,NSTA).
C                       (INPUT)
C               IC(J) = SCRATCH ARRAY USED IN PACKING THE DATA 
C                       (J=1,ND1).  (INTERNAL)
C                 ND1 = THE MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT
C                       WITH ON THIS RUN.  DIMENSION OF RECORD( ) AND 
C                       IC( ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH, WHICH IS
C                       THE NUMBER OF VALUES IN RECORD( ) TO WRITE.
C                       (INPUT)
C            IPACK(J) = SCRATCH ARRAY USED IN PACKING THE DATA (J=1,ND5).
C                       (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ).  (INPUT)
C               MINPK = VALUES ARE PACKED IN GROUPS OF MINIMUM SIZE
C                       MINPK.  ONLY WHEN THE NUMBER OF BITS TO HANDLE
C                       A GROUP CHANGES WILL A NEW GROUP BE FORMED.  
C                       (INPUT)
C              IS0(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 0 (L=1,ND7).  (INTERNAL)
C              IS1(L) = HOLDS THE VALUES TO FURNISH FOR GRIB
C                       SECTION 1 (L=1,ND7).  (INPUT)
C              IS2(L) = HOLDS THE VALUES FOR GRIB SECTION 2 (L=1,ND7).
C                       NOT ACTUALLY USED.  (INTERNAL)
C              IS4(L) = HOLDS THE VALUES FOR GRIB SECTION 4.  NONE OF 
C                       THE VALUES NEED BE FURNISHED BY THE USER.
C                       IS4(2) IS SET TO INDICATE NON-GRIDPOINT DATA,
C                       COMPLEX PACKING, ORIGINAL SCALED VALUES TO
C                       BE PACKED (NOT SECOND ORDER SPATIAL
C                       DIFFERENCES), AND MISSING VALUES OR NOT
C                       DEPENDING ON WHETHER OR NOT XMISS NE OR EQ ZERO,
C                       RESPECTIVELY.  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ),AND IS4( ).
C                       (INPUT)
C               PLAIN = THE PLAIN LANGUAGE DESCRIPTION OF THE PREDICTORS
C                       EQUIVALENCED TO IPLAIN( , ). (CHARACTER*32)
C         IPLAIN( , ) = NAME OF VARIABLE TO PACK.  THIS IS CHARACTER 
C                       DATA IN AN INTEGER ARRAY.  (INPUT)
C               NCHAR = THE NUMBER OF CHARACTERS OF PLAIN LANGUAGE IN
C                       PLAIN TO PACK WITH THE DATA.  (INPUT)
C              XMISSP = PRIMARY MISSING VALUE INDICATOR.  (INPUT)
C              XMISSS = SECONDARY MISSING VALUE INDICATOR.  SET TO 0 TO 
C                       IN CALLING PROGRAM TO INDICATE THERE WILL BE NO 
C                       SECONDARY MISSING VALUE INDICATOR (SEE XMISSP).
C                       (INPUT)
C                  LX = THE NUMBER OF GROUPS (THE NUMBER OF 2ND ORDER 
C                       MINIMA).  WHILE NEEDED ONLY IN SUBROUTINE PACK,
C                       IT IS OUTPUT IN THE ARGUMENT LIST OF PAWRA IN
C                       CASE THE USER WANTS TO KNOW IT.  (OUTPUT)
C              IOCTET = THE TOTAL MESSAGE SIZE IN OCTETS.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2). 
C                       (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD VALUE.
C                       OTHER VALUES COME FROM CALLED SUBROUTINES.
C 
C        NON SYSTEM SUBROUTINES CALLED 
C           PACK1D, UNPKBG, WRTDLM
C
      CHARACTER*32 PLAIN
      DIMENSION RECORD(ND1),IC(ND1)
      DIMENSION IPACK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION IPLAIN(L3264W,4)
      DIMENSION ID(4)
C
      IER=0
C
C        ZERO ARRAYS, BUT NOT IS1( ) AND IS2( ).  IS2( ) IS THE GRID
C        DEFINITION SECTION AND IS NOT USED; THIS IS INDICATED BY
C        SETTING IS1(2) = 0 IN THE CALLING PROGRAM.  IS1(17) IS INPUT
C        AS THE SCALE FACTOR IN THE CALLING PROGRAM.
C
      DO 110 J=1,ND7
      IS0(J)=0
      IS4(J)=0
 110  CONTINUE
C
C        SET IDS INTO IS1( ).
C
      IS1(9)=ID(1)
      IS1(10)=ID(2)
      IS1(11)=ID(3)
      IS1(12)=ID(4)
C
C        PUT PLAIN LANGUAGE INTO IS1( ) FOR PACKING.
C
      LOC=1
C        LOC = WORD POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
      IPOS=1
C        IPOS = BIT POSITION IN IPLAIN(1,1) TO START UNPACKING.
C        UNPKBG UPDATES IT.
C
      IS1(22)=NCHAR
CINTEL
C
C        USE IACHAR FUNCTION TO PUT ONE CHARACTER (BYTE) FROM
C        PLAIN INTO ONE IS1( ) WORD.
C
      DO J=1, NCHAR
         IS1(J+22)=IACHAR(PLAIN(J:J))
      END DO
CC
C      DO 120 J=1,NCHAR
C      CALL UNPKBG(KFILDO,IPLAIN(1,1),4*L3264W,LOC,IPOS,
C     1            IS1(J+22),8,L3264B,IER,*900)
CC        NOTE THAT THIS PUTS ONE BYTE PER IS1( ) WORD.
C 120  CONTINUE
CINTEL
C
C        PACK FROM RECORD( ) TO IPACK( ).
C
      CALL PACK1D(KFILDO,RECORD,IC,NSTA,IS0,IS1,IS2,IS4,
     1            ND7,XMISSP,XMISSS,IPACK,ND5,
     2            MINPK,LX,IOCTET,L3264B,IER)
      IF(IER.NE.0)GO TO 900
C
C        WRITE THE DATA.
C      
      CALL WRTDLM(KFILDO,KFILX,CFILX,ID,IPACK,(IOCTET*8)/L3264B,
     1            2,0,L3264B,IER)
 900  RETURN
      END
      
