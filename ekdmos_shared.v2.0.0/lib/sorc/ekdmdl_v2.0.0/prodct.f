      SUBROUTINE PRODCT(KFILDO,KFIL10,IP12,
     1                  IDPARS,JD,NDATE,
     2                  KFILRA,RACESS,NUMRA,
     3                  SDATA,ND1,NSTA,NSLAB,
     4                  IPACK,IWORK,DATA,ND5,
     5                  FD1,ND2X3,
     6                  CCALL,ICALLD,CCALLD,ISDATA,
     7                  LASTL,LASTD,NSTORE,L3264W,
     8                  LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,
     9                  NFETCH,IS0,IS1,IS2,IS4,ND7,
     A                  ISTAV,L3264B,IER)
C
C        JUNE    2004   LIANG  MDL   MOS-2000 
C        JULY    2004   LIANG  CORRECTED AN ERROR WHEREIN THE WRONG DATE
C			       WAS BEING USED TO CALCULATE LIGHTNING 
C			       CLIMATOLOGY.
C        NOVMBR  2004  CHARBA  IMPROVED DOCUMENTATION.  
C        NOVMBR  2004  CHARBA  ADDED THE PRODUCT CONSISTING OF THE AD-
C			       JUSTED 3-H MOS TSTM PROB AND TOPOGRAPHY.
C        MAY     2005  CHARBA  CHANGED VARIABLE IDS AND IMPROVED DOCU-
C			       MENTATION.  ALSO CHANGED FD2 TO SDATA IN
C			       CALL TO CONST.
C        SEPMBR  2005  CHARBA  CHANGED SUCH THAT DD IN ID(1) SWITCHED
C		  	       FROM 08 TO 05.
C        MAY     2006  CHARBA  REMOVED FD2( ) SINCE IT WAS NOT BEING 
C                              USED AND CHANGED DIMENSION OF FD1( ) 
C                              FROM ND1 TO ND2X3.  ALSO MADE OTHER COS-
C                              METIC CHANGES TO SATISFY CODE WALK-THRU.
C
C        PURPOSE 
C           SUBROUTINE PRODCT COMPUTES THE INTERACTIVE PREDICTOR, DEFIN
C	    ED AS THE PRODUCT OF TWO BASIC PREDICTORS. THE TWO BASIC
C	    PREDICTORS COULD BE INGESTED FROM SEQUENTIAL OR RANDOM 
C	    ACCESS FILES IN TDPPACK FORMAT.
C            
C           THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C               207  190   3-H ADJ MOS TSTM PROB * 2-H LAMP TSTM RF
C               207  191   3-H ADJ MOS TSTM PROB * LAMP TSTM TOPOG
C
C        DATA SET USE
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT(PRINT) FILE.
C                       (OUTPUT)
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM
C                       ACCESS.(INPUT-OUTPUT)
C            IP12   - INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                     STATIONS ON THE INPUT FILES WILL BE PRINTED TO
C                     THE FILE WHOSE UNIT NUMBER IS IP12.
C
C        VARIABLES
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT(PRINT) FILE.
C                       (OUTPUT)
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM
C                       ACCESS.(INPUT-OUTPUT)
C              IP12   = INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE INPUT FILES WILL BE PRINTED TO
C                       THE FILE WHOSE UNIT NUMBER IS IP12.
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE 
C                       PREDICTOR ID CORRESPONDING TO ID() (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY
C                                      1 LAYER)
C                       J=7--LTLTLTLT (TOP OF LAYER)
C                       J=8--T (TRANSFORMATION)
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND 
C                                BACK IN TIME)
C                       J=10-OT (TIME APPLICATION)
C                       J=11-OH (TIME PERIOD IN HOURS)
C                       J=12-TAU (PROJECTION IN HOURS)
C                       J=13-I (INTERPOLATION TYPE)
C                       J=14-S (SMOOTHING INDICATOR)
C                       J=15-G (GRID INDICATOR)
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT
C                       THE PORTIONS PERTAINING TO PROCESSING
C                       ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15).
C                       JD() IS USED TO HELP IDENTIFY THE BASIC MODEL
C                       FIELDS AS READ FROM THE ARCHIVE. (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.
C                       (INPUT)
C           KFILRA(J) = THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                       ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES ASSOCIATED WITH KFILRA(J) 
C			(J=1,NUMRA)(CHARACTER*60).  (INPUT)
C               NUMRA = THE NUMBER OF VALUES IN KFILRA( ) AND RACESS( ).
C                       (INPUT)
C            SDATA(K) = RELATIVE FREQUENCY OR TOPOGRAPHY (CONSTANT) DATA
C			RETURNED FROM SUBROUTINE CONST (K=1,NSTA).  
C			ALSO CONTAINS PRODUCT/OUTPUT VARIABLE.  
C			(OUTPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT 
C			WITH.  (INPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT 
C			WITH.  DIMENSION OF SDATA( ) AND ISDATA( ).  
C			(INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR(, ,) AND
C                       IN NGRIDC(,) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID. (OUTPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5). (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5). (INTERNAL)
C             DATA(J) = WORK ARRAY (J=1,ND5). (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ).
C                       (INPUT)
C               ND2X3 = DIMENSION OF FD1( ).  (INPUT)
C              FD1(K) = WORK ARRAY(CONTAINS MOS 3H TSTM PROBABILITY) 
C                       (K=1,ND2X3).  (INTERNAL)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ) AND CCALLP( ).  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN 
C			INTEGER VARIABLE (L=1,L3264W) (K=1,NSTA).
C                       EQUIVALENCED TO CCALLD( ).  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,NSTA).  (INPUT) 
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C               LASTL = THE LAST LOCATION IN CORE( ) USED FOR MOS-2000 
C			INTERNAL STORAGE.  INITIALIZED TO 0 ON FIRST 
C			ENTRY TO GSTORE.  ALSO INITIALIZED IN U201 IN 
C			CASE GSTORE IS NOT ENTERED.  (INPUT-OUTPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR 
C			MOS-2000 INTERNAL STORAGE.  MUST BE CARRIED 
C			WHENEVER GSTORE IS TO BE CALLED.  (INPUT)
C              NSTORE = THE NUMBER OF TIMES GSTORE HAS BEEN ENTERED.  
C			GSTORE  KEEPS TRACK OF THIS AND RETURNS THE 
C			VALUE.  (OUTPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.  
C			(INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE
C                       DATA STORED (L=1,12) (J=1,LITEMS).
C                       (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE() WHERE
C                              THE DATA START.  WHEN ON DISK,
C                              THIS IS MINUS THE RECORD NUMBER WHERE
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR(, ,L) AND
C                              IN NGRIDC(,L) DEFINING THE
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID(,N) (N=1,NPRED) FOR WHICH
C                              THIS VARIABLE IS NEEDED, WHEN IT IS
C                              NEEDED ONLY ONCE FROM LSTORE(,).
C                              WHEN IT IS NEEDED MORE THAN ONCE, THE 
C                              VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING
C                              MOSTORE(,). LATER USED AS A WAY OF
C                              DETERMINING WHETHER TO KEEP THIS
C                              VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE(,). (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE(,)
C                       THAT HAVE BEEN USED IN THIS RUN. (INPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA
C                       IDENTIFIED IN LSTORE(,) (J=1,ND10).
C                       WHEN CORE() IS FULL DATA ARE STORED ON DISK.
C                       (INPUT)
C                ND10 = DIMENSION OF CORE(). (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE. (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING COUNT FROM THE BEGINNING OF
C                       THE PROGRAM.  THIS COUNT IS MAINTAINED IN
C                       CASE THE USER NEEDS IT(DIAGNOSTICS, ETC.).
C                       (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       IS2(3) AND IS2(4) ARE USED BY THE CALLING
C                       PROGRAM AS THE GRID DIMENSIONS.
C                       (INTERNAL-OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0(),IS1(),IS2(), AND IS4().
C                       NOT ALL LOCATIONS ARE USED. (INPUT)
C               ISTAV = 1 SINCE THE DATA RETURNED ARE STATION DATA.
C                       (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING 
C                       USED (EITHER 32 OR 64). (INPUT)
C                 IER = STATUS RETURN
C                         0 = GOOD RETURN
C                       103 = VARIABLE NOT ACCOMMODATED BY ROUTINE.
C                       SEE GFETCH AND CONST FOR OTHER VALUES.
C                       WHEN IER NE 0, DATA ARE RETURNED AS MISSING.
C               NPACK = 2 FOR TDL GRIB PACKED DATA; 1 FOR NOT PACKED
C                       THIS IS RETURNED FROM GFETCH. (INTERNAL)
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE,
C                       THAT THE RECORD HAS BEEN FETCHED.  THIS IS 
C                       STORED IN LSTORE(9,). (INTERNAL)
C              NWORDS = NUMBER OF WORDS RETURNED IN DATA().  THIS 
C                       IS RETURNED FROM GFETCH (INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED BY
C                       GFETCH AS 0 WHEN DATA ARE NOT PACKED. (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED BY
C                       GFETCH AS 0 WHEN DATA ARE NOT PACKED.  
C                       (INTERNAL)
C                NIDS = NUMBER OF VARIABLES THAT CAN BE COMPUTED.
C			(PARAMETER)
C	  ITABLE(3,K) = TABLE OF VARIABLE IDS (K=1,NIDS).  (INTERNAL)
C
      PARAMETER(NIDS=2)
C     
      CHARACTER*60 RACESS(NUMRA) 
      CHARACTER*8 CCALL(ND1,6),CCALLD(ND5)
C
      INTEGER ICALL(L3264W,ND1,6)
      INTEGER NUMRA,NSTA,LASTL,LASTD,NSTORE
      INTEGER LSTORE(12,ND9)
      INTEGER IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      INTEGER IPACK(ND5),IWORK(ND5)
      INTEGER IDPARS(15),LDPARS(15),JD(4),LD(4)
      INTEGER IER,ISTAV,KFILDO,KFIL10,L3264B,LITEMS,
     1        NBLOCK,ND2X3,ND1,ND5,ND7,ND9,ND10,
     2        NDATE,NFETCH,NSLAB,NRR,LDATE
      INTEGER KFILRA(NUMRA)
      INTEGER ICALLD(L3264W,ND5),ISDATA(ND1)
C
      DIMENSION CORE(ND10),DATA(ND5)
      DIMENSION FD1(ND2X3)
      DIMENSION SDATA(ND1),ITABLE(3,NIDS)
C
      DATA ITABLE/207190,207101,437501,
     1            207191,207101,409320/
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        INITIALIZATION
C
      IER=0
      ISTAV=1
C
C        MAKE SURE THIS SUBROUTINE ACCOMMODATES THE PREDICTOR
C           207190 = MOS 3H TSTM PROB * LIGHTNING CLIMATOLOGY
C           207191 = MOS 3H TSTM PROB * TOPOGRAPHY           
C
      KK=0
      DO 100 N=1,NIDS
        IF((IDPARS(1).EQ.ITABLE(1,N)/1000).AND.
     1     (IDPARS(2).EQ.MOD(ITABLE(1,N),1000)))THEN
           LD1=ITABLE(2,N)
           LD2=ITABLE(3,N)
           KK=N
           GO TO 105 
        ENDIF
 100  CONTINUE
      IF(KK.EQ.0)THEN
        WRITE(12,101)(JD(J),J=1,4)
 101    FORMAT(/,'****IDPARS(1) AND IDPARS(2) NOT ACCOMMODATED ',
     1           'IN PRODCT. ',I9.9,2I10.9,I4.3,
     2           ' ...SET IER = 103 AND SUPPLY MISSING VALUES.')
        IER=103
        GO TO 200
      ENDIF
C
C        FETCH MOS 3H TSTM PROBABILITY INTO FD1( )...THIS IS VECTOR DATA
C        THAT IS BEING STORED IN AN ARRAY NORMALLY USED FOR GRIDS.
C  
 105  LD(1)=LD1*1000+IDPARS(3)*100+08       
      LD(2)=0
      LD(3)=IDPARS(9)*1000000+IDPARS(12)  
      LD(4)=950000000 
      CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,0,IER)
C
      IF(IER.NE.0)THEN
        WRITE(KFILDO,110) (LD(J),J=1,4),NDATE
 110    FORMAT(' IN PRODCT, COULD NOT FETCH DATA FOR ',
     1         4I10,2X,I10,' ...SUPPLY MISSING VALUES.')
        GOTO 200
      ENDIF
C
C        FETCH THE MONTHLY LIGHTNING RELATIVE FREQUENCIES OR TOPO-
C	 GRAPHY [INTO SDATA( )] FROM THE RANDOM-ACCESS FILE...BOTH ARE
C        VECTOR FIELDS.
C
      IF(LD2.EQ.ITABLE(3,1)) THEN
         LDATE=NDATE
         NRR=IDPARS(9)
         IF(NRR.GT.0)THEN
            CALL UPDAT(NDATE,-NRR,LDATE)
         ENDIF
C
         LD(1)=LD2*1000  
         LD(2)=030000000
         LD(3)=IDPARS(12)
         LD(4)=500000000  
      ELSEIF(LD2.EQ.ITABLE(3,2)) THEN
C           NOTE:  RA TOPO VECTOR FILE WAS CREATED BY U352.
         LD(1)=LD2*1000
	 LD(2)=0
	 LD(3)=0
	 LD(4)=000000140
      ENDIF
      CALL PRSID1(KFILDO,LD,LDPARS)
      CALL CONST(KFILDO,KFIL10,IP12,
     1           LD,LDPARS,JD,LDATE,
     2           KFILRA,RACESS,NUMRA,
     3           CCALL,ICALLD,CCALLD,
     4           ISDATA,SDATA,ND1,NSTA,
     5           IPACK,IWORK,DATA,ND5,
     6           LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     7           NBLOCK,LASTD,NSTORE,NFETCH,
     8           IS0,IS1,IS2,IS4,ND7,
     9           ISTAV,L3264B,L3264W,IER)
C
      IF(IER.NE.0)THEN
        WRITE(KFILDO,110) (LD(J),J=1,4),LDATE
        GOTO 200
      ENDIF
      IF(LD2.EQ.ITABLE(3,1)) THEN
        DO 120 J=1,NSTA
          SDATA(J)=SDATA(J)*100.
 120    CONTINUE
      ENDIF
C
C        COMPUTE THE PRODUCT OF THE MOS 3H TSTM PROBABILITY (DECIMAL) 
C	 AND THE 2-H LIGHTNING RELATIVE FREQUENCY(%) / TOPOGRAPHY(M).
C
      DO 140 J=1,NSTA 
        IF(NINT(FD1(J)).EQ.9999) THEN
          SDATA(J)=9999.
        ELSE  
          SDATA(J)=FD1(J)*SDATA(J)
        ENDIF
  140 CONTINUE
      GO TO 300
C 
C        SET OUTPUT FIELD TO MISSING WHEN AN ERROR HAS OCCURRED.
C
  200 DO 210 J=1,ND1 
      SDATA(J)=9999.
  210 CONTINUE
C
  300 RETURN
      END
