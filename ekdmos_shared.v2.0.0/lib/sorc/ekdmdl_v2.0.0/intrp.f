      SUBROUTINE INTRP(KFILDO,P,FD,NX,NY,DIR,ND1,NSTA,SDATA) 
C 
C        MAY      1994   GLAHN   GLAHN   TDL   MOS-2000
C        FEBRUARY 1998   GLAHN   SET NEGATIVE VALUES TO ZERO
C        AUGUST   2002   GLAHN   RESTRICTED NBX TO RANGE 1 TO NX-1 AND
C                                NBY TO RANGE 1 TO NY-1
C 
C        PURPOSE 
C            TO PREPARE PRECIPITATION AMOUNT FIELD FOR INTERPOLATION,
C            AND TO BILINEARLY INTERPOLATE INTO IT FOR ALL STATION
C            LOCATIONS.  THE ZERO LINE IS PRESERVED AS APPROXIMATELY
C            HALF WAY BETWEEN PLUS AND ZERO GRIDPOINT VALUES.
C 
C        DATA SET USE
C            KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            P(IX,JY) = FIELD TO INTERPOLATE INTO (IX=1,NX) (JY=1,NY).
C                       (INPUT)
C           FD(IX,JY) = WORK ARRAY (IX=1,NX) (JY=1,NY).  (INTERNAL)
C                  NX = THE IX (WEST-EAST) EXTENT OF P( , ) AND FD( , ).
C                       (INPUT)
C                  NY = THE JY (SOUTH-NORTH) EXTENT OF P( , ) AND FD( , ).
C                       (INPUT)
C            DIR(K,J) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR STATION K (K=1,NSTA).  (INPUT)
C                 ND1 = THE MAXIMUM NUMBER OF STATIONS.  FIRST DIMENSION
C                       OF DIR( , ).  (INPUT)
C                NSTA = THE NUMBER OF STATIONS BEING DEALT WITH.  (INPUT)
C            SDATA(K) = THE INTERPOLATED VALUES FOR ALL STATIONS (K=1,NSTA).
C                       (OUTPUT)
C                 SUM = SUM OF GRIDPOINTS 1 GRIDLENGTH FROM P(IX,JY) 
C                       WHICH HAVE POSITIVE VALUES.  (INTERNAL) 
C               KOUNT = NUMBER OF GRIDPOINTS  1 GRIDLENGTH FROM 
C                       P(IX,JY) WHICH HAVE POSITIVE VALUES.  (INTERNAL)  
C
C        NONSYSTEM SUBROUTINES CALLED
C            NONE.
C
      DIMENSION DIR(ND1,2)
      DIMENSION P(NX,NY),FD(NX,NY)
      DIMENSION SDATA(NSTA)
C
C       PREPARE FIELD FOR INTERPOLATION.
C
      DO 120 JY=1,NY 
      DO 119 IX=1,NX 
      SUM=0. 
      KOUNT=0 
      IF(P(IX,JY).LE.0.)GO TO 102 
      FD(IX,JY)=P(IX,JY) 
      GO TO 119 
C 
 102  IF(JY.GE.NY)GO TO 105 
      IF(P(IX,JY+1).LE.0.)GO TO 105 
      SUM=SUM+P(IX,JY+1) 
      KOUNT=KOUNT+1 
C 
 105  IF(JY.LE.1)GO TO 108 
      IF(P(IX,JY-1).LE.0.)GO TO 108 
      SUM=SUM+P(IX,JY-1) 
      KOUNT=KOUNT+1 
C 
 108  IF(IX.GE.NX)GO TO 111 
      IF(P(IX+1,JY).LE.0.)GO TO 111 
      SUM=SUM+P(IX+1,JY) 
      KOUNT=KOUNT+1 
C 
 111  IF(IX.LE.1)GO TO 115 
      IF(P(IX-1,JY).LE.0.)GO TO 115 
      SUM=SUM+P(IX-1,JY) 
      KOUNT=KOUNT+1 
C 
 115  IF(KOUNT.GT.0)GO TO 116 
      FD(IX,JY)=P(IX,JY) 
      GO TO 119 
C 
 116  FD(IX,JY)=-SUM/KOUNT 
C 
 119  CONTINUE 
 120  CONTINUE 
C
C        DO INTERPOLATION.
C 
      DO 160 K=1,NSTA
      BX=DIR(K,1)
      BY=DIR(K,2) 
      NBX=BX 
      NBY=BY 
C        
C        MAKE SURE GRID IS ACCESSED WITHIN BOUNDARIES.
C   
      IF(NBX.LT.1)THEN
         NBX=1
      ELSEIF(NBX.GT.NX-1)THEN
         NBX=NX-1
      ENDIF
C
      IF(NBY.LT.1)THEN
         NBY=1
      ELSEIF(NBY.GT.NY-1)THEN
         NBY=NY-1
      ENDIF 
C
      NBXP1=NBX+1 
      NBYP1=NBY+1 
      DX=BX-FLOAT(NBX) 
      DY=BY-FLOAT(NBY) 
      SDATA(K)=FD(NBX,NBY)
     1  +(FD(NBXP1,NBY)-FD(NBX,NBY))*DX
     2  +(FD(NBX,NBYP1)-FD(NBX,NBY))*DY
     3  +(FD(NBX,NBY)+FD(NBXP1,NBYP1)-FD(NBX,NBYP1)-FD(NBXP1,NBY))*DX*DY 
C
D     IF(SDATA(K).LT.0.)THEN
C
D        DO 150 JY=NBY-2,NBY+2
D        WRITE(KFILDO,141)(P(IX,JY),IX=NBX-2,NBX+2)
D141     FORMAT(' '5F10.2)
D150     CONTINUE
C
D        DO 155 JY=NBY-2,NBY+2
D        WRITE(KFILDO,141)(FD(IX,JY),IX=NBX-2,NBX+2)
D155     CONTINUE
C
D        WRITE(KFILDO,156)K,SDATA(K),BX,BY,NBX,NBY
D156     FORMAT(' 'I6,3F12.2,2I5)
C
D        STOP 55555
D     ENDIF
C
C        SET ANY NEGATIVE VALUES = 0.
C
      IF(SDATA(K).LT.0.)SDATA(K)=0.
 160  CONTINUE
C
      RETURN 
      END 
