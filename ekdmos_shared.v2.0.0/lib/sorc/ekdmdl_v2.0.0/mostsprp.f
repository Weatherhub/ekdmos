      SUBROUTINE MOSTSPRP(KFILDO,KFIL10,ID,IDPARS,JD,NDATE,
     1                    SDATA,ND1,NSTA,
     2                    IPACK,IWORK,CCALLD,ND5,
     3                    FD1,FD2,ND2X3,
     4                    LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     5                    IS0,IS1,IS2,IS4,ND7,
     6                    ISTAV,L3264B,IER)
C
C        JUNE     2004  LIANG    MDL   MOS-2000 
C        JANUARY  2005  CHARBA   MODIFIED TO ACCOMMODATE NEW OUTPUT 
C				 ID(1), WHICH IS 207101108.  THE PREVI-
C				 OUS ID(1) OF 207101008 WAS LIKELY CHOS-
C				 EN BECAUSE A NON-ZERO "B" WOULD HAVE 
C			         RESULTED IN THE FIELD BEING CONVERTED 
C				 TO BINARY IN PRED21/22.  THE PROBLEM IS
C				 THE ZERO "B" RESULTS IN THE THRESHOLD 
C				 VALUE BEING CHANGED TO ZERO IN U602/-
C				 RDSTRT, WHICH DIS-ALLOWS THE VARIABLE 
C				 BEING USED FOR STRATIFICATION IN U602.
C			         A REPRECUSSION OF CHANGING THE "B" VAL-
C			         UE TO 1 IS THAT IT REQUIRED A PATCH TO
C				 BE PUT INTO PRED21/22, AS SUGGESTED 
C				 ABOVE.  THE PATCH PREVENTS PRED21/22 
C				 FROM CONVERTING THE FORECASTS TO BI-
C				 NARY.
C        AUGUST  2005  CHARBA    CHANGED NAME OF 9-POINT WEIGHTED 
C				 SMOOTHING ROUTINE FROM SMTH9 TO SMTH9V.
C	 NOVEMBR 2005  CHARBA    SET MOS PROBS OF 9997 TO 0 BEFORE THE
C				 ADJUSTMENTS ARE PERFORMED.  ALSO, FOR
C				 THE CASE WHERE MOS PROBS ARE TIME-
C				 INTERPOLATED, ADDED CHECK THAT BOTH 
C				 PROBS ARE NOT 9999.  IF EITHER IS 9999
C				 THE INTERPOLATED VALUE IS SET TO 9999.
C        MAY     2006  CHARBA    CHANGED SUBROUTINE NAME FROM MSLPTS TO
C                                MOSTSPRP, AND MADE STRUCTURAL AND OTHER
C                                COSMETIC CHANGES TO SATISFY CODE 
C                                WALK-THRU.
C                                NOTE:  MOSTSPRP MUST BE CALLED AFTER
C                                       TIMTRP IN OPTION.  OTHERWISE,
C                                       MISSING DATA WILL BE RETURNED 
C                                       FOR NON-THREE-HOURLY FORECAST
C                                       PROJECTIONS.
C        JULY   2006   CHARBA    CHANGED 9999 TO 9998.5 IN CALL TO
C                                SMTH9V.
C        PURPOSE
C           DO TEMPORAL MATCHING-INTERPOLATION OF MOS 3-H THUNDERSTORM
C           CYCLES/VALID PERIODS WITH LAMP 2-H THUNDERSTORM VALID
C	    PERIODS.  FOUR OF THE SIX LAMP VALID PERIODS FALL WITHIN THE
C	    MOS 3-H VALID PERIODS;  FOR THESE THE MOS FORECASTS ARE
C	    SIMPLY ASSIGNED TO THE LAMP PERIODS.  FOR THE REMAINING TWO
C	    LAMP PERIODS THAT STRADDLE TWO MOS PERIODS THE LAMP FORECAST
C	    IS TAKEN AS A SIMPLE MEAN OF THE TWO MOS FORECASTS.  BEFORE 
C	    THE MATCHING-INTERPOLATION IS PERFORMED THE MOS PROBABILITY
C	    FORECASTS ARE TRUNCATED TO THE RANGE 0.0 TO 1.0.  AFTER THE
C           MATCHING-INTERPOLATION IS COMPLETED, A NINE-POINT SMOOTHER
C           IS APPLIED TO REMOVE FINE-SCALE VARIABILITY IN THE MOS
C           FORECASTS.
C
C	    THE FOLLOWING ID(1) AND ID(3) ARE ACCOMMODATED: 
C	       ID(1)      ID(3) 
C            207101108  0RR000ttt  - ADJUSTED MOS 3-H TSTM PROBABILITY
C                                    (DECIMAL) WHERE RR IS THE NUMBER OF
C                                    HOURS TO "LOOK BACK" AND ttt IS THE
C                                    FORECAST PROJECTION IS RELATIVE TO
C                                    THE MOS CYCLE TIME.
C
C        DATA SET USE
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (OUTPUT)
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT-OUTPUT)
C
C        VARIABLES
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT)
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM
C                       ACCESS.  (INPUT-OUTPUT)
C	        ID(J) = VARIABLE IDS INPUT FROM U201.CN (J=1,4)  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       PREDICTAND ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE)
C                       J=2--FFF (SUBCLASS OF VARIABLE)
C                       J=3--B (BINARY INDICATOR)
C                       J=4--DD (DATA SOURCE, MODEL NUMBER)
C                       J=5--V (VERTICAL APPLICATION)
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1
C                            LAYER)
C                       J=7--LTLTLTLT (TOP OF LAYER)
C                       J=8--T (TRANSFORMATION)
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK
C                            IN TIME)
C                       J=10--OT (TIME APPLICATION)
C                       J=11--OH (TIME PERIOD IN HOURS)
C                       J=12--TAU (PROJECTION IN HOURS), WHICH IN THIS
C			      CASE IS RELATIVE TO THE MOS CYCLE TIME, 
C			      NOT THE LAMP CYCLE TIME.
C                       J=13--I (INTERPOLATION TYPE)
C                       J=14--S (SMOOTHING INDICATOR)
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC VARIABLE IDS (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE
C                       PORTIONS PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3)
C                       T = IDPARS(8)
C                       I = IDPARS(13)
C                       S = IDPARS(14)
C                       G = IDPARS(15) AND THRESH.
C                       JD( ) IS USED TO IDENTIFY THE BASIC MODEL
C                       FIELDS AS READ FROM THE ARCHIVE.  (INPUT)
C               NDATE = THE DATE/TIME CONTAINED IN THE U201.CN DATE
C			LIST.  NOTE THAT THE CYCLE TIME APPLIES TO LAMP
C			NOT MOS.  (INPUT)
C            SDATA(J) = ARRAY TO HOLD RETURNED DATA WHEN THE DATA ARE
C                       AT STATIONS. (J=1,ND1).  (OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF GRIDPOINTS THAT CAN BE DEALT.
C                       DIMENSION OF IPACK( ) AND IWORK( ).  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT 
C                       WITH.  (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C	    CCALLD(J) = CONTAINS GRIDPOINT IDS (CHARACTER*8) (J=1,ND5).
C                       (INPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND CCALLD( ).
C                       (INPUT)
C              FD1(J) = WORK ARRAY TO HOLD MOS 3-H TSTM FORECAST 
C			FOR THE FIRST PROJECTION (J=1,ND2X3).  
C                       (INTERNAL)
C              FD2(J) = WORK ARRAY TO HOLD MOS 3-H TSTM FORECAST 
C			FOR THE SECOND PROJECTION (J=1,ND2X3).  
C                       (INTERNAL)
C               ND2X3 = DIMENSION OF FD1( ) AND FD2( ).  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK,
C                              THIS IS MINUS THE RECORD NUMBER WHERE
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR
C                              NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN
C                              RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE
C                              CHARACTERISTICS OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTAND IN THE
C                              SORTED LIST IN ID( ,N) (N=1,NPRED)
C                              FOR WHICH THIS VARIABLE IS NEEDED, WHEN
C                              IT IS NEEDED ONLY ONCE FROM
C                              LSTORE( , ).  WHEN IT IS NEEDED MORE
C                              THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING
C                              MSTORE( , ). LATER USED AS A WAY OF
C                              DETERMINING WHETHER TO KEEP THIS
C                              VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ) THAT HAVE
C		        BEEN USED IN THIS RUN.  (INPUT)
C              LITEMS = NUMBER OF MOS FORECAST FIELDS HELD IN THE MOS 
C			INTERNAL STORAGE SYSTEM.  (INPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETIREVE THE DATA
C                       IDENTIFIED IN LSTORE( , ) (J=1,ND10). WHEN
C                       CORE( ) IS FULL DATA ARE STORED ON DISK.
C                       (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING  COUNT FROM THE BEGINNING OF
C                       THE PROGRAM.  THIS COUNT IS MAINTAINED IN
C                       CASE THE USER NEEDS IT (DIAGNOSTICS, ETC.).
C                       NEEDS IT (DIAGNOSTICS, ETC.).  (INTERNAL)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0, IS1, IS2, AND IS4. NOT ALL
C                       LOCATIONS ARE USED.  (INPUT)
C               ISTAV = 1 SINCE THE DATA RETURNED ARE STATION DATA.
C                       (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                        33 = ERROR CONVERTING STATION IDS
C                       103 = VARIABLE NOT ACCOMMODATED
C                       SEE GFETCH FOR OTHER VALUES.  (INTERNAL-OUTPUT)
C               NSLAB = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ) FOR THE FIRST FIELD.  THIS IS THE
C                       VALUE OF NSLAB RETURNED.  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED.  (OUTPUT)
C               LD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       DATA INGEST ARRAYS IN GFETCH (J=1,4).  
C			(INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  (INTERNAL)
C              NWORDS = NUMBER OF WORDS RETURNED IN RF DATA INGEST 
C			USED BY GFETCH.  (INTERNAL)
C               IX(N) = HOLDS X-COORDINATES OF MOS FORECAST GRIDPOINTS
C                       (N=1,ND1).  (ALLOCATED) 
C               JY(N) = HOLDS Y-COORDINATES OF MOS FORECAST GRIDPOINTS
C                       (N=1,ND1).  (ALLOCATED) 
C		   NX = NUMBER OF GRID POINTS IN THE X-DIRECTION.
C		        (PARAMETER)
C		   NY = NUMBER OF GRID POINTS IN THE Y-DIRECTION.
C		        (PARAMETER)
C          XDATA(I,J) = WORK ARRAY (I=1,NX,J=1,NY).  (AUTOMATIC)
C           GRID(I,J) = WORK ARRAY USED IN CALL TO SPATIAL SMOOTHER  
C			(I=1,NX,J=1,NY).  (AUTOMATIC)
C
C        NONSYSTEM SUBROUTINES CALLED
C            GFETCH, SMTH9V
C
      PARAMETER(NX=301, NY=225)
C
      CHARACTER*8 CCALLD(ND5)
C
      INTEGER IER,ISTAV,KK,
     1        KFILDO,KFIL10,
     2        LITEMS,L3264B,
     3        MISSP,MISSS,NX,NY,
     4        NSTA,ND1,ND2X3,ND7,ND9,ND10,
     5        NDATE,NBLOCK,NFETCH,NTIMES,NWORDS,NPACK,NSLAB
      INTEGER IDPARS(15),ID(4),JD(4),ICCCFFF(4,2)
      INTEGER IPACK(ND5),IWORK(ND5)
      INTEGER IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      INTEGER LSTORE(12,ND9)
C
      INTEGER, ALLOCATABLE, DIMENSION(:) :: IX
      INTEGER, ALLOCATABLE, DIMENSION(:) :: JY
C
      REAL CORE(ND10),SDATA(ND1)
      REAL FD1(ND2X3),FD2(ND2X3)
      REAL XDATA(NX,NY),GRID(NX,NY)
C        XDATA( , ) AND GRID( , ) ARE AUTOMATIC ARRAYS
C
      INTEGER LD(4)/4*0/,IENTRY/0/
C
      SAVE IX,JY,IENTRY
C
C23456789012345678901234567890123456789012345678901234567890123456789012
C
C        CHECK IF THIS CODE ACCOMMODATES MOS 3H TSTM PROBABILITY (DEC.)
C
      IF(IDPARS(1).NE.207.OR.IDPARS(2).NE.101)THEN  
         WRITE(KFILDO,100)(ID(J),J=1,2)
 100     FORMAT(/' **** IDPARS(1) AND IDPARS(2) = ',2I10,
     1           ' NOT ACCOMMODATED IN MOSTSPRP...SET IER = 103 AND ',
     2           'SUPPLY MISSING VALUES')
         IER=103
         GO TO 700
      ENDIF
C
C        INITIALIZATION  
C
      NTAUP=0
      DO 110 J=1,NY
      DO 110 I=1,NX
         XDATA(I,J)=9999.
 110  CONTINUE
      DO 115 K=1,ND1
         SDATA(K)=9999.
 115  CONTINUE
C
C        GET GRID COORDINATES FROM GRIDPOINT IDS AND SAVE THEM IN ALLO-
C        CATED ARRAYS ...DONE ONLY ON FIRST ENTRY.
C
      IF(IENTRY.EQ.0) THEN
         ALLOCATE(IX(ND1),STAT=IOS)
         ALLOCATE(JY(ND1),STAT=IOS)
         DO 120 N=1,NSTA
            READ(CCALLD(N),'(I4.4,4X)',ERR=600) IX(N)
            READ(CCALLD(N),'(4X,I4.4)',ERR=600) JY(N)
 120     CONTINUE
         IENTRY=1
      ENDIF
C
C        FETCH RAW MOS 3-H TSTM FORECAST THAT MATCHES LAMP VALID PERIOD.
C
      LD(1)=(ID(1)/1000-1)*1000+IDPARS(3)*100+IDPARS(4)
      LD(2)=ID(2)
      LD(4)=ID(4)
C
C	 THE "MATCH" BETWEEN A LAMP VALID PERIOD AND A MOS VALID PERIOD
C	 FALLS INTO ONE OF THREE CONDITIONS:
C 
C        1. THE ENDING OF 2-H LAMP VALID PERIOD EVENLY DIVISIBLE BY 3,
C           FETCH THE 3-H MOS VALID PERIOD THAT HAS SAME ENDING TIME.
C
C        2. THE BEGINNING OF 2-H LAMP VALID PERIOD EVENLY DIVISIBLE BY
C	    3, FETCH THE ORGINAL 3-H MOS THAT HAS SAME BEGINNING TIME.
C         
C        3. NEITHER BEGINNING OR ENDING OF 2-H LAMP VALID PERIOD
C           EVENLY DIVISIBLE BY 3, FETCH CONSECUTIVE MOS VALID PERIODS
C	    THAT BEGINNING AND ENDING OF LAMP PERIODS FALL IN, AND
C	    COMPUTE AVERAGE. 
C
      IF(MOD(IDPARS(12),3).EQ.0)THEN
         NTAU=IDPARS(12)
      ELSE IF(MOD(IDPARS(12)-2,3).EQ.0)THEN
         NTAU=IDPARS(12)+1
      ELSE
         NTAU=IDPARS(12)-MOD(IDPARS(12),3)
         NTAUP=NTAU+3
      ENDIF
C
      LD(3)=IDPARS(9)*1000000+NTAU
C
C        MOS PROBS, WHICH ARE VECTOR DATA, ARE FETCHED INTO FD1( ),
C        AN ARRAY USUALLY USED FOR GRIDDED DATA.
C
      CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD1,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3            NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,140) (LD(J),J=1,4),NDATE
 140     FORMAT(' IN MOSTSPRP, COULD NOT FETCH MOS PROBS FOR ',
     1          4I10,2X,I10,' ...SUPPLY MISSING VALUES.')
         GO TO 700
      ENDIF
C
C        TRUNCATE MOS PROBABILITY TO THE RANGE 0.0 TO 1.0 AND PUT INTO
C	 2-DIM. ARRAY.  ALSO SET 9997 TO 0.0.
C
      DO 150 N=1,NSTA
         IF(NINT(FD1(N)).EQ.9997) THEN
            FD1(N)=0.0
         ELSEIF(NINT(FD1(N)).NE.9999) THEN  
            IF(FD1(N).LT.0.0) FD1(N)=0.0
            IF(FD1(N).GT.1.0) FD1(N)=1.0
         ENDIF
         XDATA(IX(N),JY(N))=FD1(N)
 150  CONTINUE
C
C        IF AVERAGE OF TWO MOS FORECASTS IS NEEDED, FETCH SECOND MOS
C	 FORECAST ...AS ABOVE PUT INTO FD2( ), AN ARRAY USUALLY USED FOR
C        GRIDDED DATA.
C 
      IF(NTAUP.NE.0)THEN
         LD(3)=IDPARS(9)*1000000+NTAUP
         CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1               IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FD2,ND2X3,
     2               NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,NBLOCK,
     3               NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
C
         IF(IER.NE.0)THEN
            WRITE(KFILDO,155) (LD(J),J=1,4),NDATE
 155        FORMAT(' IN MOSTSPRP, COULD NOT FETCH MOS PROBS FOR ',
     1             4I10,2X,I10,' ...SUPPLY MISSING VALUES')
            GO TO 700
         ENDIF
C
C           TRUNCATE SECOND MOS PROBABILITY TO THE RANGE 0.0 TO 1.0 AS
C	    FOR FIRST FORECAST.
C
         DO 160 N=1,NSTA
            IF(NINT(FD2(N)).EQ.9997) THEN
               FD2(N)=0.0
            ELSEIF(NINT(FD2(N)).NE.9999) THEN  
               IF(FD2(N).LT.0.0) FD2(N)=0.0
               IF(FD2(N).GT.1.0) FD2(N)=1.0
            ENDIF
 160     CONTINUE
C
C           COMPUTE AVERAGE TWO OF CONSECUTIVE MOS FORECASTS AND PUT IN
C	    2-DIM. ARRAY.
C
         DO 170 N=1,NSTA
            IF(NINT(FD1(N)).NE.9999.AND.NINT(FD2(N)).NE.9999) THEN
               XDATA(IX(N),JY(N))=(FD1(N)+FD2(N))/2.
            ELSE
               XDATA(IX(N),JY(N))=9999.
            ENDIF
 170     CONTINUE
C
      ENDIF 
C
C        APPLY WEIGHTED SPATIAL SMOOTHING (9 POINTS) TO ABOVE MOS
C        FORECAST.
C
      CALL SMTH9V(XDATA,NX,NY,1.0,GRID,9998.5)
C
      DO 200 N=1,NSTA
         SDATA(N)=XDATA(IX(N),JY(N))
 200  CONTINUE
C
      ISTAV=1
      GO TO 900
C
 600  WRITE(KFILDO,601)  
 601  FORMAT('**** ERROR OCCURRED DURING CALL LETTER CONVERSION ',
     1       'IN MOSTSPRP ...SUPPLY MISSING VALUES AND SET IER = 33')
      IER=33
C
 700  DO 800 N=1,ND1
         SDATA(N)=9999.
 800  CONTINUE
C
 900  RETURN
      END
