      SUBROUTINE CONSTG(KFILDO,KFILRA,RACESS,ID,
     1                  IPACK,IWORK,DATA,ND5,
     2                  IS0,IS1,IS2,IS4,ND7,
     3                  ISTAV,L3264B,IER)
C 
C        SEPTEMBER 2000   GLAHN   MOS-2000
C        APRIL     2003   GLAHN   CORRECTED LIST OF NON-SYSTEM
C                                 ROUTINES CALLED; SPELLCHECK
C        OCTOBER   2004   GLAHN   ADDED UNIT RANGE CHECK 42-44
C                                 AND IER = 160 IF OUT OF RANGE
C
C        PURPOSE 
C            TO PROVIDE FOR MOS-2000 PROGRAMS GRIDPOINT CONSTANTS.
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                     (OUTPUT) 
C            KFILRA - THE UNIT NUMBER TO READ DATA FROM.  (INPUT)
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.
C                       (INPUT) 
C              KFILRA = THE UNIT NUMBER FOR WHICH THE RANDOM ACCESS
C                       FILE IS AVAILABLE.  WHILE ACCESS THROUGH
C                       CONST FROM U201 REQUIRES KFILRA = 44, 
C                       THERE IS NOTHING IN CONSTG THAT RESTRICTS
C                       THE UNIT NUMBER.  VECTOR ORIENTED PROGRAMS
C                       (E.G., U660) DO NOT ACCOMMODATE GRIDPOINT
C                       DATA, AND CONSTG WILL NOT BE NEEDED.  (INPUT)
C              RACESS = THE FILE NAME ASSOCIATED WITH KFILRA.
C                       (CHARACTER*60)  (INPUT)
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = ARRAY TO HOLD RETURNED DATA. (J=1,ND5).
C                       (OUTPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), DATA( ).
C                       THIS MUST BE LARGE ENOUGH TO HANDLE THE
C                       UNPACKED GRID.  (INPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).
C                       (OUTPUT)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).
C                       (OUTPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       (OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (OUTPUT)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C               ISTAV = 0 BECAUSE THE DATA RETURNED ARE GRID DATA OR DATA
C                       ARE NOT AVAILABLE FOR RETURN.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C                 IER = STATUS RETURN.
C                           0 = GOOD RETURN.
C                         160 = UNIT NUMBER NOT 42-44.
C                         OTHER VALUES FROM RDTDLM OR UNPACK
C                       (INTERNAL-OUTPUT)
C
C        NONSYSTEM SUBROUTINES USED 
C            RDTDLM, UNPACK
C
      CHARACTER*60 RACESS
C
      DIMENSION ID(4)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
C
D     CALL TIMPR(KFILDO,KFILDO,'START CONSTG        ')
C
      IER=0
      ISTAV=0
C        ISTAV = 0 INDICATES DATA ARE NOT AT STATIONS.
C
C        VERIFY UNIT NUMBER IS IN RANGE 42-44.
C
      IF(KFILRA.LT.42.OR.KFILRA.GT.44)THEN
         WRITE(KFILDO,105)KFILRA,(ID(J),J=1,4)
 105     FORMAT(/' ****UNIT NUMBER =',I4,' IN CONST1 IS NOT 42 TO 44.',
     1           '  READING DATA FOR ID = ',4I12,' NOT DONE.')
         IER=160
         GO TO 290
      ENDIF
C
C        RETRIEVE THE DATA AND UNPACK.
C
      CALL RDTDLM(KFILDO,KFILRA,RACESS,ID,IPACK,ND5,NVD,
     1               L3264B,IER)
C        NVD IS THE NUMBER OF DATA VALUES, AND IS NOT NEEDED.
C
D     WRITE(KFILDO,110)IER
D110  FORMAT(/' RETURN FROM RDTDLM IN CONSTG--IER',I5)
D     WRITE(KFILDO,111)ND7,ND5
D111  FORMAT(/' AT 111 IN CONSTG--ND7,ND5',2I10)
C
      IF(IER.NE.0)GO TO 290
C
      IGIVE=2
C        IGIVE IN CALL TO UNPACK MEANS TO GET DATA AS
C        WELL AS FILL THE ISX( ) ARRAYS.
      CALL UNPACK(KFILDO,IPACK,IWORK,DATA,ND5,
     1            IS0,IS1,IS2,IS4,ND7,MISSP,MISSS,
     2            IGIVE,L3264B,IER)
C
D     WRITE(KFILDO,150)(IS2(J),J=1,ND7)
D150  FORMAT(' IN CONSTG AT 150--IS2( )'/(10I10))
C
D     WRITE(KFILDO,160)IER
D160  FORMAT(/' RETURN FROM UNPACK IN CONSTG--IER',I5)
D     WRITE(KFILDO,161)(DATA(JJ),JJ=1,IS2(3)*IS2(4))
D161  FORMAT(10F10.2)
C
      IF(IER.EQ.0)GO TO 300
C
C        SET VALUES TO MISSING WHEN THERE IS AN ERROR.  THIS
C        IS FOR SAFETY.  A DIAGNOSTIC WILL HAVE BEEN PRINTED
C        BY UNPACK.
C
 290  DO 295 K=1,ND5
         DATA(K)=9999.
 295  CONTINUE
C
 300  RETURN
      END
