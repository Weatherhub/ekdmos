      SUBROUTINE CONST1(KFILDO,KFIL10,IP12,
     1                  ID,IDPARS,JD,NDATE,
     2                  KFILRA,RACESS,NUMRA,
     3                  CCALL,ICALLD,CCALLD,NAME,STALAT,STALON,
     4                  ISDATA,SDATA,DIR,ND1,NSTA,
     5                  NGRIDC,NGRID,ND11,NSLAB,
     6                  IPACK,IWORK,DATA,ND5,
     7                  LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     8                  NBLOCK,LASTD,NSTORE,NFETCH,
     9                  IS0,IS1,IS2,IS4,ND7,
     A                  ISTAV,L3264B,L3264W,IER)
C 
C        NOVEMBER  1996   GLAHN   TDL   MOS-2000 
C        JUNE      1997   GLAHN   SLIGHT CHANGE TO COMMENTS, NOT CODE
C                                 D COMPILE OPTION REMOVED OR COMMENTED OUT
C        JANUARY   1998   GLAHN   ADDED SUBSTITUTE STATIONS IN CCALL( , );
C                                 MODIFIED DIAGNOSTICS
C        MAY       1998   GLAHN   ADDED ITIMEZ( ) 
C        SEPTEMBER 1998   GLAHN   MODIFIED PRINT WHEN NUMRA = 0 AT 111
C        OCTOBER   1998   GLAHN   REDUCED CALL SEQUENCE AND CALLED FINDST
C        NOVEMBER  1998   GLAHN   CHANGED DIMENSIONS OF CCALLD( )
C                                 AND ICALLD( , ) FROM ND1 TO ND5;
C                                 ELIMINATED SDATA1(ND1)
C        NOVEMBER  1998   GLAHN   OMITTED PRINT TO IP16 EXCEPT IN FINDST
C        NOVEMBER  1998   GLAHN   ADDED IER RETURN FROM FINDST
C        DECEMBER  1998   GLAHN   NCOMBO CHANGED FROM 9999 TO 999;
C                                 CHECK MSTA = NVALUE REMOVED
C        JULY      1999   GLAHN   ADDED IP12; DELETED IP16;
C                                 HARDWIRED UNIT NUMBERS TO CCC
C        AUGUST    1999   GLAHN   CHANGED IP16 IN CALL TO FINDST TO IP12
C        AUGUST    1999   GLAHN   ELIMINATED CALL TO COMPID FOR CCC
C                                 NE 4XX
C        MARCH     2000   DALLAVALLE   MODIFIED FORMAT STATEMENTS TO
C                                 CONFORM TO FORTRAN 90 STANDARDS 
C                                 ON IBM SP
C        MAY       2000   GLAHN   CORRECTED COMMENT FOR JD( ); SPELLING
C        MAY       2000   GLAHN   ELIMINATED DIAGNOSTIC WHEN NUMRA = 0
C        SEPTEMBER 2000   GLAHN   ADDED CALL TO CONSTG WHEN KFILRA = 44;
C                                 CHANGED DIMENSION OF KFILRA TO NUMRA;
C                                 DEFINED IGIVE, USED IT IN CALL TO UNPACK
C        FEBRUARY  2002   GLAHN   CHANGED DIMENSION OF RACESS FROM 5 TO
C                                 NUMRA
C        MAY       2002   GLAHN   ADDED TO COMMENT FOR SDATA( )
C        MAY       2002   GLAHN   MODIFIED CALL AND CALL TO CONSTG;
C                                 ADDED CALL TO GRCOMB
C        FEBRUARY  2003   GLAHN   ADDED TO ARGUMENTS; CHANGED NAME TO
C                                 FROM CONST TO CONST1 TO BE CALLED FROM
C                                 OPTION; ADDED CALL TO GRCOMB
C        APRIL     2003   GLAHN   MOD TO TRY TO GET VECTOR CONSTANT DATA
C                                 WHEN GRID FETCH FAILED FOR UNIT NO. 44;
C                                 CALL TO CONST FOR VECTOR DATA; COMMENTS 
C        MARCH     2005   JPD     MODIFIED TO ALLOW UNIT NUMBERS 42 AND
C                                 43 TO BE USED FOR GRIDDED RANDOM ACCESS
C                                 FILES.  ALSO MODIFIED SO THAT 
C                                 GRIDDED MOS FORECASTS COULD BE READ.
C                                 AT THIS TIME, NO RESTRICTION WAS PLACED
C                                 ON UNIT NUMBERS 42-44, THAT IS, 
C                                 GRIDDED DATA COULD BE READ FROM ANY
C                                 OF THE THREE FILES.
C
C
C        PURPOSE 
C            TO PROVIDE FOR MOS-2000 PROGRAMS CONSTANTS, FORECASTS,
C            AND OTHER DATA THAT ARE AVAILABLE IN MOS-2000 EXTERNAL
C            RANDOM ACCESS FILES.  NORMALLY, CONST1 IS CALLED BY U201 TYPE
C            PROGRAMS (WHICH CAN PROCESS GRIDDED AS WELL AS VECTOR DATA),
C            BUT CONST IS CALLED FOR OTHER PURELY VECTOR PROGRAMS.  FOR
c            CONSTANTS IN CCC = 4XX SERIES, INTERPOLATION MAY BE DONE
c            TO GET THE DATE WANTED.  THE CCC IN THE MOS-2000 ID 
c            DETERMINES THE UNIT NUMBER:
C
C               CCC RANGE      KFILX      USE          TYPE OF DATA
C                NO LIMIT       42       OUTPUT/INPUT  GRIDPOINT
C                NO LIMIT       43       INPUT         GRIDPOINT
C                NO LIMIT       44       INPUT         GRIDPOINT
C                400-499        45       INPUT         VECTOR
C                500-699        46       INPUT         VECTOR
C                800-899        47       INPUT         VECTOR
C                200-299        48       INPUT         VECTOR
C                200-299        49       OUTPUT/INPUT  VECTOR
C
C            THE FOLLOWING 4XX CONSTANTS ARE ACCOMMODATED IN CONST AND
C            COMPID:
C               40XXXX = TRULY CONSTANT VALUES; DO NOT DEPEND ON TIME
C               41XXXX = DAILY VALUES; NO INTERPOLATION
C               422XXX = 5-DAY VALUES (JAN 5, JAN 10, ... 
C                        DEC 31 = 365TH DAY); INTERPOLATION ASSUMED
C               43XXXX = MONTHLY VALUES; INTERPOLATION ASSUMED
C               44XXXX = 6-MONTH SEASONS (APRIL-SEPTEMBER, ETC);
C                        NO INTERPOLATION
C               45XXXX = 3-MONTH SEASONS (MARCH-MAY, JUNE-AUGUST, ETC.);
C                        NO INTERPOLATION
C               46XXXX = YEARLY VALUES
C               47XXXX = MONTHLY VALUES; NO INTERPOLATION
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C            IP12   - INDICATES WHETHER (>1) OR NOT (=0) THE LIST OF
C                     STATIONS ON THE INPUT FILES WILL BE PRINTED TO 
C                     THE FILE WHOSE UNIT NUMBER IS IP12.
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C                IP12 = INDICATES WHETHER (>0) OR NOT (=0) THE LIST OF
C                       STATIONS ON THE EXTERNAL RANDOM ACCESS FILES
C                       WILL BE LISTED TO UNIT IP12.  (INPUT)
C               ID(J) = THE PREDICTOR ID (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID( ) (J=1,15).  (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C             JD(J,N) = THE BASIC INTEGER VARIABLE ID (J=1,4) (N=1,NPRED).
C                       THIS IS THE SAME AS ID(J,N), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3, ),
C                       T = IDPARS(8, ),
C                       I = IDPARS(13, ),
C                       S = IDPARS(14, ),
C                       G = IDPARS(15, ), AND
C                       THRESH( ).
C                       JD( , ) IS USED TO FOR INPUT TO CONSTG, BECAUSE
C                       INTERPOLATION INTO THE GRID MAY BE REQUIRED,
C                       BUT IS NOT PART OF THE BASIC ID.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C           KFILRA(J) = THE UNIT NUMBERS FOR WHICH RANDOM ACCESS FILES
C                       ARE AVAILABLE (J=1,NUMRA).  (INPUT)
C           RACESS(J) = THE FILE NAMES ASSOCIATED WITH KFILRA(J) (J=1,NUMRA).
C                       (CHARACTER*60)  (INPUT)
C               NUMRA = THE NUMBER OF VALUES IN KFILRA( ) AND RACESS( ).
C                       (INPUT)
C          CCALL(K,J) = 8-CHARACTER STATION CALL LETTERS (OR GRIDPOINT
C                       LOCATIONS FOR GRID DEVELOPMENT) TO PROVIDE
C                       OUTPUT FOR (J=1) AND 5 POSSIBLE OTHER STATION
C                       CALL LETTERS (J=2,6) THAT CAN BE USED INSTEAD
C                       IF THE PRIMARY (J=1) STATION CANNOT BE FOUND 
C                       IN AN INPUT DIRECTORY (K=1,NSTA).  ALL STATION
C                       DATA ARE KEYED TO THIS LIST, EXCEPT POSSIBLY 
C                       CCALLD( ).  EQUIVALENCED TO ICALL( , , ). 
C                       (CHARACTER*8)  (INPUT)
C         ICALLD(L,K) = 8 STATION CALL LETTERS AS CHARACTERS IN AN INTEGER
C                       VARIABLE (L=1,L3264W) (K=1,ND5).  THIS ARRAY IS USED 
C                       TO READ THE STATION DIRECTORY FROM A MOS-2000
C                       EXTERNAL FILE.  EQUIVALENCED TO CCALLD( ). 
C                       (CHARACTER*8)  (INTERNAL)
C           CCALLD(K) = 8 STATION CALL LETTERS (K=1,ND5).  THIS ARRAY IS USED 
C                       IN CONST TO READ THE STATION DIRECTORY.  EQUIVALENCED 
C                       TO ICALLD( , ).  (CHARACTER*8)  (INTERNAL)
C             NAME(K) = NAMES OF STATIONS (K=1,NSTA).  USED FOR PRINTOUT
C                       ONLY.  (CHARACTER*20)  (INPUT)
C           STALAT(K) = LATITUDE OF STATIONS (K=1,NSTA).  (INPUT/OUTPUT)
C           STALON(K) = LONGITUDE OF STATIONS (K=1,NSTA).  (INPUT/OUTPUT)
C           ISDATA(K) = WORK ARRAY (K=1,ND1).  (INTERNAL)
C            SDATA(K) = CONSTANT DATA RETURNED WHEN DATA ARE VECTOR
C                       (K=1,NSTA).  (OUTPUT)
C          DIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE GRID
C                       FOR THE COMBINATION OF GRID CHARACTERISTICS M
C                       (M=1,NGRID) AND STATION K (K=1,NSTA) IN NGRIDC( ,M).
C                       (INPUT/OUTPUT)
C                 ND1 = MAXIMUM NUMBER OF STATIONS THAT CAN BE DEALT WITH.
C                       DIMENSION OF SEVERAL VARIABLES.  (INPUT)
C                NSTA = NUMBER OF STATIONS OR LOCATIONS BEING DEALT WITH.
C                       (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C                       (INPUT/OUTPUT)
C               NGRID = THE NUMBER OF GRID COMBINATIONS IN DIR( , , ),
C                       MAXIMUM OF ND11.  (INPUT/OUTPUT)
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ) AND DIR( , , ).  (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , , ) AND
C                       IN NGRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID.  SEE LSTORE(10, ).  FOR THE
C                       COMPUTATION ROUTINES RETURNING A GRID, THIS
C                       VALUE MUST BE OUTPUT BY GFETCH.  (OUTPUT) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = ARRAY TO HOLD RETURNED DATA WHEN THE DATA ARE
C                       AT GRIDPOINTS. (J=1,ND5).  (OUTPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), DATA( ) AND
C                       CCALLD( ).  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (OUTPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C               LASTL = THE LAST LOCATION IN CORE( ) USED FOR MOS-2000 INTERNAL
C                       STORAGE.  INITIALIZED TO 0 ON FIRST ENTRY TO GSTORE.
C                       ALSO INITIALIZED IN U201 IN CASE GSTORE IS NOT ENTERED.
C                       MUST BE CARRIED WHENEVER GSTORE IS TO BE CALLED.
C                       (INPUT/OUTPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C               LASTD = TOTAL NUMBER OF PHYSICAL RECORDS ON DISK FOR MOS-2000
C                       INTERNAL STORAGE.  MUST BE CARRIED WHENEVER GSTORE
C                       IS TO BE CALLED.  (INPUT)
C              NSTORE = THE NUMBER OF TIMES GSTORE HAS BEEN ENTERED.  GSTORE
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              NFETCH = THE NUMBER OF TIMES GFETCH HAS BEEN ENTERED.  GFETCH
C                       KEEPS TRACK OF THIS AND RETURNS THE VALUE.  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C               ISTAV = 1 WHEN THE DATA RETURNED ARE STATION DATA.  
C                       0 WHEN THE DATA RETURNED ARE GRID DATA OR DATA
C                         ARE NOT AVAILABLE FOR RETURN.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING USED
C                       (EITHER 32 OR 64).  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS, EITHER 1 OR 2.  (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       SEE CALLED ROUTINES FOR OTHER VALUES.
C                       (INTERNAL-OUTPUT)
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES USED 
C            CONSTG, GRCOMB, CONST
C
      CHARACTER*8 CCALL(ND1,6),
     1            CCALLD(ND5)
      CHARACTER*20 NAME(ND1)
      CHARACTER*60 RACESS(NUMRA)
C
      DIMENSION ISDATA(ND1),SDATA(ND1),STALAT(ND1),STALON(ND1)
      DIMENSION DIR(ND1,2,ND11),NGRIDC(6,ND11)
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5),ICALLD(L3264W,ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION KFILRA(NUMRA)
C
D     WRITE(KFILDO,100)
D100  FORMAT(/,' ENTERING CONST1')
C
      IER=0
C
C        FIND THE FILE UNIT NUMBER AND NAME BY ASSOCIATION
C        WITH CCC.  THESE VALUES ARE HARDWIRED.
C 
      DO 105 J=1,NUMRA
C
C        FOR NOW, THIS IF TEST WAS MODIFIED TO ALLOW GRIDDED
C        CONSTANT DATA TO BE READ FROM UNITS 42, 43, OR 44,
C        AND TO ALLOW ANY ID'S TO BE READ FROM THE GRIDDED
C        FILES.
C
C     IF(KFILRA(J).EQ.44.AND.
C    1   IDPARS(1).GE.400.AND.
C    2   IDPARS(1).LE.499)THEN
C
      IF(KFILRA(J).EQ.42.OR.KFILRA(J).EQ.43.OR.
     1   KFILRA(J).EQ.44)THEN
         CALL CONSTG(KFILDO,KFILRA(J),RACESS(J),JD,
     1               IPACK,IWORK,DATA,ND5,
     2               IS0,IS1,IS2,IS4,ND7,
     3               ISTAV,L3264B,IER)
         ISTAV=0
C           ISTAV = 0 MEANS THE DATA ARE AT GRIDPOINTS OR
C           ARE UNAVAILABLE IF IER NE 0.
         IF(IER.EQ.0)THEN
            CALL GRCOMB(KFILDO,IP12,IS2,ND7,NGRIDC,ND11,NGRID,NSLAB,
     1                  CCALL,NAME,STALAT,STALON,DIR,ND1,NSTA,IER)
C              UPON RETURN FROM GRCOMB, NSLAB IS THE NUMBER OF THE GRID
C              COMBINATION IN NGRIDC OF THE GRID TO STORE.
C              ON SUBSEQUENT ENTRIES TO GRCOMB (E.G., IN PRED22),
C              CHARACTERISTICS OF A NEW GRID WILL BE PRINTED.
C              CCALL IN GRCOMB IS SINGLE DIMENSIONED; THIS IS OK.
C              DIR(K,J,NSLAB) IS FILLED WITH STATION LOCATIONS 
C              ON THE GRID (K=1,NSTA) (J=1,2).  IF THE GRID'S MAP
C              PROJECTON IS NOT RECOGNIZED, THE VALUES ARE 9999;
C              THIS MAY BE OK, SO DON'T CHECK IER.
         ELSE
            GO TO 105
C              IF THE ID IS NOT FOUND BY CONSTG, THE CONSTANT
C              MAY BE A VECTOR, SO LET THE SEARCH PROCEED.
C              NOTE THAT THERE IS NO WAY UPON ENTRY TO KNOW WHETHER
C              A VARIABLE IS A GRID OR A VECTOR, AND THE
C              UNIT TO USE IS NOT KNOWN.  SO, IF UNITS 42, 43, OR 44
C              ARE AVAILABLE, TRY CCC FOR A GRID.  THIS ARRANGEMENT
C              ALLOWS FOR EITHER GRID OR VECTOR CONSTANTS OR 
C              GRID OR VECTOR MOS FORECASTS OR ANY COMBINATION THEREOF
C              IN THE SAME RUN.  PROBABLY WOULD ALLOW MORE THAN
C              ONE UNIT NO. 44, EXCEPT U201 WOULD HAVE PROBABLY
C              CHECKED THAT AND NOT ALLOWED IT; THIS COULD BE 
C              CHANGED IF NEEDED.
         ENDIF
C
         GO TO 310
C           NOTE THAT A RETURN OF IER NE 0 MAY BE MADE.  IN THIS CASE,
C           CONSTG WILL HAVE RETURNED MISSING = 9999 IN DATA( ).
      ENDIF
C
 105  CONTINUE
C
C        NO UNIT NUMBER MATCHES WITH CCC IN ID.
C
      CALL CONST(KFILDO,KFIL10,IP12,
     1           ID,IDPARS,JD,NDATE,
     2           KFILRA,RACESS,NUMRA,
     3           CCALL,ICALLD,CCALLD,
     4           ISDATA,SDATA,ND1,NSTA,
     5           IPACK,IWORK,DATA,ND5,
     6           LSTORE,ND9,LITEMS,CORE,ND10,LASTL,
     7           NBLOCK,LASTD,NSTORE,NFETCH,
     8           IS0,IS1,IS2,IS4,ND7,
     9           ISTAV,L3264B,L3264W,IER)
C
C        FOR IER NE 0, THE DATA ARRAY SDATA( ) WILL BE SET TO
C        MISSING AND ISTOP INCREMENTED IN PRED1 OR PRED2.
C        IF DATA ARE GOOD, IER IS RETURNED AS FROM FINDST,
C        WHICH WHEN NOT ZERO MEANS ONE OR MORE STATIONS
C        COULD NOT BE FOUND.
C
 310  RETURN
      END
