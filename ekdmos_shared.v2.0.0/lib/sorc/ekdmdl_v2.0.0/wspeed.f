      SUBROUTINE WSPEED(KFILDO,KFIL10,IDPARS,JD,NDATE,
     1                  NGRIDC,ND11,NSLAB,IPACK,IWORK,SPEED,ND5,
     2                  LSTORE,ND9,LITEMS,CORE,ND10,NBLOCK,NFETCH,
     3                  IS0,IS1,IS2,IS4,ND7,
     4                  FDU,FDV,ND2X3,
     5                  ISTAV,L3264B,MISTOT,IER)
C
C        FEBRUARY  1995   GLAHN    TDL   MOS-2000
C        AUGUST    1996   GLAHN   ADDED MISSS AND MISTOT 
C        APRIL     1997   FIEBRICH COMMENTED OUT "D"S IN COLUMN 1 FOR
C                                 SUCCESSFUL COMPILE ON CRAY
C        OCTOBER   1997   FIEBRICH ADDED CAPABILITY TO COMPUTE WSPEED 
C                                 AT 10 M
C        JANUARY   1998   GLAHN   REMOVED TEST ON GRID SIZE AFTER 200
C        APRIL     1999   GLAHN   ITABLE( , ) ADDED.
C        MAY       2002   GLAHN   CHANGED ND5 TO ND2X3 IN CALLS TO
C                                 GFETCH
C        OCTOBER   2002   WEISS   CHANGED ND5 TO ND2X3 IN IPACK, IWORK
C                                 AND SPEED
C        MAY       2003   GLAHN   SET DIMENSION OF IPACK( ), IWORK( ),
C                                 SPEED( ) = ND5; REARRANGED DIMENSION
C                                 STATEMENTS
C        SEPTEMBER 2003   GLAHN   ADDED SIGMA LEVEL CAPABILITY
C        OCTOBER   2003   SMB     CORRECTED ID TABLE SO AS TO COMPUTE
C                                 SIGMA LAYER WINDS; USED TO COMPUTE
C                                 WSPEED IN THE BOUNDARY LAYER (30 MB
C                                 DEPTH)--ALSO ADDED BY TRIMARCO
C                                 IN JULY 2004.  VARIOUS VERSIONS
C                                 WERE MERGED INTO ONE.
C        MAY       2006   JPD     ADDED CAPABILITY TO COMPUTE THE
C                                 WIND SPEED IN KNOTS AT THE 
C                                 10-M LEVEL FROM MODEL U AND V
C                                 COMPONENTS
C
C        PURPOSE 
C            TO COMPUTE WIND SPEED IN M/SEC FROM U AND V WINDS.
C            THE SINGLE LEVEL IS CONTAINED IN JD(2).  RUN TIME
C            OFFSET RR, TAU, AND OH ARE ACCOMMODATED.  SINCE
C            THERE ARE NO SPATIAL RELATIONSHIPS INVOLVED, THE 
C            COMPUTATION CAN BE IN A LINEAR ARRAY.
C
C            THE FOLLOWING IDPARS(1) AND IDPARS(2) ARE ACCOMMODATED:
C
C               004 210 - WIND SPEED FROM U- AND V-WINDS (PRESSURE)
C               004 211 - WIND SPEED FROM U- AND V-WINDS (HEIGHT)
C               004 216 - WIND SPEED FROM U- AND V-WINDS (SIGMA)
C               004 221 - WIND SPEED FROM U- AND V-WINDS (HEIGHT) AND
C                         CONVERTED FROM M/SEC TO KNOTS
C
C        DATA SET USE 
C            KFILDO - DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT) 
C            KFIL10 - UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                     (INPUT-OUTPUT) 
C 
C        VARIABLES 
C              KFILDO = DEFAULT UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT) 
C              KFIL10 = UNIT NUMBER OF TDL MOS-2000 FILE SYSTEM ACCESS.
C                       (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID CORRESPONDING TO ID( ) (J=1,15).
C                       (INPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C               JD(J) = THE BASIC INTEGER PREDICTOR ID (J=1,4).
C                       THIS IS THE SAME AS ID(J), EXCEPT THAT THE PORTIONS
C                       PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3),
C                       T = IDPARS(8),
C                       I = IDPARS(13),
C                       S = IDPARS(14),
C                       G = IDPARS(15), AND
C                       THRESH.
C                       JD( ) IS USED TO HELP IDENTIFY THE BASIC MODEL FIELDS
C                       AS READ FROM THE ARCHIVE.  (INPUT)
C               NDATE = THE DATE/TIME FOR WHICH PREDICTOR IS NEEDED.  (INPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS (L=1,6) FOR EACH GRID
C                       COMBINATION (M=1,NGRID).
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC). 
C                       L=2--GRID LENGTH IN METERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *1000,
C                       L=4--GRID ORIENTATION IN DEGREES *1000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *1000,
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *1000.
C                ND11 = MAXIMUM NUMBER OF GRID COMBINATIONS THAT CAN BE
C                       DEALT WITH ON THIS RUN.  LAST DIMENSION OF
C                       NGRIDC( , ).  (INPUT)
C               NSLAB = THE NUMBER OF THE SLAB IN DIR( , , ) AND
C                       IN NGRIDC( , ) DEFINING THE CHARACTERISTICS
C                       OF THIS GRID.  (OUTPUT) 
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            SPEED(K) = WIND SPEED IN M/SEC CALCULATED FROM U AND V WINDS
C                       (J=1,ND5).  SINCE THERE ARE NO SPATIAL
C                       RELATIONSHIPS INVOLVED, THE COMPUTATION CAN 
C                       BE IN A LINEAR ARRAY. UPON RETURN, THE ARRAY
C                       WILL BE NX X NY.  (OUTPUT)
C                 ND5 = DIMENSION OF IPACK( ),IWORK( ), AND SPEED( ).
C                       (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT-OUTPUT)
C                       L=1,4--THE 4 ID'S FOR THE DATA.
C                       L=5  --LOCATION OF STORED DATA.  WHEN IN CORE,
C                              THIS IS THE LOCATION IN CORE( ) WHERE
C                              THE DATA START.  WHEN ON DISK, 
C                              THIS IS MINUS THE RECORD NUMBER WHERE 
C                              THE DATA START.
C                       L=6  --THE NUMBER OF 4-BYTE WORDS STORED.
C                       L=7  --2 FOR DATA PACKED IN TDL GRIB, 1 FOR NOT.
C                       L=8  --THE DATE/TIME OF THE DATA IN FORMAT
C                              YYYYMMDDHH.
C                       L=9  --NUMBER OF TIMES DATA HAVE BEEN RETRIEVED.
C                       L=10 --NUMBER OF THE SLAB IN DIR( , ,L) AND
C                              IN NGRIDC( ,L) DEFINING THE CHARACTERISTICS
C                              OF THIS GRID.
C                       L=11 --THE NUMBER OF THE PREDICTOR IN THE SORTED
C                              LIST IN ID( ,N) (N=1,NPRED) FOR WHICH THIS
C                              VARIABLE IS NEEDED, WHEN IT IS NEEDED ONLY
C                              ONCE FROM LSTORE( , ).  WHEN IT IS NEEDED
C                              MORE THAN ONCE, THE VALUE IS SET = 7777.
C                       L=12 --USED INITIALLY IN ESTABLISHING MSTORE( , ).
C                              LATER USED AS A WAY OF DETERMINING WHETHER
C                              TO KEEP THIS VARIABLE.
C                 ND9 = THE SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS (COLUMNS) IN LSTORE( , ) THAT 
C                       HAVE BEEN USED IN THIS RUN.  (INPUT)
C             CORE(J) = THE ARRAY TO STORE OR RETRIEVE THE DATA IDENTIFIED IN
C                       LSTORE( , ) (J=1,ND10).  WHEN CORE( ) IS FULL
C                       DATA ARE STORED ON DISK.  (INPUT)
C                ND10 = DIMENSION OF CORE( ).  (INPUT)
C              NBLOCK = THE BLOCK SIZE IN WORDS OF THE MOS-2000 RANDOM
C                       DISK FILE.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME GFETCH IS ENTERED.
C                       IT IS A RUNNING  COUNT FROM THE BEGINNING OF THE 
C                       PROGRAM.  THIS COUNT IS MAINTAINED IN CASE THE USER 
C                       NEEDS IT (DIAGNOSTICS, ETC.).  (OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       IS2(3) AND IS2(4) ARE USED BY THE CALLING
C                       PROGRAM AS THE GRID DIMENSIONS.  (INTERNAL-OUTPUT)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       NOT ALL LOCATIONS ARE USED.  (INPUT)
C              FDU(K) = WORK ARRAY TO HOLD THE U WINDS IN M/SEC 
C                       (K=1,ND2X3).  (INTERNAL)
C              FDV(K) = WORK ARRAY TO HOLD THE V WINDS IN M/SEC
C                       (K=1,ND2X3).  (INTERNAL)
C               ND2X3 = DIMENSION OF SEVERAL VARIABLES.  THE SIZE OF THE
C                       GRID IS NOT KNOWN BEFORE THE U AND V WINDS ARE
C                       FETCHED.  (INPUT)
C               ISTAV = 0 SINCE THE DATA RETURNED ARE GRID DATA.  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING USED
C                       (EITHER 32 OR 64).  (INPUT)
C              MISTOT = TOTAL NUMBER OF TIMES A MISSING INDICATOR
C                       HAS BEEN ENCOUNTERED IN UNPACKING GRIDS.
C                       (INPUT-OUTPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN.
C                       100 = THE TWO GRIDS NEEDED ARE NOT THE SAME SIZE.
C                       103 = IDPARS(1) AND IDPARS(2) DO NOT INDICATE 
C                             WIND SPEED.
C                       SEE GFETCH FOR OTHER VALUES.
C                       WHEN IER NE 0, DATA ARE RETURNED AS MISSING.
C                       (INTERNAL-OUTPUT)
C              NSLABU = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ) FOR THE U WIND.  THIS IS THE
C                       VALUE OF NSLAB RETURNED.  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED.  (INTERNAL) 
C              NSLABV = RETURNED FROM GFETCH AS THE VALUE STORED IN
C                       LSTORE(10, ) FOR THE V WIND.  WHEN IER NE 0, THIS
C                       VALUE SHOULD NOT BE USED.  (INTERNAL) 
C              NTIMES = THE NUMBER OF TIMES, INCLUDING THIS ONE, THAT THE 
C                       RECORD HAS BEEN FETCHED.  THIS IS STORED IN
C                       LSTORE(9, ).  (INTERNAL)
C               LD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       FDU( ) (J=1,4).  (INTERNAL)
C               MD(J) = HOLDS THE 4 ID WORDS OF THE DATA RETRIEVED INTO
C                       FDV( ) (J=1,4).  (INTERNAL)
C               MISSP = PRIMARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (INTERNAL)
C               MISSS = SECONDARY MISSING VALUE INDICATOR.  RETURNED AS ZERO
C                       WHEN DATA ARE NOT PACKED.  (INTERNAL)
C         ITABLE(I,J) = CCCFFF OF THE SPEED (I=1) AND THE NEEDED
C                       U COMPONENT (I=2) AND V COMPONENT (I=3) FOR 
C                       CONSTANT PRESSURE (J=1) AND CONSTANT HEIGHT (J=2).
C                       (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES CALLED 
C            GFETCH
C
      DIMENSION IDPARS(15),JD(4)
      DIMENSION FDU(ND2X3),FDV(ND2X3)
      DIMENSION IPACK(ND5),IWORK(ND5),SPEED(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION NGRIDC(6,ND11)
      DIMENSION LD(4),MD(4)
      DIMENSION ITABLE(3,4)
C
      DATA ITABLE/004210, 004000, 004100,
     1            004211, 004020, 004120,
     2            004216, 004070, 004170,
     3            004221, 004020, 004120/
C
CD     CALL TIMPR(KFILDO,KFILDO,'START WSPEED        ')
C
      IER=0
      ISTAV=0
C
C        FIND THE LOCATION IN THE TABLE.
C
      DO 105 JJ=1,4
      IF(ITABLE(1,JJ).EQ.IDPARS(1)*1000+IDPARS(2))GO TO 115
 105  CONTINUE
C
      WRITE(KFILDO,110)(JD(L),L=1,4)
 110  FORMAT(/' ****WSPEED ENTERED FOR PREDICTOR',
     1          2X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,
     2        ' NOT ACCOMMODATED.')
      IER=103
      GO TO 800
C
C        GET THE U WINDS.  THE ID INCLUDES THE RUN OFFSET TIME.
C
 115  LD(1)=ITABLE(2,JJ)*1000+IDPARS(4)
      LD(2)=JD(2)
      LD(3)=IDPARS(9)*1000000+IDPARS(12)+IDPARS(11)
      IF(IDPARS(10).GT.4)LD(3)=IDPARS(12)
C        THE USE OF IDPARS(10) AND IDPARS(11) IN THE ABOVE 2 
C        STATEMENTS IS NECESSARY WHEN COMPUTING WIND SPEED
C        FOR SUBROUTINE TIMEP.  ONLY WHEN TIMEP IS USED WILL
C        IDPARS(10) AND IDPARS(11) NOT EQUAL ZERO.  THIS
C        ACCOMMODATES RUN OFFSET RR, TAU, AND OH.
      LD(4)=0
      CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDU,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLABU,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
      IF(IER.NE.0)GO TO 800
      NX=IS2(3)
      NY=IS2(4)
C
C        GET THE V WINDS.  THE ID INCLUDES THE RUN OFFSET TIME.
C
      MD(1)=ITABLE(3,JJ)*1000+IDPARS(4)
      MD(2)=JD(2)
      MD(3)=IDPARS(9)*1000000+IDPARS(12)+IDPARS(11)
      IF(IDPARS(10).GT.4)MD(3)=IDPARS(12)
C        THE USE OF IDPARS(10) AND IDPARS(11) IN THE ABOVE 2 
C        STATEMENTS IS NECESSARY WHEN COMPUTING WIND SPEED
C        FOR SUBROUTINE TIMEP.  ONLY WHEN TIMEP IS USED WILL
C        IDPARS(10) AND IDPARS(11) NOT EQUAL ZERO.  THIS
C        ACCOMMODATES RUN OFFSET RR, TAU, AND OH.
      MD(4)=0
      CALL GFETCH(KFILDO,KFIL10,MD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,FDV,ND2X3,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLABV,MISSP,MISSS,L3264B,1,IER)
      IF(MISSP.NE.0)MISTOT=MISTOT+1
      IF(IER.NE.0)GO TO 800
      IF(NSLABU.EQ.NSLABV.AND.NX.EQ.IS2(3).AND.NY.EQ.IS2(4))GO TO 200
C        THE GRID CHARACTERISTICS ARE NOT THE SAME.
      WRITE(KFILDO,150)(LD(J),J=1,4),(NGRIDC(J,NSLABU),J=1,6),NX,NY,
     1                 (MD(J),J=1,4),(NGRIDC(J,NSLABV),J=1,6),
     2                 IS2(3),IS2(4)
 150  FORMAT(/' ****THE TWO GRIDS NEEDED IN WSPEED HAVE DIFFERENT',
     1        ' CHARACTERISTICS.  PREDICTOR NOT COMPUTED.',
     2        '  VALUES FROM NGRIDC( , ) AND NX, NY.'/
     3        (5X,I9.9,I10.9,I10.9,I4.3,4X,6I10,4X,2I5))
      IER=100
      GO TO 800
C
 200  NSLAB=NSLABU
C
C        COMPUTE THE WIND SPEED.  SINCE THERE ARE NO SPATIAL
C        RELATIONSHIPS INVOLVED, THE COMPUTATIONS CAN BE IN THE LINEAR
C        ARRAYS.
C
      DO 220 IXJY=1,NX*NY
      SPEED(IXJY)=SQRT(FDU(IXJY)*FDU(IXJY)+FDV(IXJY)*FDV(IXJY))
 220  CONTINUE
C
C        CONVERT TO KTS IF NECESSARY.  CONVERSION IS 1.9425 KTS
C        PER M/S AS PER MOS-2000 NOTEBOOK, CHAPTER 3.
C
      IF(IDPARS(2).EQ.221)THEN
         DO 230 IXJY=1,NX*NY
         SPEED(IXJY)=SPEED(IXJY)*1.9425
 230     CONTINUE
      ENDIF
C
      GO TO 900
C
C        SET OUTPUT FIELD TO MISSING WHEN AN ERROR HAS OCCURRED.
C
 800  DO 801 J=1,ND2X3
      SPEED(J)=9999.
 801  CONTINUE
C
 900  RETURN
      END      
