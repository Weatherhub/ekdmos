         SUBROUTINE CHKGRID(KFILDO,IS2,ND7,MPROJ,IGDSTMPL,IGDSTMPLEN)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: CHKGRID
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: CHECK THAT THE GRID CHARACTERISTICS FOUND ON THE TDLPACK 
C   INPUT FILE ARE EQUAL TO THOSE FOUND IN THE GRID 
C   DEFINITION FILE.  THE LATITUDE AND LONGITUDE COMPARISON 
C   BETWEEN GRIDS ARE PERFORMED TO THE NEAREST MILLIDEGREE.  
C   THE TRUE MESH COMPARISON IS PERFORMED TO THE NEAREST 
C   MILLIMETER.  NOTE THAT THE LONGITUDE VALUES READ FROM 
C   THE GRID DEFINITION FILE ARE IN POSITIVE DEGREES EAST 
C   WHILE THE LONGITUDE VALUES STORED IN TDLPACK ARE IN 
C   POSITIVE DEGREES WEST.
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES: NONE
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  PARAMETER SET IN DRU135.  
C                       (INPUT)
C               MPROJ = MAP PROJECTION IDENTIFIER SET AT THE TOP OF
C                       U135.CN (CHARACTER*6).  (INPUT)
C         IGDSTMPL(J) = RELEVANT GRID TEMPLATE VALUES DESCRIPTOR VALUES (INPUT).  
C                       LAMBERT CONFORMAL GRIDS  
C                          (8) - NX NR POINTS ALONG X-AXIS
C                          (9) - NY NR POINTS ALONG Y-AXIS
C                         (10) - LA1 LAT OF ORIGIN (LOWER LEFT)
C                         (11) - LO1 LON OF ORIGIN (LOWER LEFT)
C                         (12) - RESOLUTION AND COMPONENT FLAG
C                         (13) - LATITUDE WHERE DX AND DY ARE SPECIFIED
C                         (14) - LOV - ORIENTATION OF GRID
C                         (15) - DX - X-DIR INCREMENT
C                         (16) - DY - Y-DIR INCREMENT
C                         (17) - PROJECTION CENTER FLAG
C                         (18) - SCANNING MODE FLAG
C                         (19) - LATIN 1 - FIRST LAT FROM POLE OF
C                                SECANT CONE INTERSECTION
C                         (20) - LATIN 2 - SECOND LAT FROM POLE OF
C                                SECANT CONE INTERSECTION
C                       POLAR STEREOGRAPHIC GRIDS
C                          (8) - N(I) NR POINTS ALONG LAT CIRCLE
C                          (9) - N(J) NR POINTS ALONG LON CIRCLE
C                         (10) - LA(1) LATITUDE OF ORIGIN
C                         (11) - LO(1) LONGITUDE OF ORIGIN
C                         (12) - RESOLUTION AND COMPONENT FLAG
C                         (13) - LAD LATITUDE WHERE DX AND DY ARE SPECIFIED
C                         (14) - LOV GRID ORIENTATION
C                         (15) - DX - X DIRECTION INCREMENT
C                         (16) - DY - Y DIRECTION INCREMENT
C                         (17) - PROJECTION CENTER FLAG
C                         (18) - SCANNING MODE
C                      MERCATOR GRIDS 
C                          (8) - N(I) NR POINTS ON LATITUDE CIRCLE
C                          (9) - N(J) NR POINTS ON LONGITUDE MERIDIAN
C                         (10) - LA(1) LATITUDE OF ORIGIN
C                         (11) - LO(1) LONGITUDE OF ORIGIN
C                         (12) - RESOLUTION AND COMPONENT FLAG
C                         (13) - LAD LATITUDE(S) AT WHICH THE MERCATOR
C                                PROJECTION INTERSECTS THE EARTH
C                         (14) - LA(2) LATITUDE OF LAST GRIDPOINT
C                         (15) - LO(2) LONGITUDE OF LAST GRIDPOINT
C                         (16) - SCANNING MODE
C                         (17) - ORIENTATION OF THE GRID
C                         (18) - LONGITUDINAL DIR GRID LENGTH
C                         (19) - LATITUDINAL DIR GRID LENGTH
C          IGDSTMPLEN = MAXIMUM DIMENSION OF IGDSTMPL( ).  GRID DEFINITION
C                       SECTION OF THE GRIB2 MESSAGE (SECTION 3).  (INPUT)
C                LTAG = FLAG REPRESENTING THE TYPE OF MAP PROJECTION
C                       FOR WHICH AN ERROR OCCURRED IN COMPARING GRID
C                       CHARACTERISTICS.  (INTERNAL) 
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  970 - GRID CHARACTERISTICS IN THE TDLPACK FILE
C                        DO NOT MATCH THOSE IN GRID DEFN. FILE
C                  980 - GRID CHARACTERISTICS IN THE TDLPACK FILE
C                        DO NOT MATCH THOSE IN GRID DEFN. FILE
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      CHARACTER*2 LPROJ
      CHARACTER*6 MPROJ
      DIMENSION IS2(ND7),IGDSTMPL(IGDSTMPLEN)
C
C        ASSIGN VALUES TO THE NECESSARY VARIABLES.
C
      LTAG=0
      NNXS=IGDSTMPL(8)
      NNYS=IGDSTMPL(9)
      LPROJ=MPROJ(3:4)
C
      IF(LPROJ.EQ.'LC') THEN
         JMAP=3      
      ELSEIF(LPROJ.EQ.'PS') THEN
         JMAP=5
      ELSEIF(LPROJ.EQ.'MR') THEN
         JMAP=7
      ENDIF
C
C        PLACE THE GRID CHARACTERISTICS FOUND IN THE GRID DEFINITION
C        FILE INTO A FORM THAT WILL ALLOW A COMPARISON WITH VALUES
C        FOUND IN THE TDLPACK INPUT FILE.
C
      IF((IS2(2).EQ.3).OR.(IS2(2).EQ.5)) THEN
C
         JALAT=IGDSTMPL(10)/1000
         JALON=(360000000-IGDSTMPL(11))/1000
         JORIENT=(360000000-IGDSTMPL(14))/1000
         JXLAT=IGDSTMPL(13)/1000
         JDX=IGDSTMPL(15)/1000
C
      ELSEIF(IS2(2).EQ.7) THEN
C
         JALAT=IGDSTMPL(10)/1000
         JALON=(360000000-IGDSTMPL(11))/1000
         JORIENT=(360000000-IGDSTMPL(17))/1000
         JXLAT=IGDSTMPL(13)/1000
         JDX=IGDSTMPL(18)/1000
C
      ENDIF
C
C        CONVERT THE IS2( ) VALUES TO THE NEAREST MILLIDEGREE AND 
C        MILLIMETER FOR COMPARISON PURPOSES.
C
      IS2_5=IS2(5)/10
      IS2_6=IS2(6)/10
      IS2_7=IS2(7)/10
      IS2_8=IS2(8)/1000
      IS2_9=IS2(9)/10
C
C        CHECK THE GRID CHARACTERISTICS.
C 
      IF((IS2(2).NE.JMAP).OR.
     1   (IS2(3).NE.NNXS).OR.
     2   (IS2(4).NE.NNYS).OR.
     3   (IS2_5.NE.JALAT).OR.
     4   (IS2_6.NE.JALON).OR.
     5   (IS2_7.NE.JORIENT).OR.
     6   (IS2_8.NE.JDX).OR.
     7   (IS2_9.NE.JXLAT)) THEN 
C
         IF((JMAP.EQ.3).OR.(JMAP.EQ.5)) THEN
            LTAG=1
         ELSEIF(JMAP.EQ.7) THEN
            LTAG=2
         ENDIF
C        
C           GOTO ERROR DIAGNOSTIC PRINT.
C
         GOTO 950
C
      ENDIF
C
C        IF ALL GRIDDED METADATA ARE CONSISTENT, EXIT THE PROGRAM. 
C
      GOTO 985
C
 950  WRITE(KFILDO,960)
 960  FORMAT(/,' ****THE GRID CHARACTERISTICS FOUND IN THE TDLPACK',
     1         ' FILE DO NOT MATCH THE',/,5X,'GRID',
     2         ' CHARACTERISTICS FOUND IN THE GRID DEFINITION FILE. ',
     3         ' STOP IN CHKGRID.',/,5X,'NOTE: THE LATITUDE AND',
     4         ' LONGITUDE VALUES PRINTED IN THE TABLE BELOW', 
     5         ' ARE',/,5X,'ROUNDED TO THE NEAREST MILLIDEGREE. ',
     6         ' THE TRUE MESH IS ROUNDED TO THE',/,5X,'NEAREST',
     7         ' MILLIMETER.')    
C
      IF(LTAG.EQ.1) THEN
         WRITE(KFILDO,970) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,
     2                     IS2_7,JORIENT,IS2_8,JDX,
     3                     IS2_9,JXLAT 
 970     FORMAT(/,26X,'VALUES IN THE TDLPACK FILE',5X,
     1           'VALUES IN GRID DEFINITION FILE',//,5X,
     2            'MAP PROJECTION',21X,I2,29X,I2,/,5X,'NX',28X,
     3            I7,24X,I7,/,5X,'NY',28X,I7,24X,I7,/,5X,
     4            'LOWER LEFT LATITUDE',11X,I7,24X,I7,/,5X,
     5           'LOWER LEFT LONGITUDE',10X,I7,24X,I7,/,5X,
     6           'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     7           'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     8           'LATITUDE AT TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID')
         STOP 970 
C
      ELSEIF(LTAG.EQ.2) THEN
C
         WRITE(KFILDO,980) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,IS2_7,JORIENT,
     2                     IS2_8,JDX,IS2_9,JXLAT 
 980     FORMAT(/,26X,'VALUES IN THE TDLPACK FILE',5X,
     1           'VALUES IN GRID DEFINITION FILE',//,5X,
     2           'MAP PROJECTION',21X,I2,29X,I2,/,5X,'NX',28X,I7,
     3           24X,I7,/,5X,'NY',28X,I7,24X,I7,/,5X,
     4           'LOWER LEFT LATITUDE',11X,I7,24X,I7,/,5X,
     5           'LOWER LEFT LONGITUDE',10X,I7,24X,I7,/,5X,
     6           'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     7           'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     8           'LATITUDINAL TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID')
         STOP 980 
C 
      ENDIF
C
 985  RETURN
      END
