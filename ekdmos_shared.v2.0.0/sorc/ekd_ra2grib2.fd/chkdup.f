      SUBROUTINE CHKDUP(KFILDO,FSCT2,NFSCT2,ISCT2LEN,JBIRG2,
     1                  IDMDL,JPLAIN,IPDSNUM,IPDSTMPL,
     2                  IPDSTMPLEN,JBIRG4,IDRSNUM,IDRSTMPL,
     3                  IDRSTMPLEN,JBIRG5,NGROUPS,JGROUPS,
     4                  JEND,ND10,ND12,ISTOP)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: CHKGRID
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: TO ELIMINATE INTER-GROUP DUPLICATE ENTRIES FOUND IN  
C   THE INPUT FILES PERTAINING TO SECTIONS 4 AND 5. 
C   FLAG THE END OF EACH GROUP WITH A VALUE OF ONE. 
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY    ADDED NCEP DOCBLOCK.  
C   13-11-15  HUNTEMANN  ADDED SECTION 2 HANDLING FOR WX.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES: NONE
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C            KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C      FSCT2(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED LOCAL 
C                     USE FILE. EACH INTEGER IN THE ARRAY IS A DECIMAL 
C                     REPRESENTATION THAT CORRESPONDS TO AN ASCII 
C                     CHARACTER. 0 IN THE ARRAY MARKS THE ENDING OF A 
C                     UNIQUE WEATHER KEY (N=1,NFSCT2) (K=1,ND12)
C                     (J=1,ND10)  (INPUT)
C       NFSCT2(K,J) = DIMENSION OF FSCT2(N).  LOCAL USE SECTION 
C                     OF THE GRIB2 MESSAGE (SECTION 2). (K=1,ND12)
C                     (J=1,ND10)  (INPUT)
C          ISCT2LEN = MAXIMUM DIMENSION OF FSCT2( ).  LOCAL USE
C                     SECTION OF THE GRIB2 MESSAGE (SECTION 2).
C       JBIRG2(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                     FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                     CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                     BEING PROCESSED FOR SECTION 2 (K=1,ND10),
C                     (J=1,ND12).  (INPUT/OUTPUT)
C      IDMDL(4,K,J) = FOUR WORD MOS-2000 ID TO BE PROCESSED (K=1,ND10),
C                     (J=1,ND12).  (INPUT/OUTPUT)
C    JPLAIN(32,K,J) = THE PLAIN LANGUAGE DESCRIPTION OF THE VARIABLES
C                     READ FROM THE PRODUCT DEFINITION FILE (K=1,ND10),
C                     (J=1,ND12).  (CHARACTER*32)  (INPUT/OUTPUT) 
C      IPDSNUM(K,J) = PRODUCT DEFINITION TEMPLATE NUMBER (SEE CODE
C                     TABLE 4.0) (K=1,ND10),(J=1,ND12).  (INPUT/OUTPUT)
C   IPDSTMPL(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED PRODUCT
C                     DEFINITION TEMPLATE (N=IPDSNUM).  EACH ELEMENT OF
C                     THIS INTEGER ARRAY CONTAINS AN ENTRY (IN THE ORDER
C                     SPECIFIED) OF THE PRODUCT DEFINITION TEMPLATE 4.N.
C                     (K=1,ND10),(J=1,ND12).  (INPUT/OUTPUT)
C        IPDSTMPLEN = MAXIMUM DIMENSION OF IPDSTMPL( ).  PRODUCT
C                     DEFINITION SECTION OF THE GRIB2 MESSAGE (SECTION 4).
C                     (INPUT)
C       JBIRG4(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                     FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                     CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                     BEING PROCESSED FOR SECTION 4 (K=1,ND10),
C                     (J=1,ND12).  (INPUT/OUTPUT)
C      IDRSNUM(K,J) = DATA REPRESENTATION TEMPLATE NUMBER (SEE CODE
C                     TABLE 5.0) (K=1,ND10),(J=1,ND12).  (INPUT/OUTPUT)
C   IDRSTMPL(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED DATA
C                     REPRESENTATION TEMPLATE (N=IDRSNUM).  EACH ELEMENT
C                     OF THIS INTEGER ARRAY CONTAINS AN ENTRY (IN THE
C                     ORDER SPECIFIED) OF DATA REPRESENTATION TEMPLATE 5.N.
C                     NOTE THAT SOME VALUES IN THIS TEMPLATE (E.G.
C                     REFERENCE VALUES, NUMBER OF BITS, ETC...) MAY BE
C                     CHANGED BY THE DATA PACKING ALGORITHMS.  USE THIS
C                     TO SPECIFY SCALING FACTORS AND ORDER OF SPATIAL
C                     DIFFERENCING, IF DESIRED.  (INPUT/OUTPUT)
C        IDRSTMPLEN = MAXIMUM DIMENSION OF IDRSTMPL( ).  DATA REPRESENTATION
C                     SECTION OF THE GRIB2 MESSAGE (SECTION 5).  (INPUT)
C       JBIRG5(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                     FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                     CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                     BEING PROCESSED FOR SECTION 5 (K=1,ND10),
C                     (J=1,ND12).  (INPUT/OUTPUT)
C           NGROUPS = TOTAL NUMBER OF GROUPS (OR MESSAGES) THAT WILL BE
C                     PROCESSED IN ONE RUN OF U135.  (INPUT)
C        JGROUPS(J) = NUMBER OF ENTRIES (GRIDS) IN EACH GROUP THAT WILL BE
C                     PLACED INTO GRIB2 (J=1,ND12).  (INPUT/OUTPUT)
C         JEND(K,J) = FLAG (HAVING A VALUE OF ONE) INDICATING THE LAST
C                     ENTRY IN A PARTICULAR GROUP (K=1,ND10),(J=1,ND12).
C                     (INPUT/OUTPUT)
C              ND10 = MAXIMUM NUMBER OF FIELDS ALLOWED IN ANY ONE
C                     SPECIFIC GROUP.  (INPUT)
C              ND12 = MAXIMUM NUMBER OF GROUPS (OR MESSAGES) THAT CAN
C                     BE PROCESSED IN ONE RUN OF U135.  (INPUT)
C          ISTOP(J) = FOR J=1, ISTOP( ) IS INCREMENTED BY 1 EACH TIME
C                     FOR J=2, ISTOP( ) IS INCREMENTED BY 1 WHENEVER
C                     AN INPUT DATA RECORD IS NOT FOUND.  (INPUT/OUTPUT)
C      IREPEAT(K,J) = FLAG (HAVING A VALUE OF ONE) INDICATES THAT A
C                     PARTICULAR ENTRY IS A DUPLICATE IN THE GROUP
C                     BEING PROCESSED (K=1,ND10),(J=1,ND12).  (INTERNAL)
C             ITERM = TERMINATOR FOUND AFTER EACH ENTRY IN EACH GROUP 
C                     FOR SECTIONS 4 AND 5. (INTERNAL)
C
C        SUBPROGRAMS CALLED:  NONE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      CHARACTER*32 JPLAIN(32,ND10,ND12)
      CHARACTER*80 LINE(ND10,ND12),FMT1,DUMMY
C
      DIMENSION NFSCT2(ND10,ND12),FSCT2(ISCT2LEN,ND10,ND12),
     1          IDRSTMPL(IDRSTMPLEN,ND10,ND12),
     2          IPDSTMPL(IPDSTMPLEN,ND10,ND12),
     3          IPDSNUM(ND10,ND12),IDRSNUM(ND10,ND12)
      DIMENSION IREPEAT(ND10,ND12),IDMDL(4,ND10,ND12),
     1          JEND(ND10,ND12)
      DIMENSION JBIRG2(ND10,ND12),JBIRG4(ND10,ND12),
     1          JBIRG5(ND10,ND12),JGROUPS(ND12)
      DIMENSION ISTOP(2)
C
      DATA ITERM/999999/
C
C        TEST FOR DUPLICATE ENTRIES IN THE PRODUCT DEFINITION LIST.
C        CHECK 'IDMDL( , , )' SINCE THIS IS WHERE THE GRIB PRODUCT
C        DEFINITION IS SPECIFIED.  IF THE MOS-2000 ID IS A DUPLICATE,
C        ALL ASSOCIATED DATA (FOR ALL OTHER SECTIONS IN THAT ENTRY)
C        IS CONSIDERED A DUPLICATE AS WELL.
C
C        INITIALIZE 'IREPEAT( , )' TO ZERO.
C
      DO 101 J=1,ND12
         DO 100 K=1,ND10
            IREPEAT(K,J)=0
 100     CONTINUE
 101  CONTINUE
C
      DO 160 J=1,NGROUPS
C
         DO 155 K=1,JGROUPS(J)-1
C
            DO 150 L=K+1,JGROUPS(J)
C
               IF((IDMDL(1,K,J).EQ.IDMDL(1,L,J)).AND.
     1            (IDMDL(2,K,J).EQ.IDMDL(2,L,J)).AND.
     2            (IDMDL(3,K,J).EQ.IDMDL(3,L,J)).AND.
     3            (IDMDL(4,K,J).EQ.IDMDL(4,L,J))) THEN
C
                  IREPEAT(L,J)=1
C
                  WRITE(KFILDO,105) L,J 
 105              FORMAT(/,' ****THE FOLLOWING ENTRY NUMBER ',
     1                     I4,' FOR MESSAGE ',I4,' IS A DUPLICATE ',
     2                     'AND WILL BE REMOVED PRIOR TO',
     3                     ' PROCESSING.')
C
                  WRITE(KFILDO,110)
 110              FORMAT(/,' CORRESPONDING ENTRY WAS FOUND IN THE',
     1                     ' LOCAL USE FILE (SECTION 2).',/)
C
                  WRITE(KFILDO,118)
 118              FORMAT(/,' CORRESPONDING ENTRY FOUND IN THE',
     1                     ' PRODUCT DEFINITION LIST FILE',
     2                     ' (SECTION 4):',/)
C
                  WRITE(KFILDO,120) (IDMDL(N,L,J),N=1,4),
     1                              (JPLAIN(MM,L,J),MM=1,32)
 120              FORMAT(3I10.9,1X,I10.10,1X,32A1)
C
                  WRITE(KFILDO,121) IPDSNUM(L,J),
     1                              (IPDSTMPL(N,L,J),N=1,IPDSTMPLEN),
     2                              ITERM,JBIRG4(L,J)
 121              FORMAT(3(10I10/),2I10)
C
                  WRITE(KFILDO,122)
 122              FORMAT(/,' CORRESPONDING ENTRY FOUND IN THE',
     1                     ' DATA REPRESENTATION LIST FILE',
     2                     ' (SECTION 5):',/)
C
                  WRITE(KFILDO,123) IDRSNUM(L,J),
     1                              (IDRSTMPL(N,L,J),N=1,IDRSTMPLEN),
     2                              ITERM,JBIRG5(L,J)
 123              FORMAT(2(10(I10),/),2I10)
C
                  ISTOP(1)=ISTOP(1)+1
C
               ENDIF
C
 150        CONTINUE
C
 155     CONTINUE
C
 160  CONTINUE
C
      DO 262 J=1,NGROUPS
C
         JREMOVE=0
C
         DO 261 K=1,JGROUPS(J)
C
            IF((IREPEAT(K,J).EQ.1).AND.(K.NE.JGROUPS(J))) THEN
C
               DO 260 L=K+1,JGROUPS(J)
C
                  NFSCT2(K,J)=NFSCT2(L,J)
C
                  DO 251 NN=1,NFSCT2(K,J)
                     FSCT2(NN,K,J)=FSCT2(NN,L,J)
 251              CONTINUE
C
                  JBIRG2(K,J)=0
C
                  DO 252 NN=1,4
                     IDMDL(NN,K,J)=IDMDL(NN,L,J)
 252              CONTINUE
C
                  DO 253 NN=1,32
                     JPLAIN(NN,K,J)=JPLAIN(NN,L,J)
 253              CONTINUE
C
                  IPDSNUM(K,J)=IPDSNUM(L,J)
C
                  DO 254 NN=1,IPDSTMPLEN
                     IPDSTMPL(NN,K,J)=IPDSTMPL(NN,L,J)
 254              CONTINUE
C
                  JBIRG4(K,J)=0
C
                  IDRSNUM(K,J)=IDRSNUM(L,J)
C
                  DO 255 NN=1,IDRSTMPLEN
                     IDRSTMPL(NN,K,J)=IDRSTMPL(NN,L,J)
 255              CONTINUE
C
                  JBIRG5(K,J)=0
C
                  JREMOVE=JREMOVE+1
C
 260           CONTINUE
C
            ELSEIF((IREPEAT(K,J).EQ.1).AND.(K.EQ.JGROUPS(J))) THEN
C
               JREMOVE=JREMOVE+1
C    
            ENDIF
C
 261     CONTINUE 
C
C           ASSIGN THE NEW NUMBER OF FIELDS IN THE MESSAGE IF DUPLICATES
C           ARE FOUND.
C
         JGROUPS(J)=JGROUPS(J)-JREMOVE
C
C           ENSURE THAT A TERMINATOR IS ATTACHED TO THE LAST ENTRY
C           IN THE GROUP.
C
         JBIRG2(JGROUPS(J),J)=777777
         JBIRG4(JGROUPS(J),J)=777777
         JBIRG5(JGROUPS(J),J)=777777
C
 262  CONTINUE
C
C        LABEL THE BEGINNING OF A SET OF ENTRIES AS THE FINAL ENTRY
C        IN THAT GROUP AS 'JEND( )=1'.  TALLY THE TOTAL NUMBER OF
C        GRIB2 MESSAGES DESIRED.  THIS IS TO ALLOW THE REPETITION
C        OF SECTIONS 3,4,5,6 AND 7 WHEN CREATING A GRIB2 MESSAGE.
C        THANK YOU MALONEY!
C
      DO 368 J=1,NGROUPS
C
         DO 365 K=1,JGROUPS(J)
C
            IF(JBIRG4(K,J).NE.777777) THEN
C
               IF(K.EQ.1) THEN
                  IF(JGROUPS(J).EQ.K) THEN
                     JEND(K,J)=1
                  ELSE
                     JEND(K,J)=0
                  ENDIF
C
               ELSE
C
                  JEND(K,J)=0
C
               ENDIF
C
            ELSE
C
               JEND(K,J)=1
C
            ENDIF
C
 365     CONTINUE
C
 368  CONTINUE 
C
      RETURN
      END
