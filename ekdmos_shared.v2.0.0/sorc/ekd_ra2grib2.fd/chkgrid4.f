         SUBROUTINE CHKGRID4(KFILDO,IS2,IS2BMAP,ND7)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: CHKGRID4
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: CHECK THAT THE GRID CHARACTERISTICS IN THE GRIDDED 
C   BITMAP RANDOM ACCESS FILE ARE SAME AS THE GRID
C   CHARACTERISTICS DEFINED IN THE INPUT TDLPACK DATA FILE.
C   THE LATITUDE AND LONGITUDE COMPARISON BETWEEN GRIDS 
C   ARE PERFORMED TO THE NEAREST MILLIDEGREE.  THE TRUE 
C   MESH COMPARISON IS PERFORMED TO THE NEAREST MILLIMETER.
C   NOTE THAT THE LONGITUDE VALUES READ FROM THE GRID 
C   DEFINITION FILE ARE IN POSITIVE DEGREES EAST WHILE THE 
C   LONGITUDE VALUES STORED IN TDLPACK ARE IN POSITIVE 
C   DEGREES WEST.
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK  ADAPTED FROM CHKGRID3
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES: NONE
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S FROM THE INPUT DATA
C                       FILE(J=1,12).  (INPUT)
C          IS2BMAP(J) = MOS-2000 GRIB SECTION 2 ID'S FROM THE EXTERNAL
C                       GRIDDED BITMAP RANDOM ACCESS FILE (J=1,12).  
C                       (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  PARAMETER SET IN DRU135.  (INPUT)
C                JMAP = MAP PROJECTION OF OUTPUT GRID.  (INTERNAL)
C                       3 = N.H. LAMBERT
C                       5 = N.H. POLAR STEREOGRAPHIC
C                       7 = MERCATOR
C                NNXS = X DIMENSION OF BITMAP INPUT GRID.  (INTERNAL)
C                NNYS = Y DIMENSION OF BITMAP INPUT GRID.  (INTERNAL)
C               JALAT = LATITUDE OF ORIGIN (1,1) OF BITMAP INPUT GRID 
C                       (DEGREES N).  (INTERNAL)
C               JALON = LONGITUDE OF ORIGIN (1,1) OF BITMAP INPUT GRID
C                       (POSITIVE DEGREES WEST).  (INTERNAL)
C             JORIENT = ORIENTATION OF BITMAP INPUT GRID.  MERIDIAN
C                       PARALLEL TO Y-AXIS (POSITIVE DEGREES WEST).  (INTERNAL)
C               JXLAT = FOR LAMBERT CONICAL CONFORMAL, THE LATITUDE WHERE
C                       CONE IS TANGENT FOR THE OUTPUT GRID.  FOR NORTH POLAR
C                       STEREOGRAPHIC, LATITUDE WHERE GRID LENGTH APPLIES.
C                       THIS VARIABLE APPLIES TO THE BITMAP INPUT GRID. 
C                       (INTERNAL)
C                 JDX = X INCREMENT AT JXLAT OF BITMAP INPUT GRID.  
C                       (INTERNAL)
C                LTAG = FLAG REPRESENTING THE TYPE OF MAP PROJECTION
C                       FOR WHICH AN ERROR OCCURRED IN COMPARING GRID
C                       CHARACTERISTICS.  (INTERNAL) 
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  970 - GRID CHARACTERISTICS IN THE TDLPACK FILE
C                        DO NOT MATCH THOSE IN GRIDDED BITMAP FILE
C                  980 - GRID CHARACTERISTICS IN THE TDLPACK FILE
C                        DO NOT MATCH THOSE IN GRIDDED BITMAP FILE
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      DIMENSION IS2(ND7),IS2BMAP(ND7)
C
      LTAG=0
C
C        REASSIGN GRID METADATA VALUES FROM THE GRIDDED EXTERNAL 
C        BITMAP FILE.  CONVERT THE IS2BMAP( ) ARRAY TO THE NEAREST 
C        MILLIDEGREE AND MILLIMETER FOR COMPARISON PURPOSES.
C
      JMAP=IS2BMAP(2)
      NNXS=IS2BMAP(3)
      NNYS=IS2BMAP(4)
      JALAT=IS2BMAP(5)/10
      JALON=IS2BMAP(6)/10
      JORIENT=IS2BMAP(7)/10
      JDX=IS2BMAP(8)/1000
      JXLAT=IS2BMAP(9)/10
C
C        CONVERT THE IS2( ) VALUES TO THE NEAREST MILLIDEGREE AND
C        MILLIMETER FOR COMPARISON PURPOSES.
C
      IS2_5=INT(IS2(5)/10)
      IS2_6=INT(IS2(6)/10)
      IS2_7=INT(IS2(7)/10)
      IS2_8=INT(IS2(8)/1000)
      IS2_9=INT(IS2(9)/10)
C
C        CHECK THE GRID CHARACTERISTICS.
C 
      IF((IS2(2).NE.JMAP).OR.
     1   (IS2(3).NE.NNXS).OR.
     2   (IS2(4).NE.NNYS).OR.
     3   (IS2_5.NE.JALAT).OR.
     4   (IS2_6.NE.JALON).OR.
     5   (IS2_7.NE.JORIENT).OR.
     6   (IS2_8.NE.JDX).OR.
     7   (IS2_9.NE.JXLAT)) THEN 
C
         IF((JMAP.EQ.3).OR.(JMAP.EQ.5)) THEN
            LTAG=1
         ELSEIF(JMAP.EQ.7) THEN
            LTAG=2
         ENDIF
C        
C           GOTO ERROR DIAGNOSTIC PRINT.
C
         GOTO 950
C
      ENDIF
C
C        IF ALL THE GRID METADATA IS CONSISTENT EXIT THE PROGRAM. 
C
      GOTO 985
C
 950  WRITE(KFILDO,960)
 960  FORMAT(/,' ****THE GRID CHARACTERISTICS FOUND IN THE INPUT',
     1         ' DATA FILE DO NOT MATCH THE',/,5X,'GRID',
     2         ' CHARACTERISTICS FOUND IN THE GRIDDED BITMAP',
     3         ' RANDOM ACCESS FILE.',/,5X,'STOP 970 IN CHKGRID4.')
C
      IF(LTAG.EQ.1) THEN
         WRITE(KFILDO,970) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,
     2                     IS2_7,JORIENT,IS2_8,JDX,
     3                     IS2_9,JXLAT 
 970     FORMAT(/,26X,'VALUES IN INPUT DATA FILE',5X,
     1           'VALUES IN GRIDDED BITMAP RANDOM ACCESS FILE',
     2           //,5X,'MAP PROJECTION',21X,I2,29X,I2,/,5X,'NX',
     3           28X,I7,24X,I7,/,5X,'NY',28X,I7,24X,I7,/,5X,
     4           'LOWER LEFT LATITUDE',11X,I7,24X,I7,/,5X,
     5           'LOWER LEFT LONGITUDE',10X,I7,24X,I7,/,5X,
     6           'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     7           'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     8           'LATITUDE AT TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID4')
         STOP 970 
C
      ELSEIF(LTAG.EQ.2) THEN
C
         WRITE(KFILDO,980) IS2(2),JMAP,IS2(3),NNXS,IS2(4),NNYS,
     1                     IS2_5,JALAT,IS2_6,JALON,IS2_7,JORIENT,
     2                     IS2_8,JDX,IS2_9,JXLAT 
 980     FORMAT(/,26X,'VALUES IN INPUT DATA FILE',5X,
     1          'VALUES IN GRIDDED BITMAP RANDOM ACCESS FILE',//,5X,
     2          'MAP PROJECTION',21X,I2,29X,I2,/,5X,'NX',28X,I7,24X,
     3          I7,/,5X,'NY',28X,I7,24X,I7,/,5X,'LOWER LEFT LATITUDE',
     4          11X,I7,24X,I7,/,5X,'LOWER LEFT LONGITUDE',10X,I7,24X,
     5          I7,/,5X,'GRID ORIENTATION',14X,I7,24X,I7,/,5X,
     6          'TRUE MESH LENGTH',14X,I7,24X,I7,/,5X,
     7          'LATITUDINAL TRUE MESH',9X,I7,24X,I7) 
         CALL W3TAGE('CHKGRID4')
         STOP 980 
C 
      ENDIF
C
 985  RETURN
      END
