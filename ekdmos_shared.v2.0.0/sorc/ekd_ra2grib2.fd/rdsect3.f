      SUBROUTINE RDSECT3(KFILDO,KFILSC3,MPROJ,JGRID,IGDS,
     1                   IGDSTMPL,IGDSTMPLEN,JGPLAIN,ND11,IP14) 
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDSECT3
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: TO READ THE VALUES FOUND IN THE GRID DEFINITION 
C   FILE AND STORE THESE VALUES. 
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES:
C         FORT.KFILSC3 - UNIT NUMBER OF FILE CONTAINING THE 
C                        GRID DEFINITION LIST.  (INPUT)
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C             KFILSC3 = UNIT NUMBER OF THE GRID DEFINITION FILE.  (INPUT)
C               MPROJ = MAP PROJECTION IDENTIFIER SET AT THE TOP OF
C                       U135.CN (CHARACTER*6).  (INPUT)
C               JGRID = THE SPECIFIC ENTRY NUMBER IN THE GRID DEFINITION FILE
C                       THAT MATCHES 'MPROJ'.  (OUTPUT)    
C           IGDS(J,K) = SECTION GRID INFORMATION (J=1,4),(K=1,ND11).  (OUTPUT)
C                         (1,K) - SOURCE OF GRID INFORMATION
C                         (2,K) - NUMBER OF DATA POINTS
C                         (3,K) - NUMBER OF OCTETS FOR OPTIMAL LIST OF NUMBERS DEFINING # OF POINTS
C                         (4,K) - INTERPRETATION LIST OF NUMBERS DEFINING # OF POINTS 
C                         (5,K) - GRID DEFINITION TEMPLATE NUMBER
C       IGDSTMPL(N,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED GRID DEFINITION
C                       TEMPLATE (NN=IGDS(5,J)).  EACH ELEMENT OF THIS INTEGER ARRAY
C                       CONTAINS AN ENTRY (IN THE ORDER SPECIFIED) OF GRID
C                       DEFINITION TEMPLATE 3.NN (N=IGDSTMPLEN),(J=1,ND11).  (OUTPUT)
C                       LAMBERT CONFORMAL GRIDS 
C                         (1,K) - SHAPE OF THE EARTH
C                         (2,K) - SCALE FACTOR OF RADIUS OF SPHERICAL EARTH
C                         (3,K) - SCALED VALUE OF RADIUS OF SPHERICAL EARTH
C                         (4,K) - SCALE FACTOR OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (5,K) - SCALED VALUE OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (6,K) - SCALE FACTOR OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (7,K) - SCALED VALUE OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (8,K) - NX NR POINTS ALONG X-AXIS
C                         (9,K) - NY NR POINTS ALONG Y-AXIS
C                        (10,K) - LA1 LAT OF ORIGIN (LOWER LEFT)
C                        (11,K) - LO1 LON OF ORIGIN (LOWER LEFT)
C                        (12,K) - RESOLUTION AND COMPONENT FLAG
C                        (13,K) - LATITUDE WHERE DX AND DY ARE SPECIFIED
C                        (14,K) - LOV - ORIENTATION OF GRID
C                        (15,K) - DX - X-DIR INCREMENT
C                        (16,K) - DY - Y-DIR INCREMENT
C                        (17,K) - PROJECTION CENTER FLAG
C                        (18,K) - SCANNING MODE FLAG
C                        (19,K) - LATIN 1 - FIRST LAT FROM POLE OF
C                                 SECANT CONE INTER
C                        (20,K) - LATIN 2 - SECOND LAT FROM POLE OF
C                                 SECANT CONE INTER
C                       POLAR STEREOGRAPHIC GRIDS 
C                         (1,K) - SHAPE OF THE EARTH
C                         (2,K) - SCALE FACTOR OF RADIUS OF SPHERICAL EARTH
C                         (3,K) - SCALED VALUE OF RADIUS OF SPHERICAL EARTH
C                         (4,K) - SCALE FACTOR OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (5,K) - SCALED VALUE OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (6,K) - SCALE FACTOR OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (7,K) - SCALED VALUE OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (8,K) - N(I) NR POINTS ALONG LAT CIRCLE
C                         (9,K) - N(J) NR POINTS ALONG LON CIRCLE
C                        (10,K) - LA(1) LATITUDE OF ORIGIN
C                        (11,K) - LO(1) LONGITUDE OF ORIGIN
C                        (12,K) - RESOLUTION AND COMPONENT FLAG
C                        (13,K) - LAD LATITUDE WHERE DX AND DY ARE SPECIFIED
C                        (14,K) - LOV GRID ORIENTATION
C                        (15,K) - DX - X DIRECTION INCREMENT
C                        (16,K) - DY - Y DIRECTION INCREMENT
C                        (17,K) - PROJECTION CENTER FLAG
C                        (18,K) - SCANNING MODE
C                      MERCATOR GRIDS 
C                         (1,K) - SHAPE OF THE EARTH
C                         (2,K) - SCALE FACTOR OF RADIUS OF SPHERICAL EARTH
C                         (3,K) - SCALED VALUE OF RADIUS OF SPHERICAL EARTH
C                         (4,K) - SCALE FACTOR OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (5,K) - SCALED VALUE OF MAJOR AXIS OF OBLATE SPHERICAL EARTH
C                         (6,K) - SCALE FACTOR OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (7,K) - SCALED VALUE OF MINOR AXIS OF OBLATE SPHERICAL EARTH
C                         (8,K) - N(I) NR POINTS ON LATITUDE CIRCLE
C                         (9,K) - N(J) NR POINTS ON LONGITUDE MERIDIAN
C                        (10,K) - LA(1) LATITUDE OF ORIGIN
C                        (11,K) - LO(1) LONGITUDE OF ORIGIN
C                        (12,K) - RESOLUTION AND COMPONENT FLAG
C                        (13,K) - LAD LATITUDE(S) AT WHICH THE MERCATOR
C                                 PROJECTION INTERSECTS THE EARTH
C                        (14,K) - LA(2) LATITUDE OF LAST GRIDPOINT
C                        (15,K) - LO(2) LONGITUDE OF LAST GRIDPOINT
C                        (16,K) - SCANNING MODE
C                        (17,K) - ORIENTATION OF THE GRID
C                        (18,K) - LONGITUDINAL DIR GRID LENGTH
C                        (19,K) - LATITUDINAL DIR GRID LENGTH
C          IGDSTMPLEN = MAXIMUM DIMENSION OF IGDSTMPL(N,J).  GRID DEFINITION
C                       SECTION OF THE GRIB2 MESSAGE (SECTION 3).  (INPUT)
C          JGPLAIN(N) = THE SIX CHARACTER STRING MAP PROJECTION IDENTIFIER
C                       AS READ FROM THE GRID DEFINITION FILE (N=1,ND11).
C                       (CHARACTER*6)  (INTERNAL)
C                ND11 = MAXIMUM NUMBER OF OUTPUT GRID ENTRIES DEFINED IN
C                       THE GRID DEFINITION FILE CORRESPONDING TO SECTION 3.
C                       (INPUT)
C                IP14 = UNIT NUMBER TO WHERE THE LIST OF VALUES PERTAINING
C                       TO SECTION 3 IN THE GRIB2 MESSAGE ARE PRINTED.
C                       THESE VALUES ARE SET IN THE GRIB2 GRID DEFINITION
C                       INPUT FILE.  (INPUT)
C              JMATCH = FLAG (HAVING A VALUE OF ONE) WHEN A MATCH HAS BEEN
C                       FOUND BETWEEN THE GRID IDENTIFIER IN THE U135.CN
C                       AND THE GRID IDENTIFIER FOUND IN THE GRID DEFINITION
C                       FILE.  (INTERNAL)
C
C        SUBPROGRAMS CALLED:  W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  117 - TOO MANY ENTRIES IN GRID IDENTIFIER FILE
C                  250 - INVALID MAP PROJECTION IDENTIFIER
C                  300 - INVALID SCANNING MODE
C                  310 - INVALID SCANNING MODE
C                  925 - ERROR READING PRODUCT DEFINITION FILE
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      CHARACTER*6 MPROJ
      CHARACTER*6 JGPLAIN(ND11)
C
      DIMENSION IGDSTMPL(IGDSTMPLEN,ND11),IGDS(5,ND11)
C
      JMATCH=0
C
C        READ ALL THE GRID IDENTIFIERS AND ASSOCIATED GRID METADATA
C        IN THE GRID DEFINITION FILE.
C      
      K=1
C
 113  READ(KFILSC3,114,ERR=900,END=120) JGPLAIN(K),(IGDS(J,K),J=1,5),
     1                                  (IGDSTMPL(J,K),J=1,22)
 114  FORMAT(A6,9I10,/,10(I10)/,8I10)
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        ENTRIES IN THE GRID DEFINITION FILE DOES NOT EXCEED THE
C        MAXIMUM NUMBER OF ANTICIPATED ENTRIES (ND11) TO BE PROCESSED.
C 
      IF(K.GT.ND11) THEN
         WRITE(KFILDO,117) K,ND11
 117     FORMAT(/,' ****THE NUMBER OF GRID IDENTIFIERS READ IN',
     1            ' THE GRID DEFINITION FILE (',I4,')',' IS',
     2            ' GREATER',/,5X,'THAN THE ANTICIPATED MAXIMUM',
     3            ' NUMBER OF GRID IDENTIFIERS (ND11 = ',
     4            I4,').',/,5X,'STOP 117 IN RDSECT3.')
         CALL W3TAGE('RDSECT5')
         STOP 117
      ENDIF
C 
C        TALLEY THE NUMBER OF GRID IDENTIFIERS READ.  READ THE
C        NEXT ENTRY IN THE GRID DEFINITION FILE.
      K=K+1
C 
      GOTO 113
C
 120  NGRIDS=K-1
C
      IF(IP14.GT.0) THEN
         WRITE(IP14,145)
 145     FORMAT(/,' THE FOLLOWING GRID SPECIFICATIONS ARE FOUND IN',
     1            ' THE GRID DEFINITION FILE:')
         DO 160 K=1,NGRIDS 
            WRITE(IP14,150) JGPLAIN(K),(IGDS(J,K),J=1,5),
     1                      (IGDSTMPL(J,K),J=1,22)
 150        FORMAT(/,A6,1X,'(GRID IDENTIFIER)',/,5I10,/,2(10I10/),
     1             2I10)
 160     CONTINUE
      ENDIF
C
C        ENSURE THAT THE MAP PROJECTION IDENTIFIER STIPULATED AT THE
C        TOP OF U135.CN MATCHES ONE OF THE ENTRIES IN THE GRID
C        DEFINITION FILE.  ASSIGN THE GRID TO ARRAY 'IGDTMPL( , )'.
C        IF A MATCH IS NOT FOUND, THE PROGRAM TERMINATES.
C
      DO 200 K=1,NGRIDS
         IF(MPROJ.EQ.JGPLAIN(K)) THEN
            JMATCH=1
            JGRID=K
         ENDIF
 200  CONTINUE
C
      IF(JMATCH.EQ.0) THEN
         WRITE(KFILDO,250) MPROJ
 250     FORMAT(/,' ****THE MAP PROJECTION IDENTIFIER ',A6,' IS',
     1            ' NOT FOUND IN THE GRID DEFINITION FILE.',/,5X,
     2            'PLEASE RECHECK THIS GRID IDENTIFIER.  STOP 250',
     3            ' IN RDSECT3.')
         CALL W3TAGE('RDSECT3')
         STOP 250
      ENDIF
C
C        CHECK THAT THE SCANNING MODE READ IN THE LIST OF ENTRIES
C        HAS A VALUE OF 64 OR 80.
C
      IF((IGDS(5,JGRID).EQ.20).OR.(IGDS(5,JGRID).EQ.30)) THEN
         IF((IGDSTMPL(18,JGRID).NE.80).AND.
     1      (IGDSTMPL(18,JGRID).NE.64)) THEN
            WRITE(KFILDO,300) IGDSTMPL(18,JGRID),JGPLAIN(JGRID)
 300        FORMAT(/,' ****THE SCANNING MODE OF ',I3,' SUPPLIED',
     1               ' TO THE MAP PROJECTION DESCRIPTOR ',A6,' IS',
     2               ' NOT SUPPORTED.',/,5X,'ONLY VALUES OF 64 AND',
     3               ' 80 ARE SUPPORTED.  STOP 300 IN RDSECT3.')
            CALL W3TAGE('RDSECT3')
            STOP 300
         ENDIF
      ELSEIF(IGDS(5,JGRID).EQ.10) THEN
         IF((IGDSTMPL(16,JGRID).NE.80).AND.
     1      (IGDSTMPL(16,JGRID).NE.64)) THEN
            WRITE(KFILDO,310) IGDSTMPL(16,JGRID),JGPLAIN(JGRID)
 310        FORMAT(/,' ****THE SCANNING MODE OF ',I3,' SUPPLIED',
     1               ' TO THE MAP PROJECTION DESCRIPTOR ',A6,' IS',
     2               ' NOT SUPPORTED.',/,5X,'ONLY VALUES OF 64 AND',
     3               ' 80 ARE SUPPORTED.  STOP 310 IN RDSECT3.')
            CALL W3TAGE('RDSECT3')
            STOP 310
         ENDIF
      ENDIF
C
C        CHECK THE INPUT VALUE STIPULATED IN SECTION 3 INPUT
C        FILE FOR THE SCANNING MODE OF THE GRIB2 GRIDDED DATA.
C        IF THE SCANNING MODE OF THE DATA IS TO BE PACKED INTO
C        GRIB2 BOUSTROPHEDONICALLY, (BEGINNING AT THE LOWER
C        LEFT PORTION OF THE GRID MOVING RIGHT TO LEFT),
C        NOTIFY THE USER THROUGH A DIAGNOSTIC PRINT. 
C
      IF((((IGDS(5,JGRID).EQ.20).OR.(IGDS(5,JGRID).EQ.30)).AND.
     1     (IGDSTMPL(18,JGRID).EQ.80)).OR.
     2   ((IGDS(5,JGRID).EQ.10).AND.(IGDSTMPL(16,JGRID).EQ.80)))THEN
         WRITE(KFILDO,500) 
 500     FORMAT(/,' ****THE USER DESIGNATED A SCANNING MODE VALUE OF',
     1            ' 80.  THE TDLPACK INPUT DATA FIELDS (WHICH HAVE A',
     2            /,5X,'SCANNING MODE OF 64) WILL BE RESTRUCTURED TO',
     3            ' A SCANNING MODE OF 80 FOR GRIB2 PACKING.')
      ENDIF 
C
      GOTO 950
C
 900  WRITE(KFILDO,925) KFILSC3 
 925  FORMAT(/,' ****PROBLEM READING THE GRID IDENTIFIERS',
     1         ' IN THE GRID DEFINITION FILE ON UNIT NUMBER ',
     2         I2,'.',/,5X,'STOP 925 IN RDSECT3.')
      CALL W3TAGE('RDSECT3')
      STOP 925
C
 950  RETURN
      END
