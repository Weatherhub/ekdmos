      SUBROUTINE RDSECT5(KFILDO,KFILSC5,IDRSNUM,IDRSTMPL,
     1                   IDRSTMPLEN,ND10,ND12,IP16,JBIRG5,
     2                   NGROUPS5,JGROUPS5) 
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: RDSECT5
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2004-11-11
C
C ABSTRACT: TO READ THE VALUES FOUND IN THE DATA REPRESENTATION
C   SECTION FILE AND STORE THESE VALUES.
C
C PROGRAM HISTORY LOG:
C   04-11-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES:
C         FORT.KFILSC5 - UNIT NUMBER OF FILE CONTAINING VALUES IN
C                        THE DATA REPRESENTATION LIST.  (INPUT)
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  INITIALLY,
C                       THIS IS SET BY DATA STATEMENT.  LATER, IN 
C                       IPOPEN, IF IP(1) NE 0, KFILDO IS SET = IP(1).
C                       THIS ALLOWS CHANGING THE "DEFAULT" PRINT FILE ON 
C                       THE FLY.  OTHERWISE, ON SOME SYSTEMS, THE OUTPUT
C                       FILE MIGHT HAVE THE SAME NAME AND BE
C                       OVERWRITTEN.  WHEN THE OUTPUT FILE IS NOT THE
C                       ORIGINAL DEFAULT, THE NAME IS GENERATED AND CAN
C                       BE DIFFERENT FOR EACH RUN.  (INPUT)
C             KFILSC5 = UNIT NUMBER OF FILE CONTAINING THE
C                       VALUES IN THE DATA REPRESENTATION LIST.  (INPUT)
C        IDRSNUM(K,J) = DATA REPRESENTATION TEMPLATE NUMBER (SEE CODE TABLE 5.0)
C                       (K=1,ND10),(J=1,ND12).  (INTERNAL)
C     IDRSTMPL(N,K,J) = CONTAINS THE DATA VALUES FOR THE SPECIFIED DATA 
C                       REPRESENTATION TEMPLATE (N=IDRSNUM).  EACH ELEMENT 
C                       OF THIS INTEGER ARRAY CONTAINS AN ENTRY (IN THE ORDER 
C                       SPECIFIED) OF DATA REPRESENTATION TEMPLATE 5.N.  
C                       NOTE THAT SOME VALUES IN THIS TEMPLATE (E.G. REFERENCE 
C                       VALUES, NUMBER OF BITS, ETC...) MAY BE CHANGED BY
C                       THE DATA PACKING ALGORITHMS.  USE THIS TO SPECIFY 
C                       SCALING FACTORS AND ORDER OF SPATIAL DIFFERENCING, 
C                       IF DESIRED (K=1,ND10),(J=1,ND12).  (OUTPUT)
C          IDRSTMPLEN = MAXIMUM DIMENSION OF IDRSTMPL(N,K,J).  DATA 
C                       REPRESENTATION SECTION OF THE GRIB2 MESSAGE
C                       (SECTION 5).  (INPUT)
C                ND10 = MAXIMUM NUMBER OF FIELDS ALLOWED IN ANY ONE
C                       SPECIFIC GROUP.  (INPUT)
C                ND12 = MAXIMUM NUMBER OF GROUPS (OR MESSAGES) THAT CAN
C                       BE PROCESSED IN ONE RUN OF U135.  (INPUT)
C                IP16 = UNIT NUMBER TO WHERE THE LIST OF VALUES PERTAINING
C                       TO SECTION 5 IN THE GRIB2 MESSAGE ARE PRINTED.
C                       THESE VALUES ARE SET IN THE GRIB2 DATA
C                       REPRESENTATION INPUT FILE.  (INPUT)
C         JBIRG5(K,J) = VALUE OF '777777' INDICATING THAT THE FINAL
C                       FIELD HAS BEEN REACHED AND THAT THE MESSAGE
C                       CAN NOW BE CONSIDERED COMPLETE FOR THE GROUP
C                       BEING PROCESSED (PERTAINING TO SECTION 5)
C                       (K=1,ND10),(J=1,ND12).  (OUTPUT)
C            NGROUPS5 = TOTAL NUMBER OF GROUPS (OR MESSAGES) THAT WILL BE
C                       PROCESSED IN ONE RUN OF U135 PERTAINING TO SECTION 5.
C                       (OUTPUT)
C         JGROUPS5(J) = NUMBER OF ENTRIES (GRIDS) IN EACH GROUP THAT WILL BE
C                       PLACED INTO GRIB2 PERTAINING TO SECTION 5 (J=1,ND12).
C                       (OUTPUT)
C               JTERM = FLAG HAVING A VALUE OF ONE IF THE TERMINATOR
C                       HAS BEEN REACHED, OTHERWISE ZERO.  (INTERNAL)
C
C        SUBPROGRAMS CALLED:  MKIEEE, W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C              G2 - MKIEEE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                  117 - TOO MANY ENTRIES IN DATA REPRESENTATION LIST
C                  119 - TOO MANY GROUPS IN INDICATOR & ID LIST FILE
C                  925 - ERROR READING DATA REPRESENTATION LIST
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      DIMENSION IDRSNUM(ND10,ND12),IDRSTMPL(IDRSTMPLEN,ND10,ND12),
     1          JBIRG5(ND10,ND12),JGROUPS5(ND12)
C
      JTERM=0
C
C        READ ALL THE ENTRIES IN THE DATA REPRESENTATION FILE.
C      
      K=1
      J=1
C
 113  READ(KFILSC5,115,ERR=900,END=120) IDRSNUM(K,J),
     1                          (IDRSTMPL(N,K,J),N=1,IDRSTMPLEN),ITERM,
     2                           JBIRG5(K,J)
 115  FORMAT(2(10(I10),/),2I10)
C
C        PLACE THE PRIMARY MISSING AND/OR SECONDARY MISSING VALUE(S) IN
C        32-BIT IEEE FLOATING POINT FORMAT.
C
      IF(IDRSTMPL(7,K,J).EQ.1) THEN
C
C          A PRIMARY MISSING VALUE IS TO BE USED.
C
        CALL MKIEEE(FLOAT(IDRSTMPL(8,K,J)),IDRSTMPL(8,K,J),1)
C
      ELSEIF(IDRSTMPL(7,K,J).EQ.2)  THEN
C
C          A PRIMARY AND SECONDARY MISSING VALUE ARE TO BE USED.
C
        CALL MKIEEE(FLOAT(IDRSTMPL(8,K,J)),IDRSTMPL(8,K,J),1)
        CALL MKIEEE(FLOAT(IDRSTMPL(9,K,J)),IDRSTMPL(9,K,J),1)
C
      ENDIF 
C
C        TALLY THE NUMBER OF ENTRIES IN EACH GROUP.
C
      JGROUPS5(J)=JGROUPS5(J)+1
C
C        IF THE '777777' TERMINATOR HAS BEEN REACHED, INCREMENT TO
C        THE NEXT GROUP.
C
      IF(JBIRG5(K,J).EQ.777777) THEN
        J=J+1
        INEW=1
      ENDIF
C
C        CHECK IF TERMINATOR HAS BEEN REACHED.
C
      IF(IDRSNUM(K,J).EQ.99999999) THEN
         JTERM=1
         GOTO 116
      ENDIF
C
      IF(INEW.EQ.1) THEN
         INEW=0
         K=1
      ELSE
         K=K+1
      ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        ENTRIES IN THE DATA REPRESENTATION LIST FILE DOES NOT
C        EXCEED THE MAXIMUM NUMBER OF ANTICIPATED ENTRIES (ND10)
C        TO BE PROCESSED. 
C 
 116  IF(K.GT.ND10) THEN
         WRITE(KFILDO,117) K,ND10
 117     FORMAT(/,' ****THE NUMBER OF RECORDS READ IN THE',
     1            ' DATA REPRESENTATION FILE (',I4,')',' IS',
     2            ' GREATER',/,5X,'THAN THE ANTICIPATED MAXIMUM',
     3            ' NUMBER OF RECORDS TO BE PACKED (ND10 = ',
     4            I4,').  STOP 117 IN RDSECT5.')
         CALL W3TAGE('RDSECT5')
         STOP 117
      ENDIF
C
C        PERFORM A CHECK AND ENSURE THAT THE ACTUAL NUMBER OF
C        GROUPS IN THE INDICATOR AND IDENTIFICATION LIST FILE
C        DOES NOT EXCEED THE MAXIMUM NUMBER OF ANTICIPATED
C        GROUPS (ND12) TO BE PROCESSED.
C
      IF(JGROUPS5(J).GT.(ND12-1)) THEN
         WRITE(KFILDO,119) J,ND12
 119     FORMAT(/,' ****THE NUMBER OF GROUPS READ IN THE',
     1            ' INDICATOR AND IDENTIFICATION DEFINITION',
     2            ' FILE (',I4,') IS GREATER',/,5X,
     3            ' THAN THE ANTICIPATED MAXIMUM NUMBER OF',
     4            ' GROUPS TO BE PACKED (ND12 = ',I4,'). ',
     5            ' STOP 119 IN RDSECT5.')
         CALL W3TAGE('RDSECT5')
         STOP 119
      ENDIF
C
C        READ THE NEXT ENTRY IN THE DATA REPRESENTATION LIST.
C
      IF(JTERM.EQ.0) GOTO 113
C
 120  NGROUPS5=J-1
C
      IF(IP16.GT.0) THEN
         WRITE(IP16,145)
 145     FORMAT(/,' THE FOLLOWING ENTRIES ARE FOUND IN',
     1            ' THE DATA REPRESENTATION FILE AND',
     2            ' ARE TO BE PLACED INTO GRIB2:',/)
         DO 170 J=1,NGROUPS5
            DO 160 K=1,JGROUPS5(J) 
               WRITE(IP16,115) IDRSNUM(K,J),
     1                         (IDRSTMPL(N,K,J),N=1,IDRSTMPLEN),
     2                         ITERM,JBIRG5(K,J)
 160        CONTINUE
 170     CONTINUE 
      ENDIF
C
      GOTO 950
C
 900  WRITE(KFILDO,925) KFILSC5 
 925  FORMAT(/,' ****PROBLEM READING THE GRIB ENTRIES IN',
     1         ' THE DATA REPRESENTATION FILE ON UNIT NUMBER ',
     2         I2,'.',/,5X,'STOP 925 IN RDSECT5.')
      CALL W3TAGE('RDSECT5')
      STOP 925
C
 950  RETURN
      END
