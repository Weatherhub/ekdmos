      SUBROUTINE GRIBCLOSE(KFILDO,KFILGO,CGRIB,LCGRIB,NTOTBG)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GRIBCLOSE
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2005-01-11
C
C ABSTRACT: ADD THE FINAL SECTION 8 TO THE GRIB2 MESSAGE,
C   UPDATE THE LENGTH OF THE MESSAGE AND WRITE THE 
C   MESSAGE OUT TO OUTPUT FILE 'KFILGO'.
C
C PROGRAM HISTORY LOG:
C   05-01-11  RUDACK
C   05-03-16  MALONEY ADDED NCEP DOCBLOCK.  ADDED CALLS TO W3TAGE.
C
C USAGE:  CALLED BY RDTDLPK
C
C        DATA SET USE:
C        INPUT FILES: NONE
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C          FORT.KFILGO - UNIT NUMBER OF GRIB2 OUTPUT FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILGO = UNIT NUMBER OF GRIDPOINT OUTPUT FILE. (INPUT)
C            CGRIB(J) = CHARACTER ARRAY THAT CONTAINS THE GRIB2 MESSAGE
C                       (J=1,LCGRIB).  (INPUT)
C              LCGRIB = MAXIMUM LENGTH (BYTES) OF GRIB2 MESSAGE (STORED IN
C                       CHARACTER ARRAY CGRIB( ).)  (INPUT)
C             LENGRIB = NUMBER OF WORDS CONTAINED IN THE MESSAGE.  (INTERNAL)
C              NTOTBG = THE TOTAL NUMBER OF BYTES WRITTEN TO THE GRIB2
C                       OUTPUT FILE.  (OUTPUT)
C               IERR  = ERROR DIAGNOSTICS RETURNED FROM THE SUBROUTINE 
C                       'GRIBEND'.  (INTERNAL)
C
C        SUBPROGRAMS CALLED:  GRIBEND, WRYTE, W3TAGE
C          UNIQUE: - NONE
C          LIBRARY:
C           W3LIB - W3TAGE
C           BACIO - GRIBEND, WRYTE
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                   70 - GRIB2 MESSAGE NOT INITIALIZED
C                   71 - GRIB2 MESSAGE ALREADY COMPLETE
C                   72 - SUM OF SECTION BYTE COUNTS DO NOT ADD
C                        UP TO TOTAL BYTE COUNT
C                   73 - PREVIOUS SECTION WAS NOT 7
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      CHARACTER*1 CGRIB(LCGRIB)
C
      IERR=0
C
      CALL GRIBEND(CGRIB,LCGRIB,LENGRIB,IERR)
C
C        ERROR CHECKS.
C
      IF(IERR.EQ.1) THEN
         WRITE(KFILDO,70) IERR
 70      FORMAT(/,' ****GRIB2 MESSAGE WAS NOT',
     1            ' INITIALIZED.  NEED TO CALL',
     2            ' ROUTINE GRIBCREATE FIRST.',/,5X,
     3            'STOP 70 IN GRIBCLOSE.  IERR = ',I2)
         CALL W3TAGE('GRIBCLOSE')
         STOP 70
      ELSEIF(IERR.EQ.2) THEN
         WRITE(KFILDO,71) IERR
 71      FORMAT(/,' ****GRIB2 MESSAGE ALREADY',
     1            ' COMPLETE.  STOP 71 IN GRIBCLOSE.',
     2            '  IERR = ',I2)
         CALL W3TAGE('GRIBCLOSE')
         STOP 71
      ELSEIF(IERR.EQ.3) THEN
         WRITE(KFILDO,72) IERR
 72      FORMAT(/,' ****SUM OF SECTION BYTE COUNTS',
     1            ' DOES NOT ADD TO TOTAL BYTE COUNT.',
     2            /,5X,'STOP 72 IN GRIBCLOSE.',
     3            '  IERR = ',I2)
         CALL W3TAGE('GRIBCLOSE')
         STOP 72
      ELSEIF(IERR.EQ.4) THEN
         WRITE(KFILDO,73) IERR
 73      FORMAT(/,' ****PREVIOUS SECTION WAS NOT 7.',
     1            '  STOP 73 IN GRIBCLOSE.',
     2            '  IERR = ',I2)
         CALL W3TAGE('GRIBCLOSE')
         STOP 73
      ENDIF
C
C        WRITE THE GRIB2 MESSAGE TO OUTPUT FILE 'KFILGO'.
C
      CALL WRYTE(KFILGO,LENGRIB,CGRIB)
C
C        TALLY THE NUMBER OF BYTES AND RECORDS WRITTEN TO
C        OUTPUT FILE 'KFILGO'.
C
      NTOTBG=NTOTBG+LENGRIB
C
      RETURN
      END
