      SUBROUTINE SCLCIG(KFILDO,KFIL10,NDATE,ID,IDPARS,JD,
     1                  XDATA,FD2,FD3,ND1,NVAL,
     2                  NCAT,CONST,NSCALE,
     3                  LSTORE,ND9,LITEMS,
     4                  IS0,IS1,IS2,IS4,ND7,
     5                  IPACK,IWORK,DATA,ND5,
     6                  CORE,ND10,NBLOCK,NFETCH,
     7                  L3264B,ISTOP,IER)
C
C        AUGUST    2009   GLAHN   TDL   MOS-2000
C                                 ADAPTED FROM SCLQ12
C        DECEMBER  2009   GLAHN   CHANGED SCALING FROM CONTINUOUS
C                                 OUTPUT TO FRACTIONS OF CATEGORIES
C        DECEMBER  2009   GLAHN   MODIFIED; ELIMINATED SOME VARIABLES
C                                 FROM CALL
C        DECEMBER  2009   GLAHN   ADDED FD3( ) TO CALL; MODIFIED TO
C                                 SCALE UPPER CATEGORY 8
C        MARCH     2009   GLAHN   CORRECTED CATEGORY NCAT
C
C        PURPOSE
C            TO SCALE THE VALUES IN A CATEGORY OF A VARIABLE TO
C            A CATEGORY AND FRACTION (E.G., 2 MAY BECOME 2.4)
C            ACCORDING TO THE PROBABILITY RANGE FOR THIS CASE
C            OVER THE DATA BEING ANALYZED, THEN TIMES A 
C            CONST*10**NSCALE.  THE NUMBER OF PROBABILITY CATEGORIES
C            IS NCAT.  THIS ROUTINE IS FOR VECTOR DATA BUT IF A GRID
C            FILLS THE ARRAY, IT CAN BE TREATED AS VECTOR.  THIS
C            WAS WRITTEN FOR CEILING HEIGHT, AND ITABLE( , ) IS
C            SPECIFIC TO THOSE CATEGORIES.  THEREFORE, NCAT MUST EQUAL
C            IDCAT-1.  THE HIGHER PROBABILITIES INDICATE LOWER VALUES
C            IN THE CATEGORY, EXCEPT FOR THE UPPER CATEGORY WHERE THE
C            REVERSE IS TRUE.  THE INPUT IS A WHOLE NUMBER CATEGORY;
C            THE OUTPUT IS THE SAME CATEGORY NUMBER AND A FRACTION.
C            THE WHOLE NUMBER THEN REPRESENTS THE LOWEST VALUE OF THE 
C            RANGE OF THE CATEGORY AND A CATEGORY NUMBER WITH A
C            HIGH FRACTION REPRESENTS NEAR THE UPPER END OF THE 
C            RANGE, EXCEPT FOR THE UPPER CATEGORY WHERE THE OPPOSITE
C            IS TRUE.
C
C            THE PROBABILITIES ARE CUMULATIVE FROM BELOW.  A DISCRETE
C            PROBABILITY IS NOT CALCULATED.
C
C        DATA SET USE
C            KFILDO   - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFIL10   - UNIT NUMBER FOR INTERNAL RANDOM ACCESS STORAGE.
C                       (INPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C              KFIL10 = UNIT NUMBER FOR INTERNAL RANDOM ACCESS STORAGE.
C                       (INPUT/OUTPUT)
C               NDATE = DATE/TIME, YYYYMMDDHH, OF ANALYSIS RUN.
C                       (INPUT)
C               ID(J) = 4-WORD ID OF VARIABLE TO PROVIDE FIRST GUESS FOR
C                       (J=1,4).  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       VARIABLE ID'S CORRESPONDING TO ID( ,N)
C                       (J=1,15), (N=1,ND4).
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C                       (INPUT)
C               JD(J) = THE BASIC INTEGER VARIABLE ID'S (J=1,4) 
C                       (N=1,ND4).
C                       THIS IS THE SAME AS ID(J,N), EXCEPT THAT THE
C                       PORTIONS PERTAINING TO PROCESSING ARE OMITTED:
C                       B = IDPARS(3, ),
C                       T = IDPARS(8,),
C                       I = IDPARS(13, ),
C                       S = IDPARS(14, ),
C                       G = IDPARS(15, ), AND
C                       THRESH( ).
C                       NOT ACTUALLY USED.  (INPUT)
C            XDATA(K) = CATEGORICAL VALUES ON INPUT; SCALED VALUES
C                       ON OUTPUT (K=1,NVAL). (INPUT/OUTPUT)
C              FD2(K) = WORK ARRAY (K=1,NVAL).  HOLDS THE SCALED
C                       CATEGORY VALUES.  (INTERNAL)
C              FD2(K) = WORK ARRAY (K=1,NVAL).  HOLDS THE SUM OF THE
C                       PROBABILITIES OF CATEGORIES 1 THROUGH NCAT-1.
C                      (INTERNAL)
C                 ND1 = FIRST DIMENSION OF XDATA( ) AND DIMENSION
C                       OF FD1( ).  (INPUT)
C                NVAL = NUMBER OF STATIONS BEING USED; THE NUMBER
C                       OF VALUES IN XDATA( ).  (INPUT)
C                NCAT = NUMBER OF PROBABILITY CATEGORIES PLUS 1.  (INPUT)
C               CONST = THE MULTIPLIER FOR SCALING.  (INPUT)
C              NSCALE = THE POWER OF TEN FOR SCALING.  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT/OUTPUT)
C                 ND9 = MAXIMUM NUMBER OF FIELDS STORED IN LSTORE( , ).
C                       SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS J IN LSTORE( ,L).  
C                       (INPUT/OUTPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,4).
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,21+).
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       (INPUT)
C            IPACK(J) = WORK ARRAY FOR GFETCH (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY FOR GFETCH (J=1,ND5).  (INTERNAL)
C             DATA(J) = WORK ARRAY FOR GFETCH (J=1,ND5) AND COMPUTATIONS.
C                       (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), WORK( ), AND DATA( ).
C                       (INPUT)
C             CORE(J) = SPACE ALLOCATED FOR SAVING PACKED GRIDPOINT 
C                       FIELDS (J=1,ND10).  WHEN THIS SPACE IS 
C                       EXHAUSTED, SCRATCH DISK WILL BE USED.  THIS IS 
C                       THE SPACE USED FOR THE MOS-2000 INTERNAL RANDOM 
C                       ACCESS SYSTEM.  (INPUT)
C                ND10 = THE MEMORY IN WORDS ALLOCATED TO THE SAVING OF 
C                       DATA CORE( ).  WHEN THIS SPACE IS EXHAUSTED,
C                       SCRATCH DISK WILL BE USED.  (INPUT)
C              NBLOCK = BLOCK SIZE IN WORDS OF INTERNAL MOS-2000 DISK
C                       STORAGE.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME DATA ARE FETCHED BY
C                       GFETCH.  IT IS A RUNNING COUNT FROM THE
C                       BEGINNING OF THE PROGRAM.  THIS COUNT 
C                       IS MAINTAINED IN CASE THE USER NEEDS IT
C                       (DIAGNOSTICS, ETC.).  (OUTPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C            ISTOP(J) = ISTOP(1) IS INCREMENTED BY 1 WHENEVER AN ERROR 
C                       OCCURS AND THE PROGRAM PROCEEDS.  ISTOP(3) IS
C                       INCREMENTED BY 1 WHEN A DATA RECORD COULD
C                       NOT BE FOUND.  WHEN AN ERROR OCCURS AND IER IS
C                       RETURNED NE 0, THE INCREMENTING OF ISTOP(1)
C                       IS DONE IN THE CALLING PROGRAM (U405A).
C                       (INPUT/OUTPUT)
C                 IER = ERROR CODE. 
C                         0 = GOOD RETURN.
C                       103 = COULD NOT IDENTIFY ID IN INTERNAL TABLE.
C                       777 = ANY OTHER ERROR.
C                        OTHER VALUES FROM CALLED ROUTNES.  EVERY
C                        ERROR IS FATAL FOR THIS ELEMENT.
C                       (OUTPUT) 
C               NSLAB = SLAB OF THE GRID CHARACTERISTICS.  RETURNED
C                       BY GFETCH.  (INTERNAL)
C              NTIMES = THE NUMBER OF TIMES GFETCH HAS BEEN ACCESSED.
C                       (INTERNAL)
C         ITABLE(I,J) = HOLDS THE 4-WORD IDS OF THE NCAT PROBABILITIES
C                       (I=1,4) (J=1,NCAT-1) AND OF THE ACTUAL CEILING
C                       HEIGHT CATEGORY (J=NCAT).  THE IDCAT ENTRY IS
C                       THE 4-WORD ID OF THE VARIABLE BEING PROCESSED
C                       SANS THE DD AND TAU (E.G., THE CATEGORICAL
C                       VARIABLE)  (INTERNAL)     
C        1         2         3         4         5         6         7 X
C
C        NONSYSTEM SUBROUTINES USED 
C            GFETCH
C
      PARAMETER (IDCAT=9)
C
      DIMENSION ID(4),IDPARS(15),JD(4)
      DIMENSION XDATA(ND1),FD2(ND1),FD3(ND1)
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION ISTOP(3),ITABLE(4,IDCAT),LD(4)
C
      DATA ITABLE/208070200,0,0,150001000,
     2            208070200,0,0,450001000,
     3            208070200,0,0,950001000,
     4            208070200,0,0,195002000,
     5            208070200,0,0,305002000,
     6            208070200,0,0,655002000,
     7            208070200,0,0,120503000,
     8            208070200,0,0,999994000,
     9            208071000,0,0,000000000/
C
      IER=0
C
C        DETERMINE WHETHER VARIABLE IS IN THE ITABLE( ,IDCAT).
C        THE DD IS NOT IN THE TABLE IN CASE THE MODEL CHANGES.
C        THE TAU IS NOT IN THE TABLE TO MAKE IT GENERIC, BUT
C        IS IN ID(3).
C
D     WRITE(KFILDO,101)NCAT,
D    1              ((ITABLE(I,J),I=1,4),J=1,NCAT)
D101  FORMAT(/' AT 101 IN SCLCIG--NCAT,',
D    1             '((ITABLE(I,J),I=1,4),J=1,NCAT)',/,
D    2         I6,/,(4I11))
C
      IF(ID(1).EQ.ITABLE(1,IDCAT)+IDPARS(4).AND.
     1   ID(2).EQ.ITABLE(2,IDCAT).AND.
     2   (ID(3)/1000).EQ.(ITABLE(3,IDCAT)/1000).AND.
     3   ID(4).EQ.ITABLE(4,IDCAT))THEN
         GO TO 111
      ENDIF
C
C        DROP THROUGH HERE MEANS THE ID WAS NOT FOUND.
C
      IER=103
      WRITE(KFILDO,110)(ID(J),J=1,4),IER
 110  FORMAT(/' ****VARIABLE ',I9.9,I10.9,I10.9,I4.3,' NOT',
     1        ' ACCOMMODATED IN SUBROUTINE SCLCIG.  IER =',I3)
      GO TO 900
C
 111  IF(NCAT.NE.IDCAT-1)THEN
         IER=103
         WRITE(KFILDO,112)NCAT,(ID(J),J=1,4),IER
 112     FORMAT(/,' ****NCAT =',I3,' DOES NOT EQUAL IDCAT-1 IN SCLCIG.',
     1            '  CANNOT PROCESS VARIABLE ',I9.9,I10.9,I10.9,I4.3,
     2            '.  IER =',I3)
         GO TO 900
      ENDIF
C
C        FIND THE NCAT PROBABILITIES AND SCALE.  THE CATEGORICAL
C        VALUES ARE IN XDATA( ) ON INPUT AND WILL BE MODIFIED.
C
      FACTOR=CONST*10**NSCALE
C
D     WRITE(KFILDO,113)ND1,NVAL,FACTOR,(IDPARS(M1),M1=1,15)
D113  FORMAT(/' AT 113--,ND1,NVAL,FACTOR,(IDPARS(M1),M1=1,15)',
D    1          2I12,F10.4/(15I8))
C
D     WRITE(KFILDO,114)(K,XDATA(K),K=1,NVAL)
D114  FORMAT(/,' IN SCLCIG AT 114',/,(8(I7,F8.2)))
C
C        SET FD2( ) = 9999.
C
      DO 115 K=1,NVAL
      FD2(K)=9999.
      FD3(K)=0.
C        SUM OF PROBABILITIES SET TO ZERO.
 115  CONTINUE
C
      DO 200 J=1,NCAT
C        PROBABILITIES ARE AVAILABLE FOR ONLY NCAT-1 CATEGORIES.
C        SUM THE NCAT-1 PROBABILITIES TO USE FOR CATEGORY NCAT.
C
D     WRITE(KFILDO,117)J,(ITABLE(M1,J),M1=1,4)
D117  FORMAT(/' AT 116--J,(ITABLE(M1,J),M1=1,4)',
D    1          5I12)
C
      IF(J.LT.NCAT)THEN
C           GET THE PROBABILITY OF CATEGORY J.  CAN BE A 
C           GRID OR VECTOR.
C
         LD(1)=ITABLE(1,J)+IDPARS(4)
C           THE DD IS ADDED.
         LD(2)=ITABLE(2,J)
         LD(3)=ITABLE(3,J)+IDPARS(12)
C           THE TAU IS ADDED.
         LD(4)=ITABLE(4,J)
         CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1               IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,DATA,ND5,
     2               NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3               NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER) 
C
         IF(IER.NE.0)THEN
            ISTOP(3)=ISTOP(3)+1
            WRITE(KFILDO,120)(LD(M1),M1=1,4)
 120        FORMAT(/' ****COULD NOT FIND PROBABILITY RECORD',
     1              3I10.9,I10,'.  FATAL ERROR IN SCLCIG AT 120.')
            GO TO 900
         ENDIF
C
      ELSE
C
C           THESE ARE CUMULATIVE PROBABILITIES, SO THE PROBABILITY
C           OF THE UPPER CATEGORY, NCAT=8, IS UNITY MINUS THE 
C           PROBABILITY OF CATEGORY NCAT-1.
C
         DO 135 K=1,NVAL
            DATA(K)=1.-DATA(K)
 135     CONTINUE
C
      ENDIF
C
C        FIND THE MAX AND MIN PROBABILITY FOR THIS CATEGORY.
C
 1490 XMAX=-99999.
      XMIN=99999.
D     ICOUNT=0
C
      DO 150 K=1,NVAL
C
      IF(NINT(DATA(K)).EQ.9999)GO TO 150
C        CHECKING THE PROBABILITY.  IT IS POSSIBLE THERE COULD
C        BE MISSING PROBABILITIES EVEN THOUGH THE CATEGORICAL.
C        VALUE IS THERE.
C
      IF(NINT(XDATA(K)).EQ.J)THEN
D        ICOUNT=ICOUNT+1
C
         IF(DATA(K).LT.XMIN)THEN
            XMIN=DATA(K)
         ENDIF
C   
         IF(DATA(K).GT.XMAX)THEN
            XMAX=DATA(K)
         ENDIF
C
D        WRITE(KFILDO,1495)XDATA(K),DATA(K),XMAX,XMIN,ICOUNT
D1495    FORMAT(' IN SCLCIG AT 1495--XDATA(K),DATA(K),XMAX,XMIN',
D    1          'ICOUNT',4F12.3,I7)
      ENDIF
C
 150  CONTINUE
C
      IF(XMAX.EQ.-99999.)THEN
C           THERE WERE NO FORECASTS IN THIS CATEGORY.
         WRITE(KFILDO,152)J
 152     FORMAT(/' THERE WERE NO FORECASTS IN CATEGORY',I4)
         GO TO 200
      ENDIF
C
      IF(XMAX.EQ.XMIN)THEN
C
         A=J+.5
         B=0.
         RANGE=0.
C           FOR A CONSTANT VALUE, THE OUTPUT IS THE MIDPOINT
C           OF THE CATEGORY NUMBER.  THIS MIGHT HAPPEN
C           IF THERE WERE ONLY ONE INSTANCE OF THE CATEGORY.
C           NOTE THAT IF THERE ARE ONLY TWO INSTANCES AND
C           THEY ARE DIFFERENT, THE OUTPUT WILL BE ONE VALUE
C           AT THE LOW END OF THE CATEGORY AND ONE AT THE 
C           HIGH END--PROBABLY NOT A GOOD THING AND MAY 
C           HAVE TO BE MODIFIED, BUT OUGHT TO HAPPEN VERY
C           INFREQUENTLY.
      ELSE
         RANGE=XMAX-XMIN
         B=1./RANGE
         A=J+1+B*XMIN
      ENDIF
C
D     WRITE(KFILDO,160)XMAX,XMIN,RANGE,A,B
D160  FORMAT(/,' IN SCLCIG AT 160--XMAX,XMIN,RANGE,A,B',
D    1         5F10.3)
C
      XMAXJ=J+.99
C        XMAXJ IS USED TO KEEP THE CATEGORY FROM GOING INTO
C        THE NEXT HIGHER CATEGORY.  THIS IS CRITICAL FOR THE
C        UPPER CATEGORY.
C
      DO 170 K=1,NVAL
C******************************
CCC      WRITE(KFILDO,162)K,XDATA(K),DATA(K)
CCC 162  FORMAT(' ',I7,2F9.3)
C******************************
C
      IF(XDATA(K).GT.9998.9)GO TO 170
C        IF THE CATEGORICAL VALUE IS MISSING, NO COMPUTATIONS
C        POSSIBLE.  FD2( ) HAS ALREADY BEEN INITIALIZED TO 9999.
C
      IF(DATA(K).GT.9998.9)GO TO 170
C        THIS GUARDS AGAINST A PROBABILITY BEING MISSING WHEN
C        A CATEGORICAL VALUE IS THERE.  THIS SHOULD NOT REALLY
C        HAPPEN.
C
      IF(NINT(XDATA(K)).EQ.J)THEN
C
         IF(J.LT.NCAT)THEN
            FD2(K)=MIN(A-B*DATA(K),XMAXJ)*FACTOR
C
C              FOR LOWER CATEGORIES, A HIGH PROBABILITY MEANS
C              A LOW CEILING HEIGHT.  FOR THE UPPER CATEGORY,
C              NCAT, A HIGHER PROBABILITY MEANS A HIGH CEILING
C              HEIGHT.  REVERSE THE ORIENTATION HERE.
         ELSE
            FD2(K)=MIN(J+B*(DATA(K)-XMIN),XMAXJ)*FACTOR
         ENDIF
C
D        WRITE(KFILDO,165)J,K,A,B,DATA(K),XDATA(K),FD2(K)
D165     FORMAT(' AT 165--J,K,A,B,DATA(K),XDATA(K),FD2(K)',
D    1           2I6,5F10.3)
C
      ENDIF
C
 170  CONTINUE
C
 200  CONTINUE
C
C        THE COMPUTATIONS WERE IN FD2( ); PUT THEM IN XDATA( ).
C
      DO 210 K=1,NVAL
      XDATA(K)=FD2(K)
 210  CONTINUE
C
D     WRITE(KFILDO,225)(K,XDATA(K),K=1,NVAL)
D225  FORMAT(/,' IN SCLCIG AT 225',/,(8(I7,F8.2)))
C
 900  RETURN
      END
