      SUBROUTINE CIGFT(KFILDO,XDATA,NVAL,TLO,SETLO,THI,SETHI,
     1                 CONST,NSCAL,EX1,EX2,IER)
C
C        MARCH     2008   GLAHN   TDL   MOS-2000
C
C        PURPOSE
C            TO POSTPROCESS A CEILING HEIGHT CATEGORY INTO A
C            CONTINUOUS VARIABLE FT.  THE VARIABLE IN XDATA( )
C            IS FIRST SET TO XDATA( )*CONST*10**NSCAL AFTER SETTING
C            ALL VALUES LT TLO TO SETLO AND ALL VALUES GT THI
C            TO SETHI.  EX1 AND EX2 ARE FOR POSSIBLE FUTURE USE.
C            THE HIGHEST CATEGORY (NOCAT=8) MEANS GT 12,000 FT
C            OR UNLIMITED; IT IS SET TO 888.
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            XDATA(K) = THE DATA TO SCALE (K=1,NVAL).  (INPUT-OUTPUT)
C                NVAL = THE NUMBER OF VALUES IN XDATA( ) BEING DEALT
C                       WITH.  (INPUT)
C                 TLO = LOW THRESHOLD.  WHEN A LAST PASS GRIDPOINT IS
C                       LT TLOD, IT IS SET TO SETLOD, THEN CONST
C                       AND NSCAL APPLIED.  (INPUT)
C               SETLO = SEE TLOD.  (INPUT)
C                 THI = HIGH THRESHOLD.  WHEN A LAST PASS  GRIDPOINT IS
C                       GT THID, IT IS SET TO SETHID, THEN CONST
C                       AND NSCAL APPLIED.  (INPUT)
C               SETHI = SEE THID.  (INPUT)
C               CONST = ADDITIVE CONSTANT TO FURNISH TO THRESHOLDING
C                       AND SCALING SUBROUTINE.  (INPUT)
C               NSCAL = SCALING CONSTANT TO FURNISH TO THRESHOLDING
C                       AND SCALING SUBROUTINE.  (INPUT)
C                 EX1 = EXTRA PARAMETER NOT YET USED FOR THRESHOLDING.
C                       (INPUT)
C                 EX2 = EXTRA PARAMETER NOT YET USED FOR THRESHOLDING.
C                       (INPUT)
C                 IER = ERROR RETURN.
C                       0 = GOOD RETURN.
C                       (OUTPUT)
C          TABLE(J,M) = HOLDS THE LOWER AND UPPER CATEGORY BREAKPOINTS
C                       FOR THE NOCAT CATEGORIES OF CEILING HEIGHT
C                       (M=1,NOCAT), (J=1,2).  (INTERNAL)
C               NOCAT = THE NUMBER OF CEILING HEIGHT CATEGORIES FORECAST
C                       BY LAMP.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      PARAMETER(NOCAT=8)
C
      DIMENSION XDATA(NVAL)
      DIMENSION TABLE(2,NOCAT)
C
      DATA TABLE/  -.5, 1.5,
     2             1.5, 4.5,
     3             4.5, 9.5,
     4             9.5, 19.5,
     5            19.5, 30.5,
     6            30.5, 65.5,
     7            65.5,120.0,
     8           120.0,999.0/
C
CD     CALL TIMPR(KFILDO,KFILDO,'START CIGFT         ')
      IER=0
C
      WRITE(KFILDO,102)TLO,SETLO,THI,SETHI,CONST,NSCAL,EX1,EX2
 102  FORMAT(/' AT 102 IN CIGFT--TLO,SETLO,THI,SETHI,CONST,NSCAL,',
     1        'EX1,EX2',5F10.4,I4,2F10.4)
C
CD     WRITE(KFILDO,105)(XDATA(K),K=1,NVAL)
CD105  FORMAT(/,' IN CIGFT AT 105--XDATA(K)',/,(15F8.2))
C
      FACTOR=CONST*10.**NSCAL
      SETLOF=SETLO*FACTOR
      SETHIF=SETHI*FACTOR
C
      IF(TLO.LE.-99999.5.AND.
     1   THI.GE.+99998.5)GO TO 125
C
C         TRUNCATE AT HIGH AND/OR LOW ENDS.
C
      DO 120 K=1,NVAL
C
      IF(NINT(XDATA(K)).NE.9999)THEN
C
         IF(XDATA(K).LT.TLO)THEN
            XDATA(K)=SETLOF
         ELSEIF(XDATA(K).GT.THI)THEN
            XDATA(K)=SETHIF
         ENDIF
C
      ENDIF
C
 120  CONTINUE
C
C        TRUNCATION HAS BEEN DONE, IF NEEDED.  NOW TURN
C        ANALYZED CATEGORICAL VALUES INTO ACTUAL HEIGHTS IN FEET.
C
 125  DO 160 K=1,NVAL
C
      IF(XDATA(K).GT.9998.5)GO TO 160
C        MISSINGS HAVE BEEN INSERTED WITH CLIPPING FOR THE
C        ARCHIVE.  FOR THE DISPOSABLE, THE BORDERS ARE FIRST
C        GUESS = 0.
C
      J=XDATA(K)+.499
C        THIS TRUNCATES TO THE CATEGORY NUMBER J.  A VALUE OF
C        2.5 TO 3.5 SHOULD GO INTO CATEGORY 3.
C        NOTE THAT J CAN GO OUTSIDE THE RANGE 1 TO NOCAT FOR
C        ANAYZED VALUES.
C
      IF(J.GE.NOCAT)THEN
         XDATA(K)=888.
C           THE LAST CATEGORY IS GT 120 OR UNLIMITED.
C           SET TO 888.
      ELSEIF(J.LT.1)THEN
         XDATA(K)=0.
C           ANY ANALYZED CATEGORICAL VALUE LT 1, SET HEIGHT TO
C           ZERO FT.
      ELSE
         
         R=TABLE(2,J)-TABLE(1,J)
C
CD        WRITE(KFILDO,135)J,TABLE(1,J),TABLE(2,J),XDATA(K),R
CD135     FORMAT(' AT 135 IN CIGFT--J,TABLE(1,J),TABLE(2,J),',
CD    1          'XDATA(K),R',I4,4F10.3)
C     
         XDATA(K)=(XDATA(K)-(J-.5))*R+TABLE(1,J)
C
CD        WRITE(KFILDO,136)J,XDATA(K),R
CD136     FORMAT(' AT 136 IN CIGFT--J,XDATA(K),R',I4,2F10.3)
C
         XDATA(K)=XDATA(K)*FACTOR
      ENDIF
C
 160  CONTINUE
C
CD     WRITE(KFILDO,165)(XDATA(K),K=1,NVAL)
CD165  FORMAT(/,' IN CIGFT AT 165--XDATA(K)',/,(15F8.2))
C
      RETURN
      END
