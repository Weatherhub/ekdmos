      SUBROUTINE VISOBC(KFILDO,XDATA,NVAL,IER)
C
C        MAY       2010   GLAHN  MDL   MOS-2000
C
C        PURPOSE
C            TO PREPROCESS A VISIBILITY IN MILES TO A
C            SCALED CATEGORICAL VALUE MATCHING THE VISIBILITY
C            CATEGORIES USED IN MOS.  THE CATEGORY IS GIVEN
C            AS A SCALED CONTINUOUS VALUE REPRESENTING THE ACTUAL
C            VISIBILITY.  FOR INSTANCE, ANY VALUE IN MI THAT IS
C            LE TABLE(2) (.5 MI) WILL BE GIVEN A VALUE FROM
C            1. TO < 2., INDICATING THE NEARNESS TO THE END
C            POINTS 0. AND .5.  THE DISCRETE NATURE OF VISIBILITY
C            REPORTS MAKE ONLY CERTAIN VALUES POSSIBLE.  AN OB
C            OF > 20 MI, IS GIVEN A SCALED VALUE 7.99.  A VALUE
C            JUST OVER 6 MI IS GIVEN A VALUE JUST OVER 7.  THIS
C            ALLOWS A GRID WITH VALUES UP TO AND INCLUDING 20,
C            WHICH IS CONSISTENT WITH THE LAMP FORECAST GRID.
C
C            THE CATEGORIES ARE DEFINED SUCH THE THE LOWEST OF
C            THE FOLLOWING IS MET:
C
C            CATEGORY    HEIGHT (FT)
C                1        <     .5
C                2        <    1.0
C                3        <    2.0
C                4        <    3.0
C                5        <=   5.0
C                6        <=   6.0
C                7        >    6.0
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C            XDATA(K) = THE DATA TO TRANSFORM TO CATEGORIES (K=1,NVAL).
C                       IT IS ASSUMED INCOMING DATA ARE IN MILES.
C                       (INPUT-OUTPUT)
C                NVAL = THE NUMBER OF VALUES IN XDATA( ) BEING DEALT
C                       WITH.  (INPUT)
C                 IER = ERROR RETURN.
C                       0 = GOOD RETURN.
C                       (OUTPUT)
C            TABLE(J) = HOLDS THE UPPER CATEGORY BREAKPOINTS FOR THE
C                       NOCAT CATEGORIES OF CEILING (M=2,NOCAT+1).
C                       THE LAST ONE REPRESENTS A LIMIT TO LIKELY
C                       REPORTED VALUES.  (INTERNAL)
C               NOCAT = THE NUMBER OF VISIBILITY CATEGORIES.
C                       (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      PARAMETER(NOCAT=7)
C
      DIMENSION XDATA(NVAL)
      DIMENSION TABLE(NOCAT+1)
C
      DATA TABLE/ 0., .5, 1.0, 2.0, 3.0, 5.01, 6.01, 20.01/
C
D     CALL TIMPR(KFILDO,KFILDO,'START VISOBC        ')
      IER=0
C
CCCCD     WRITE(KFILDO,105)(K,XDATA(K),K=1,NVAL)
CCCCD105  FORMAT(/,' IN VISOBC AT 105',/,(8(I7,F8.2)))
C
      DO 160 K=1,NVAL
C
      IF(XDATA(K).GT.9998.9)GO TO 160
C
      DO 150 J=1,NOCAT
C
      IF(XDATA(K).GE.TABLE(J).AND.XDATA(K).LT.TABLE(J+1))THEN 
C
D        SAVEX=XDATA(K)
C
         XDATA(K)=J+(XDATA(K)-TABLE(J))/(TABLE(J+1)-TABLE(J))
C
D        WRITE(KFILDO,144)K,J,SAVEX,XDATA(K),TABLE(J+1),TABLE(J)
D144     FORMAT(' IN VISOBC--K,J,SAVEX,XDATA(K),TABLE(J+1),TABLE(J)--',
D    1          2I6,F8.1,F12.5,2F8.2)             
C
         GO TO 160
      ENDIF
C
 150  CONTINUE
C
      IF(XDATA(K).LT.0.)THEN
C           GUARD AGAINST NEGATIVES, WHICH SHOULD NOT BE PRESENT.
C           BY PUTTING THE CHECK HERE, IT IS EXECUTED VERY INFREQUENTLY.
         WRITE(KFILDO,155)XDATA(K)
 155     FORMAT(/' NEGATIVE VISIBILITY =',F10.4,
     1           'FOUND IN VISOBC.  SET TO MISSING.')
         XDATA(K)=9999.
      ELSE
         XDATA(K)=7.99
C           THERE IS NO LIMIT TO THE REPORTED VISIBILITY. THE 
C           SCALING IS LIMITED TO 7.99 REPRESENTING 20 MI. OR GREATER.
      ENDIF
C
 160  CONTINUE
C
      RETURN
      END
