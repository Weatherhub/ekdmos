      SUBROUTINE SPOTRM(KFILDO,KFILOG,IP16,
     1                  CCALL,XP,YP,LNDSEA,LTAGPT,NSTA,
     2                  ID,IDPARS,P,HOLD,MESH,NX,NY,
     3                  TELEV,SEALND,CPNDFD,NXE,NYE,MESHE,
     4                  IPACK,DATA,IWORK,ND5,
     5                  MODNO,NDATE,
     6                  ALATL,ALONL,NPROJ,ORIENT,XLAT,ISCALD,
     7                  DIFFA,NOCEAN,SQUEZE,DPOWER,RAY,RMAX,
     8                  IS0,IS1,IS2,IS4,ND7,
     9                  JTOTBY,JTOTRC,PLAINT,IPLANT,
     A                  L3264B,L3264W,MINPK,ISTOP,IER)
C
C        MARCH     2010   GLAHN   MDL   MOS-2000
C                                 NEW ON-THE-FLY VERSION
C        APRIL     2010   GLAHN   ADDED LTAGPT( ) AND CHECK FOR DATA
C        APRIL     2010   GLAHN   CHANGED WHERE DIST( , ) IS SET LARGE
C        MAY       2010   GLAHN   ADDED 3 CLOSEST STATIONS
C        MAY       2010   GLAHN   IMPLEMENTED SQUEZE AS COMBINATION
C                                 OF IOCEXT AND WT( , )
C        MAY       2010   GLAHN   REMOVED GO TO'S THAT HAMPERED
C                                 PARALLELIZATION; TESTED NSQEQ
C                                 EARLIER AT 100; SPELL CHECK; IPRTEL
C                                 CHANGED FROM 1 TO ZERO
C        JULY      2010   SCALLION   OPEN MP STATEMENTS PUT IN IN
C                                 SEVERAL PLACES; SPELL CHECK
C
C        PURPOSE
C            TO SMOOTH POINTS ON A GRID OVER A CIRCLE.  THE CIRCLE
C            WILL BE OF DIAMETER IOCEXT GRID LENGTHS.  IOCEXT
C            IS A FUNCTION OF THE GRIDPOINT VALUE IN THE GRID
C            DIST( , , ) WHICH IS THE DISTANCE TO THE CLOSEST STATION
C            IN GRID UNITS.  THE VARIABLE "RAY" IS USED TO CONTROL
C            THE CIRCLE SIZE.  IF THE ELEVATION DIFFERENCE BETWEEN
C            A POINT AND THE POINT BEING SMOOTHED IS GT DIFFA 
C            THE, POINT IS NOT INCLUDED IN THE SMOOTHING.  P(NX,JY),
C            TELEV(NXE,NYE), AND SEALND(NXE,NYE) MUST COVER THE SAME
C            AREA AND BE OF THE SAME MESH LENGTH.  THE GRID SIZES
C            AND MESH LENGTHS ARE CHECKED.  A MISSING POINT IS KEPT
C            AS MISSING.  SQUEZE IS USED TO INDICATE A SMOOTHING
C            COMBINATION OF IOCEXT AND WT( , ).
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILOG    - UNIT NUMBER FOR DISPOSABLE TDLPACK GRIDPOINT
C                        OUTPUT.  (OUTPUT)
C            KFILRA(J) - HOLDS THE UNIT NUMBERS FOR ACCESSING THE
C                        MOS-2000 EXTERNAL RANDOM ACCESS FILES (J=1,6).
C                        (INPUT)
C            IP16      - UNIT NUMBER FOR INDICATING WHEN A RECORD IS
C                        WRITTEN TO THE SEQUENTIAL FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE.  (INPUT)
C              KFILOG = UNIT NUMBER FOR DISPOSABLE TDLPACK GRIDPOINT
C                       OUTPUT.  THIS IS FOR DIFFERENT PASSES OF THE
C                       ANALYSES AND THEIR SMOOTHINGS.  USED HERE
C                       FOR DISTANCE MAP.  (INPUT)
C                IP16 = INDICATES WHETHER (>0) OR NOT (=0) 
C                       A STATEMENT WILL BE OUTPUT TO IP(16)
C                       WHEN A SEQUENTIAL FILE IS WRITTEN THROUGH
C                       PAWGTS.  (INPUT))
C            CCALL(J) = STATION CALL LETTERS (J=1,NSTA).  (CHARACTER*8)
C                       (INPUT)
C               XP(K) = THE X POSITION FOR STATION K (K=1,NSTA) ON 
C                       THE ANALYSIS GRID AREA AT THE CURRENT GRID MESH 
C                       LENGTH MESH.  (INPUT)
C               YP(K) = THE Y POSITION FOR STATION K (K=1,NSTA) ON 
C                       THE ANALYSIS GRID AREA AT THE CURRENT GRID MESH 
C                       LENGTH MESH.  (INPUT)
C           LNDSEA(K) = LAND/SEA INFLUENCE FLAG FOR EACH STATION
C                       (K=1,ND1).
C                       0 = WILL BE USED FOR ONLY OCEAN WATER (=0)
C                           GRIDPOINTS.
C                       3 = WILL BE USED FOR ONLY INLAND WATER (=3)
C                           GRIDPOINTS.
C                       6 = WILL BE USED FOR BOTH INLAND WATER (=3)
C                           AND LAND (=9) GRIDPOINTS.
C                       9 = WILL BE USED FOR ONLY LAND (=9) GRIDPOINTS.
C                       (INPUT)
C           LTAGPT(K) = 0 INDICATES THIS IS A BASE STATION WITH DATA
C                       (K=1,NSTA).  (INPUT)
C                NSTA = NUMBER OF STATIONS IN CCALL( ).  (INPUT)
C               ID(J) = 4 WORD ID OF VARIABLE BEING ANALYZED.  (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       PREDICTOR ID'S CORRESPONDING TO ID( ,N)
C                       (J=1,15), (N=1,ND4).
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C                       (INPUT)
C            P(IX,JY) = HOLDS A FIELD TO BE SMOOTHED (INPUT) AND
C                       THE SMOOTHED GRID (OUTPUT) (IX=1,NX)
C                       (JY=1,NY).  (INPUT/OUTPUT)
C         HOLD(IX,JY) = SCRATCH ARRAY USED IN COMPUTING THE SMOOTHED
C                       FIELD (IX=1,NX) (JY=1,NY).  (INTERNAL)
C                MESH = NOMINAL MESH LENGTH OF GRID IN P( , ).
C                       (INPUT)
C                  NX = SIZE OF P( , ) IN X DIRECTION.  (INPUT)
C                  NY = SIZE OF P( , ) IN Y DIRECTION.  (INPUT)
C        TELEV(IX,JY) = THE TERRAIN ELEVATION (IX=1,NX) (JY=1,NY) AT
C                       NOMINAL MESHLENGTH MESHE.  (INPUT)
C       SEALND(IX,JY) = THE LAND/SEA MASK (IX=1,NX) (JY=1,NY). 
C                       0 = OCEAN WATER GRIDPOINTS;
C                       3 = INLAND WATER GRIDPOINTS.
C                       9 = LAND GRIDPOINTS.
C                       (INPUT)
C       CPNDFD(IX,JY) = THE NDFD MASK (IX=1,NX) (JY=1,NY) AT
C                       NOMINAL MESHLENGTH MESHE.  A "1" MEANS THE
C                       GRIDPOINT IS WITHIN THE AREA; A "0" MEANS IT
C                       IS OUTSIDE THE AREA.  (INPUT)
C                 NXE = X-EXTENT OF TELEV( , ), SEALND( , ), AND
C                       CPNDFD( , ) AT MESH LENGTH MESHE.  (INPUT)
C                 NYE = Y-EXTENT OF TELEV( , ), SEALND( , ), AND
C                       CPNDFD( , ) AT MESH LENGTH MESHE.  (INPUT)
C               MESHE = NOMINAL MESH LENGTH OF GRID IN SEALND( , ),
C                       TELEV( , ), AND CPNDFD( , ).  (INPUT)
C            IPACK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C             DATA(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY (J=1,ND5).  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), AND DATA( ).
C                       (INPUT)
C               MODNO = OUTPUT MODEL NUMBER.  (INPUT)
C               NDATE = DATE/TIME, YYYYMMDDHH.  THIS IS THE ANALYSIS
C                       RUN TIME, INCLUDING HH.  (INPUT)
C               ALATL = NORTH LATITUDE OF LOWER LEFT CORNER POINT
C                       OF A GRID OF THE SIZE  NXL, NYL.  TRUNCATED
C                       TO TEN THOUSANDTHS OF DEGREES.  NOTE THAT THE
C                       MOS-2000 ARCHIVE IS ONLY TO THOUSANDTHS OF
C                       DEGREES.  (INPUT)
C               ALONL = WEST LONGITUDE OF LOWER LEFT CORNER POINT
C                       OF A  GRID OF THE SIZE  NXL, NYL.  TRUNCATED
C                       TO TEN THOUSANDTHS OF DEGREES.  NOTE THAT THE
C                       MOS-2000 ARCHIVE IS ONLY TO THOUSANDTHS OF
C                       DEGREES.  (INPUT)
C               NPROJ = NUMBER OF MAP PROJECTION TO WHICH THIS GRID
C                       APPLIES.
C                       3 = LAMBERT.
C                       5 = POLAR STEREOGRAPHIC.
C                       7 = MERCATOR.
C                       (INPUT)
C              ORIENT = ORIENTATION OF GRID IN WEST LONGITUDE.  (INPUT)
C                XLAT = NORTH LATITUDE AT WHICH GRIDLENGTH IS SPECIFIED
C                       IN DEGREES.  (INPUT)
C              ISCALD = DECIMAL SCALING FOR TDLPACKING.  (INPUT)
C               DIFFA = THE MAXIMUM DIFFERENCE IN ELEVATION IN METERS
C                       BETWEEN THE GRIDPOINT BEING SMOOTHED AND THE
C                       GRIDPOINT USED IN SMOOTHING.  (INPUT)
C              NOCEAN = INDICATES WHETHER (=1) OR NOT (=0) THE SPOTRM
C                       SMOTHER WILL BE EXTENDED OVER THE OCEAN.
C                       (INPUT)
C              SQUEZE = SPECIFICS A COMBINATION IOCEXT AND WT( , ) TO
C                       USE.  (INPUT)
C                       0 = IOCEXT=RAY*DIST( , ,1)
C                           WT(IX,JY)=1/DIST(IX,JY,1)**POWER
C                       1 = IOCEXT=RAY*DIST( , ,1)
C                           WT(IX,JY)=1./(((DIST(IX,JY,1)+DIST(IX,JY,2)
C                                          +DIST(IX,JY,3))/3.)
C                       2 = IOCEXT=RAY*(DIST(IX,JY,1)+DIST(IX,JY,2)
C                                          +DIST(IX,JY,3))/3.)
C                           WT(IX,JY)=1./(((DIST(IX,JY,1)+DIST(IX,JY,2)
C                                          +DIST(IX,JY,3))/3.)
C              DPOWER = THE POWER OF THE DISTANCE TO USE IN WEIGHTING.
C                       (INPUT)
C                 RAY = THE MULTIPLIER OF THE DISTANCE TO THE CLOSEST
C                       STATION TO USE AS THE RADIUS OF THE SMOOTHING
C                       CIRCLE (OR MAJOR AXIS OF ELLIPSE).  (INPUT)
C                RMAX = MAXIMUM RADIUS OF CIRCLE, TAKEN FROM THE
C                       CONSTANT RADIUS OF THE FIRST PASS R(1).  THIS
C                       KEEPS ELIMINATES EXCESSIVE COMPUTATIONS OUTSIDE
C                       AREAS OF DATA.  (INPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,4).
C                       (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,21+).
C                       (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C                       (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                       (INTERNAL)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND
C                       IS4( ).  (INPUT)
C              JTOTBY = THE TOTAL NUMBER OF BYTES ON THE FILE ASSOCIATED
C                       WITH UNIT NO. KFILOG.  (INPUT/OUTPUT)
C              JTOTRC = THE TOTAL NUMBER OF RECORDS IN THE FILE ON UNIT
C                       NUMBER KFILOG.  (INPUT/OUTPUT)
C              PLAINT = THE PLAIN LANGUAGE DESCRIPTION TO FURNISH TO
C                       GTHRES. EQUIVALENCED TO IPLANT IN CALLING
C                       ROUTINE.  (INPUT)
C         IPLANT(I,J) = EQUIVALENCED TO PLAINT IN CALLING ROUTINE
C                       (I=1,L3264W) (J=1,4).  (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT).
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).
C                       (INPUT)
C               MINPK = MINIMUM GROUP SIZE WHEN PACKING THE DATA.
C                       (INPUT)
C               ISTOP = INCREMENTED BY 1 IF THERE IS AN ERROR.
C                       (INPUT/OUTPUT)
C                 IER = RETURN CODE.
C                         0 = GOOD RETURN.
C                       777 = ANY ERROR IS RETURNED WITH A 777, BUT
C                             CALLING PROGRAM WILL PROBABLY IGNORE IT.
C                       (OUTPUT)
C       DIST(IX,JY,J) = THE FIELD OF DISTANCES TO THE NEAREST THREE
C                       GRIDPOINTS (IX=1,NX) (JY=1,NY) (J=1,3).
C                       (INTERNAL)  (ALLOCATABLE)
C           WT(IX,JY) = WEIGHTS FOR SMOOTHING (IX=1,NS) (JY=1,NY).
C                       (INTERNAL)  (ALLOCATABLE)
C              IOCEXT = THE NUMBER OF GRIDPOINTS PLUS AND MINUS 
C                       OF THE POINT P(IX,JY) TO SMOOTH.  IT IS 
C                       A FUNCTION OF RAY*DIST(IX,JY,J).   (INTERNAL)
C         ITABLE(J,L) = CORRESPONDENCE BETWEEN NOMINAL MESH LENGTH
C                       (L=1) AND VALUE FOR X IN 408CX0000 (L=2),
C                       (J=1,7).
C              IPRTEL = 1 TO WRITE THE RETRIEVED DISTANCE GRID TO THE
C                       OUTPUT FILE VIA GTHRES.  (INTERNAL)
C      NXSAVE, NYSAVE = SAVED VALUES OF NX AND NY TO MAKE SURE THE
C                       ALLOCATED ARRAYS ARE OF CORRECT SIZE.
C                      (INTERNAL)
C              IFIRST = CONTROLS THE ALLOCATION OF ARRAYS.  (INTERNAL)
C              JFIRST = CONTROLS THE WRITING OF THE DISTANCE GRID.  IT
C                       IS WRITTEN ONLY ONCE.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C
      CHARACTER*6 RACESS(6)
      CHARACTER*8 CCALL(8)
      CHARACTER*32 PLAINT
C
      DIMENSION ID(4),LD(4),IDPARS(15)
      DIMENSION XP(NSTA),YP(NSTA),LNDSEA(NSTA),LTAGPT(NSTA)
      DIMENSION P(NX,NY),HOLD(NX,NY)
      DIMENSION TELEV(NXE,NYE),SEALND(NXE,NYE),CPNDFD(NXE,NYE)
      DIMENSION IPLANT(L3264W,4)
C        EQUIVALENCED TO PLAINT IN DRIVER.
      DIMENSION IPACK(ND5),IWORK(ND5),DATA(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION ITABLE(7,2),IDSAVE(4)
C
      ALLOCATABLE WT(:,:),DIST(:,:,:)
      SAVE WT,DIST
      SAVE NXSAVE,NYSAVE,IDSAVE

      DATA ITABLE/80, 40, 20, 10, 5, 3, 1,
     1             0,  1,  2,  3, 4, 5, 6/
      DATA IPRTEL/1/
C        THIS WRITES THE CLOSEST INSTANCE GRID TO THE OUTPUT FILE
C        LFILOG.  AFTER CHECKOUT, THIS CAN GE CHANGED TO 0.
      DATA NXSAVE/0/,NYSAVE/0/
      DATA ODIST/1/
      DATA IFIRST/0/,
     1     JFIRST/0/
      DATA IDSAVE/4*0/
C
D     CALL TIMPR(KFILDO,KFILDO,'START SPOTRM        ')
C
      IER=0
      MDIST=RMAX
C        RMAX COMES IN AS THE FIRST PASS RADIUS FROM THE U405A.CN FILE.
C        THIS IS THE MAXIMUM DISTANCE TO SEARCH IN DEFINING DISTANCES.
C
      NSQEQ=SQUEZE
C        NSQEQ IS AN INTEGER VERSION OF SQUEZE
C
      IF(NSQEQ.EQ.0.OR.NSQEQ.EQ.1.OR.NSQEQ.EQ.2)THEN
         GO TO 101
      ELSE
         WRITE(KFILDO,100)NSQEQ
 100     FORMAT(/' ****SQUEZE = NSQEQ =',I3,' NOT IN RANGE ALLOWED',
     1           ' IN SPOTRM.  ABORT THIS SMOOTHING.'/)
         IER=777
         ISTOP=ISTOP+1
         GO TO 400
      ENDIF   
C
C        CHECK WHETHER THIS IS THE SAME ID,AND THE DISTANCE AND WEIGHT
C        DO NOT HAVE TO BE COMPUTED.  THIS LIMITS REUSE TO DIFFERENT
C        PASSES OF THE SAME VARIABLE, ALLOWING THAT DIFFERENT 
C        PROJECTIONS OR EVEN DIFFERENT LEVELS OF PROBABILITY COULD
C        HAVE DIFFERENT NUMBERS OF STATIONS.
C
 101  DO 102 J=1,4
      IF(ID(J).NE.IDSAVE(J))GO TO 104
 102  CONTINUE
C
C        THE ID IS THE SAME.  THIS IS AN ANALYSIS PASS AFTER THE
C        FIRST SMOOTHING, SO THE DISTANCE AND WEIGHTS DO NOT HAVE 
C        BE COMPUTED.
C
D     WRITE(KFILDO,103)(ID(J),J=1,4)
D103  FORMAT(/' SPOTRM BEING REPEATED FOR VARIABLE ',4I11)
C
      GO TO 272
C
C        THE DISTANCES AND WEIGHTS HAVE TO BE COMPUTED.
C        CHECK THE SIZE OF P( , ) AND SEALND( , ).
C
 104  IF(NX.NE.NXE.OR.NY.NE.NYE)THEN
         WRITE(KFILDO,105)NX,NY,NXE,NYE
 105     FORMAT(/' ****THE GRID P( , ) OF SIZE',I5,' X',I5,
     1           ' IS NOT THE SAME SIZE OF SEALND',I5,' X',I5,
     2           '.  SMOOTHING IN SPOTRM NOT DONE.  CONTINUING.')
         ISTOP=ISTOP+1
         IER=777
         GO TO 400
      ENDIF
C
C        CHECK THE MESH LENGTHS OF P( , ) AND SEALND( , ).
C
      IF(MESH.NE.MESHE)THEN
         WRITE(KFILDO,107)MESH,MESHE
 107     FORMAT(/' ****THE GRID P( , ) HAS A DIFFERENT NOMINAL',
     1           ' MESH LENGTH =',I3,' THAN SEALND( , ) =',I3,
     2           '.  SMOOTHING IN SPOTRM NOT DONE.  CONTINUING.')
         ISTOP=ISTOP+1
         IER=777
         GO TO 400
      ENDIF
C
 108  IF(IFIRST.EQ.0)THEN
C           ALLOCATE THE ARRAY WT( , ).
C
         ALLOCATE (DIST(NX,NY,3),WT(NX,NY),STAT=IOS)
C
         IF(IOS.EQ.1)THEN
            WRITE(KFILDO,110)
 110        FORMAT(/' ****ALLOCATION OF DIST( , , ) AND WT( , ) FAILED',
     1              ' IN SPOTRM AT 110.  ARRAY ALREADY ALLOCATED.')
            ISTOP=ISTOP+1
            IER=777
            GO TO 400
C
         ELSEIF(IOS.EQ.2)THEN
            WRITE(KFILDO,111)
 111        FORMAT(/' ****ALLOCATION OF DIST( , , ) AND WT( , ) FAILED',
     1              ' IN SPOTRM AT 111.  ARRAY NOT ALLOCATED.')
            ISTOP=ISTOP+1
            IER=777
            GO TO 400
         ENDIF
C
         IFIRST=1
         NXSAVE=NX
         NYSAVE=NY
      ELSE
C
         IF(NX.NE.NXSAVE.OR.NY.NE.NYSAVE)THEN
C              IT IS NOT EXPECTED THAT THIS WILL HAPPEN.
C              IF IT DOES, IT IS PROBABLY AN ERROR OF SOME KIND.
            WRITE(KFILDO,120)NX,NY,NXSAVE,NYSAVE
 120        FORMAT(/' ****GRID SIZE NX =',I5,' NY =',I5,
     1              ' IN SPOTRM NOT THE SAME AS FOR',
     2              ' THE PREVIOUS ENTRY NX =',I5,' NY =',I5,'.',/,
     3              '     REALLOCATING THE ARRAYS WT( , ) AND',
     4              ' DIST( , , ) AND RECALCULATING THE DISTANCES.')
C
         DEALLOCATE(DIST,WT,STAT=IOS)
         IFIRST=0
         GO TO 108
         ENDIF
C
      ENDIF
C
      JFIRST=0
C
C        SET THE DISTANCES TO A LARGE NUMBER.
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(J,JY,IX)
C
      DO 140 J=1,3
      DO 139 JY=1,NY
      DO 138 IX=1,NX
      DIST(IX,JY,J)=99999999.
 138  CONTINUE
 139  CONTINUE
 140  CONTINUE
C
!$OMP END PARALLEL DO
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(K,JY,IX)
C
      DO 250 K=1,NSTA
C
C        MAKE SURE STATION LOCATION IS PRESENT.
C
      IF(XP(K).GT.9998..OR.YP(K).GT.9998.)THEN
         WRITE(KFILDO,143)CCALL(K)
 143     FORMAT(/' ****THE LOCATION OF STATION ',A8,' MISSING.',
     1           ' IN SPOTRM.  STATION COULD NOT BE USED.')
         ISTOP=ISTOP+1
         IER=777
         GO TO 250
      ENDIF
C
C        CHECK FOR GOOD DATA.  THE LAMP STATION LIST MAY CONTAIN
C        STATIONS FOR WHICH THERE ARE NO DATA, BUT AT THIS POINT
C        THERE MAY BE MANY AUGMENTATION STATIONS THAT MUST NOT
C        BE INCLUDED HERE.
C
      IF(LTAGPT(K).NE.0.)THEN
CCCD        WRITE(KFILDO,144)CCALL(K)
CCCD144     FORMAT(' STATION ',A8,'HAS NO DATA.')
         GO TO 250
      ENDIF
C
      IF(XP(K).LT.1.-ODIST.OR.XP(K).GT.NX+ODIST)GO TO 250
      IF(YP(K).LT.1.-ODIST.OR.YP(K).GT.NY+ODIST)GO TO 250
C        STATION HAS TO BE WITHIN ODIST OF GRID.  THIS VALUE SHOULD
C        AGREE WITH PARAMETERS USED IN U155, GENERALLY ONLY 1 OR 2
C        GRIDPOINTS.  AS LONG AS A LOW VALUE IS USED, IT WON'T BE
C        VERY SENSITIVE TO THE EXACT VALUE.
      IXK=NINT(XP(K))
      JYK=NINT(YP(K))
C
      DO 200 JY=MAX(1,JYK-MDIST),MIN(NY,JYK+MDIST)
      DO 199 IX=MAX(1,IXK-MDIST),MIN(NX,IXK+MDIST)
C        IN THE ABOVE, IF THE INITIAL VALUE IS GT THE TERMINAL
C        VALUE, THE LOOP WILL NOT EXECUTE.
C
      IF(SEALND(IX,JY).GT.9998.9)GO TO  199
C
C        USE ONLY MATCHED STATION AND GRIDPOINTS
C        ACCORDING TO LAND/WATER.
C
      IF(SEALND(IX,JY).EQ.9.)THEN
C
         IF(LNDSEA(K).EQ.9)THEN
            GO TO 150
         ENDIF
C
      ELSEIF(SEALND(IX,JY).EQ.3.)THEN
C
         IF(LNDSEA(K).EQ.6.OR.LNDSEA(K).EQ.3)THEN
            GO TO 150
         ENDIF
C
      ELSEIF(SEALND(IX,JY).EQ.0.)THEN
C
         IF(LNDSEA(K).EQ.0)THEN
            GO TO 150
         ENDIF
C
      ELSE
         WRITE(KFILDO,145)SEALND(IX,JY)
 145     FORMAT(/,' ****SEALND( , ) =',F3.0,' NOT ONE OF THE',
     1            ' PERMISSIBLE VALUES OF 0, 3,OR 9',
     2            '.  SMOOTHING NOT DONE IN SPOTRM.  CONTINUING.')
         ISTOP=ISTOP+1
         IER=777
      ENDIF
C
 150  DISTSQ=(IX-XP(K))**2+(JY-YP(K))**2
C
      IF(DISTSQ.LT.DIST(IX,JY,1))THEN
         DIST(IX,JY,3)=DIST(IX,JY,2)
         DIST(IX,JY,2)=DIST(IX,JY,1)
         DIST(IX,JY,1)=DISTSQ
      ELSEIF(DISTSQ.LT.DIST(IX,JY,2))THEN
         DIST(IX,JY,3)=DIST(IX,JY,2)
         DIST(IX,JY,2)=DISTSQ
      ELSEIF(DISTSQ.LT.DIST(IX,JY,3))THEN
         DIST(IX,JY,3)=DISTSQ
      ENDIF
C
CCCD     IF(CCALL(K).EQ.'KDCA    ')THEN
CCCD        WRITE(KFILDO,198)K,CCALL(K),XP(K),YP(K),IX,JY,
CCC     1                   (DIST(IX,JY,J),J=1,3)
CCCD198     FORMAT('K,CCALL(K),XP(K),YP(K),IX,JY,',
CCC     1          '(DIST(IX,JY,J),J=1,3)',
CCCD    2           I6,1X,A8,2F8.2,2I6,F10.2)
CCCD     ENDIF
C
 199  CONTINUE
 200  CONTINUE
C
 250  CONTINUE
C
!$OMP END PARALLEL DO
C
C        FIND DISTANCE FROM SQUARE OF DISTANCE.
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(J,JY,IX)
C
      DO 260 J=1,3
      DO 259 JY=1,NY
      DO 258 IX=1,NX
C
      IF(DIST(IX,JY,J).LT.99999998.9)THEN
         DIST(IX,JY,J)=SQRT(DIST(IX,JY,J))
      ELSE
         DIST(IX,JY,J)=9999.
C           THE ORIGINAL VALULE OF DIST( , , ) WAS SET VERY LARGE.
      ENDIF
C
 258  CONTINUE
 259  CONTINUE
 260  CONTINUE
C
!$OMP END PARALLEL DO
C
C        THE GRID IN DIST( , , ) NOW CONTAINS THE DISTANCE TO THE
C        THREE CLOSEST STATIONS.
C
D     CALL TIMPR(KFILDO,KFILDO,'START WEIGHTS       ')
C
C        COMPUTE GRID OF WEIGHTS.
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(JY,IX)
C
      DO 270 JY=1,NY
      DO 269 IX=1,NX
C
      IF(DIST(IX,JY,1).GT.1.)THEN
C
         IF(NSQEQ.EQ.0)THEN
            WT(IX,JY)=1./(DIST(IX,JY,1)**DPOWER)
         ELSEIF(NSQEQ.EQ.1.OR.NSQEQ.EQ.2)THEN
            WT(IX,JY)=1./(((DIST(IX,JY,1)+DIST(IX,JY,2)+
     1                      DIST(IX,JY,3))/3.)**DPOWER)
C           NOTE THAT DIST(IX,JY, ) CAN BE MISSING = 9999., WHICH
C           WILL MAKE THE WT( , , ) ESSENTIALLY ZERO.
         ENDIF
C
      ELSE
         WT(IX,JY)=1.
      ENDIF
C
 269  CONTINUE
 270  CONTINUE
C
!$OMP END PARALLEL DO
C
D     CALL TIMPR(KFILDO,KFILDO,'END   WEIGHTS       ')
C
C        SMOOTH THE POINTS.
C
 272  CONTINUE
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(IX,JY,JY1,IX1)
C
      DO 300 JY=1,NY
      DO 299 IX=1,NX
C
      IF(CPNDFD(IX,JY).LT..5)THEN
C           THIS KEEPS SMOOTHING TO WITHIN THE NDFD CLIP AREA.
         HOLD(IX,JY)=P(IX,JY)
      ELSEIF(P(IX,JY).GT.9998..OR.DIST(IX,JY,1).GT.9998.)THEN
C           THE ABOVE CHECK ASSURES A GOOD DATUM AND DISTANCE.
         HOLD(IX,JY)=P(IX,JY)
      ELSEIF(NOCEAN.EQ.0.AND.SEALND(IX,JY).LT.3.5)THEN
C           THE ABOVE CHECK ELIMINATES OCEAN POINTS WHEN NOCEAN = 0.
         HOLD(IX,JY)=P(IX,JY)
      ELSE
C           THIS IS A POINT TO BE USED THAT IS NOT MISSING AND HAS A
C           DISTANCE.
C
         IF(NSQEQ.LE.0.OR.NSQEQ.EQ.1)THEN
            IOCEXT=MIN(RAY*DIST(IX,JY,1),RAY*RMAX)+1.
C              THIS ADDS ONE AND TRUNCATES.  WITHOUT THE ONE, THE 
C              GRIDPOINT CLOSEST TO THE STATION MIGHT NOT CONTRIBUTE.
         ELSEIF(NSQEQ.LE.2)THEN
            IOCEXT=MIN(RAY*(DIST(IX,JY,1)+DIST(IX,JY,2)+DIST(IX,JY,3))
     1                 /3.,RAY*RMAX)+1.
         ENDIF
C
      OCEXTS=IOCEXT*IOCEXT+.01
C           OCEXTS USED FOR TESTING FOR CIRCLE.  0.01 USED TO COMBAT
C           POSSIBLE ROUNDOFF.
         IXS=MAX(1,IX-IOCEXT)
         IXE=MIN(NX,IX+IOCEXT)
         JYS=MAX(1,JY-IOCEXT)
         JYE=MIN(NY,JY+IOCEXT)
         SUM=0.
         COUNT=0.
C
         DO 280 JY1=JYS,JYE
         DO 279 IX1=IXS,IXE
C
         IF(CPNDFD(IX1,JY1).LT..5)GO TO 279
C           THIS KEEPS SMOOTHING TO WITHIN THE NDFD CLIP AREA.
C           NOTE THAT THIS NEVER USES POINTS OUTSIDE THE CLIP
C           AREA FOR SMOOTHING.  THIS IS A BIG TIME SAVER.
C
         IF(SEALND(IX1,JY1).GT.3.5.AND.P(IX1,JY1).LT.9998.9)THEN
C              THE POINT IS LAND AND NOT MISSING.  NOTE THAT OCEAN
C              POINTS DO NOT ADD TO THE SMOOTHING EVEN IF OCEAN
C              POINTS ARE BEING SMOOTHED.  THIS IS DESIRABLE WHEN
C              THERE ARE NO OCEAN STATION DATA, BUT MAY NOT BE WHEN
C              THERE ARE OCEAN STATIONS.  HOWEVER, WHEN THERE ARE
C              OCEAN STATIONS, IT IS LIKELY NOCEAN = 0 AND ORSMTH
C              IS USED OVER THE OCEAN.
C
            IF(ABS(TELEV(IX,JY)-TELEV(IX1,JY1)).LT.DIFFA)THEN
C
               IF(((IX1-IX)**2+(JY1-JY)**2).LT.OCEXTS)THEN
C                 THE ABOVE KEEPS THE SMOOTHING TO A CIRCLE.
                  SUM=SUM+WT(IX1,JY1)*P(IX1,JY1)
                  COUNT=COUNT+WT(IX1,JY1)
               ENDIF
C
            ENDIF
C
         ENDIF
C      
CCCC         IF(IX.GE.1511.AND.IX.LE.1514.AND.JY.GE.457.AND.JY.LE.460)THEN
CCCC            WRITE(KFILDO,274)IX,JY,IX1,JY1,SUM,COUNT
CCCC 274        FORMAT('AT 274 IN SPOTRM--IX,JY,IX1,JY1,SUM,COUNT',
CCCC     2                                4I6,2F6.2)
CCCC         ENDIF
C
CCCC         IF(IX.EQ.1099.AND.JY.EQ.739)THEN
CCCC            WRITE(KFILDO,275)IX,JY,IX1,JY1,IOCEXT,OCEXTS,
CCCC     1                       SUM,COUNT,P(IX,JY),P(IX1,JY1),
CCCC     2                       TELEV(IX,JY),TELEV(IX1,JY1),
CCCC     3                       DIST(IX1,JY1,1),WT(IX1,JY1)
CCCC 275        FORMAT(' IX,JY,IX1,JY1,IOCEXT,OCEXTS,',
CCCC     1             'SUM,COUNT,P(IX,JY),P(IX1,JY1),',
CCCC     2             'TELEV(IX,JY),TELEV(IX1,JY1),',
CCCC     3             'DIST(IX1,JY1,1),WT(IX1,JY1)'/
CCCC     4              5I6,9F10.2)
CCCC         ENDIF
C            IX=1099, JY=739 = ???KDCA.
C              =1513,   =458 = KWRB ROBINS AFB
C
 279     CONTINUE
 280     CONTINUE
C
         IF(COUNT.EQ.0.)THEN
            HOLD(IX,JY)=P(IX,JY)
         ELSE
            HOLD(IX,JY)=SUM/COUNT
         ENDIF
C
      ENDIF
C
 299  CONTINUE
C
 300  CONTINUE
C
!$OMP END PARALLEL DO
C
C        REPLACE P( , ) WITH HOLD( , ).
C
!$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(IX,JY)
C
      DO 310 JY=1,NY
      DO 309 IX=1,NX
      P(IX,JY)=HOLD(IX,JY)
 309  CONTINUE
 310  CONTINUE
C
!$OMP END PARALLEL DO
C
C        SAVE THE ID( ).
C
      DO 312 J=1,4
      IDSAVE(J)=ID(J)
 312  CONTINUE
C
      IF(IPRTEL.EQ.0.OR.KFILOG.EQ.0.OR.JFIRST.EQ.1)GO TO 400
C        IPRTEL SET TO 1 FOR CHECKOUT, THEN CAN BE CHANGED TO ZERO.
C
C        PREPARING TO WRITE THE DISTANCE GRID TO THE DISPOSABLE FILE.
C
C        DEFINE THE ID FOR THE DISTANCE GRID, WHICH DEPENDS ON MESH
C        LENGTH.  VARIABLE ID(1) IS USED IN WORD 2.  THIS ALLOWS A DISTANCE
C        GRID TO BE IN THE RA FILE FOR DIFFERENT SETS OF STATIONS.
C
      DO 319 J=1,7
C
      IF(MESHE.EQ.ITABLE(J,1))THEN
         LD(1)=408000000+NPROJ*100000+ITABLE(J,2)*10000
         LD(2)=ID(1)
C           THIS MAKES THE GRID SPECIFIC TO A VARIABLE  THE ONLY
C           PART OF THE ID( ) THAT IS NOT HERE IS WORD 2, WHICH IS
C           HARDLY EVER USED FOR SURFACE VARIABLES.
         LD(3)=ID(3)
         LD(4)=ID(4)
CCCCD        WRITE(KFILDO,318)NDATE,(LD(JJJ),JJJ=1,4)
CCCCD318     FORMAT(' AT 318 IN U155--NDATE,,(LD(JJJ),JJJ=1,4)',5I11)
         GO TO 330
      ENDIF
C
 319  CONTINUE      
C
      WRITE(KFILDO,320)
 320  FORMAT(' ****COULD NOT FIND MESH VALUE IN TABLE AT 320',
     1       ' IN SPOTRM.  SMOOTHING DONE, BUT THE GRID NOT',
     2       ' WRITTEN TO THE DISPOSABLE FILE.')
      ISTOP=ISTOP+1
      IER=777
      GO TO 400
C
C        PACK AND WRITE TO THE DISPOSABLE FILE.
C
 330  ITAUM=0
      NSEQ=0
      NCHAR=32
C        32 CHARACTERS OF PLAIN LANGUAGE ARE PACKED.
      XMISSP=9999.
      XMISSS=0.
C        THE DISTANCE GRID IS ON A CLIPPED GRID, SO THERE
C        WILL BE MISSING VALUES.
C
      CALL PAWGTS(KFILDO,KFILOG,'KFILOG',IP16,NDATE,
     1            LD,IDPARS(12),ITAUM,MODNO,NSEQ,ISCALD,
     2            NPROJ,ALATL,ALONL,ORIENT,MESH,XLAT,NX,NY,
     3            DIST,DATA,IWORK,IPACK,ND5,MINPK,
     4            IS0,IS1,IS2,IS4,ND7,
     5            IPLANT,PLAINT,NCHAR,
     6            XMISSP,XMISSS,LX,IOCTET,
     7            JTOTBY,JTOTRC,L3264B,L3264W,IER)
C        PAWGTS WRITES DIAGNOSTIC TO IP16.
      JFIRST=1
C        JFIRST KEEPS DUPLICATE DISTANCE GRIDS FROM BEING WRITTEN.
      IF(IER.NE.0)THEN
         IER=777
         ISTOP=ISTOP+1
      ENDIF
C
 400  CONTINUE
C
D     CALL TIMPR(KFILDO,KFILDO,'END   SPOTRM        ')
C
      RETURN
      END
