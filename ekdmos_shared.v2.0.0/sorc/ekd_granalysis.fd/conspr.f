      SUBROUTINE CONSPR(KFILDO,KFIL10,IP16,ID,IDPARS,
     2                  P,NX,NY,
     3                  LSTORE,ND9,LITEMS,NDATE,
     4                  IS0,IS1,IS2,IS4,ND7,
     5                  IPACK,IWORK,DATA,ND5,
     6                  CORE,ND10,NBLOCK,NFETCH,NSLAB,
     6                  IPLAIN,L3264W,L3264B,ISTOP,JER,IER)
C
C        JULY      2010   GLAHN   TDL   MOS-2000
C                                 MODIFIED FROM CONVPR
C        JULY      2010   GLAHN   MODIFIED ALGORITHM
C                                 
C
C        PURPOSE
C            TO GUARANTEE FOR THE 5 DISCRETE SKY CATEGORIES 
C               CLEAR, FEW, SCATTERED, BROKEN, AND OVERCAST THAT
C               THE SUM IS 100 PERCENT FOR EACH GRIDPOINT.
C               THE CATEGORIES ARE ANALYZED FROM THE HIGHER COVERAGE
C               DOWN TO THE LOWEST.
C
C               THIS IS DONE BY TWO PROCESSES:
C
C            (1) THE PROBABILITY OF THE CATEGORY BEING ANALYZED PLUS
C                THE ONES OF HIGHER COVERAGE DO NOT EXCEED 100 PERCENT.
C                IF IT DOES, THE ONE BEING ANALYZED IS CAPPED SO THAT
C                THE TOTAL IS 100%.
C            (2) THE PROBABILITY OF THE CLEAR CATEGORY BEING
C                ANALYZED PLUS ALL THE OTHERS OF HIGHER COVERAGE,
C                ALREADY ANALYZED, ADD TO 100 PERCENT.  IF THEY DON'T,
C                THE CLEAR OR FEW CATEGORY IS AUGMENTED SO THAT THE
C                TOTAL IS 100 PERCENT.
C
C                THIS ASSUMES OVERCAST MANY TIMES HAS A HIGH
C                PROBABILITY, AND SHOULD NOT BE CAPPED BY THE SUM 
C                OF THE LOWER ONES BEING NEAR 100 PERCENT; BETTER
C                TO HAVE THE LOWER ONES BE MODIFIED.
C
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFIL10    - UNIT NUMBER FOR INTERNAL RANDOM ACCESS STORAGE.
C                       (INPUT)
C            IP16      - UNIT NUMBER FOR INDICATING WHEN A RECORD IS
C                        WRITTEN TO INTERNAL STORAGE.  (OUTPUT)C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFIL10 = UNIT NUMBER FOR INTERNAL RANDOM ACCESS STORAGE.
C                       (INPUT)
C                IP16 = INDICATES WHETHER (>0) OR NOT (=0) 
C                       A STATEMENT WILL BE OUTPUT TO IP16
C                       WHEN A SEQUENTIAL FILE IS WRITTEN THROUGH
C                       PAWGTS,A RANDOM ACCESS FILE IS WRITTEN
C                       THROUGH PAWRAC, OR A FILE IS WRITTEN TO
C                       INTERNAL STORAGE BY GSTORE.  (INPUT)
C               ID(J) = ID OF VARIABLE BEING ANALYZED (J=1,4).
C                       (INPUT)
C           IDPARS(J) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       ID'S CORRESPONDING TO ID( )
C                       (J=1,15).  (INPUT)
C            P(IX,JY) = GRID OF SKY COVER PROBABILITIES
C                       (IX=1,NX) (JY=1,NY).  (INPUT/OUTPUT)
C                  NX = X EXTENT OF GRID IN P( , ).  (INPUT)
C                  NY = Y EXTENT OF GRID IN P( , ).  (INPUT)
C         LSTORE(L,J) = THE ARRAY HOLDING INFORMATION ABOUT THE DATA 
C                       STORED (L=1,12) (J=1,LITEMS).  (INPUT/OUTPUT)
C                 ND9 = MAXIMUM NUMBER OF FIELDS STORED IN LSTORE( , ).
C                       SECOND DIMENSION OF LSTORE( , ).  (INPUT)
C              LITEMS = THE NUMBER OF ITEMS J IN LSTORE( ,L).  
C                       (INPUT/OUTPUT)
C               NDATE = THE DATE/TIME OF THE RUN.  (INPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,4).
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,21+).
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       (INPUT)
C            IPACK(J) = WORK ARRAY FOR GFETCH (J=1,ND5).  (INTERNAL)
C            IWORK(J) = WORK ARRAY FOR GFETCH (J=1,ND5).  (INTERNAL)
C         DATA(IX,JY) = WORK ARRAY (IX=1,NX) (JY=1,NY).  DIMENSIONED
C                       ND5 IN CALLING PROGRAM.  (INTERNAL)
C                 ND5 = DIMENSION OF IPACK( ), AND IWORK( ).
C                       (INPUT)
C             CORE(J) = SPACE ALLOCATED FOR SAVING PACKED GRIDPOINT 
C                       FIELDS (J=1,ND10).  WHEN THIS SPACE IS 
C                       EXHAUSTED, SCRATCH DISK WILL BE USED.  THIS IS 
C                       THE SPACE USED FOR THE MOS-2000 INTERNAL RANDOM 
C                       ACCESS SYSTEM.  (INPUT)
C                ND10 = THE MEMORY IN WORDS ALLOCATED TO THE SAVING OF 
C                       DATA CORE( ).  WHEN THIS SPACE IS EXHAUSTED,
C                       SCRATCH DISK WILL BE USED.  (INPUT)
C              NBLOCK = BLOCK SIZE IN WORDS OF INTERNAL MOS-2000 DISK
C                       STORAGE.  (INPUT)
C              NFETCH = INCREMENTED EACH TIME DATA ARE FETCHED BY
C                       GFETCH.  IT IS A RUNNING COUNT FROM THE
C                       BEGINNING OF THE PROGRAM.  THIS COUNT 
C                       IS MAINTAINED IN CASE THE USER NEEDS IT
C                       (DIAGNOSTICS, ETC.).  (OUTPUT)
C               NSLAB = SLAB OF THE GRID CHARACTERISTICS.  RETURNED
C                       BY GFETCH.  USED FOR CHECKING FOR EQUAL
C                       CHARACTERISTICS OF GRIDS READ.  (OUTPUT)
C         IPLAIN(L,J) = 32 CHARACTERS (L=1,L3264W) (J=1,4) OF PLAIN
C                       LANGUAGE DESCRIPTION OF VARIABLE TO BE WRITTEN.
C                       NOTE THAT THIS REQUIRES TWO 32-BIT WORDS TO HOLD
C                       THE DESCRIPTION BUT ONLY ONE 64-BIT WORD.
C                       EQUIVALENCED TO PLAIN( ) IN DRU155.  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).
C                       (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  (INPUT)
C            ISTOP(J) = ISTOP(1)--IS INCREMENTED BY 1 EACH TIME AN ERROR 
C                                 OCCURS.
C                       ISTOP(3)--IS INCREMENTED WHEN A DATA RECORD 
C                                 COULD NOT BE FOUND.
C                       (INPUT/OUTPUT)
C                 IER = ERROR RETURN.
C                       0 = GOOD RETURN.
C                       (OUTPUT)
C            TABLE(M) = HOLDS THE 4TH WORD ID FOR THE NOCAT CATEGORIES
C                       OF SKY COVER (M=1,NOCAT).  (INTERNAL)
C               NOCAT = THE NUMBER OF SKY COVER CATEGORIES FORECAST
C                       BY LAMP MINUS 1.  (INTERNAL)
C        1         2         3         4         5         6         7 X
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      PARAMETER(NOCAT=5)
C
      DIMENSION ID(4),IDPARS(15),LD(4)
      DIMENSION IPLAIN(L3264W,4)
      DIMENSION P(NX,NY),DATA(NX,NY)
      DIMENSION WORK(NX,NY,NOCAT-1)
C        WORK( , , ) IS AN AUTOMATIC ARRAY.
      DIMENSION IPACK(ND5),IWORK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION LSTORE(12,ND9)
      DIMENSION CORE(ND10)
      DIMENSION ITABLE(NOCAT)
      DIMENSION ISTOP(5)
C
      DATA ITABLE/ 999905000,
     1             700001000,
     2             400001000,
     3             250001000,
     4             150001000/
C
D     CALL TIMPR(KFILDO,KFILDO,'START CONSPR        ')
      IER=0
C
C        FIND THE CATEGORY.
C
      DO 110 J=1,NOCAT
C
      IF(ID(4).EQ.ITABLE(J))THEN
C
         IF(J.EQ.1)THEN
C              THIS IS THE HIGHEST CATEGORY.  CAN'T CHECK A HIGHER ONE.
C              BECAUSE ALL LEVELS ARE USED TO COMPUTE THE LAPSE, THE
C              U405A.CN IS USED FOR ALL PROBABILITY LEVELS.  
C              THEREFORE, CONSPR OR CONCPR (OR EQUIVALENT) HAS TO
C              BE IN THE u405A.CN FOR THE HIGHEST LEVEL OR NO LEVELS
C              WILL BE CHECKED.
            WRITE(KFILDO,105)
 105        FORMAT(/' PROBABILITY OF OVERCAST CANNOT CHECKED FOR',
     1              ' CONSISTENCY WITH NEXT LOWER LEVEL.')
            GO TO 210
         ELSE
            GO TO 120
C              J HAS BEEN DEFINED.
         ENDIF
C
      ENDIF
C
 110  CONTINUE
C
C        DROP THROUGH HERE MEANS THE CATEGORY WAS NOT FOUND.
C
      WRITE(KFILDO,115)(ID(L),L=1,4)
 115  FORMAT(/,' ****COULDN''T FIND THRESHOLD FOR ID ',4I10,
     1         ' IN TABLE IN CONSPR.',/,
     2         '     GRID NOT CHECKED',
     3         ' FOR CONSISTENCY WITH LOWER CATEGORY.')
      ISTOP(1)=ISTOP(1)+1
C        NOT COUNTED AS FATAL.	
      GO TO 350
C        THIS CATEGORY IS UNKNOWN TO CONSPR, SO IT DOES NOT
C        NEED TO BE WRITTEN.
C
C        RETRIEVE ALL THE OTHER SKY CATEGORIES ALREADY ANALYZED.
C
 120  DO 150 LOOP =1,J-1
C        J = 1 HAS ALREADY BEEN BYPASSED.  IT IS NOT CHECKED
C        WITH ANYTHING.
      LD(1)=ID(1)
      LD(2)=ID(2)
      LD(3)=ID(3)
      LD(4)=ITABLE(LOOP)+1
C        THE +1 IS TO GET THE "CONSISTENT" GRID.  THE ONE
C        WRITTEN IN U405A IS BEFORE THE CHECKING IS DONE.
      CALL GFETCH(KFILDO,KFIL10,LD,7777,LSTORE,ND9,LITEMS,
     1            IS0,IS1,IS2,IS4,ND7,IPACK,IWORK,WORK(1,1,LOOP),NX*NY,
     2            NWORDS,NPACK,NDATE,NTIMES,CORE,ND10,
     3            NBLOCK,NFETCH,NSLAB,MISSP,MISSS,L3264B,1,IER)
C
      IF(IER.NE.0)THEN
         WRITE(KFILDO,130)(LD(L),L=1,4),(ID(L),L=1,4)
 130     FORMAT(/,' ****COULDN''T FETCH GRID ',4I10, 
     1            ' FROM INTERNAL STORAGE IN CONSPR.',/,
     2            '     GRID ',4I10,' NOT CHECKED',
     3            ' FOR CONSISTENCY WITH LOWER CATEGORY.')
         ISTOP(1)=ISTOP(1)+1
         ISTOP(3)=ISTOP(3)+1
         IER=0
C           COUNTED AS FATAL.	
         GO TO 350
C           THIS CATEGORY IS NOT WRITTEN.  FURTHER CHECKING IS
C           NOT DONE.  LIKELY, NO GRID HAS BEEN MODIFIED UP TO THIS
C           POINT, BUT IF IT HAS, THAT IS OK. 
      ENDIF
C  
 150  CONTINUE
C   
      IPOS=0
      INEG=0
      AVGPOS=0.
      AVGNEG=0.
      CHGMXP=0.
      CHGMXN=0.
C
D     ICOUNT=0

      DO 200 JY=1,NY
      DO 199 IX=1,NX
C
      IF(P(IX,JY).GT.9998.)GO TO 199
C        P( , ) WILL HAVE BEEN CLIPPED TO THE OUTPUT AREA, BUT NOT
C        THE DATA IN INTERNAL STORAGE READ INTO WORK( , , ).
C
      SUM=P(IX,JY)
C
      DO 160 LOOP=1,J-1
C     
      IF(WORK(IX,JY,LOOP).LT.9998.)THEN
         SUM=SUM+WORK(IX,JY,LOOP)
C
         IF(P(IX,JY).GT.100.)THEN
            WRITE(KFILDO,155)J,IX,JY,P(IX,JY)
 155        FORMAT(' AT 155--J,IX,JY,P(IX,JY)',3I6,F10.3)
         ENDIF
C
      ENDIF
C
 160  CONTINUE
C
      DIFF=SUM-100.
C
D     IF(DIFF.GT.20.)THEN
D        WRITE(KFILDO,165)J,IX,JY,P(IX,JY),WORK(IX,JY,J-1),
D    1                    SUM,DIFF
D165     FORMAT(' AT 165 IN CONSPR--J,IX,JY,P(IX,JY),',
D    1          'WORK(IX,JY,J-1)SUM,DIFF',3I6,4F10.3)
D     ENDIF
C    
      IF(DIFF.GT.0.)THEN
         P(IX,JY)=P(IX,JY)-DIFF
C
         IF(DIFF.GT.1.)THEN
C              ONLY CHANGES GT 1 PERCENT ARE COUNTED.
            INEG=INEG+1
            AVGNEG=AVGNEG+DIFF
            CHGMXN=MAX(CHGMXN,DIFF)
         ENDIF
C
      ELSEIF(J.EQ.NOCAT)THEN
C           THIS MODIFIES ONLY THE CLEAR CATEGORY.  IT WOULD
C           BE HIGHLY UNUSUAL FOR DIFF TO BE EXACTLY ZERO,
C           SO THERE IS NO NEED TO TEST IT.  IF IT IS ZERO,
C           P( , ) WILL REMAIN THE SAME.
C
D        IF(DIFF.LT.0..AND.ICOUNT.LT.500000)THEN
D           ICOUNT=ICOUNT+1
D           WRITE(KFILDO,175)J,IX,JY,P(IX,JY),
D    1           (WORK(IX,JY,M),M=1,J-1),SUM,DIFF,ICOUNT
D175        FORMAT(' AT 175 IN CONSPR--J,IX,JY,P(IX,JY),',
D    1             '(WORK(IX,JY,M),M=1,J-1),SUM,DIFF,ICOUNT',
D    2             3I6,7F10.3,I8)
D        ENDIF
C
         P(IX,JY)=P(IX,JY)+DIFF
C
         IF(ABS(DIFF).GT.1.)THEN
C              ONLY CHANGES GT 1 PERCENT ARE COUNTED.
            IPOS=IPOS+1
            AVGPOS=AVGPOS+ABS(DIFF)
            CHGMXP=MAX(CHGMXP,ABS(DIFF))
         ENDIF         
C
      ENDIF
C
 199  CONTINUE
 200  CONTINUE
C
      IF(INEG.GT.0)THEN
         AVGNEG=AVGNEG/INEG
         WRITE(KFILDO,205)INEG,J,AVGNEG,CHGMXN
 205     FORMAT(/,I8,' VALUES IN THE SKY COVER PROBABILITY GRID',
     1           ' NO.',I3,' DECREASED > 1%.',
     2           '  AVG CHG =',F7.2,'%  MAX CHG =',F7.2,'%')
      ELSE
         WRITE(KFILDO,206)J
 206     FORMAT(/23X,' SKY COVER PROBABILITY GRID NO.',I3,
     1           ' CONSISTENT WITH OTHERS OF HIGHER COVERAGE.',
     2           ' WITHIN 1% WITHOUT DECREASES.')
      ENDIF
C
      IF(IPOS.GT.0)THEN       
         AVGPOS=AVGPOS/IPOS
         WRITE(KFILDO,207)IPOS,J,AVGPOS,CHGMXP
 207     FORMAT(I8,' VALUES IN THE SKY COVER PROBABILITY GRID',
     1          ' NO.',I3,' INCREASED > 1%.',
     2          '  AVG CHG =',F7.2,'%  MAX CHG =',F7.2,'%')
      ELSE
         WRITE(KFILDO,208)J
 208     FORMAT(23X,'SKY COVER PROBABILITY GRID NO.',I3,
     1           ' CONSISTENT WITH OTHERS OF HIGHER COVERAGE.',
     2           ' WITHIN 1% WITHOUT INCREASES.')
      ENDIF
C 
C        GSTORE WRITES THE "CONSISTENT" GRID TO INTERNAL STORAGE
C        ON FILE KFIL10.  TO DISTINGUISH IT FROM THE ONE WRITTEN
C        IN U405A, IDPARS(15) IS SET TO 1.  THIS GRID IS ALWAYS
C        WRITTEN, ANTICIPATING A CHECK WITH THE ONE ABOVE.
C        NOTE THAT THIS IS NOT CLIPPED AS IT MAY BE FOR OUTPUT
C        TO OTHER MEDIA.
C
 210  LD(1)=ID(1)
      LD(2)=ID(2)
      LD(3)=ID(3)
      LD(4)=ID(4)+1
      NSLAB=0
C        THE PLUS 1 PUTS A 1 IN IDPARS(15) TO DISTINGUISH THE
C        FIELD FROM THE ONE THAT MAY HAVE BEEN WRITTEN IN U405A.
      CALL GSTORE(KFILDO,KFIL10,LD,NSLAB,LSTORE,ND9,LITEMS,
     1            P,NX*NY,1,0,NDATE,
     2            CORE,ND10,LASTL,NBLOCK,LASTD,NSTORE,L3264B,IER)
C        "NSLAB" IS STORED AS ZERO SIGNIFYING THE DATA ARE NOT
C        PACKED AND CAN BE TREATED AS VECTOR DATA.
C
      IF(IER.EQ.0)THEN
C
         IF(IP16.NE.0)THEN
            WRITE(IP16,347)(LD(JJ),JJ=1,4),
     1                    ((IPLAIN(I,JJ),I=1,L3264W),JJ=1,4),NDATE
 347        FORMAT(/' WRITING DATA TO UNIT KFIL10',3I10.9,I10.3,3X,
     1               8A4,' FOR DATE',I12)
         ENDIF
C
      ELSE 
C           A DIAGNOSTIC WILL HAVE OCCURRED IN GSTORE.
         ISTOP(1)=ISTOP(1)+1
         JER=1
         WRITE(KFILDO,348)(LD(JJ),JJ=1,4)
 348     FORMAT('     ERROR WRITING VARIABLE',3(1X,I9.9),1X,I10.3,
     1          ' TO INTERNAL STORAGE.',/,
     2          '     CHECKING OF PROBABILITY GRIDS FOR CONSISTENCY',
     3          ' MAY NOT BE ABLE TO BE MADE.')
      ENDIF
C 
D     CALL TIMPR(KFILDO,KFILDO,'END   CONSPR        ')
 350  RETURN
      END
