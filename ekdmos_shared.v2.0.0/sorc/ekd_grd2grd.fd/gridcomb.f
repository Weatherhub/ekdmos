      SUBROUTINE GRIDCOMB(KFILDO,ND2X3,ND7,ND11,IS2,
     1                    XYDIR,NGRIDC,NGRID,NX,NY,MPROJ,
     2                    XLATLL,YLONLL,XMESH,ORIENT,XLAT)
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GRIDCOMB  
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2002-07-01
C
C ABSTRACT: TO DETERMINE ALL POSSIBLE INPUT GRID CHARACTERISTICS SO 
C           THAT GRID INTERPOLATION FOR EACH VARIABLE POSSESSING THE 
C           SAME GRID CHARACTERISTICS AS OTHER VARIABLES NEED NOT 
C           BE DONE MORE THAN ONCE.
C
C PROGRAM HISTORY LOG:
C   02-07-01  RUDACK
C   05-11-03  MALONEY ADDED NCEP DOCBLOCK.
C
C USAGE:  CALLED BY RDINTRP
C
C        DATA SET USE:
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C               ND2X3 = THE DIMENSION OF SEVERAL ARRAYS (ND2*ND3).  SET 
C                       BY PARAMETER.  (INPUT)
C                 ND7 = DIMENSION OF IS2( ).  SHOULD BE GE 54.  (INPUT)
C                ND11 = MAXIMUM NUMBER OF INPUT GRID COMBINATIONS IN A RUN OF 
C                       U365.  (INPUT)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INPUT)
C        XYDIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE OUTPUT
C                       GRID (K=1,NX*NY) FOR A PARTICULAR GRID 
C                       CHARACTERISTIC (M=1,NGRID).  (INPUT/OUTPUT)
C         NGRIDC(L,M) = HOLDS THE GRID CHARACTERISTICS FOR THE INPUT GRID
C                       BEING PROCESSED (M=1,NGRIDTOT).  (INPUT/OUTPUT)
C                       L=1--MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR
C                            STEREOGRAPHIC AND 7=MERCATOR).  (OUTPUT)
C                       L=2--GRID LENGTH IN MILLIMETERS,
C                       L=3--LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000,
C                       L=4--GRID ORIENTATION IN DEGREES *10000,
C                       L=5--LATITUDE OF LL CORNER IN DEGREES *10000, AND
C                       L=6--LONGITUDE OF LL CORNER IN DEGREES *10000.
C               NGRID = THE NUMBER CORRESPONDING TO THE PARTICULAR GRID 
C                       TYPE BEING PROCESSED.  (OUTPUT)
C                  NX = THE X-EXTENT OF THE OUTPUT GRID.  (INPUT)
C                  NY = THE Y-EXTENT OF THE OUTPUT GRID.  (INPUT)
C               MPROJ = MAP PROJECTION OF THE OUTPUT GRID.  (INPUT)
C              XLATLL = LATITUDE OF LOWER LEFT CORNER POINT OF THE
C                       OUTPUT GRID.  (INPUT)
C              YLONLL = WEST LONGITUDE OF LOWER LEFT CORNER POINT
C                       OF THE OUTPUT GRID.  DO NOT USE NEGATIVE.  (INPUT)
C               XMESH = MESH LENGTH OF OUTPUT GRID IN KM AT XLAT DEGREES 
C                       NORTH LATITUDE.  (INPUT)
C              ORIENT = ORIENTATION OF OUTPUT GRID IN DEGREES WEST 
C                       LONGITUDE.  DO NOT USE NEGATIVE.  (INPUT)
C                XLAT = LATITUDE OF OUTPUT GRID IN DEGREES AT WHICH 
C                       XMESH APPLIES. ALSO THE LATITUDE WHERE THE 
C                       PROJECTION CUTS THE EARTH.  DO NOT USE NEGATIVE.
C                       (INPUT)
C            NGRIDTOT = TOTAL NUMBER OF GRID COMBINATIONS USED IN PROCESSING
C                       ALL VARIABLES.  (INTERNAL) 
C  
C        SUBPROGRAMS CALLED:  GRID_INTRP
C          UNIQUE: GRID_INTRP
C          LIBRARY: NONE
C           
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                   SEE OTHER ROUTINES FOR OTHER VALUES.
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (xlf90 compiler)
C   MACHINE:  IBM SP
C
C$$$
C
      DIMENSION XYDIR(ND2X3,2,ND11)
      DIMENSION IS2(ND7)
      DIMENSION NGRIDC(6,ND11)
C
      DATA NGRIDTOT/0/
C       
      DO 100 M=1,NGRIDTOT
C
         IF((IS2(2).EQ.NGRIDC(1,M)).AND.
     1      (IS2(8).EQ.NGRIDC(2,M)).AND.
     2      (IS2(9).EQ.NGRIDC(3,M)).AND.
     3      (IS2(7).EQ.NGRIDC(4,M)).AND.
     4      (IS2(5).EQ.NGRIDC(5,M)).AND.
     5      (IS2(6).EQ.NGRIDC(6,M))) THEN
C
C              EXIT THE LOOP AND EQUATE THE LOOP COUNTER TO NGRID.  
C              IN OTHER WORDS, THE CURRENT GRID BEING PROCESSED HAS
C              THE SAME GRID CHARACTERISTICS AS ANOTHER GRID THAT 
C              HAS ALREADY BEEN PROCESSED HAVING THE SAME VALUE OF 
C              NGRID.  DO NOT RECALCULATE THE 'XI' AND 'YJ' 
C              COORDINATES FOR THE CURRENT INPUT GRID.
C
            NGRID=M
            GOTO 300
C
         ENDIF
C
 100  CONTINUE
C
C        IF A DIFFERENT GRID CHARACTERISTIC HAS BEEN 
C        ENCOUNTERED, CALCULATE THE NEW 'XI' AND 'YJ'
C        COORDINATES.  ADD ONE TO THE TOTAL NUMBER OF 
C        DIFFERENT GRID CHARACTERISTICS FOUND ON THE
C        INPUT FILES.
C 
      NGRIDTOT=NGRIDTOT+1
C
      NGRID=NGRIDTOT
C
      CALL GRID_INTRP(KFILDO,ND2X3,ND7,NX,NY,
     1                IS2,MPROJ,XLATLL,YLONLL,XMESH,
     2                ORIENT,XLAT,XYDIR(1,1,NGRID))
C
C        ADD ANOTHER GRID CHARACTERISTIC TO NGRIDC.
C
      NGRIDC(1,NGRID)=IS2(2)
      NGRIDC(2,NGRID)=IS2(8)
      NGRIDC(3,NGRID)=IS2(9)
      NGRIDC(4,NGRID)=IS2(7)
      NGRIDC(5,NGRID)=IS2(5)
      NGRIDC(6,NGRID)=IS2(6)
C
 300  RETURN
      END
