      SUBROUTINE INTRP_OUT(KFILDO,KFILRA,RACESS,ND2X3,ND4,ND5,ND7,
     1                     N,IDATE,MINPK,ISCALD,
     2                     ID,IDPARS,IPACK,IWORK,DATA,
     3                     IS0,IS1,IS2,IS4,IPLAIN,PLAIN,
     4                     XYDIR,MPROJ,NX,NY,
     5                     XLATLL,YLONLL,XMESH,ORIENT,XLAT,
     6                     IP13,IP16,JP,L3264B,L3264W,
     7                     NTOTBG,NTOTRG,ISTOP,IER)  
C
C$$$   SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: DATA_INTRP
C   PRGMMR: RUDACK         ORG: W/OST22          DATE: 2002-08-01
C
C ABSTRACT: TO INTERPOLATE THE GRIDDED DATA TO A DIFFERENT GRID AND 
C           PACK THE DATA.
C
C
C PROGRAM HISTORY LOG:
C   02-07-01  RUDACK
C   05-11-02  MALONEY ADDED NCEP DOCBLOCK. 
C   05-11-08  MALONEY REPLACED KFILGO WITH KFILRA; ADDED RACESS TO
C                     SUBROUTINE CALL; ADDED CALL TO PACKGR_OPER FOR
C                     PACKING GRIDS TO RANDOM ACCESS FILE
C   12-06-25  ENGLE   ADDED PLAIN( ) TO THE CALLING SEQUENCE FOR ALL
C                     CALLS TO PACKG AND PACKGR_OPER.
C
C USAGE:  CALLED BY RDINTRP
C
C        DATA SET USE:
C        OUTPUT FILES:
C          FORT.KFILDO - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C          FORT.KFILRA - UNIT NUMBER OF GRIDPOINT OUTPUT FILE. (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILRA = UNIT NUMBER OF GRIDPOINT OUTPUT FILE. (INPUT)
C               ND2X3 = THE DIMENSION OF SEVERAL ARRAYS (ND2*ND3).  SET 
C                       BY PARAMETER.  (INPUT)
C                 ND4 = THE MAXIMUM NUMBER OF VARIABLES IN A RUN OF U365.
C                       (INPUT)
C                 ND5 = DIMENSION OF IPACK( ), IWORK( ), DATA( ) AND 
C                       ISDATA( ). THESE ARE GENERAL PURPOSE ARRAYS, 
C                       SOMETIMES USED FOR GRIDS.  TO AVIOD ERRORS IN 
C                       CERTAIN ROUTINES, AND TO AVOID CONFUSION, ND5 
C                       SHOULD BE SET EQUAL TO ND2X3.  ALSO, BECAUSE 
C                       IPACK( ) AND IWORK( ) ARE USED AS WORK ARRAYS 
C                       IN RDSNAM, ND5 SHOULD NOT BE LT ND12.  (INPUT)
C                 ND7 = DIMENSION OF IS0( ), IS1( ), IS2( ), AND IS4( ).
C                       SHOULD BE GE 54.  (INPUT)
C                   N = THE VARIABLE NUMBER CURRENTLY BEING PROCESSED. (INPUT)
C               IDATE = THE CURRENT DATE BEING PROCESSED.  (INPUT)
C               MINPK = MINIMUM GROUP SIZE WHEN PACKING THE GRIDDED
C                       VALUES.  (INPUT)
C           ISCALD(N) = THE DECIMAL SCALING CONSTANT TO USE WHEN PACKING
C                       THE GRIDDED DATA (N=1,ND4).  NO BINARY 
C                       SCALING IS PROVIDED FOR.  (INPUT)
C             ID(J,N) = THE PREDICTOR ID'S (J=1,4) (N=1,NVRBLS).  (INPUT)
C         IDPARS(J,N) = THE PARSED, INDIVIDUAL COMPONENTS OF THE PREDICTOR
C                       ID'S CORRESPONDING TO ID( ,N) (J=1,15), (N=1,NVRBLS).
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE), (NOT USED)
C                       J=14--S (SMOOTHING INDICATOR), AND (NOT USED)
C                       J=15--G (GRID INDICATOR).  (NOT USED)
C            IPACK(J) = HOLDS THE TDLPACK RECORD (J=1,NWORDS).  NWORDS
C                       IS CALCULATED NWORDS=NBYTES*8/L3264B, WHERE NBYTES
C                       IS THE LENGTH IN BYTES READ FROM THE RECORD ITSELF.
C                       (INTERNAL)
C            IWORK(J) = ARRAY TO FURNISH TO SUBROUTINE PACK (J=1,ND5).  
C                       (INTERNAL)
C             DATA(K) = WORK ARRAY THAT CONTAINS THE VALUES OF THE
C                       VARIABLE ON THE INPUT GRID (K=1,ND5).  (INPUT)
C              IS0(J) = MOS-2000 GRIB SECTION 0 ID'S (J=1,3).  (INTERNAL)
C              IS1(J) = MOS-2000 GRIB SECTION 1 ID'S (J=1,22+).  (INTERNAL)
C              IS2(J) = MOS-2000 GRIB SECTION 2 ID'S (J=1,12).  (INTERNAL)
C              IS4(J) = MOS-2000 GRIB SECTION 4 ID'S (J=1,4).  (INTERNAL)
C       IPLAIN(L,J,N) = 32 CHARACTERS (L=1,L3264W) (J=1,4) OF PLAIN
C                       LANGUAGE DESCRIPTION OF VARIABLES (N=1,ND4).
C                       NOTE THAT THIS REQUIRES TWO 32-BIT WORDS TO HOLD
C                       THE DESCRIPTION BUT ONLY ONE 64-BIT WORD.
C                       EQUIVALENCED TO PLAIN( ).  (INPUT)
C            PLAIN(N) = THE PLAIN LANGUAGE DESCRIPTION OF THE VARIABLES
C                       (N=1,ND4).  EQUIVALENCED TO IPLAIN( , , ).
C                       (CHARACTER*32)  (INPUT)
C        XYDIR(K,J,M) = THE IX (J=1) AND JY (J=2) POSITIONS ON THE OUTPUT
C                       GRID (K=1,NX*NY) FOR A PARTICULAR GRID 
C                       CHARACTERISTIC (M=1,NGRID).  (INPUT)
C               MPROJ = MAP PROJECTION OF THE OUTPUT GRID.  (INPUT)
C                  NX = X-EXTENT OF THE OUTPUT GRID.  (INPUT)
C                  NY = Y-EXTENT OF THE OUTPUT GRID.  (INPUT)
C              XLATLL = LATITUDE OF LOWER LEFT CORNER POINT OF THE
C                       OUTPUT GRID.  (INPUT)
C              YLONLL = WEST LONGITUDE OF LOWER LEFT CORNER POINT
C                       OF THE OUTPUT GRID.  DO NOT USE NEGATIVE.  (INPUT)  
C               XMESH = MESH LENGTH OF OUTPUT GRID IN KM AT XLAT DEGREES 
C                       NORTH LATITUDE.  (INPUT)
C              ORIENT = ORIENTATION OF OUTPUT GRID IN DEGREES WEST 
C                       LONGITUDE.  DO NOT USE NEGATIVE.  (INPUT)  
C                XLAT = LATITUDE OF OUTPUT GRID IN DEGREES AT WHICH 
C                       XMESH APPLIES. ALSO THE LATITUDE WHERE THE 
C                       PROJECTION CUTS THE EARTH.  DO NOT USE NEGATIVE.
C                       (INPUT)
C                IP13 = THE IP VALUE WHICH DIRECTS THE PRINT OUTPUT OF  
C                       THE INTERPOLATED GRID VALUES.  (INPUT)
C                IP16 = THE IP VALUE WHICH DIRECTS THE PRINT THAT SHOWS
C                       WHICH VARIABLES HAVE BEEN PACKED.  (INPUT)
C             JP(J,N) = JP(1,N) INDICATES WHETHER (>0) OR NOT (=0)
C                       VARIABLE N WILL BE OUTPUT FOR VIEWING.
C                       JP(2,N) INDICATES THE TYPE OF INTERPOLATION
C                       TO BE PERFORMED ON THE INPUT GRID IN OBTAINING
C                       THE OUTPUT GRID.  (N=1,ND4).  (INPUT)
C              L3264B = INTEGER WORD LENGTH IN BITS OF MACHINE BEING
C                       USED (EITHER 32 OR 64).  SET BY PARAMETER.  (INPUT)
C              L3264W = NUMBER OF WORDS IN 64 BITS (EITHER 1 OR 2).  (INPUT)
C              NTOTBG = THE TOTAL NUMBER OF BYTES ON THE FILE 
C                       ASSOCIATED WITH UNIT NO. KFILRA (THE GRIDPOINT
C                       FILE).  IT IS UPDATED WHEN THE DATA IN
C                       IPACK( ) ARE WRITTEN.  (INPUT/OUTPUT)
C              NTOTRG = THE TOTAL NUMBER OF RECORDS IN THE GRIDPOINT
C                       FILE.  IT IS UPDATED AS NEEDED IN WRITEP.  
C                       (INPUT/OUTPUT) 
C            ISTOP(J) = FOR J=1, ISTOP( ) IS INCREMENTED BY 1 EACH TIME
C                       FOR J=2, ISTOP( ) IS INCREMENTED BY 1 WHENEVER
C                       AN INPUT DATA RECORD IS NOT FOUND.
C                 IER = STATUS RETURN.
C                        0 = GOOD RETURN.
C                        > 0 VALUES RETURNED FROM OTHER ROUTINES. 
C                       (INTERNAL-OUTPUT)
C           NGRIDG(J) = GRID CHARACTERISTICS OF OUTPUT GRID (J=1,6).  
C                       (INTERNAL)
C           ISDATA(J) = WORK ARRAY (J=1,ND2X3). (ALLOCATABLE ARRAY) (INTERNAL)  
C            GDATA(K) = STORES THE INTERPOLATED DATA OF THE OUTPUT GRID
C                       (K=1,ND2X3).  (ALLOCATABLE ARRAY) (INTERNAL) 
C
C        SUBPROGRAMS CALLED:  DATA_INTRP, PACKG
C          UNIQUE: NONE
C          LIBRARY:
C           MOSLIB - PACKG
C
C        EXIT STATES:
C          COND =    0 - SUCCESSFUL RUN
C                   SEE OTHER ROUTINES FOR OTHER VALUES.
C
C REMARKS:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:  FORTRAN 90 (ifort compiler)
C   MACHINE:  IBM SP, INTEL/LINUX
C
C$$$
C
      CHARACTER*32 PLAIN(ND4)
      CHARACTER*60 RACESS
C
      REAL, ALLOCATABLE, DIMENSION(:) :: GDATA,ISDATA
C      
      DIMENSION XYDIR(ND2X3,2)
      DIMENSION ISCALD(ND4),ID(4,ND4),IDPARS(15,ND4),JP(3,ND4)
      DIMENSION IPLAIN(L3264W,4,ND4)
      DIMENSION IPACK(ND5),DATA(ND5),IWORK(ND5)
      DIMENSION IS0(ND7),IS1(ND7),IS2(ND7),IS4(ND7)
      DIMENSION NGRIDG(6)
C        NOTE: NGRIDG IS AN AUTOMATIC ARRAY.
      DIMENSION ISTOP(2)
C
      ALLOCATE(GDATA(ND2X3),ISDATA(ND2X3))
C
      IER=0
C
C        INTERPOLATE THE DATA FROM THE INPUT GRID TO 
C        THE OUTPUT GRID.
C
      CALL DATA_INTRP(KFILDO,ND2X3,ND5,NX,NY,
     1                JP(2,N),DATA,GDATA,XYDIR,
     2                IS2(3),IS2(4))
C
C        PRINT GRIDPOINT VALUES IF DESIRED. 
C
      IF(IP13.EQ.0) GOTO 245
      IF(JP(1,N).EQ.0) GOTO 245
C
      WRITE(IP13,207)(ID(J,N),J=1,4),IDATE
 207  FORMAT(/,' INTERPOLATED GRIDPOINT VALUES FOR VARIABLE',
     1         I11.9,3I11,' FOR DATE',I12)
C
      DO 240 JY=1,NY
C
         IF(ISCALD(N).LE.-1)THEN
            WRITE(IP13,215)(GDATA(K),K=(NY-JY)*NX+1,
     1                                 (NY-JY)*NX+NX)
 215        FORMAT(' ',10F10.1/(' ',10F10.1))
         ELSEIF(ISCALD(N).EQ.0)THEN
            WRITE(IP13,220)(GDATA(K),K=(NY-JY)*NX+1,
     1                                 (NY-JY)*NX+NX)                    
 220        FORMAT(' ',10F10.2/(' ',10F10.2))
         ELSEIF(ISCALD(N).EQ.1)THEN
            WRITE(IP13,225)(GDATA(K),K=(NY-JY)*NX+1,
     1                                 (NY-JY)*NX+NX)
 225        FORMAT(' ',10F10.3/(' ',10F10.3))
         ELSEIF(ISCALD(N).EQ.2)THEN
            WRITE(IP13,230)(GDATA(K),K=(NY-JY)*NX+1,
     1                                 (NY-JY)*NX+NX)
 230        FORMAT(' ',10F10.4/(' ',10F10.4))
         ELSE
            WRITE(IP13,235)(GDATA(K),K=(NY-JY)*NX+1,
     1                                 (NY-JY)*NX+NX)
 235        FORMAT(' ',10F10.5/(' ',10F10.5))
         ENDIF
C
 240  CONTINUE
C
C        IF NO OUTPUT FILE IS SPECIFIED, DO NOT PACK DATA.
C        EXIT THE ROUTINE.
 245  IF(KFILRA.EQ.0) GOTO 300
C
C        REDEFINE IS2( ) IN TERMS OF THE OUTPUT GRID FOR PACKING.
C 
C        MAP PROJECTION NUMBER (3=LAMBERT, 5=POLAR STEREOGRAPHIC &
C                               7=MERCATOR) OF INPUT GRID. 
      IS2(2)=MPROJ
      NGRIDG(1)=IS2(2)
C        NUMBER OF GRID POINTS ON THE OUTPUT GRID IN THE IX DIRECTION.
      IS2(3)=NX
C        NUMBER OF GRID POINTS ON THE OUTPUT GRID IN THE JY DIRECTION.
      IS2(4)=NY
C        GRID LENGTH IN MILLIMETERS OF OUTPUT GRID.
      NGRIDG(2)=NINT(XMESH*1000000.)
C        LATITUDE AT WHICH GRID LENGTH IS CORRECT *10000 OF OUTPUT 
C        GRID.
      NGRIDG(3)=NINT(XLAT*10000.)
C        GRID ORIENTATION IN DEGREES *10000 OF OUTPUT GRID.
      NGRIDG(4)=NINT(ORIENT*10000.)
C        LATITUDE OF LL CORNER IN DEGREES *10000 OF OUTPUT GRID.
      NGRIDG(5)=NINT(XLATLL*10000.)
C        LONGITUDE OF LL CORNER IN DEGREES *10000 OF OUTPUT GRID.
      NGRIDG(6)=NINT(YLONLL*10000.)
C
C        SET UP SOME VALUES FOR LOADING IS1( ).
C
      NYR=IDATE/1000000
      NMO=IDATE/10000-NYR*100
      NDA=IDATE/100-NYR*10000-NMO*100
      NHR=IDATE-NYR*1000000-NMO*10000-NDA*100
C
      XMISSP=9999.
      XMISSS=9997.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C         PACK THE DATA FROM THE SEQUENTIAL FILE(S) INTO KFILGO.
   
         CALL PACKG(KFILDO,KFILRA,ID(1,N),IDPARS(1,N),
     1           ISCALD(N),0,NGRIDG,
     2           IPLAIN(1,1,N),PLAIN(N),IDATE,NYR,NMO,NDA,NHR,
     3           ISDATA,GDATA,ND2X3,NX,NY,IPACK,IWORK,ND5,
     4           MINPK,IS0,IS1,IS2,IS4,ND7,XMISSP,XMISSS,
     5           NWORDS,NTOTBG,NTOTRG,
     6           L3264B,L3264W,ISTOP(1),IER)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        RANDOM ACCESS GRIDDED PACKING
C
CCC      CALL PACKGR_OPER(KFILDO,KFILRA,RACESS,ID(1,N),IDPARS(1,N),
CCC  1                    ISCALD(N),0,NGRIDG,
CCC  2                    IPLAIN(1,1,N),PLAIN(N),IDATE,NYR,NMO,NDA,NHR,
CCC  3                    ISDATA,GDATA,ND2X3,IS2(3),IS2(4),IPACK,IWORK,
CCC  +                    ND5,
CCC  4                    MINPK,IS0,IS1,IS2,IS4,ND7,
CCC  5                    XMISSP,XMISSS,NWORDS,NTOTBG,NTOTRG,
CCC  6                    L3264B,L3264W,ISTOP(1),IER)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C        IF A PROBLEM EXISTS WITH THE DATA DO NOT PACK.
      IF(IER.NE.0) GOTO 300
C
C        PRINT A DIAGNOSTIC TO IP16 TELLING THE USER WHICH VARIABLES 
C        HAVE BEEN PACKED.
C
      IF(IP16.GT.0) THEN
         WRITE(IP16,250) KFILRA,(IS1(JJ),JJ=9,12),PLAIN(N),IDATE
 250     FORMAT(/,' WRITING DATA TO UNIT ',I2,2X,
     1            3(I9.9,1X),I10.3,5X,A32,3X,'FOR DATE',2X,I10)
      ENDIF
C
 300  DEALLOCATE(GDATA,ISDATA)
C
      RETURN
      END

